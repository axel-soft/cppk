#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Не ЗначениеЗаполнено(Параметры.Задача) Тогда
		ВызватьИсключение НСтр("ru = 'Не указан параметр ""Предмет рассмотрения"".'");
	КонецЕсли;
	
	ПредметРассмотрения = РаботаСВопросами.ПредметРассмотрения(Параметры.Задача);
	СписокВопросов.Параметры.УстановитьЗначениеПараметра("ПредметРассмотрения", ПредметРассмотрения);
	
	ИспользоватьВремяВСрокахЗадач = ПолучитьФункциональнуюОпцию("ИспользоватьДатуИВремяВСрокахЗадач");
	
	ФорматДатыДляКолонок = ?(ИспользоватьВремяВСрокахЗадач, 
		"ДФ='dd.MM.yy H:mm'",
		"ДФ='dd.MM.yy'");
	
	Элементы.СписокВопросовДата.Формат = ФорматДатыДляКолонок;
	Элементы.СписокВопросовСтарыйСрок.Формат = ФорматДатыДляКолонок;
	Элементы.СписокВопросовНовыйСрок.Формат = ФорматДатыДляКолонок;
	
	Заголовок = СтрШаблон(
		НСтр("ru = 'Вопросы выполнения задачи: %1'"),
		ПредметРассмотрения);
		
	ЭтоМобильныйКлиент = ПараметрыСеанса.ЭтоМобильныйКлиент;
	Если ЭтоМобильныйКлиент Тогда
		МК_НастроитьЭлементыФормы();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ЗаданВопросАвторуЗадачи" И Параметр = ПредметРассмотрения Тогда
		Элементы.СписокВопросов.Обновить();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаКлиенте
Процедура СписокВопросовПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	
	Отказ = Истина;
	
	РаботаСВопросамиКлиент.СоздатьВопрос(ПредметРассмотрения);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокВопросовОбработкаЗапросаОбновления(Элемент)
	
		Элементы.СписокВопросов.Обновить();
		
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область СлужебныеПроцедурыИФункции_МобильныйКлиент

&НаСервере
Процедура МК_НастроитьЭлементыФормы()
	
	СворачиваниеЭлементовПоВажности = СворачиваниеЭлементовФормыПоВажности.НеИспользовать;
	ВертикальныйИнтервал = ИнтервалМеждуЭлементамиФормы.Половинный;
	ПоложениеКоманднойПанели = ПоложениеКоманднойПанелиФормы.Нет;
	
	Элементы.МК_НижнийБлок.Видимость = Истина;
	МК.ОформитьАкцентнуюКнопку(Элементы.МК_Создать);
	
	Элементы.СписокВопросовГруппаОписаниеИСроки.Видимость = Ложь;
	Элементы.СписокВопросовАвтор.Видимость = Ложь;
	Элементы.СписокВопросовПринятоеРешение.Видимость = Ложь;
	
	Элементы.СписокВопросов.Шапка = Ложь;
	Элементы.СписокВопросов.ПоложениеСтрокиПоиска = ПоложениеСтрокиПоиска.ПотянутьСверху;
	Элементы.СписокВопросов.ПоложениеКоманднойПанели = ПоложениеКоманднойПанелиЭлементаФормы.Нет;
	Элементы.МК_ГруппаКолонок.Видимость = Истина;
	
	Элементы.Переместить(Элементы.СписокВопросовСостояниеКартинка, Элементы.МК_ПерваяКолонка);
	Элементы.Переместить(Элементы.СписокВопросовОписание, Элементы.МК_ВтораяКолонка);
	Элементы.Переместить(Элементы.СписокВопросовИнициаторПроцесса, Элементы.МК_ВтораяКолонка);
	Элементы.Переместить(Элементы.СписокВопросовДата, Элементы.МК_ТретьяКолонка);
	
	Элементы.СписокВопросовСостояниеКартинка.КартинкаЗначений = БиблиотекаКартинок.МК_КоллекцияСостоянияПроцессовИЗадач;
	МК.НастроитьКолонкуСКартинкойДляСписка(Элементы.СписокВопросовСостояниеКартинка);
	МК.УстановитьПолюУсловноеОформлениеСкрытНеВидим(СписокВопросов, "СостояниеКартинка", 0);

	Элементы.СписокВопросовОписание.РастягиватьПоГоризонтали = Истина;
	
	Элементы.СписокВопросовИнициаторПроцесса.Ширина = 1;
	Элементы.СписокВопросовИнициаторПроцесса.Высота = 1;
	Элементы.СписокВопросовИнициаторПроцесса.РастягиватьПоГоризонтали = Истина;
	Элементы.СписокВопросовИнициаторПроцесса.РастягиватьПоВертикали = Ложь;
	Элементы.СписокВопросовИнициаторПроцесса.Шрифт = ШрифтыСтиля.МелкийШрифтТекста;
	Элементы.СписокВопросовИнициаторПроцесса.ЦветТекста = ЦветаСтиля.МК_ЦветАвтораИсполнителя;
	
	Элементы.СписокВопросовДата.Высота = 1;
	Элементы.СписокВопросовДата.Ширина = 2;
	Элементы.СписокВопросовДата.Формат = МК.ПолучитьФорматДатыДляЗадач();
	МК.НастроитьКолонкуСДатойДляСписка(Элементы.СписокВопросовДата);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
