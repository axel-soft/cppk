#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Если Не ЗначениеЗаполнено(Значение) Тогда
		Возврат
	КонецЕсли;
	
	КаталогСуществует = Ложь;
	Файл = Новый Файл(Значение);
	Если Не Файл.Существует() Или Не Файл.ЭтоКаталог() Тогда
		Отказ = Истина;
		ТекстОшибки = СтрШаблон(
			НСтр("ru = 'Не удалось записать каталог внешнего буфера данных: каталог ""%1"" не найден'"),
			Значение);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки);
		Возврат;
	КонецЕсли;
	
	Попытка
		РазделительПути = ПолучитьРазделительПути();
		ИмяКаталогаТестовое = Значение + ?(Прав(Значение, 1) = РазделительПути, "", РазделительПути) + "Test";
		СоздатьКаталог(ИмяКаталогаТестовое);
		УдалитьФайлы(ИмяКаталогаТестовое);
	Исключение
		Отказ = Истина;
		ТекстОшибки = 
			НСтр("ru = 'Не удалось записать каталог внешнего буфера данных: при создании тестового подкаталога возникла ошибка.
					|Возможно, учетная запись, от которой работает сервер 1С:Предприятия, не имеет прав доступа к каталогу.'");
		Информация = ИнформацияОбОшибке();
		Если Информация.Причина <> Неопределено Тогда
			ТекстОшибки = ТекстОшибки + Символы.ПС + Информация.Причина.Описание;
			Если Информация.Причина.Причина <> Неопределено Тогда
				ТекстОшибки = ТекстОшибки + ": " + Информация.Причина.Причина.Описание;
			КонецЕсли;
		Иначе
			ТекстОшибки = ТекстОшибки + Информация.Описание;
		КонецЕсли;
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки);
	КонецПопытки;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	
	Если Не ЗначениеЗаполнено(Значение) Тогда
		Возврат
	КонецЕсли;
	
	РазделительПути = ПолучитьРазделительПути();
	Если Прав(Значение, 1) <> РазделительПути Тогда
		Значение = Значение + РазделительПути;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли