
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

// Фиксирует начальные границы отметок исторических данных.
//
// Параметры:
//	Дата - Дата - Границ исторических данных.
//
Процедура УстановитьНачальнуюГраницу(Дата) Экспорт
	
	Границы = ПолучитьГраницы();
	НовыеГраницы = Новый ФиксированнаяСтруктура(
		"НачальнаяГраница, ТекущаяГраница, КонечнаяГраница",
		Дата, Границы.ТекущаяГраница, Границы.КонечнаяГраница);
	
	Константы.ГраницыОтметокВремениИсторическихДанных.Установить(
		Новый ХранилищеЗначения(НовыеГраницы));
	
КонецПроцедуры

// Фиксирует текущую границу отметок исторических данных.
//
// Параметры:
//	Дата - Дата - Граница исторических данных.
//
Процедура УстановитьТекущуюГраницу(Дата) Экспорт
	
	Границы = ПолучитьГраницы();
	
	НовыеГраницы = Новый ФиксированнаяСтруктура(
		"НачальнаяГраница, ТекущаяГраница, КонечнаяГраница",
		Границы.НачальнаяГраница, Дата, Границы.КонечнаяГраница);
	
	Константы.ГраницыОтметокВремениИсторическихДанных.Установить(
		Новый ХранилищеЗначения(НовыеГраницы));
	
КонецПроцедуры

// Возвращает начальную границу отметок исторических данных.
//
// Возвращаемое значение:
//	Дата - Начальная граница исторических данных.
//
Функция ПолучитьНачальнуюГраницу() Экспорт
	
	Границы = ПолучитьГраницы();
	Возврат Границы.НачальнаяГраница;
	
КонецФункции

// Возвращает текущую границу отметок исторических данных.
//
// Возвращаемое значение:
//	Дата - Текущая граница исторических данных.
//
Функция ПолучитьТекущуюГраницу() Экспорт
	
	Границы = ПолучитьГраницы();
	Возврат Границы.ТекущаяГраница;
	
КонецФункции

// Возвращает текущую границу отметок исторических данных.
//
// Возвращаемое значение:
//	Дата - Текущая граница исторических данных.
//
Функция ПолучитьКонечнуюГраницу() Экспорт
	
	Границы = ПолучитьГраницы();
	Возврат Границы.КонечнаяГраница;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Возвращает границы отметок исторических данных.
//
// Возвращаемое значение:
//	Структура Из КлючИЗначение:
//		* Ключ - Вид границы.
//		* Значение - Дата границы.
//
Функция ПолучитьГраницы()
	
	Границы = Константы.ГраницыОтметокВремениИсторическихДанных.Получить().Получить();
	Если Границы = Неопределено Тогда
		Границы = Новый ФиксированнаяСтруктура(
			"НачальнаяГраница, ТекущаяГраница, КонечнаяГраница",
			Дата(1,1,1), Дата(1,1,1), Дата(1,1,1));
	Иначе
		НачальнаяГраница =
			ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Границы, "НачальнаяГраница", Дата(1, 1, 1));
		ТекущаяГраница =
			ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Границы, "ТекущаяГраница", Дата(1, 1, 1));
		КонечнаяГраница =
			ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Границы, "КонечнаяГраница", Дата(1, 1, 1));
			
		Границы = Новый ФиксированнаяСтруктура(
			"НачальнаяГраница, ТекущаяГраница, КонечнаяГраница",
			НачальнаяГраница, ТекущаяГраница, КонечнаяГраница);
	КонецЕсли;
	
	Возврат Границы;
	
КонецФункции

#КонецОбласти

#КонецЕсли
