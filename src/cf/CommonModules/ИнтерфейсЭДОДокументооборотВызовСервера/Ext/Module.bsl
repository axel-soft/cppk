// @strict-types
#Область ПрограммныйИнтерфейс

// Заполнить параметры исходящего документа ЭДО.
// 
// Параметры:
//  ДокументДО - ОпределяемыйТип.ДокументДОДляЭДО
//  ПараметрыСозданияДокумента - См. ИнтерфейсДокументовЭДОКлиентСервер.НовыеПараметрыСозданияДокументаПоФайлу
//  ДокументГотовКСозданию - Булево - Истина, если все параметры заполнены и документ готов к созданию
//  ТекущийДокументЭДО - ДокументССылка.ЭлектронныйДокументВходящийЭДО,
//  				   - ДокументССылка.ЭлектронныйДокументИсходящийЭДО
//
Процедура ЗаполнитьПараметрыИсходящегоДокумента(Знач ДокументДО, ПараметрыСозданияДокумента,
	ДокументГотовКСозданию, ТекущийДокументЭДО = Неопределено) Экспорт

	ИнтерфейсЭДОДокументооборот.ЗаполнитьПараметрыИсходящегоДокумента(ДокументДО,
		ПараметрыСозданияДокумента, ДокументГотовКСозданию, ТекущийДокументЭДО);

КонецПроцедуры

// Проверяет, доступны ли текущему пользователю для чтения все электронные документы в пакете документов
//  по переданному электронному документу. Используется в карточке документа предприятия для проверки возможности
//  открытия карточки ЭД.
// 
// Параметры:
//  ЭлектронныйДокумент - ДокументСсылка.ЭлектронныйДокументВходящийЭДО, ДокументСсылка.ЭлектронныйДокументИсходящийЭДО - 
//   Документ, пакет которого проверяется на доступность
// 
// Возвращаемое значение:
//  Булево - Истина, если документ в пакете и все документы пакета доступны для чтения, 
//  или документ не в пакете и доступен для чтения. Ложь, если не все документы пакета доступны для чтения.
Функция ДоступныДляЧтенияВсеДокументыПакета(ЭлектронныйДокумент) Экспорт

	Результат = Истина;

	ИдентификаторПакета = ПакетыДокументовЭДО.ИдентификаторПакетаДокумента(ЭлектронныйДокумент);
	Если Не ЗначениеЗаполнено(ИдентификаторПакета) Тогда
		Возврат ДокументооборотПраваДоступа.ПраваПользователяПоОбъекту(ЭлектронныйДокумент).Чтение;
	КонецЕсли;
	
	СоставПакета = ПакетыДокументовЭДО.ДокументыПакета(ИдентификаторПакета);
	
	Права = ДокументооборотПраваДоступа.ПраваПользователейПоОбъектам(СоставПакета, Истина,
		Пользователи.ТекущийПользователь());
	
	Для Каждого Документ Из СоставПакета Цикл

		ПраваПоДокументу = Права.Найти(Документ);
		
		Если ПраваПоДокументу = Неопределено Или Не ПраваПоДокументу.Чтение Тогда
			Результат = Ложь;
			Прервать;
		КонецЕсли;
		
	КонецЦикла;
	

	Возврат Результат;

КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Заполнить параметры файла ЭД.
// 
// Параметры:
//  ФайлЭД - СправочникСсылка.Файлы
//  ПараметрыФайла - Структура - описывает файл, который нужно отправить:
//  * ИмяФайла - Строка - имя файла вместе с расширением.
//  * АдресХранилища - Строка - адрес временного хранилища, в котором содержатся двоичные данные файла.
//
Процедура ЗаполнитьПараметрыФайлаЭД(Знач ФайлЭД, ПараметрыФайла) Экспорт

	ИнтерфейсЭДОДокументооборот.ЗаполнитьПараметрыФайлаЭД(ФайлЭД, ПараметрыФайла);

КонецПроцедуры

#КонецОбласти