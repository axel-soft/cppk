
////////////////////////////////////////////////////////////////////////////////
// Подсистема "Мультипредметность"
// Модуль МультипредметностьКОРП: сервер, внешнее соединение
//
// Содержит процедуры и функции обработки объектов и механизмов, 
// поддерживающих мультипредметность в редакциях КОРП/ДГУ.
//  
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс_ПроверкаПравНаПредметы

// Проверяет права участников процессов на предметы.
// В случае отсутствия прав, отправляет уведомление автору процессу.
//
// Параметры:
//  ПроцессОбъект - БизнесПроцессОбъект - процесс.
//  АвторПроцесса - СправочникСсылка.Сотрудники,
//                  СправочникСсылка.Пользователи - автор процесса.
//
Процедура ПроверитьПраваУчастниковПроцессаИОтправитьУведомления(ПроцессОбъект, АвторПроцесса) Экспорт
	
	Отказ = Ложь;
	ПроцессОбъект.ПроверитьПраваУчастниковПроцессаНаПредметы(ПроцессОбъект, Отказ, Ложь);
	
	Если Отказ Тогда
		Сообщения = ПолучитьСообщенияПользователю(Истина);
		
		ТекстУведомления = НСтр("ru = 'Не все участники процесса имеют права на предметы:
			|%1'");
			
		ТекстСообщений = "";
		Разделитель = "";
			
		Для Каждого Сообщение Из Сообщения Цикл
			ТекстСообщений = ТекстСообщений
				+ Разделитель
				+ Сообщение.Текст;
				
			Разделитель = "" + Символы.ПС + Символы.ПС;
		КонецЦикла;
		
		ТекстУведомления = СтрШаблон(ТекстУведомления, ТекстСообщений);
		
		РаботаСУведомлениями.ОбработатьУведомлениеПрограммы(
			ТекстУведомления,
			Сотрудники.ПользовательСотрудника(АвторПроцесса),
			ПроцессОбъект.Ссылка);
		
	КонецЕсли;
	
КонецПроцедуры

// Обновляет данные действия в регистре сведений ПредметыИУчастникиДействийКомплексныхПроцессов.
//
// Параметры:
//  Действие - СправочникОбъект.<ИмяШаблонаПроцесса>
//             СправочникСсылка.<ИмяШаблонаПроцесса> - действие комплексного процесса.
//
Процедура ОбновитьКэшПредметовИУчастниковДействияКомплексныхПроцессов(Действие) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если ОбщегоНазначения.ЭтоСсылка(ТипЗнч(Действие)) Тогда
		РеквизитыДействия = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
			Действие, "ПометкаУдаления, ВладелецШаблона, КомплексныйПроцесс, Ссылка");
	Иначе
		РеквизитыДействия = Действие;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(РеквизитыДействия.КомплексныйПроцесс) Тогда
		Возврат;
	КонецЕсли;
	
	Если РеквизитыДействия.ПометкаУдаления Тогда
		
		ДобавитьЗаписиПоДействию = Ложь;
		
	ИначеЕсли Не ОбщегоНазначения.СсылкаСуществует(РеквизитыДействия.КомплексныйПроцесс) Тогда
		
		ДобавитьЗаписиПоДействию = Истина;
		
	Иначе
		
		ДобавитьЗаписиПоДействию = Ложь;
		
		ТекущееДействие = РеквизитыДействия.Ссылка;
		ТекущийВладелец = РеквизитыДействия.ВладелецШаблона;
		Пока ТекущийВладелец <> РеквизитыДействия.КомплексныйПроцесс Цикл
			ТекущееДействие = ТекущийВладелец;
			ТекущийВладелец = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ТекущийВладелец, "ВладелецШаблона");
		КонецЦикла;
		
		Схема = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(
			РеквизитыДействия.КомплексныйПроцесс, "Схема");
		
		Если ЗначениеЗаполнено(Схема) Тогда
						
			ПараметрыСхемы = 
				Справочники.ПараметрыСхемДляКомплексныхПроцессов.ПараметрыПоСхеме(Схема);
			
			Если ЗначениеЗаполнено(ПараметрыСхемы) Тогда
				
				ТипТекущегоДействия = ТипЗнч(ТекущееДействие);
				
				ДанныеПараметров = 
					Справочники.ПараметрыСхемДляКомплексныхПроцессов.ДанныеПараметровСхемы(
					ПараметрыСхемы);
					
				ПроцессыЭлементов = РаботаСКомплекснымиБизнесПроцессамиСервер.
					ПроцессыЭлементовСхемыПоКомплексномуПроцессу(
					РеквизитыДействия.КомплексныйПроцесс);
				
				ИмяЭлемента = "";
				Для Каждого ИмяЭлементаИНастройка Из ДанныеПараметров.НастройкиЭлементов Цикл
					НастрокаЭлемента = ИмяЭлементаИНастройка.Значение;
					Если ТипЗнч(НастрокаЭлемента) = ТипТекущегоДействия
						И НастрокаЭлемента = ТекущееДействие Тогда
						
						ИмяЭлемента = ИмяЭлементаИНастройка.Ключ;
						Прервать;
					КонецЕсли;
				КонецЦикла;
				
				Если Не ЗначениеЗаполнено(ПроцессыЭлементов[ИмяЭлемента]) Тогда
					ДобавитьЗаписиПоДействию = Истина;
				КонецЕсли;
							
			КонецЕсли;
			
		Иначе
			
			Запрос = Новый Запрос;
			Запрос.Текст = 
				"ВЫБРАТЬ
				|	КомплексныйПроцессЭтапы.ЗапущенныйБизнесПроцесс
				|ИЗ
				|	БизнесПроцесс.КомплексныйПроцесс.Этапы КАК КомплексныйПроцессЭтапы
				|ГДЕ
				|	КомплексныйПроцессЭтапы.ШаблонБизнесПроцесса = &ШаблонБизнесПроцесса
				|	И КомплексныйПроцессЭтапы.Ссылка = &КомплексныйПроцесс";
			Запрос.УстановитьПараметр("ШаблонБизнесПроцесса", ТекущееДействие);
			Запрос.УстановитьПараметр("КомплексныйПроцесс", РеквизитыДействия.КомплексныйПроцесс);
			
			Выборка = Запрос.Выполнить().Выбрать();
			Если Выборка.Количество() = 0
				Или Выборка.Следующий()
					И Не ЗначениеЗаполнено(Выборка.ЗапущенныйБизнесПроцесс) Тогда
				
				ДобавитьЗаписиПоДействию = Истина;
			КонецЕсли;
			
		КонецЕсли;
	
	КонецЕсли;
	
	Если ДобавитьЗаписиПоДействию Тогда
		РегистрыСведений.ПредметыИУчастникиДействийКомплексныхПроцессов.
			ДобавитьЗаписиПоДействию(Действие);
	Иначе
		РегистрыСведений.ПредметыИУчастникиДействийКомплексныхПроцессов.
			УдалитьЗаписиДействия(РеквизитыДействия.Ссылка);
	КонецЕсли;
	
КонецПроцедуры

// Удаляет данные действия комплексного процесса из регистра сведений ПредметыИУчастникиДействийКомплексныхПроцессов.
//
// Параметры:
//  Действие - СправочникСсылка.<ИмяШаблонаПроцесса> - действие комплексного процесса.
//
Процедура УдалитьДействиеКомплексногоПроцессаИзКэшаПредметовИУчастников(Действие) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	РегистрыСведений.ПредметыИУчастникиДействийКомплексныхПроцессов.УдалитьЗаписиДействия(Действие);
	
	Если ТипЗнч(Действие) = Тип("СправочникСсылка.ШаблоныКомплексныхБизнесПроцессов") Тогда
		
		ПодчиненныеДействия = Новый Массив;
		
		ПолучитьДействияКомплексногоПроцесса(Действие, ПодчиненныеДействия);
		
		Для Каждого ПодчиненноеДействие Из ПодчиненныеДействия Цикл
			РегистрыСведений.ПредметыИУчастникиДействийКомплексныхПроцессов.
				УдалитьЗаписиДействия(ПодчиненноеДействие);
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции_ПроверкаПравНаПредметы

// Обработчик подписки ОбновлениеПредметовИУчастниковДействийКомплексногоПроцесса
//
Процедура ОбновлениеПредметовИУчастниковДействийКомплексногоПроцессаПриЗаписи(
	Источник, Отказ) Экспорт
	
	Если Источник.ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если МиграцияДанныхИзВнешнихСистемСервер.ЭтоОбъектИзДругойСистемы(Источник.ИсточникДанных) Тогда
		Возврат;
	КонецЕсли;
	
	Если Источник.ДополнительныеСвойства.Свойство("ВидЗаписи")
		И Источник.ДополнительныеСвойства.ВидЗаписи <>
			"ЗаписьСЗаполнениемСлужебныхРеквизитовКомплексныхПроцессов" Тогда
		
		Возврат;
	КонецЕсли;
	
	ОбновитьКэшПредметовИУчастниковДействияКомплексныхПроцессов(Источник);
	
КонецПроцедуры

// Помещает все действия комплексного процесса в массив.
// Рекурсивная процедура.
//
// Параметры:
//  КомплексныйПроцесс - СправочникСсылка.ШаблоныКомплексныхБизнесПроцессов - комплексный процесс.
//  Действия - Массив
//   * СправочникСсылка.<ИмяШаблонаПроцесса> - действие комплексного процесса. В этот параметр является результатом
//                                             выполнения процедуры.
//
Процедура ПолучитьДействияКомплексногоПроцесса(КомплексныйПроцесс, Действия)
	
	УстановитьПривилегированныйРежим(Истина);
	
	Схема = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(КомплексныйПроцесс, "Схема");
	
	Если ЗначениеЗаполнено(Схема) Тогда
		
		ПараметрыСхемы = Справочники.ПараметрыСхемДляКомплексныхПроцессов.ПараметрыПоСхеме(Схема);
		
		Если ЗначениеЗаполнено(ПараметрыСхемы) Тогда
			ДанныеПараметров = 
				Справочники.ПараметрыСхемДляКомплексныхПроцессов.ДанныеПараметровСхемы(
				ПараметрыСхемы);
			
			НастройкиЭлементов = РаботаСКомплекснымиБизнесПроцессамиСервер.
				НастройкиДействийИВложенныхПроцессовВДанныхПараметров(ДанныеПараметров);
				
			Для Каждого НастройкаЭлемента Из НастройкиЭлементов Цикл
				
				Действия.Добавить(НастройкаЭлемента);
			
				Если ТипЗнч(НастройкаЭлемента) =
					Тип("СправочникСсылка.ШаблоныКомплексныхБизнесПроцессов") Тогда
					
					ПолучитьДействияКомплексногоПроцесса(НастройкаЭлемента, Действия);
				КонецЕсли;
				
			КонецЦикла; 
			
		КонецЕсли;
			
	Иначе
		
		Запрос = Новый Запрос;
		Запрос.Текст =
			"ВЫБРАТЬ
			|	ШаблоныКомплексныхБизнесПроцессовЭтапы.ШаблонБизнесПроцесса
			|ИЗ
			|	Справочник.ШаблоныКомплексныхБизнесПроцессов.Этапы КАК ШаблоныКомплексныхБизнесПроцессовЭтапы
			|ГДЕ
			|	ШаблоныКомплексныхБизнесПроцессовЭтапы.Ссылка = &КомплексныйПроцесс";
		Запрос.УстановитьПараметр("КомплексныйПроцесс", КомплексныйПроцесс);
		Выборка = Запрос.Выполнить().Выбрать();
		
		Пока Выборка.Следующий() Цикл
			
			Если Не ЗначениеЗаполнено(Выборка.ШаблонБизнесПроцесса) Тогда
				Продолжить;
			КонецЕсли;
			
			Действия.Добавить(Выборка.ШаблонБизнесПроцесса);
			
			Если ТипЗнч(Выборка.ШаблонБизнесПроцесса) =
				Тип("СправочникСсылка.ШаблоныКомплексныхБизнесПроцессов") Тогда
				
				ПолучитьДействияКомплексногоПроцесса(Выборка.ШаблонБизнесПроцесса, Действия);
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
		
КонецПроцедуры

#КонецОбласти