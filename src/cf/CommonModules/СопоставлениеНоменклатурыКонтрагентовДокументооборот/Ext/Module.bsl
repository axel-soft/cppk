// @strict-types


#Область ПрограммныйИнтерфейс

// Определяет свойства набора номенклатур информационной базы.
// Описание свойств в методе СопоставлениеНоменклатурыКонтрагентовКлиентСервер.НовыеСвойстваНоменклатурыИБ.
//
// Параметры:
//  НаборНоменклатуры - Массив из ОпределяемыйТип.НоменклатураБЭД - ссылки на номенклатуру ИБ, для которой требуется определить свойства.
//  СвойстваНоменклатурИБ - Соответствие Из КлючИЗначение:
//  * Ключ     - ОпределяемыйТип.НоменклатураБЭД - ссылка на номенклатуру ИБ.
//  * Значение - См. СопоставлениеНоменклатурыКонтрагентовКлиентСервер.НовыеСвойстваНоменклатурыИБ.
//
Процедура ПриОпределенииСвойствНоменклатурыИнформационнойБазы(Знач НаборНоменклатуры, СвойстваНоменклатурИБ) Экспорт
	
	СвойстваНоменклатуры = СопоставлениеНоменклатурыКонтрагентовКлиентСервер.НовыеСвойстваНоменклатурыИБ();
	СвойстваНоменклатуры.ИспользоватьХарактеристики           = Ложь;
	СвойстваНоменклатуры.ИспользоватьУпаковки                 = Ложь;
	СвойстваНоменклатуры.ОбязательноеЗаполнениеХарактеристики = Ложь;
	
	Если ТипЗнч(НаборНоменклатуры) = Тип("Массив") Тогда
		Для Каждого Номенклатура Из НаборНоменклатуры Цикл
			СвойстваНоменклатурИБ.Вставить(Номенклатура, СвойстваНоменклатуры);
		КонецЦикла;
	Иначе
		СвойстваНоменклатурИБ.Вставить(НаборНоменклатуры, СвойстваНоменклатуры);
	КонецЕсли;
	
КонецПроцедуры

// Определяет заголовки форм и полей сопоставления номенклатуры, которые могут зависеть от прикладного решения и его настроек.
//
// Параметры:
//  Представления - Структура - набор представлений, которые можно переопределить:
//   * ВладелецНоменклатурыПредставлениеОбъекта    - Строка - представление поля владельца номенклатуры.
//                                                            По умолчанию берется из определяемого типа ВладелецНоменклатурыБЭД.
//   * НоменклатураПредставлениеОбъекта            - Строка - представление поля номенклатуры.
//                                                            По умолчанию берется из определяемого типа НоменклатураБЭД.
//   * ХарактеристикаПредставлениеОбъекта          - Строка - представление поля характеристики номенклатуры.
//                                                            По умолчанию берется из определяемого типа ХарактеристикаНоменклатурыБЭД.
//   * УпаковкаПредставлениеОбъекта                - Строка - представление поля упаковки номенклатуры.
//                                                            По умолчанию берется из определяемого типа УпаковкаНоменклатурыБЭД.
//   * НоменклатураКонтрагентаПредставлениеСписка  - Строка - представление списка номенклатуры контрагентов.
//                                                            По умолчанию "Номенклатура контрагентов".
//   * НоменклатураКонтрагентаПредставлениеОбъекта - Строка - представление объекта номенклатуры контрагентов.
//                                                            По умолчанию "Номенклатура контрагента".
//   * ВладелецНоменклатурыВРодительномПадеже      - Строка - представление поля владелец номенклатуры в родительном падеже.
//                                                            По умолчанию "Контрагента".
//
// Пример:
//  Представления.НоменклатураКонтрагентаПредставлениеСписка  = НСтр("ru = 'Номенклатура партнеров'");
//  Представления.НоменклатураКонтрагентаПредставлениеОбъекта = НСтр("ru = 'Номенклатура партнера'");
//  Представления.ВладелецНоменклатурыПредставлениеОбъекта    = НСтр("ru = 'Партнер'");
//  Представления.ВладелецНоменклатурыВРодительномПадеже      = НСтр("ru = 'Партнера'").
//
Процедура ПриОпределенииПредставленийСопоставленияНоменклатуры(Представления) Экспорт
	
	Представления.НоменклатураПредставлениеОбъекта = НСтр("ru = 'Номенклатура'");
	
КонецПроцедуры

// Определяет взаимосвязь между частями номенклатуры информационной базы.
// Устанавливает зависимость характеристик и упаковок от номенклатуры.
// Используется при заполнении свойства "СвязиПараметровВыбора" в элементах формы.
//
// Параметры:
//  СтруктураНоменклатуры - Структура - описание структуры номенклатуры информационной базы (для изменения):
//   * ИмяПараметраСвязиХарактеристики - Строка - имя параметра связи выбора характеристики по номенклатуре. По умолчанию "Отбор.Владелец".
//   * ИмяПараметраСвязиУпаковки       - Строка - имя параметра связи выбора упаковки по номенклатуре. По умолчанию "Отбор.Владелец".
//
Процедура ПриОпределенииСтруктурыНоменклатурыИнформационнойБазы(СтруктураНоменклатуры) Экспорт
	
	СтруктураНоменклатуры.ИмяПараметраСвязиХарактеристики = "";
	СтруктураНоменклатуры.ИмяПараметраСвязиУпаковки       = "";
	
КонецПроцедуры

// В процедуре требуется определить признак использования характеристик номенклатуры в ИБ.
//
// Параметры:
//  ХарактеристикиИспользуются - Булево - в информационной базе используются характеристики номенклатуры.
//
Процедура ПроверитьИспользованиеХарактеристик(ХарактеристикиИспользуются) Экспорт
	
	ХарактеристикиИспользуются = Ложь;
	
КонецПроцедуры


// Переопределяет значение наименования номенклатуры или характеристики (если используется), используемое в механизме сопоставления номенклатуры.
//
// Параметры:
//  СсылкаНаОбъект       - ОпределяемыйТип.ДанныеСопоставленияБЭД - ссылка номенклатуры или характеристики.
//  ЗначениеНаименования - Строка                                 - значение наименования номенклатуры или характеристики сопоставления.
//
Процедура ЗначениеНаименованияДанныхСопоставленияНоменклатурыБЭД(Знач СсылкаНаОбъект, ЗначениеНаименования) Экспорт
	
	Если ТипЗнч(СсылкаНаОбъект) = Тип("СправочникСсылка.Номенклатура") Тогда
		ЗначениеНаименования = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СсылкаНаОбъект, "Наименование");
	КонецЕсли;
	
КонецПроцедуры

// Переопределяет значения наименований номенклатуры или характеристики (если используется), используемое в механизме сопоставления номенклатуры.
//
// Параметры:
//  НаборСсылокНаОбъекты             - Массив из ОпределяемыйТип.ДанныеСопоставленияБЭД - набор ссылок номенклатуры или характеристики.
//  СоответствиеЗначенийНаименований - Соответствие Из КлючИЗначение:
//   * Ключ     - ОпределяемыйТип.ДанныеСопоставленияБЭД - ссылка номенклатуры или характеристики.
//   * Значение - Строка                                 - значение наименования номенклатуры или характеристики.
//
Процедура ЗначенияНаименованийДанныхСопоставленияНоменклатурыБЭД(Знач НаборСсылокНаОбъекты, СоответствиеЗначенийНаименований) Экспорт
	
	МассивНоменклатуры = Новый Массив; // Массив Из ОпределяемыйТип.ДанныеСопоставленияБЭД
	Для Каждого СсылкаНаОбъект Из НаборСсылокНаОбъекты Цикл
		Если ТипЗнч(СсылкаНаОбъект) = Тип("СправочникСсылка.Номенклатура") Тогда
			МассивНоменклатуры.Добавить(СсылкаНаОбъект);
		КонецЕсли;
	КонецЦикла;
	
	СоответствиеЗначенийНаименований = Новый Соответствие;
	ДанныеНоменклатуры = ОбщегоНазначения.ЗначенияРеквизитовОбъектов(МассивНоменклатуры, "Наименование");
	Для Каждого ЭлементСоответствия Из ДанныеНоменклатуры Цикл
		ЗначениеСоответствия = ЭлементСоответствия.Значение;
		//@skip-check property-return-type
		СоответствиеЗначенийНаименований.Вставить(ЭлементСоответствия.Ключ, ЗначениеСоответствия.Наименование);
	КонецЦикла;
	
КонецПроцедуры

// Переопределяет текст запроса поиска сопоставления по упаковкам/единицам измерения в информационной базе.
// Необходимо описать поиск упаковок/единиц измерения ИБ по наименованию упаковки, единицы измерения или коду единицы измерения
// по данным номенклатуры контрагента. 
// Необходимо учитывать права пользователей при получении данных из информационной базы.
//
// Рекомендации поиска:
//  Упаковки рекомендуется искать по полному наименованию (если используется), по наименованию и по наименованию и/или коду базовой единицы измерения,
//  отобрав упаковки по владельцу (если владельцем упаковки является номенклатура). 
//  Единицы измерения по классификатору ОКЕИ рекомендуется искать по коду единицы измерения и наименованию.
//
// Доступна временная таблица:
//  ТаблицаНоменклатуры - содержит данные номенклатуры контрагента по сопоставленным номенклатурам ИБ. Колонки:
//   НоменклатураИБ        - ОпределяемыйТип.НоменклатураБЭД - ссылка сопоставленной номенклатуры ИБ. Используется для связи с владельцем упаковки 
//                                                             (если владельцем упаковки является номенклатура).
//   ЕдиницаИзмеренияКод   - Строка                          - код единицы измерения по данным номенклатуры контрагента.
//                                                             Используется для поиска единицы измерения по классификатору ОКЕИ.
//   ЕдиницаИзмерения      - Строка                          - наименование единицы измерения по данным номенклатуры контрагента.
//                                                             Используется для поиска упаковки или единицы измерения.
//   ИдентификаторУпаковки - Строка                          - идентификатор упаковки по данным номенклатуры контрагента.
//                                                             Используется для связи сопоставленных данных.
//  Индексируемые поля: НоменклатураИБ, ЕдиницаИзмерения.
//
// Необходимо вернуть предопределенные поля:
//  НоменклатураИБ        - ОпределяемыйТип.НоменклатураБЭД         - ссылка сопоставленной номенклатуры ИБ.
//  ИдентификаторУпаковки - Строка                                  - идентификатор упаковки по данным номенклатуры контрагента.
//  УпаковкаИБ            - ОпределяемыйТип.УпаковкаНоменклатурыБЭД - найденная упаковка или единица измерения по данным номенклатуры контрагента.
//
// Параметры:
//  ТекстЗапроса - Строка - текст запроса по данным информационной базы.
//
Процедура ТекстЗапросаПоискаВариантовСопоставленияУпаковкиНоменклатурыБЭД(ТекстЗапроса) Экспорт
	
	ТекстЗапроса =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ЕдиницыИзмерения.Ссылка КАК УпаковкаИБ,
	|	ТаблицаНоменклатуры.ИдентификаторУпаковки КАК ИдентификаторУпаковки,
	|	ТаблицаНоменклатуры.НоменклатураИБ КАК НоменклатураИБ
	|ИЗ
	|	ТаблицаНоменклатуры КАК ТаблицаНоменклатуры
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлассификаторЕдиницИзмерения КАК ЕдиницыИзмерения
	|		ПО ТаблицаНоменклатуры.ЕдиницаИзмерения = ЕдиницыИзмерения.Наименование";
	
КонецПроцедуры

// Заполняет форму номенклатуру по данным контрагента.
//
// Параметры:
//  НоменклатураКонтрагента - Структура - данные контрагента для заполнения формы номенклатуры.
//                                        См. СопоставлениеНоменклатурыКонтрагентовКлиентСервер.НоваяНоменклатураКонтрагента.
//  Форма - см. Справочник.Номенклатура.Форма.ФормаЭлемента
//
Процедура ПриЗаполненииФормыНоменклатурыПоДаннымКонтрагента(Знач НоменклатураКонтрагента, Форма) Экспорт
	
	Объект = Форма.Объект;
	Объект.Наименование       = НоменклатураКонтрагента.Наименование;
	Объект.Артикул            = НоменклатураКонтрагента.Артикул;
	
КонецПроцедуры

// Переопределяет наименование объекта сопоставления номенклатуры БЭД: номенклатуры или характеристики, если используется.
//
// Параметры:
//  СсылкаНаОбъект          - ОпределяемыйТип.ДанныеСопоставленияБЭД - ссылка на номенклатуру или характеристику сопоставления.
//  НаименованиеТипаОбъекта - Строка                                 - содержит наименование типа объекта,
//                                                                     должно передаваться наименование: "Номенклатура" или "Характеристика".
//
Процедура ОпределитьНаименованиеТипаОбъектаСопоставленияНоменклатурыБЭД(Знач СсылкаНаОбъект, НаименованиеТипаОбъекта) Экспорт
	
	НаименованиеТипаОбъекта = "Номенклатура";
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Код процедур и функций

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Код процедур и функций

#КонецОбласти
