////////////////////////////////////////////////////////////////////////////////
// Правила обработки клиент: содержит процедуры и функции обеспечивающие
//                                     работу с обработками объектов на клиенте.
//  
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Проверить возможность смены ключевых реквизитов. 
// Вернет структуру, что делать - показать вопрос, прервать операцию или ничего не делать.
// 
// Параметры:
//  ВидОбъектаИзменен - Булево.
//  Предмет - ОпределяемыйТип.ПредметДействия.
//  
//  Возвращаемое значение:
//  Структура
//    *ЗадатьВопрос - Булево
//    *Отказ  - Булево
//    *ТекстОшибки - Строка
//    *ТекстВопроса - Строка
//
Функция ПроверитьВозможностьСменыКлючевыхРеквизитов(ВидОбъектаИзменен, Предмет) Экспорт
	
	ОписаниеВозврата = Новый Структура("НадоЗадатьВопрос, Отказ, ТекстОшибки, ТекстВопроса",
		Ложь, Ложь, "", "");
	
	Если Не ЗначениеЗаполнено(Предмет) Тогда
		Возврат ОписаниеВозврата;
	КонецЕсли;		
	
	СтруктураРезультата = ПравилаОбработкиВызовСервера.ПолучитьЗапущеннуюОбработкуИНастройки(Предмет);
	ОбработкаОбъекта = СтруктураРезультата.ОбработкаОбъекта;
	
	Если Не ЗначениеЗаполнено(ОбработкаОбъекта) Тогда
		Возврат ОписаниеВозврата;
	КонецЕсли;	
	
	
	Если ВидОбъектаИзменен Тогда
		ОписаниеВозврата.Отказ = Истина;
		ОписаниеВозврата.ТекстОшибки = НСтр("ru = 'Изменение вида документа, запущенного в обработку, запрещено. 
			|Остановите обработку и повторите действие.'");
	КонецЕсли;	

	ПроверятьСхемуПриПерезаполненииОбработки = СтруктураРезультата.ПроверятьСхемуПриПерезаполненииОбработки;
	
	Если Не ПроверятьСхемуПриПерезаполненииОбработки Тогда //упрощенный режим
	
		ОписаниеВозврата.НадоЗадатьВопрос = Истина;
		ОписаниеВозврата.ТекстВопроса = НСтр("ru = 'Изменен реквизит, влияющий на обработку документа. 
				|Это может привести к некорректному прохождению обработки. 
				|Вы уверены?'");

	Иначе // умный режим (с проверкой схемы)
			
		ПравилаОбработкиВызовСервера.ПроверитьВозможностьСменыРеквизитовСПроверкойСхемы(
			Предмет, ОбработкаОбъекта, ОписаниеВозврата.Отказ, ОписаниеВозврата.ТекстОшибки);	
			
	КонецЕсли;
	
	Возврат ОписаниеВозврата;
	
КонецФункции	

#КонецОбласти
