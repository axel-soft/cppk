////////////////////////////////////////////////////////////////////////////////
// Подсистема "Внешний буфер данных" (сервер, повт. исп.).
// 
// Выполняется на сервере, возвращаемые значения кэшируются на время сеанса.
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Возвращает строку с именем корневого каталога и разделителем в конце.
//
// Возвращаемое значение:
//  Строка - например, "\\Server\1CServiceFiles\".
//
Функция КорневойКаталог() Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если РегламентныеЗаданияСервер.РаботаСВнешнимиРесурсамиЗаблокирована() Тогда
		ВызватьИсключение НСтр(
			"ru = 'Обращение к внешнему буферу данных запрещено, т.к. заблокирована работа с внешними ресурсами.'");
	КонецЕсли;
	
	ИмяПапки = Константы.КаталогВнешнегоБуфераДанных.Получить();
	Если Не ЗначениеЗаполнено(ИмяПапки) Тогда
		ТекстОшибки = СтрШаблон(НСтр("ru = 'Не заполнена константа ""%1""'"),
			Метаданные.Константы.КаталогВнешнегоБуфераДанных.Имя);
		ВызватьИсключение ТекстОшибки;
	КонецЕсли;
	
	// При отсутствии каталога программа его не создает, выдает ошибку.
	Каталог = Новый Файл(ИмяПапки);
	Если Не Каталог.Существует() Тогда
		ТекстОшибки = СтрШаблон(НСтр("ru = 'Не найден каталог %1'"), ИмяПапки);
		ВызватьИсключение ТекстОшибки;
	КонецЕсли;
	
	Возврат ИмяПапки;
	
КонецФункции

// Создает объект ЗаписьДанных.
// 
// Параметры:
//  ПолноеИмяФайла - Строка - имя файла, которое будет передано в конструктор.
//
// Возвращаемое значение:
//  ЗаписьДанных - новый объект, созданный на основании файла.
// 
Функция ЗаписьДанных(ПолноеИмяФайла) Экспорт
	
	ЗаписьДанных = Новый ЗаписьДанных(ПолноеИмяФайла,,,, Истина, "");
	Возврат ЗаписьДанных;
	
КонецФункции

#КонецОбласти
