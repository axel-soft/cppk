///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2023, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

////////////////////////////////////////////////////////////////////////////////
// Модуль процедур, исполняемых на сервере и на клиенте

#Область СлужебныйПрограммныйИнтерфейс

// Возвращает текст вопроса о замене участников, который будет выведен в диалог пользователю.
//
// Параметры:
//   РезультатПроверки - см. РаботаСУчастникамиВызовСервера.ПроверитьДействительностьУчастников
//
// Возвращаемое значение:
//   Строка
//
Функция ТекстВопросаПроверкиДействительностиУчастников(РезультатПроверки) Экспорт
	
	ЕстьНедействительныеСотрудники = РезультатПроверки.ЕстьНедействительныеСотрудники;
	ЕстьНедействительныеПользователи = РезультатПроверки.ЕстьНедействительныеПользователи;
	ТолькоНедействительныеПользователи = ЕстьНедействительныеПользователи И Не ЕстьНедействительныеСотрудники;
	ТолькоНедействительныеСотрудники = Не ЕстьНедействительныеПользователи И ЕстьНедействительныеСотрудники;
	
	ВсеНедействительные = РезультатПроверки.ПредставленияНедействительныхУчастников;
	КоличествоНедействительных = ВсеНедействительные.Количество();
	
	КоличествоОтображаемыхУчастников = Мин(3, КоличествоНедействительных);
	ОтображаемыеУчастники = Новый Массив(); // Массив Из Строка
	Для СчетчикОтображаемыхУчастников = 1 По КоличествоОтображаемыхУчастников Цикл
		ОтображаемыеУчастники.Добавить(ВсеНедействительные[СчетчикОтображаемыхУчастников - 1]);
	КонецЦикла;
	
	ОтображаемыеУчастники = ОбщегоНазначенияДокументооборотКлиентСервер.СортироватьМассив(ОтображаемыеУчастники);
	
	ПредставленияНедействительных = СтрСоединить(ОтображаемыеУчастники, Символы.ПС);
	Если КоличествоНедействительных > КоличествоОтображаемыхУчастников Тогда
		ПредставленияНедействительных = ПредставленияНедействительных + Символы.ПС +
			СтрШаблон(НСтр("ru = 'и другие (всего %1)'"), КоличествоНедействительных);
	КонецЕсли;
	
	Если КоличествоНедействительных = 1 Тогда
		ТекстРекомендация = НСтр("ru = 'Рекомендуется его заменить.'");
	Иначе
		ТекстРекомендация = НСтр("ru = 'Рекомендуется их заменить.'");
	КонецЕсли;
	
	ТекстВопроса = "";
	
	Если ТолькоНедействительныеПользователи Тогда
		Если КоличествоНедействительных = 1 Тогда
			ТекстВопроса =
				НСтр("ru = 'Одному из выбранных сотрудников не разрешен вход в программу'") + ". "
					+ ТекстРекомендация;
		Иначе
			ТекстВопроса = НСтр(
				"ru = 'Некоторым выбранным сотрудникам не разрешен вход в программу'") + ". "
					+ ТекстРекомендация;
		КонецЕсли;
	КонецЕсли;
	
	Если ТолькоНедействительныеСотрудники Тогда
		Если КоличествоНедействительных = 1 Тогда
			ТекстВопроса =
				НСтр("ru = 'Среди выбранных сотрудников есть недействительный'") + ". " + ТекстРекомендация;
		Иначе
			ТекстВопроса =
				НСтр("ru = 'Среди выбранных сотрудников есть недействительные'") + ". " + ТекстРекомендация;
		КонецЕсли;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ТекстВопроса) Тогда
		Если КоличествоНедействительных = 1 Тогда
			ТекстВопроса =
				НСтр("ru = 'Среди выбранных сотрудников есть недействительный и без доступа в программу'") + ". "
					+ ТекстРекомендация;
		Иначе
			ТекстВопроса =
				НСтр("ru = 'Среди выбранных сотрудников есть недействительные или без доступа в программу'") + ". "
					+ ТекстРекомендация;
		КонецЕсли;
	КонецЕсли;
	
	ТекстВопроса = ТекстВопроса + Символы.ПС + ПредставленияНедействительных;
	
	Возврат ТекстВопроса;
	
КонецФункции

#КонецОбласти