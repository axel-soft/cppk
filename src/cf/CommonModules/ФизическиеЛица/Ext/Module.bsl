////////////////////////////////////////////////////////////////////////////////
// Подсистема "Физические лица"
//
////////////////////////////////////////////////////////////////////////////////

////////////////////////////////////////////////////////////////////////////////
// ПРОГРАММНЫЙ ИНТЕРФЕЙС

#Область ПрограммныйИнтерфейс

// Формирует фамилию и инициалы либо по наименованию элемента справочника ФизическиеЛица,
// либо по переданным строкам.
// Если передан Объект, то извлеченная из него строка считается совокупностью 
// Фамилия + Имя + Отчество, разделенными пробелами.
//
// Параметры
//  ОбъектИлиСтрока - строка, ссылка или объект элемента справочника ФизическиеЛица.
//  Фамилия		    - фамилия физического лица.
//  Имя		        - имя физического лица.
//  Отчество	    - отчество физического лица.
//  СуффиксЯзыка - Строка
//
// Возвращаемое значение 
//  Строка - фамилия и инициалы одной строкой. 
//  В параметрах Фамилия, Имя и Отчество записываются вычисленные части.
//
// Пример:
//  Результат = ФамилияИнициалыФизЛица("Иванов Иван Иванович"); // Результат = "Иванов И. И."
//
Функция ФамилияИнициалыФизЛица(ОбъектИлиСтрока = "", Фамилия = " ", Имя = " ", Отчество = " ", СуффиксЯзыка = "") Экспорт

	ТипОбъекта = ТипЗнч(ОбъектИлиСтрока);
	Если ТипОбъекта = Тип("Строка") Тогда
		ФИО = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(СокрЛП(ОбъектИлиСтрока), " ");
	ИначеЕсли ТипОбъекта = Тип("СправочникСсылка.ФизическиеЛица") Или ТипОбъекта = Тип("СправочникОбъект.ФизическиеЛица") Тогда
		ПредставлениеФИО = Справочники.ФизическиеЛица.ПредставлениеФизЛицаСУчетомЯзыка(ОбъектИлиСтрока, СуффиксЯзыка);
		ФИО = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(СокрЛП(ПредставлениеФИО), " ");
	Иначе
		// используем возможно переданные отдельные строки
		Возврат ?(Не ПустаяСтрока(Фамилия), 
		          Фамилия + ?(Не ПустаяСтрока(Имя), " " + Лев(Имя,1) + "." + ?(Не ПустаяСтрока(Отчество), Лев(Отчество,1) + ".", ""), ""),
		          "")
	КонецЕсли;
	
	КоличествоПодстрок = ФИО.Количество();
	Фамилия            = ?(КоличествоПодстрок > 0, ФИО[0], "");
	Имя                = ?(КоличествоПодстрок > 1, ФИО[1], "");
	Отчество           = ?(КоличествоПодстрок > 2, ФИО[2], "");
	
	Возврат ?(Не ПустаяСтрока(Фамилия), 
	          Фамилия + ?(Не ПустаяСтрока(Имя), " " + Лев(Имя,1) + "." + ?(Не ПустаяСтрока(Отчество), Лев(Отчество, 1) + ".", ""), ""),
	          "");
	
КонецФункции

// Функция склоняет переданную фразу
// Параметры:
//  ФИО (обязательный), тип строка
//   Параметр должен содержать фамилию имя отчества в именительном падеже, которые необходимо просклонять.
//
//  Падеж (обязательный), тип число
//   Падеж, в который необходимо поставить ФИО.
//   1 - Именительный
//   2 - Родительный
//   3 - Дательный
//   4 - Винительный
//   5 - Творительный
//   6 - Предложный
//
//  Результат (обязательный), тип строка
//   Переменная, в которую будет возвращен результат склонения.
//
//  Пол (необязательный), тип Перечисление.ПолФизическогоЛица
//   Пол физического лица
//
Функция Просклонять(Знач ФИО, Падеж, Результат, Пол = Неопределено) Экспорт
	
	ПодключитьВнешнююКомпоненту("ОбщийМакет.КомпонентаСклоненияФИО", "Decl");
	Компонента = Новый("AddIn.Decl.CNameDecl");
	
	Результат = "";
	
	МассивСтрок = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(ФИО, " ");
	
	// Выделим первые 3 слова, так как компонента не умеет склонять фразу большую 3х символов
	НомерНесклоняемогоСимвола = 4;
	Для Номер = 1 По Мин(МассивСтрок.Количество(), 3) Цикл
		Если Не ФизическиеЛицаКлиентСервер.ФИОНаписаноВерно(МассивСтрок[Номер-1], Истина) Тогда
			НомерНесклоняемогоСимвола = Номер;
			Прервать;
		КонецЕсли;

		Результат = Результат + ?(Номер > 1, " ", "") + МассивСтрок[Номер-1];
	КонецЦикла;
	
	Если ПустаяСтрока(Результат) Тогда
		Результат = ФИО;
		Возврат Ложь;
	КонецЕсли;
	
	Попытка
		Если Пол = Перечисления.ПолФизическогоЛица.Мужской Тогда
			Результат = Компонента.Просклонять(Результат, Падеж, 1) + " ";
			
		ИначеЕсли Пол = Перечисления.ПолФизическогоЛица.Женский Тогда
			Результат = Компонента.Просклонять(Результат, Падеж, 2) + " ";
			
		Иначе
			Результат = Компонента.Просклонять(Результат, Падеж) + " ";
			
		КонецЕсли;
		
	Исключение
		Результат = ФИО;
		Возврат Ложь;
		
	КонецПопытки;
	
	// Остальные символы добавим без склонения
	Для Номер = НомерНесклоняемогоСимвола По МассивСтрок.Количество() Цикл
		Результат = Результат + " " + МассивСтрок[Номер-1];
	КонецЦикла;
	
	Результат = СокрЛП(Результат);
	
	Возврат Истина;
	
КонецФункции

// Функция - Описание нового ФИО
// 
// Возвращаемое значение:
//  Структура:
//   * ФИО - Строка
//   * ФизическоеЛицо - СправочникСсылка.ФизическиеЛица
//   * ИменительныйПадеж - Строка
//   * РодительныйПадеж - Строка
//   * ВинительныйПадеж - Строка
//   * ДательныйПадеж - Строка
//   * ТворительныйПадеж - Строка
//   * ПредложныйПадеж - Строка
//   * ФИОВОбъекте - Строка
//
Функция НовоеОписаниеПриСменеФИО() Экспорт
	
	Описание = Новый Структура;
	Описание.Вставить("ФИО", "");
	Описание.Вставить("ФИОВОбъекте", "");
	Описание.Вставить("ФизическоеЛицо", Справочники.ФизическиеЛица.ПустаяСсылка());
	Описание.Вставить("ИменительныйПадеж", "");
	Описание.Вставить("РодительныйПадеж", "");
	Описание.Вставить("ВинительныйПадеж", "");
	Описание.Вставить("ДательныйПадеж", "");
	Описание.Вставить("ТворительныйПадеж", "");
	Описание.Вставить("ПредложныйПадеж", "");
	
	Возврат Описание;
	
КонецФункции

// Обновляет наименования и склонения физ. лиц, сотрудников и пользователей по актуальному ФИО физ. лица
//
// Параметры:
//  ФизЛицо - СправочникСсылка.ФизическиеЛица
//
Процедура ОбновитьНаименованиеИСклоненияФизЛица(ФизЛицо) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ТекущаяДата = ТекущаяДатаСеанса();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ФИОФизическихЛицСрезПоследних.ФизическоеЛицо КАК ФизическоеЛицо,
		|	ФИОФизическихЛицСрезПоследних.ФИО КАК ФИО,
		|	ФИОФизическихЛицСрезПоследних.ИменительныйПадеж КАК ИменительныйПадеж,
		|	ФИОФизическихЛицСрезПоследних.РодительныйПадеж КАК РодительныйПадеж,
		|	ФИОФизическихЛицСрезПоследних.ДательныйПадеж КАК ДательныйПадеж,
		|	ФИОФизическихЛицСрезПоследних.ВинительныйПадеж КАК ВинительныйПадеж,
		|	ФИОФизическихЛицСрезПоследних.ТворительныйПадеж КАК ТворительныйПадеж,
		|	ФИОФизическихЛицСрезПоследних.ПредложныйПадеж КАК ПредложныйПадеж,
		|	ФИОФизическихЛицСрезПоследних.ФИОЯзык1 КАК ФИОЯзык1,
		|	ФИОФизическихЛицСрезПоследних.ФИОЯзык2 КАК ФИОЯзык2
		|ПОМЕСТИТЬ ФИОФизическихЛицСрезПоследних
		|ИЗ
		|	РегистрСведений.ФИОФизическихЛиц.СрезПоследних(&ТекущаяДата, ФизическоеЛицо = &ФизЛицо) КАК
		|		ФИОФизическихЛицСрезПоследних
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ФИОФизическихЛицСрезПоследних.ФизическоеЛицо КАК ФизическоеЛицо,
		|	ФИОФизическихЛицСрезПоследних.ФИО КАК ФИО,
		|	ФИОФизическихЛицСрезПоследних.ИменительныйПадеж КАК ИменительныйПадеж,
		|	ФИОФизическихЛицСрезПоследних.РодительныйПадеж КАК РодительныйПадеж,
		|	ФИОФизическихЛицСрезПоследних.ДательныйПадеж КАК ДательныйПадеж,
		|	ФИОФизическихЛицСрезПоследних.ВинительныйПадеж КАК ВинительныйПадеж,
		|	ФИОФизическихЛицСрезПоследних.ТворительныйПадеж КАК ТворительныйПадеж,
		|	ФИОФизическихЛицСрезПоследних.ПредложныйПадеж КАК ПредложныйПадеж,
		|	ФизическиеЛица.Наименование КАК ФИОВОбъекте,
		|	ФИОФизическихЛицСрезПоследних.ФИО <> ФизическиеЛица.Наименование
		|	ИЛИ ФИОФизическихЛицСрезПоследних.ФИОЯзык1 <> ФизическиеЛица.НаименованиеЯзык1
		|	ИЛИ ФИОФизическихЛицСрезПоследних.ФИОЯзык2 <> ФизическиеЛица.НаименованиеЯзык2 КАК ИзменилосьФИО,
		|	ЕСТЬNULL(ФИОФизическихЛицСрезПоследних.ИменительныйПадеж <> (ВЫРАЗИТЬ(СклоненияПредставленийОбъектов.ИменительныйПадеж
		|		КАК СТРОКА(100)))
		|	ИЛИ ФИОФизическихЛицСрезПоследних.РодительныйПадеж <> (ВЫРАЗИТЬ(СклоненияПредставленийОбъектов.РодительныйПадеж КАК
		|		СТРОКА(100)))
		|	ИЛИ ФИОФизическихЛицСрезПоследних.ДательныйПадеж <> (ВЫРАЗИТЬ(СклоненияПредставленийОбъектов.ДательныйПадеж КАК
		|		СТРОКА(100)))
		|	ИЛИ ФИОФизическихЛицСрезПоследних.ВинительныйПадеж <> (ВЫРАЗИТЬ(СклоненияПредставленийОбъектов.ВинительныйПадеж КАК
		|		СТРОКА(100)))
		|	ИЛИ ФИОФизическихЛицСрезПоследних.ТворительныйПадеж <> (ВЫРАЗИТЬ(СклоненияПредставленийОбъектов.ТворительныйПадеж КАК
		|		СТРОКА(100)))
		|	ИЛИ ФИОФизическихЛицСрезПоследних.ПредложныйПадеж <> (ВЫРАЗИТЬ(СклоненияПредставленийОбъектов.ПредложныйПадеж КАК
		|		СТРОКА(100))), ИСТИНА) КАК ИзменилосьСклонение,
		|	ФИОФизическихЛицСрезПоследних.ФИОЯзык1 КАК ФИОЯзык1,
		|	ФИОФизическихЛицСрезПоследних.ФИОЯзык2 КАК ФИОЯзык2
		|ИЗ
		|	ФИОФизическихЛицСрезПоследних КАК ФИОФизическихЛицСрезПоследних
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ФизическиеЛица КАК ФизическиеЛица
		|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СклоненияПредставленийОбъектов КАК СклоненияПредставленийОбъектов
		|			ПО ФизическиеЛица.Ссылка = СклоненияПредставленийОбъектов.Объект
		|		ПО ФИОФизическихЛицСрезПоследних.ФизическоеЛицо = ФизическиеЛица.Ссылка";
	
	Запрос.УстановитьПараметр("ТекущаяДата", ТекущаяДата);
	Запрос.УстановитьПараметр("ФизЛицо", ФизЛицо);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ТаблицаАктуальногоФИОИСклонений = РезультатЗапроса.Выгрузить();
	Если ТаблицаАктуальногоФИОИСклонений.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	АктуальныеФИОИСклонения = ТаблицаАктуальногоФИОИСклонений[0];
	
	НовоеФИО = АктуальныеФИОИСклонения.ФИО;
	ИзменилосьФИО = АктуальныеФИОИСклонения.ИзменилосьФИО;
	ИзменилосьСклонение = ИзменилосьФИО Или АктуальныеФИОИСклонения.ИзменилосьСклонение;
	Если ИзменилосьФИО Или ИзменилосьСклонение Тогда
		
		НачатьТранзакцию();
		Попытка
			ТекущееФизЛицо = АктуальныеФИОИСклонения.ФизическоеЛицо;
			
			Блокировка = Новый БлокировкаДанных;
			
			Если ИзменилосьФИО Тогда
				
				ПользовательФизЛица = ПользователиДокументооборот.ПользовательФизЛица(ФизЛицо);
				
				Если ЗначениеЗаполнено(ПользовательФизЛица) Тогда
					ЭлементБлокировки = Блокировка.Добавить(Метаданные.Справочники.Пользователи.ПолноеИмя());
					ЭлементБлокировки.УстановитьЗначение("Ссылка", ПользовательФизЛица);
				КонецЕсли;
				
				ЭлементБлокировки = Блокировка.Добавить(Метаданные.Справочники.ФизическиеЛица.ПолноеИмя());
				ЭлементБлокировки.УстановитьЗначение("Ссылка", ТекущееФизЛицо);
				
				ЭлементБлокировки = Блокировка.Добавить(Метаданные.РегистрыСведений.ФИОФизическихЛиц.ПолноеИмя());
				ЭлементБлокировки.УстановитьЗначение("ФизическоеЛицо", ТекущееФизЛицо); 
				
			КонецЕсли;
			
			Если ИзменилосьСклонение Тогда
				
				ХешПредставления = ОбщегоНазначения.КонтрольнаяСуммаСтрокой(НовоеФИО);
				ЭлементБлокировки = Блокировка.Добавить(Метаданные.РегистрыСведений.СклоненияПредставленийОбъектов.ПолноеИмя());
				ЭлементБлокировки.УстановитьЗначение("Объект", ТекущееФизЛицо);
				ЭлементБлокировки.УстановитьЗначение("ХешПредставления", ХешПредставления);
				
			КонецЕсли;
			
			Блокировка.Заблокировать();
			
			Если ИзменилосьФИО Тогда
				
				// Обновляем объект физ. лица. С ним обновляются и сотрудники
				ОбъектФизЛица = ТекущееФизЛицо.ПолучитьОбъект();
				ОбъектФизЛица.Наименование = НовоеФИО;
				ОбъектФизЛица.НаименованиеЯзык1 = АктуальныеФИОИСклонения.ФИОЯзык1;
				ОбъектФизЛица.НаименованиеЯзык2 = АктуальныеФИОИСклонения.ФИОЯзык2;
				ОбъектФизЛица.ДополнительныеСвойства.Вставить("НеСоздаватьЗаписьОбИстории", Истина);
				ОбъектФизЛица.Записать();
				
				// Обновляем пользователя
				Если ЗначениеЗаполнено(ПользовательФизЛица) Тогда
					ПользовательОбъект = ПользовательФизЛица.ПолучитьОбъект();
					ПользовательОбъект.Наименование = НовоеФИО;
					ПользовательОбъект.Записать();
				КонецЕсли;
				
			КонецЕсли;
			
			Если ИзменилосьСклонение Тогда
				
				// Обновляем склонения в РС СклоненияПредставленийОбъектов
				НаборЗаписейСклонения = РегистрыСведений.СклоненияПредставленийОбъектов.СоздатьНаборЗаписей();
				НаборЗаписейСклонения.Отбор.Объект.Установить(ФизЛицо);
				НаборЗаписейСклонения.Очистить();
				НоваяСтрока = НаборЗаписейСклонения.Добавить();
				НоваяСтрока.Объект = ФизЛицо;
				НоваяСтрока.ХешПредставления = ХешПредставления;
				ЗаполнитьЗначенияСвойств(НоваяСтрока, АктуальныеФИОИСклонения); 
				НаборЗаписейСклонения.ДополнительныеСвойства.Вставить("НеОбновлятьИсторию", Истина);
				НаборЗаписейСклонения.Записать();
				
			КонецЕсли;
			
			ЗафиксироватьТранзакцию();
			
		Исключение
			
			ОтменитьТранзакцию();
			ТекстСообщения = НСтр("ru='К сожалению, произошла ошибка'") + Символы.ПС
				+ ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
			
		КонецПопытки;
		
	КонецЕсли;
	
КонецПроцедуры

// Возвращает Истина, если это физ. лицо связано с сотрудникома
//
// Параметры:
//  ФизЛицо - СправочникСсылка.Сотрудники
// 
// Возвращаемое значение:
//  Булево
//
Функция ЭтоФизЛицоСотрудника(ФизЛицо) Экспорт
	
	Возврат Сотрудники.ВсеСотрудникиФизЛица(ФизЛицо).Количество() > 0;
	
КонецФункции

// Возвращает склонения ФИО физ. лица
//
// Параметры:
//  ФизЛицо - СправочникСсылка.Сотрудники
// 
// Возвращаемое значение:
//  Структура:
// * Именительный - ОпределяемыйТип.ФИО
// * Родительный - ОпределяемыйТип.ФИО
// * Дательный - ОпределяемыйТип.ФИО
// * Винительный - ОпределяемыйТип.ФИО
// * Творительный - ОпределяемыйТип.ФИО
// * Предложный - ОпределяемыйТип.ФИО
//
Функция СклоненияАктуальногоФИОФизЛица(ФизЛицо) Экспорт
	
	СтруктураПадежей = Новый Структура;
	СтруктураПадежей.Вставить("Именительный", "");
	СтруктураПадежей.Вставить("Родительный", "");
	СтруктураПадежей.Вставить("Дательный", "");
	СтруктураПадежей.Вставить("Винительный", "");
	СтруктураПадежей.Вставить("Творительный", "");
	СтруктураПадежей.Вставить("Предложный", "");
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ФИОФизическихЛицСрезПоследних.ИменительныйПадеж КАК Именительный,
		|	ФИОФизическихЛицСрезПоследних.ДательныйПадеж КАК Дательный,
		|	ФИОФизическихЛицСрезПоследних.ВинительныйПадеж КАК Винительный,
		|	ФИОФизическихЛицСрезПоследних.ПредложныйПадеж КАК Предложный,
		|	ФИОФизическихЛицСрезПоследних.РодительныйПадеж КАК Родительный,
		|	ФИОФизическихЛицСрезПоследних.ТворительныйПадеж КАК Творительный
		|ИЗ
		|	РегистрСведений.ФИОФизическихЛиц.СрезПоследних(, ФизическоеЛицо = &ФизЛицо) КАК ФИОФизическихЛицСрезПоследних";
	
	Запрос.УстановитьПараметр("ФизЛицо", ФизЛицо);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Если Выборка.Следующий() Тогда
		
		ЗаполнитьЗначенияСвойств(СтруктураПадежей, Выборка);
		
	КонецЕсли;
	
	Возврат СтруктураПадежей;
	
КонецФункции

// Функция - Фамилия инициалы физ лица на дату
//
// Параметры:
//  ФизическоеЛицо - СправочникСсылка.ФизическиеЛица
//  Дата - Дата
// 
// Возвращаемое значение:
//  Строка
//
Функция ФамилияИнициалыФизЛицаНаДату(ФизическоеЛицо, Дата) Экспорт
	
	Возврат ФамилияИнициалыФизЛица(РегистрыСведений.ФИОФизическихЛиц.ФИОФизЛицаНаДату(ФизическоеЛицо, Дата));
	
КонецФункции

// Возвращает 1, если мужской пол, 2 - если женский, иначе - Неопределено
//
// Параметры:
//  ПолФизЛица - ПеречислениеСсылка.ПолФизическогоЛица
// 
// Возвращаемое значение:
//  Число, Неопределено
//
Функция ПолЦифрой(ПолФизЛица) Экспорт
	
	ПолЦифрой = 0;
	Если ПолФизЛица = Перечисления.ПолФизическогоЛица.Мужской Тогда
		ПолЦифрой = 1;
	ИначеЕсли ПолФизЛица = Перечисления.ПолФизическогоЛица.Женский Тогда
		ПолЦифрой = 2;
	ИначеЕсли Не ЗначениеЗаполнено(ПолФизЛица) Тогда
		ПолЦифрой = Неопределено;
	Иначе
		ВызватьИсключение НСтр("ru='Некорректное значение пола'");
	КонецЕсли;
	
	Возврат ПолЦифрой;
		
КонецФункции

// Возвращает Ложь, если можно менять данные ФИО физ. лица, иначе - Истина
//
// Параметры:
//  ФизЛицо -СправочникСсылка.ФизическиеЛица
// 
// Возвращаемое значение:
//  Булево
//
Функция НельзяИзменитьФИОФизЛицу(ФизЛицо) Экспорт
	
	ПерсональныеДанныеУничтожены = РегистрыСведений.УничтоженныеПерсональныеДанные.
		ЕстьСубъектыСУничтоженнымиПерсональнымиДанными(ФизЛицо);
	
	Если ПерсональныеДанныеУничтожены Тогда
		
		Возврат Истина;
		
	КонецЕсли;
	
	Если Пользователи.ЭтоПолноправныйПользователь() Тогда
		
		Возврат Ложь;
		
	КонецЕсли;
	
	ЭтоФизЛицоСотрудника = ЭтоФизЛицоСотрудника(ФизЛицо);
	
	Возврат ЭтоФизЛицоСотрудника;
	
КонецФункции

#КонецОбласти

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЙ ПРОГРАММНЫЙ ИНТЕРФЕЙС

// См. описание этой же процедуры в модуле СтандартныеПодсистемыСервер.
Процедура ПриДобавленииОбработчиковСлужебныхСобытий(КлиентскиеОбработчики, СерверныеОбработчики) Экспорт
	
	// СЕРВЕРНЫЕ ОБРАБОТЧИКИ.
	
	СерверныеОбработчики["СтандартныеПодсистемы.ОбновлениеВерсииИБ\ПриДобавленииОбработчиковОбновления"].Добавить(
		"ФизическиеЛица");
	
	СерверныеОбработчики["СтандартныеПодсистемы.БазоваяФункциональность\ПриДобавленииИсключенийПоискаСсылок"].Добавить(
		"ФизическиеЛица");
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

////////////////////////////////////////////////////////////////////////////////
// Обработчики служебных событий подсистем БСП

// Заполняет массив списком имен объектов метаданных, данные которых могут содержать ссылки на различные объекты метаданных,
// но при этом эти ссылки не должны учитываться в бизнес-логике приложения.
//
// Параметры:
//  Массив       - массив строк, например, "РегистрСведений.ВерсииОбъектов".
//
Процедура ПриДобавленииИсключенийПоискаСсылок(Массив) Экспорт
	
	Массив.Добавить(Метаданные.РегистрыСведений.ДокументыФизическихЛиц.ПолноеИмя());
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Обновление информационной базы

// Добавляет процедуры-обработчики обновления, необходимые данной подсистеме.
//
// Параметры:
//  Обработчики - ТаблицаЗначений - см. описание функции НоваяТаблицаОбработчиковОбновления
//                                  общего модуля ОбновлениеИнформационнойБазы.
// 
Процедура ПриДобавленииОбработчиковОбновления(Обработчики) Экспорт
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Версия = "1.0.6.8";
	Обработчик.Процедура = "ФизическиеЛица.ПреобразоватьУдостоверенияЛичностиВДокументы";
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Версия = "1.1.1.5";
	Обработчик.Процедура = "ФизическиеЛица.ЗаменитьВидыДокументовНаПредопределенные";
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Версия = "2.1.3.31";
	Обработчик.Процедура = "ФизическиеЛица.УстановитьРеквизитДопУпорядочиванияВидовДокументов";
	
КонецПроцедуры

// Заполняет измерение ВидДокумента по ресурсу УдалитьВидДокумента,
// а также заполняет реквизит ЯвляетсяДокументомУдостоверяющимЛичность для существующих записей
//
Процедура ПреобразоватьУдостоверенияЛичностиВДокументы() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДокументыФизическихЛиц.Физлицо КАК Физлицо
	|ПОМЕСТИТЬ ВТ_Физлица
	|ИЗ
	|	РегистрСведений.ДокументыФизическихЛиц КАК ДокументыФизическихЛиц
	|ГДЕ
	|	(НЕ ДокументыФизическихЛиц.УдалитьВидДокумента = ЗНАЧЕНИЕ(Справочник.ВидыДокументовФизическихЛиц.ПустаяСсылка))
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Физлицо
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДокументыФизическихЛиц.Период КАК Период,
	|	ДокументыФизическихЛиц.Физлицо КАК Физлицо,
	|	ДокументыФизическихЛиц.УдалитьВидДокумента КАК ВидДокумента,
	|	ДокументыФизическихЛиц.Серия,
	|	ДокументыФизическихЛиц.Номер,
	|	ДокументыФизическихЛиц.ДатаВыдачи,
	|	ДокументыФизическихЛиц.СрокДействия,
	|	ДокументыФизическихЛиц.КемВыдан,
	|	ДокументыФизическихЛиц.КодПодразделения,
	|	ВЫБОР
	|		КОГДА ДокументыФизическихЛиц.УдалитьВидДокумента = ЗНАЧЕНИЕ(Справочник.ВидыДокументовФизическихЛиц.ПустаяСсылка)
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК ЯвляетсяДокументомУдостоверяющимЛичность,
	|	ДокументыФизическихЛиц.Представление
	|ИЗ
	|	РегистрСведений.ДокументыФизическихЛиц КАК ДокументыФизическихЛиц
	|ГДЕ
	|	ДокументыФизическихЛиц.Физлицо В
	|			(ВЫБРАТЬ
	|				Физлица.Физлицо
	|			ИЗ
	|				ВТ_Физлица КАК Физлица)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Физлицо,
	|	Период";
	Выборка = Запрос.Выполнить().Выбрать();
	
	НаборЗаписей = РегистрыСведений.ДокументыФизическихЛиц.СоздатьНаборЗаписей();
	НаборЗаписей.ОбменДанными.Загрузка = Истина;
	
	ТекстСерия				= НСтр("ru = ', серия: %1'");
	ТекстНомер				= НСтр("ru = ', № %1'");
	ТекстДатаВыдачи			= НСтр("ru = ', выдан: %1 года'");
	ТекстСрокДействия		= НСтр("ru = ', действует до: %1 года'");
	ТекстКодПодразделения	= НСтр("ru = ', № подр. %1'");
	
	Пока Выборка.СледующийПоЗначениюПоля("Физлицо") Цикл
		НаборЗаписей.Отбор.Физлицо.Установить(Выборка.Физлицо);
		
		Пока Выборка.Следующий() Цикл
			Запись = НаборЗаписей.Добавить();
			ЗаполнитьЗначенияСвойств(Запись, Выборка);
			
			Если ПустаяСтрока(Запись.Представление) И Не Запись.ВидДокумента.Пустая() Тогда
				Запись.Представление = ""
				+ Запись.ВидДокумента
				+ ?(ЗначениеЗаполнено(Запись.Серия), СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСерия, Запись.Серия), "")
				+ ?(ЗначениеЗаполнено(Запись.Номер), СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстНомер, Запись.Номер), "")
				+ ?(ЗначениеЗаполнено(Запись.ДатаВыдачи), СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстДатаВыдачи, Формат(Запись.ДатаВыдачи,"ДФ='дд ММММ гггг'")), "")
				+ ?(ЗначениеЗаполнено(Запись.СрокДействия), СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСрокДействия, Формат(Запись.СрокДействия,"ДФ='дд ММММ гггг'")), "")
				+ ?(ЗначениеЗаполнено(Запись.КемВыдан), ", " + Запись.КемВыдан, "")
				+ ?(ЗначениеЗаполнено(Запись.КодПодразделения) И Запись.ВидДокумента = Справочники.ВидыДокументовФизическихЛиц.ПаспортРФ, СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстКодПодразделения, Запись.КодПодразделения), "");
			КонецЕсли;
		КонецЦикла;
		
		ОбновлениеИнформационнойБазы.ЗаписатьДанные(НаборЗаписей);
		НаборЗаписей.Очистить();
	КонецЦикла;
	
КонецПроцедуры

// Заменяет ссылку на виды документов физических лиц в регистре сведений ДокументыФизическихЛиц
// на добавленные предопределенные документы
//
Процедура ЗаменитьВидыДокументовНаПредопределенные() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДокументыФизическихЛиц.Физлицо КАК Физлицо
	|ПОМЕСТИТЬ ВТФизлица
	|ИЗ
	|	РегистрСведений.ДокументыФизическихЛиц КАК ДокументыФизическихЛиц
	|ГДЕ
	|	ДокументыФизическихЛиц.ВидДокумента.ПометкаУдаления
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Физлицо
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДокументыФизическихЛиц.Период КАК Период,
	|	ДокументыФизическихЛиц.Физлицо КАК Физлицо,
	|	ВЫБОР
	|		КОГДА ДокументыФизическихЛиц.ВидДокумента.Наименование = ""Военный билет""
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.ВидыДокументовФизическихЛиц.ВоенныйБилет)
	|		КОГДА ДокументыФизическихЛиц.ВидДокумента.Наименование = ""Военный билет офицера запаса""
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.ВидыДокументовФизическихЛиц.ВоенныйБилетОфицераЗапаса)
	|		КОГДА ДокументыФизическихЛиц.ВидДокумента.Наименование = ""Дипломатический паспорт гражданина РФ""
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.ВидыДокументовФизическихЛиц.ДипломатическийПаспорт)
	|		КОГДА ДокументыФизическихЛиц.ВидДокумента.Наименование = ""Загранпаспорт гражданина РФ""
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.ВидыДокументовФизическихЛиц.ЗагранпаспортРФ)
	|		КОГДА ДокументыФизическихЛиц.ВидДокумента.Наименование = ""Загранпаспорт гражданина СССР""
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.ВидыДокументовФизическихЛиц.ЗагранпаспортСССР)
	|		КОГДА ДокументыФизическихЛиц.ВидДокумента.Наименование = ""Паспорт гражданина РФ""
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.ВидыДокументовФизическихЛиц.ПаспортРФ)
	|		КОГДА ДокументыФизическихЛиц.ВидДокумента.Наименование = ""Паспорт гражданина СССР""
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.ВидыДокументовФизическихЛиц.ПаспортСССР)
	|		КОГДА ДокументыФизическихЛиц.ВидДокумента.Наименование = ""Паспорт Минморфлота""
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.ВидыДокументовФизическихЛиц.ПаспортМинморфлота)
	|		КОГДА ДокументыФизическихЛиц.ВидДокумента.Наименование = ""Паспорт моряка""
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.ВидыДокументовФизическихЛиц.ПаспортМоряка)
	|		КОГДА ДокументыФизическихЛиц.ВидДокумента.Наименование = ""Свидетельство о рождении""
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.ВидыДокументовФизическихЛиц.СвидетельствоОРождении)
	|		КОГДА ДокументыФизическихЛиц.ВидДокумента.Наименование = ""Удостоверение личности офицера""
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.ВидыДокументовФизическихЛиц.УдостоверениеОфицера)
	|		ИНАЧЕ ДокументыФизическихЛиц.ВидДокумента
	|	КОНЕЦ КАК ВидДокумента,
	|	ДокументыФизическихЛиц.Серия,
	|	ДокументыФизическихЛиц.Номер,
	|	ДокументыФизическихЛиц.ДатаВыдачи,
	|	ДокументыФизическихЛиц.СрокДействия,
	|	ДокументыФизическихЛиц.КемВыдан,
	|	ДокументыФизическихЛиц.КодПодразделения,
	|	ДокументыФизическихЛиц.ЯвляетсяДокументомУдостоверяющимЛичность,
	|	ДокументыФизическихЛиц.Представление,
	|	ДокументыФизическихЛиц.УдалитьВидДокумента
	|ИЗ
	|	РегистрСведений.ДокументыФизическихЛиц КАК ДокументыФизическихЛиц
	|ГДЕ
	|	ДокументыФизическихЛиц.Физлицо В
	|			(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|				Физлица.Физлицо
	|			ИЗ
	|				ВТФизлица КАК Физлица)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Физлицо,
	|	Период";
	Выборка = Запрос.Выполнить().Выбрать();
	
	НаборЗаписей = РегистрыСведений.ДокументыФизическихЛиц.СоздатьНаборЗаписей();
	
	Пока Выборка.СледующийПоЗначениюПоля("Физлицо") Цикл
		НаборЗаписей.Отбор.Физлицо.Установить(Выборка.Физлицо);
		
		Пока Выборка.Следующий() Цикл
			ЗаполнитьЗначенияСвойств(НаборЗаписей.Добавить(), Выборка);
		КонецЦикла;
		
		ОбновлениеИнформационнойБазы.ЗаписатьДанные(НаборЗаписей);
		НаборЗаписей.Очистить();
	КонецЦикла;
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВидыДокументовФизическихЛиц.Ссылка
	|ИЗ
	|	Справочник.ВидыДокументовФизическихЛиц КАК ВидыДокументовФизическихЛиц
	|ГДЕ
	|	(НЕ ВидыДокументовФизическихЛиц.Предопределенный)
	|	И (ВидыДокументовФизическихЛиц.Наименование = ""Военный билет""
	|			ИЛИ ВидыДокументовФизическихЛиц.Наименование = ""Военный билет офицера запаса""
	|			ИЛИ ВидыДокументовФизическихЛиц.Наименование = ""Дипломатический паспорт гражданина РФ""
	|			ИЛИ ВидыДокументовФизическихЛиц.Наименование = ""Загранпаспорт гражданина РФ""
	|			ИЛИ ВидыДокументовФизическихЛиц.Наименование = ""Загранпаспорт гражданина СССР""
	|			ИЛИ ВидыДокументовФизическихЛиц.Наименование = ""Паспорт гражданина РФ""
	|			ИЛИ ВидыДокументовФизическихЛиц.Наименование = ""Паспорт гражданина СССР""
	|			ИЛИ ВидыДокументовФизическихЛиц.Наименование = ""Паспорт Минморфлота""
	|			ИЛИ ВидыДокументовФизическихЛиц.Наименование = ""Паспорт моряка""
	|			ИЛИ ВидыДокументовФизическихЛиц.Наименование = ""Свидетельство о рождении""
	|			ИЛИ ВидыДокументовФизическихЛиц.Наименование = ""Удостоверение личности офицера"")";
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		ОбновлениеИнформационнойБазы.УдалитьДанные(Выборка.Ссылка.ПолучитьОбъект());
	КонецЦикла;
	
КонецПроцедуры

// Устанавливает РеквизитДопУпорядочивания для элементов справочника ВидыДокументовФизическихЛиц
//
Процедура УстановитьРеквизитДопУпорядочиванияВидовДокументов() Экспорт
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("ПаспортРФ",				Справочники.ВидыДокументовФизическихЛиц.ПаспортРФ);
	Запрос.УстановитьПараметр("СвидетельствоОРождении", Справочники.ВидыДокументовФизическихЛиц.СвидетельствоОРождении);
	Запрос.УстановитьПараметр("УдостоверениеОфицера",	Справочники.ВидыДокументовФизическихЛиц.УдостоверениеОфицера);
	Запрос.УстановитьПараметр("ПаспортМоряка",			Справочники.ВидыДокументовФизическихЛиц.ПаспортМоряка);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВидыДокументовФизическихЛиц.Ссылка КАК Ссылка,
	|	ВЫБОР
	|		КОГДА ВидыДокументовФизическихЛиц.Ссылка = &ПаспортРФ
	|			ТОГДА 1
	|		КОГДА ВидыДокументовФизическихЛиц.Ссылка = &СвидетельствоОРождении
	|			ТОГДА 2
	|		КОГДА ВидыДокументовФизическихЛиц.Ссылка = &УдостоверениеОфицера
	|			ТОГДА 3
	|		КОГДА ВидыДокументовФизическихЛиц.Ссылка = &ПаспортМоряка
	|			ТОГДА 4
	|		ИНАЧЕ 5
	|	КОНЕЦ КАК Порядок
	|ИЗ
	|	Справочник.ВидыДокументовФизическихЛиц КАК ВидыДокументовФизическихЛиц
	|
	|УПОРЯДОЧИТЬ ПО
	|	Порядок,
	|	ВидыДокументовФизическихЛиц.Наименование";
	
	Порядок = 1;
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		Объект = Выборка.Ссылка.ПолучитьОбъект();
		ЗаблокироватьДанныеДляРедактирования(Объект.Ссылка);
		Объект.РеквизитДопУпорядочивания = Порядок;
		ОбновлениеИнформационнойБазы.ЗаписатьДанные(Объект);
		
		Порядок = Порядок + 1;
		
	КонецЦикла;
	
КонецПроцедуры
