////////////////////////////////////////////////////////////////////////////////
// Работа с вопросами (сервер).
//
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Определяет, какой объект выступает предметом рассмотрения для задачи.
//
// Параметры:
//  Задача - ЗадачаСсылка.ЗадачаИсполнителя, ДокументСсылка.Задача, ДокументСсылка.ДействиеЗадачи.
// 
// Возвращаемое значение:
//  ЗадачаСсылка.ЗадачаИсполнителя, ДокументСсылка.Задача, ДокументСсылка.ДействиеЗадачи - Предмет рассмотрения.
//
Функция ПредметРассмотрения(Задача) Экспорт
	
	ПредметРассмотрения = Задача;
	Если ТипЗнч(Задача) = Тип("ДокументСсылка.ДействиеЗадачи") Тогда
		
		ИсточникЗадачи = РаботаСЗадачами.ИсточникДействияЗадачи(Задача);
		Если ЗначениеЗаполнено(ИсточникЗадачи) И ТипЗнч(ИсточникЗадачи) = Тип("ЗадачаСсылка.ЗадачаИсполнителя") Тогда
			ПредметРассмотрения = ИсточникЗадачи;
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат ПредметРассмотрения;
	
КонецФункции

// Собирает данные предмета рассмотрения.
//
// Параметры:
//  ПредметРассмотрения - ЗадачаСсылка.ЗадачаИсполнителя, ДокументСсылка.Задача, ДокументСсылка.ДействиеЗадачи.
// 
// Возвращаемое значение:
//  Структура - Данные предмета рассмотрения.
//   * БизнесПроцесс - БизнесПроцессСсылка.
//   * Выполнен - Булево.
//   * ГлавнаяЗадача - ЗадачаСсылка.ЗадачаИсполнителя.
//   * Дата - Дата.
//   * ИнициаторПроцесса - СправочникСсылка.ПолныеРоли, СправочникСсылка.Пользователи.
//   * Исполнитель - ОпределяемыйТип.УчастникЗадач.
//   * Проект - СправочникСсылка.Проекты.
//   * ПроектнаяЗадача - СправочникСсылка.ПроектныеЗадачи.
//   * Срок - Дата.
//
Функция ДанныеПредметаРассмотрения(ПредметРассмотрения) Экспорт
	
	ДанныеПредметаРассмотрения = Новый Структура(
		"БизнесПроцесс, Выполнен, ГлавнаяЗадача, Дата, ИнициаторПроцесса, Исполнитель, Проект, ПроектнаяЗадача, Срок");
	ДанныеПредметаРассмотрения.БизнесПроцесс = Неопределено;
	ДанныеПредметаРассмотрения.Выполнен = Ложь;
	ДанныеПредметаРассмотрения.ГлавнаяЗадача = Задачи.ЗадачаИсполнителя.ПустаяСсылка();
	ДанныеПредметаРассмотрения.Дата = Дата(1, 1, 1);
	ДанныеПредметаРассмотрения.ИнициаторПроцесса = Неопределено;
	ДанныеПредметаРассмотрения.Исполнитель = Неопределено;
	ДанныеПредметаРассмотрения.Проект = Справочники.Проекты.ПустаяСсылка();
	ДанныеПредметаРассмотрения.ПроектнаяЗадача = Справочники.ПроектныеЗадачи.ПустаяСсылка();
	ДанныеПредметаРассмотрения.Срок = Дата(1, 1, 1);
	
	Если Не ЗначениеЗаполнено(ПредметРассмотрения) Тогда
		Возврат ДанныеПредметаРассмотрения;
	КонецЕсли;
	
	Если ТипЗнч(ПредметРассмотрения) = Тип("ЗадачаСсылка.ЗадачаИсполнителя") Тогда
		
		РеквизитыПредметаРассмотрения = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
			ПредметРассмотрения,
			"СрокИсполнения, Выполнена, Автор, БизнесПроцесс, Проект, ПроектнаяЗадача, Дата, ТекущийИсполнитель");
		
		ДанныеПредметаРассмотрения.БизнесПроцесс = РеквизитыПредметаРассмотрения.БизнесПроцесс;
		ДанныеПредметаРассмотрения.Выполнен = РеквизитыПредметаРассмотрения.Выполнена;
		ДанныеПредметаРассмотрения.ГлавнаяЗадача = ПредметРассмотрения;
		ДанныеПредметаРассмотрения.Дата = РеквизитыПредметаРассмотрения.Дата;
		ДанныеПредметаРассмотрения.ИнициаторПроцесса = РеквизитыПредметаРассмотрения.Автор;
		ДанныеПредметаРассмотрения.Исполнитель = РеквизитыПредметаРассмотрения.ТекущийИсполнитель;
		ДанныеПредметаРассмотрения.Проект = РеквизитыПредметаРассмотрения.Проект;
		ДанныеПредметаРассмотрения.ПроектнаяЗадача = РеквизитыПредметаРассмотрения.ПроектнаяЗадача;
		ДанныеПредметаРассмотрения.Срок = РеквизитыПредметаРассмотрения.СрокИсполнения;
		
	ИначеЕсли ТипЗнч(ПредметРассмотрения) = Тип("ДокументСсылка.Задача") Тогда
		
		РеквизитыПредметаРассмотрения = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
			ПредметРассмотрения,
			"Автор, Дата, Проект, СостояниеЗадачи, Срок");
		ВидСостоянияЗадач = РаботаСЗадачамиПовтИсп.ВидСостоянияЗадач(РеквизитыПредметаРассмотрения.СостояниеЗадачи);
		
		ДанныеПредметаРассмотрения.Выполнен = Перечисления.ВидыСостоянийЗадач.ЭтоВыполненнаяЗадача(ВидСостоянияЗадач);
		ДанныеПредметаРассмотрения.Дата = РеквизитыПредметаРассмотрения.Дата;
		ДанныеПредметаРассмотрения.ИнициаторПроцесса = РеквизитыПредметаРассмотрения.Автор;
		ДанныеПредметаРассмотрения.Проект = РеквизитыПредметаРассмотрения.Проект;
		ДанныеПредметаРассмотрения.Срок = РеквизитыПредметаРассмотрения.Срок;
		
	ИначеЕсли ТипЗнч(ПредметРассмотрения) = Тип("ДокументСсылка.ДействиеЗадачи") Тогда
		
		ИсточникЗадачи = РаботаСЗадачами.ИсточникДействияЗадачи(ПредметРассмотрения);
		Если ЗначениеЗаполнено(ИсточникЗадачи) И ТипЗнч(ИсточникЗадачи) = Тип("ЗадачаСсылка.ЗадачаИсполнителя") Тогда
			
			РеквизитыПредметаРассмотрения = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
				ПредметРассмотрения,
				"СрокИсполнения, Выполнена, Автор, БизнесПроцесс, Проект, ПроектнаяЗадача, Дата, ТекущийИсполнитель");
			
			ДанныеПредметаРассмотрения.БизнесПроцесс = РеквизитыПредметаРассмотрения.БизнесПроцесс;
			ДанныеПредметаРассмотрения.Выполнен = РеквизитыПредметаРассмотрения.Выполнена;
			ДанныеПредметаРассмотрения.ГлавнаяЗадача = ИсточникЗадачи;
			ДанныеПредметаРассмотрения.Дата = РеквизитыПредметаРассмотрения.Дата;
			ДанныеПредметаРассмотрения.ИнициаторПроцесса = РеквизитыПредметаРассмотрения.Автор;
			ДанныеПредметаРассмотрения.Исполнитель = РеквизитыПредметаРассмотрения.ТекущийИсполнитель;
			ДанныеПредметаРассмотрения.Проект = РеквизитыПредметаРассмотрения.Проект;
			ДанныеПредметаРассмотрения.ПроектнаяЗадача = РеквизитыПредметаРассмотрения.ПроектнаяЗадача;
			ДанныеПредметаРассмотрения.Срок = РеквизитыПредметаРассмотрения.СрокИсполнения;
			
		Иначе
			
			РеквизитыДействия = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
				ПредметРассмотрения,
				"Задача, Исполнитель, СостояниеУчастникаЗадачи");
			РеквизитыПредметаРассмотрения = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
				РеквизитыДействия.Задача,
				"Автор, Дата, Проект, СостояниеЗадачи, Срок");
			ВидСостоянияУчастниковЗадач = РаботаСЗадачамиПовтИсп.ВидСостоянияУчастниковЗадач(
				РеквизитыДействия.СостояниеУчастникаЗадачи);
			
			ДанныеПредметаРассмотрения.Выполнен = Перечисления.ВидыСостоянийЗадач.ЭтоВыполненнаяЗадача(ВидСостоянияУчастниковЗадач);
			ДанныеПредметаРассмотрения.Дата = РеквизитыПредметаРассмотрения.Дата;
			ДанныеПредметаРассмотрения.ИнициаторПроцесса = РеквизитыПредметаРассмотрения.Автор;
			ДанныеПредметаРассмотрения.Исполнитель = РеквизитыДействия.Исполнитель;
			ДанныеПредметаРассмотрения.Проект = РеквизитыПредметаРассмотрения.Проект;
			ДанныеПредметаРассмотрения.Срок = РеквизитыПредметаРассмотрения.Срок;
			
		КонецЕсли;
		
	Иначе
		
		ВызватьИсключение СтрШаблон(
			НСтр("ru = 'Неизвестный тип предмета рассмотрения %1 (%2).'"),
			ПредметРассмотрения,
			ТипЗнч(ПредметРассмотрения));
		
	КонецЕсли;
	
	Возврат ДанныеПредметаРассмотрения;
	
КонецФункции

#КонецОбласти