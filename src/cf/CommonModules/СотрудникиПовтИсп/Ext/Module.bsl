////////////////////////////////////////////////////////////////////////////////
// Сотрудники (повт. исп.)
//  
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Возвращает всех сотрудников текущего пользователя.
//
// Параметры:
//  ТолькоДействующие - Булево -
// 
// Возвращаемое значение:
//  Массив из СправочникСсылка.Сотрудники -.
//
Функция ВсеСотрудникиТекущегоПользователя(ТолькоДействующие=Истина) Экспорт
	
	Если Не ТолькоДействующие Тогда
		
		Возврат Новый Массив(ПараметрыСеанса.СотрудникиПользователя);
		
	КонецЕсли;
	
	ТекПользователь = Пользователи.ТекущийПользователь();
	Возврат Сотрудники.СотрудникиПоПользователю(ТекПользователь, ТолькоДействующие);
	
КонецФункции

// Возвращает дерево, в котором ветви - структура предприятия, листья - сотрудники.
//
// Параметры:
//	Нет.
//
// Возвращаемое значение:
//	ДеревоЗначений.
//
Функция СотрудникиВПодразделениях() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	СправочникСтруктураПредприятия.Ссылка КАК ГруппаСсылка,
		|	СправочникСтруктураПредприятия.ПометкаУдаления КАК ГруппаПометкаУдаления,
		|	СправочникСтруктураПредприятия.Наименование КАК ГруппаНаименование,
		|	Сотрудники.Ссылка КАК СотрудникиСсылка,
		|	Сотрудники.ПометкаУдаления КАК СотрудникиПометкаУдаления,
		|	ПРЕДСТАВЛЕНИЕ(Сотрудники.Ссылка) КАК СотрудникиНаименование,
		|	Сотрудники.Ссылка = Сотрудники.Подразделение.Руководитель КАК ЭтоРуководитель
		|ПОМЕСТИТЬ ВрТаблица
		|ИЗ
		|	Справочник.СтруктураПредприятия КАК СправочникСтруктураПредприятия
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Сотрудники КАК Сотрудники
		|		ПО СправочникСтруктураПредприятия.Ссылка = Сотрудники.Подразделение
		|			И НЕ Сотрудники.ПометкаУдаления
		|			И Сотрудники.Действует
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВрТаблица.ГруппаСсылка,
		|	ИсполнителиРолей.РольИсполнителя,
		|	ИсполнителиРолей.РольИсполнителя.Владелец.Наименование,
		|	ИсполнителиРолей.РольИсполнителя.ПометкаУдаления,
		|	ВрТаблица.ГруппаПометкаУдаления,
		|	ВрТаблица.ГруппаНаименование
		|ПОМЕСТИТЬ ВрТаблицаРоли
		|ИЗ
		|	ВрТаблица КАК ВрТаблица
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ИсполнителиРолей КАК ИсполнителиРолей
		|		ПО ВрТаблица.СотрудникиСсылка = ИсполнителиРолей.Исполнитель
		|			И (ИсполнителиРолей.РольИсполнителя.ПометкаУдаления = ЛОЖЬ)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВрТаблица.ГруппаСсылка,
		|	ВрТаблица.ГруппаПометкаУдаления,
		|	ВрТаблица.ГруппаНаименование,
		|	ВрТаблица.СотрудникиСсылка,
		|	ВрТаблица.СотрудникиПометкаУдаления,
		|	ВрТаблица.СотрудникиНаименование,
		|	ВрТаблица.ЭтоРуководитель
		|ИЗ
		|	ВрТаблица КАК ВрТаблица
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ВрТаблицаРоли.ГруппаСсылка,
		|	ВрТаблицаРоли.ГруппаПометкаУдаления,
		|	ВрТаблицаРоли.ГруппаНаименование,
		|	ВрТаблицаРоли.РольИсполнителя,
		|	ВрТаблицаРоли.РольИсполнителяПометкаУдаления,
		|	ВрТаблицаРоли.РольИсполнителяВладелецНаименование,
		|	ЛОЖЬ
		|ИЗ
		|	ВрТаблицаРоли КАК ВрТаблицаРоли
		|УПОРЯДОЧИТЬ ПО
		|	ВрТаблица.ГруппаСсылка ИЕРАРХИЯ,
		|	ВрТаблица.СотрудникиНаименование";
		
	ВыборкаДерево = Запрос.Выполнить().Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкамСИерархией);
	
	Возврат ВыборкаДерево;

КонецФункции	

// Возвращает дерево, в котором ветви - рабочие группы, листья - сотрудники.
//
// Параметры:
//	Нет.
//
// Возвращаемое значение:
//	ДеревоЗначений.
//
Функция СотрудникиВГруппах() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	РабочиеГруппы.Ссылка КАК ГруппаСсылка,
		|	РабочиеГруппы.ПометкаУдаления КАК ГруппаПометкаУдаления,
		|	РабочиеГруппы.Наименование КАК ГруппаНаименование,
		|	Сотрудники.Ссылка КАК СотрудникиСсылка,
		|	Сотрудники.ПометкаУдаления КАК СотрудникиПометкаУдаления,
		|	ПРЕДСТАВЛЕНИЕ(Сотрудники.Ссылка) КАК СотрудникиНаименование,
		|	ИСТИНА В
		|		(ВЫБРАТЬ ПЕРВЫЕ 1
		|			ИСТИНА
		|		ИЗ
		|			Справочник.СтруктураПредприятия КАК Руководители
		|		ГДЕ
		|			Руководители.Руководитель = Сотрудники.Ссылка
		|			И Руководители.ПометкаУдаления = ЛОЖЬ) КАК ЭтоРуководитель
		|ПОМЕСТИТЬ ВрТаблица_0
		|ИЗ
		|	Справочник.РабочиеГруппы КАК РабочиеГруппы
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.РабочиеГруппы.Состав КАК РабочиеГруппыСостав
		|		ПО РабочиеГруппы.Ссылка = РабочиеГруппыСостав.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Сотрудники КАК Сотрудники
		|		ПО (РабочиеГруппыСостав.Участник = Сотрудники.Ссылка
		|				ИЛИ РабочиеГруппы.Ссылка = ЗНАЧЕНИЕ(Справочник.РабочиеГруппы.ВсеПользователи))
		|			И НЕ Сотрудники.ПометкаУдаления
		|			И Сотрудники.Действует
		|ГДЕ
		|	РабочиеГруппы.ПометкаУдаления = ЛОЖЬ
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВрТаблица_0.ГруппаСсылка,
		|	ВрТаблица_0.ГруппаПометкаУдаления,
		|	ВрТаблица_0.ГруппаНаименование,
		|	ВрТаблица_0.СотрудникиСсылка,
		|	ВрТаблица_0.СотрудникиПометкаУдаления,
		|	ВрТаблица_0.СотрудникиНаименование,
		|	МАКСИМУМ(ВрТаблица_0.ЭтоРуководитель) КАК ЭтоРуководитель
		|ПОМЕСТИТЬ ВрТаблица
		|ИЗ
		|	ВрТаблица_0 КАК ВрТаблица_0
		|СГРУППИРОВАТЬ ПО
		|	ВрТаблица_0.ГруппаСсылка,
		|	ВрТаблица_0.СотрудникиСсылка,
		|	ВрТаблица_0.ГруппаПометкаУдаления,
		|	ВрТаблица_0.ГруппаНаименование,
		|	ВрТаблица_0.СотрудникиПометкаУдаления,
		|	ВрТаблица_0.СотрудникиНаименование
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВрТаблица.ГруппаСсылка,
		|	ИсполнителиРолей.РольИсполнителя,
		|	ИсполнителиРолей.РольИсполнителя.Владелец.Наименование,
		|	ИсполнителиРолей.РольИсполнителя.ПометкаУдаления,
		|	ВрТаблица.ГруппаПометкаУдаления,
		|	ВрТаблица.ГруппаНаименование
		|ПОМЕСТИТЬ ВрТаблицаРоли
		|ИЗ
		|	ВрТаблица КАК ВрТаблица
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ИсполнителиРолей КАК ИсполнителиРолей
		|		ПО ВрТаблица.СотрудникиСсылка = ИсполнителиРолей.Исполнитель
		|			И ИсполнителиРолей.РольИсполнителя.ПометкаУдаления = ЛОЖЬ
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВрТаблица.ГруппаСсылка,
		|	ВрТаблица.ГруппаПометкаУдаления,
		|	ВрТаблица.ГруппаНаименование,
		|	ВрТаблица.СотрудникиСсылка,
		|	ВрТаблица.СотрудникиПометкаУдаления,
		|	ВрТаблица.СотрудникиНаименование,
		|	ВрТаблица.ЭтоРуководитель
		|ИЗ
		|	ВрТаблица КАК ВрТаблица
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ВрТаблицаРоли.ГруппаСсылка,
		|	ВрТаблицаРоли.ГруппаПометкаУдаления,
		|	ВрТаблицаРоли.ГруппаНаименование,
		|	ВрТаблицаРоли.РольИсполнителя,
		|	ВрТаблицаРоли.РольИсполнителяПометкаУдаления,
		|	ВрТаблицаРоли.РольИсполнителяВладелецНаименование,
		|	ЛОЖЬ
		|ИЗ
		|	ВрТаблицаРоли КАК ВрТаблицаРоли
		|УПОРЯДОЧИТЬ ПО
		|	ВрТаблица.ГруппаСсылка ИЕРАРХИЯ,
		|	ВрТаблица.СотрудникиНаименование";
		
	ВыборкаДерево = Запрос.Выполнить().Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкамСИерархией);
	
	Возврат ВыборкаДерево;

КонецФункции	

// Возвращает таблицу, содержащую всех пользователей.
//
// Параметры:
//	Нет.
//
// Возвращаемое значение:
//	ТаблицаЗначений.
//
Функция СотрудникиСписком() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	Сотрудники.Ссылка,
		|	Сотрудники.ПометкаУдаления,
		|	ПРЕДСТАВЛЕНИЕ(Сотрудники.Ссылка) КАК Наименование,
		|	ИСТИНА В
		|		(ВЫБРАТЬ ПЕРВЫЕ 1
		|			ИСТИНА
		|		ИЗ
		|			Справочник.СтруктураПредприятия КАК Руководители
		|		ГДЕ
		|			Руководители.Руководитель = Сотрудники.Ссылка
		|			И Руководители.ПометкаУдаления = ЛОЖЬ) КАК ЭтоРуководитель
		|ИЗ
		|	Справочник.Сотрудники КАК Сотрудники
		|ГДЕ
		|	Сотрудники.Действует
		|		И Сотрудники.ПометкаУдаления = ЛОЖЬ
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	РолиИсполнителей.Ссылка,
		|	РолиИсполнителей.ПометкаУдаления,
		|	РолиИсполнителей.Наименование,
		|	ЛОЖЬ
		|ИЗ
		|	Справочник.РолиИсполнителей КАК РолиИсполнителей
		|ГДЕ
		|	РолиИсполнителей.ПометкаУдаления = ЛОЖЬ
		|УПОРЯДОЧИТЬ ПО
		|	Наименование";
		
	ВыборкаТаблица = Запрос.Выполнить().Выгрузить();
	
	Возврат ВыборкаТаблица;

КонецФункции	

// Возвращает сотрудников пользователя. 
//  - таблицу с полями Сотрудник, Должность, Подразделение
//  см. одноименную функцию в РС.СотрудникиПользователей
//
Функция ТаблицаСотрудникиПользователя(Знач Пользователь, 
	Знач Действительные = Истина) Экспорт
	
	Возврат Сотрудники.ТаблицаСотрудникиПользователя(Пользователь, Действительные);
	
КонецФункции

// Возвращает текущего пользователя и всех его сотрудников.
// 
// Возвращаемое значение:
//  Массив из СправочникСсылка.Пользователи, СправочникСсылка.Сотрудники - Текущий пользователь и сотрудники.
//
Функция ТекущийПользовательИСотрудники() Экспорт
	
	Возврат Сотрудники.ТекущийПользовательИСотрудники();
	
КонецФункции

#КонецОбласти