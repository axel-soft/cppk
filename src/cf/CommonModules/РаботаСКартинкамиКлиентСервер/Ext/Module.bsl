////////////////////////////////////////////////////////////////////////////////
// Работа с картинками (клиент сервер)
//
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Запускает ImageMagick с указанными параметрами. В случае ошибки вызывается исключение,
// в остальных случаях возвращается код возврата ImageMagick.
//
// Параметры:
//   Параметры - Строка - параметры запуска ImageMagick.
//   ФайлыКУдалению - Массив - необязательный параметр, временные файлы, требующие удаления.
//   ИмяBatФайла - строка, полный путь + имя  bat файла. Может быть пуст.
//   СтрокаImageMagick - Строка - путь ImageMagick (на сервере иил клиенте)
//   ТекущийКаталог - Строка
//
// Возвращаемое значение:
//   Число - 0 в случае успешного выполнения, 300+ - при завершении с предупреждением.
//     Полный список предупреждений см. в документации к ImageMagick.
//
Функция ЗапуститьImageMagick(Параметры, ФайлыКУдалению = Неопределено, ИмяBatФайла = "", СтрокаImageMagick = "",
	ТекущийКаталог = Неопределено) Экспорт
	
	Если ФайлыКУдалению = Неопределено Тогда
		ФайлыКУдалению = Новый Массив();
	КонецЕсли;

	ПолныйПуть = СтрокаImageMagick;
	
	ТипПлатформыСервера = Неопределено;

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Тогда
	
	ТипПлатформыСервера = ОбщегоНазначенияДокументооборотПовтИсп.ТипПлатформыСервера();

#Иначе
	
	#Если Не ВнешнееСоединение Тогда
	ТипПлатформыСервера = ОбщегоНазначенияКлиент.ТипПлатформыКлиента();
	#КонецЕсли
	
#КонецЕсли

	Если (ТипПлатформыСервера = ТипПлатформы.Linux_x86 
		Или ТипПлатформыСервера = ТипПлатформы.Linux_x86_64) Тогда
	
		ИмяBatФайла = "";
		
	КонецЕсли;
	
	Если (ТипПлатформыСервера <> ТипПлатформы.Linux_x86 
		И ТипПлатформыСервера <> ТипПлатформы.Linux_x86_64) // в Линукс можно пустой путь
		И Не ЗначениеЗаполнено(ПолныйПуть) Тогда
			
		УдалитьВременныеФайлы(ФайлыКУдалению);
		ВызватьИсключение НСтр("ru = 'В настройках программы не указан полный путь к программе ImageMagick.'");
	КонецЕсли;
	
	Если ПолныйПуть = "convert.exe" Тогда
		УдалитьВременныеФайлы(ФайлыКУдалению);
		ВызватьИсключение НСтр("ru = 'В настройках программы указан полный путь к устаревшей версии ImageMagick (convert.exe).'");
	КонецЕсли;
		
	Если (ТипПлатформыСервера = ТипПлатформы.Linux_x86 
		Или ТипПлатформыСервера = ТипПлатформы.Linux_x86_64) Тогда
		
		Если ПолныйПуть <> "" Тогда
			СтрокаКоманды = СокрЛП(ПолныйПуть) + " " + Параметры;
		Иначе	
			СтрокаКоманды = Параметры;
		КонецЕсли; 
		
	ИначеЕсли СтрокаImageMagick <> "" Тогда	
		
		СтрокаКоманды = СокрЛП(ПолныйПуть) + " " + Параметры;

	Иначе	
		СтрокаКоманды = """" + СокрЛП(ПолныйПуть) + """ " + Параметры;
	КонецЕсли;
	
	СтрокаЗапуска = СтрокаКоманды;
	Если ИмяBatФайла <> "" Тогда
		ЗаписьТекста = Новый ЗаписьТекста(ИмяBatФайла, "cp866");
		ЗаписьТекста.Записать(СтрокаКоманды);
		ЗаписьТекста.Закрыть();
		ФайлыКУдалению.Добавить(ИмяBatФайла);
		СтрокаЗапуска = ИмяBatФайла;
	КонецЕсли;	
	
	КодВозврата = Неопределено;
	Попытка
		ЗапуститьПриложение(СтрокаЗапуска, ТекущийКаталог,Истина,КодВозврата);
	Исключение
		УдалитьВременныеФайлы(ФайлыКУдалению);
		ТекстИсключения = СтрШаблон(НСтр("ru = 'Ошибка при вызове ImageMagick с командной строкой:
			|%1
			|(%2)'"),
			СтрокаКоманды,
			ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		ВызватьИсключение ТекстИсключения;
	КонецПопытки;
	
	УдалитьВременныеФайлы(ФайлыКУдалению);
	
	// Успех.
	Если КодВозврата = 0 Тогда
		Возврат КодВозврата;
	КонецЕсли;
	
	// Не ImageMagick.
	Если КодВозврата = Неопределено Тогда
		ТекстИсключения = СтрШаблон(НСтр("ru = 'Нет кода возврата при вызове ImageMagick с командной строкой:
			|%1
			|Возможно, указанный путь не является путем к ImageMagick.'"),
			СтрокаКоманды);
		ВызватьИсключение ТекстИсключения;
	КонецЕсли;
	
	// Предупреждение.
	Если КодВозврата >= 300 
		И КодВозврата < 400 Тогда
		ТекстПредупреждения = СтрШаблон(НСтр("ru = 'Предупреждение %1 при вызове ImageMagick с командной строкой:
			|%2
			|Подробности см. в документации к ImageMagick.'"),
			КодВозврата,
			СтрокаКоманды);   
		ФайловыеФункции.ЗаписьЖурналаРегистрацииСервер(ТекстПредупреждения, НСтр("ru = 'Работа с картинками'"));	
		Возврат КодВозврата;
	КонецЕсли;
	
	// Ошибка ОС или иного приложения.
	Если КодВозврата < 300 Тогда
		ТекстИсключения = СтрШаблон(НСтр("ru = 'Ошибка %1 при вызове ImageMagick с командной строкой:
			|%2
			|Возможно, нарушена структура командной строки.'"),
			КодВозврата,
			СтрокаКоманды);
		ВызватьИсключение ТекстИсключения;
	КонецЕсли;
	
	// Ошибка ImageMagick.
	ТекстИсключения = СтрШаблон(НСтр("ru = 'Ошибка %1 при вызове ImageMagick с командной строкой:
		|%2
		|Подробности см. в документации к ImageMagick.'"),
		КодВозврата,
		СтрокаКоманды);
	ВызватьИсключение ТекстИсключения;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура УдалитьВременныеФайлы(ФайлыКУдалению)
	
	Если ФайлыКУдалению = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого ФайлКУдалению Из ФайлыКУдалению Цикл
		УдалитьФайлы(ФайлКУдалению);
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти