////////////////////////////////////////////////////////////////////////////////
// Работа с процессами по обработкам объектов (клиент):
// содержит клиентские процедуры и функции для работы с процессами по обработкам объектов.
//  
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Возвращает необходимость выполнения обработки объектов процессами.
//
// Возвращаемое значение:
//  Булево
//
Функция ВыполнятьОбработкуОбъектовПроцессами() Экспорт
	
	Возврат РаботаСПроцессамиПоОбработкамОбъектовВызовСервера.ВыполнятьОбработкуОбъектовПроцессами();
	
КонецФункции

#Область СхемыДляОбработокОбъектов

// Открывает карточку схемы обработки для вида объекта.
// 
// Параметры:
//  ФормаВидаОбъекта - ФормаКлиентскогоПриложения - карточка вида объекта.
//
Процедура ОткрытьСхемуОбработкиДляВидаОбъекта(ФормаВидаОбъекта) Экспорт
		
	Если ФормаВидаОбъекта = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные = ФормаВидаОбъекта.Элементы.ВидыДействий.ТекущиеДанные;
	Если ТекущиеДанные.ПолучитьРодителя() = Неопределено
		И ТекущиеДанные.ПолучитьЭлементы().Количество() = 0 Тогда
		
		ПоказатьПредупреждение(,
			НСтр("ru = 'Переход к схеме не возможен.
			|В текущем периоде нет действий.'"));
		
		Возврат;
	КонецЕсли;
	
	ОбработчикЗавершенияОткрытия = Новый ОписаниеОповещения(
		"ЗавершитьОткрытиеСхемыОбработкиДляВидаОбъекта",
		ЭтотОбъект,
		Новый Структура("ФормаВидаОбъекта", ФормаВидаОбъекта));
	
	Если ФормаВидаОбъекта.Объект.Ссылка.Пустая() Тогда 
		
		ТекстВопроса = 
			ОбщегоНазначенияДокументооборотКлиентСервер.ТекстВопросаНезаписанныхДанных(
			НСтр("ru = 'Открытие схемы обработки'"));
		ПоказатьВопрос(ОбработчикЗавершенияОткрытия, ТекстВопроса, РежимДиалогаВопрос.ОКОтмена);
		
	ИначеЕсли ФормаВидаОбъекта.Модифицированность = Истина Тогда 
		
		ВыполнитьОбработкуОповещения(ОбработчикЗавершенияОткрытия, КодВозвратаДиалога.ОК)
	Иначе
		
		ВыполнитьОбработкуОповещения(ОбработчикЗавершенияОткрытия, КодВозвратаДиалога.Да);
	КонецЕсли;
	
КонецПроцедуры

// Открывает карточку схемы обработки для объекта.
// 
// Параметры:
//  ФормаВидаОбъекта - ФормаКлиентскогоПриложения - карточка объекта с обработкой.
//
Процедура ОткрытьСхемуОбработкиОбъекта(ФормаОбъекта) Экспорт
		
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ПараметрыСхемы",
		ПредопределенноеЗначение("Справочник.ПараметрыСхемДляОбработокОбъектов.ПустаяСсылка"));
	ПараметрыФормы.Вставить("Действия", Новый Соответствие());
	
	ПараметрыФормы.Вставить("ОбработкаОбъекта",
		ПредопределенноеЗначение("Справочник.ОбработкиОбъектов.ПустаяСсылка"));
	
	ПараметрыФормы.Вставить("ТекущиеЭлементы", Новый Соответствие());
	ПараметрыФормы.Вставить("ПройденныеЭлементы", Новый Соответствие());
	
	ПараметрыФормы.Вставить("ТолькоПросмотр", Ложь);
	
	ПараметрыОткрытия =
		РаботаСПроцессамиПоОбработкамОбъектовВызовСервера.ПараметрыОтрытияСхемыОбработкиДляОбъекта(
		ФормаОбъекта.Объект.Ссылка);
	
	Если Не ЗначениеЗаполнено(ПараметрыОткрытия.ПараметрыСхемы.ПараметрыСхемы) Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Для просмотра схемы добавьте действия в обработку.'"));
		Возврат;
	КонецЕсли;
	
	ПараметрыФормы.ПараметрыСхемы = ПараметрыОткрытия.ПараметрыСхемы.ПараметрыСхемы;
	ПараметрыФормы.Действия = ПараметрыОткрытия.Действия;
	
	ПараметрыФормы.ОбработкаОбъекта = ПараметрыОткрытия.ОбработкаОбъекта;
	
	ПараметрыФормы.ТекущиеЭлементы = ПараметрыОткрытия.ТекущиеЭлементы;
	ПараметрыФормы.ПройденныеЭлементы = ПараметрыОткрытия.ПройденныеЭлементы;
	
	Если ТипЗнч(ПараметрыОткрытия.ПараметрыСхемы.Владелец) = 
		Тип("СправочникСсылка.НастройкиОбработкиВидовОбъектов") Тогда
		
		ПараметрыФормы.ТолькоПросмотр = Истина;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ФормаОбъекта.СостояниеОбработки) Тогда
		ПараметрыФормы.ТолькоПросмотр = Истина;
	КонецЕсли;     
	
	Если ФормаОбъекта.ИмяФормы = "Справочник.ДокументыПредприятия.Форма.ФормаЭлемента" Тогда
		Если ФормаОбъекта.ДокументВАрхивеИлиУничтожен Тогда
			ПараметрыФормы.ТолькоПросмотр = Истина;
		КонецЕсли;	
	КонецЕсли;	
	
#Если Не МобильныйКлиент Тогда
	ОткрытьФорму("ОбщаяФорма.КарточкаСхемыОбработкиОбъекта", ПараметрыФормы);
#Иначе
	ОткрытьФорму("ОбщаяФорма.МК_КарточкаСхемыОбработки", ПараметрыФормы);
#КонецЕсли
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

#Область КарточкаПроцесса

// Обрабатывает нажатие на описание состояния в карточке процесса по обработке/действию.
// 
// Параметры:
// 	Форма - ФормаКлиентскогоПриложения
//
Процедура ОбработатьНажатиеНаОписаниеСостоянияПроцесса(Форма) Экспорт
	
	Если Форма.Состояние <> ПредопределенноеЗначение(
		"Перечисление.СостоянияБизнесПроцессов.Прерван") Тогда
		
		Возврат;
	КонецЕсли;
	
	КтоИКогдаПрервалПроцесс =
		БизнесПроцессыИЗадачиСервер.ПолучитьИнформациюОПрерыванииПроцесса(
		Форма.Процесс);
		
	ПараметрыФормы = Новый Структура();
	ПараметрыФормы.Вставить("КтоИКогдаПрервалПроцесс", КтоИКогдаПрервалПроцесс);
	
	ОткрытьФорму("ОбщаяФорма.ПричинаПрерыванияПроцесса", ПараметрыФормы,
		Форма,,,,, РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область СхемыДляОбработокОбъектов

// Продолжение ОткрытьСхемуОбработкиДляВидаОбъекта
//
Процедура ЗавершитьОткрытиеСхемыОбработкиДляВидаОбъекта(Ответ, ДопПараметры) Экспорт
	
	ФормаВидаОбъекта = ДопПараметры.ФормаВидаОбъекта;
		
	Если Ответ = КодВозвратаДиалога.Отмена Тогда
		Возврат;
	ИначеЕсли Ответ = КодВозвратаДиалога.ОК Тогда
		
		Если Не ФормаВидаОбъекта.Записать() Тогда 
			Возврат;
		КонецЕсли;
		
		ПоказатьОповещениеПользователя(
			НСтр("ru = 'Создание:'"), 
			ПолучитьНавигационнуюСсылку(ФормаВидаОбъекта.Объект.Ссылка),
			Строка(ФормаВидаОбъекта.Объект.Ссылка),
			БиблиотекаКартинок.Информация32);
	КонецЕсли;
	
	ТекущиеДанные = ФормаВидаОбъекта.Элементы.ВидыДействий.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено
		Или Не ЗначениеЗаполнено(ТекущиеДанные.СсылкаНаНастройку) Тогда
		
		Возврат;
	КонецЕсли;	
		
	ПараметрыСхемы =
		РаботаСПроцессамиПоОбработкамОбъектовВызовСервера.ПараметрыСхемыПоВладельцу(
		ТекущиеДанные.СсылкаНаНастройку);

	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ПараметрыСхемы", ПараметрыСхемы);
	Если ФормаВидаОбъекта.ТолькоПросмотр Или Не ФормаВидаОбъекта.ПравоРедактированияОбработки Тогда
		ПараметрыФормы.Вставить("ТолькоПросмотр", Истина);
	КонецЕсли;
	
	ОткрытьФорму("ОбщаяФорма.КарточкаСхемыОбработкиОбъекта", ПараметрыФормы);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти