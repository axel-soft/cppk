////////////////////////////////////////////////////////////////////////////////
// Работа с быстрым поиском (клиент).
//
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Обработчик события "При активизации строки" таблицы "Быстрый поиск".
//
// Параметры:
//  ТаблицаБыстрыйПоиск - ТаблицаФормы - Таблица "Быстрый поиск".
//  ЯчейкаЗначение - ПолеФормы - Ячейка "Значение" таблицы "Быстрый поиск".
//  ТекущаяСтрокаБыстрогоПоиска - Число, Неопределено - Текущая строка быстрого поиска.
//  СпискиВыбораБыстрогоПоиска - Соответствие - Списки выбора быстрого поиска.
//
Процедура БыстрыйПоискПриАктивизацииСтроки(
	ТаблицаБыстрыйПоиск,
	ЯчейкаЗначение,
	ТекущаяСтрокаБыстрогоПоиска,
	СпискиВыбораБыстрогоПоиска) Экспорт
	
	Если ТаблицаБыстрыйПоиск.ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные = ТаблицаБыстрыйПоиск.ТекущиеДанные;
	
	ЯчейкаЗначение.СписокВыбора.Очистить();
	Если СпискиВыбораБыстрогоПоиска.Свойство(ТекущиеДанные.Параметр) Тогда
		
		СписокВыбораПараметра = СпискиВыбораБыстрогоПоиска[ТекущиеДанные.Параметр];
		Для Каждого ЭлементВыбора Из СписокВыбораПараметра Цикл
			ЯчейкаЗначение.СписокВыбора.Добавить(
				ЭлементВыбора.Значение,
				ЭлементВыбора.Представление,,
				ЭлементВыбора.Картинка);
		КонецЦикла;
		
		ЯчейкаЗначение.КнопкаВыпадающегоСписка = Истина;
		ЯчейкаЗначение.КнопкаВыбора = Ложь;
		
	Иначе
		
		ЯчейкаЗначение.КнопкаВыпадающегоСписка = Неопределено;
		Если ТекущиеДанные.Параметр = "ВидПриложения" Тогда
			ЯчейкаЗначение.КнопкаВыбора = Ложь;
		Иначе
			ЯчейкаЗначение.КнопкаВыбора = Истина;
		КонецЕсли;
		
	Конецесли;
	
	НовыеПараметрыВыбора = Новый Массив;
	Если ТекущиеДанные.Параметр = "Автор" Тогда
		
		НовыеПараметрыВыбора.Добавить(Новый ПараметрВыбора("Отбор.Действует", Истина));
		НовыеПараметрыВыбора.Добавить(Новый ПараметрВыбора("Отбор.ПометкаУдаления", Ложь));
		
	ИначеЕсли ТекущиеДанные.Параметр = "ВидПриложения" Тогда
		
		Если СпискиВыбораБыстрогоПоиска.Свойство("ТипПриложения") Тогда
			СписокВыбораТипПриложения = СпискиВыбораБыстрогоПоиска["ТипПриложения"];
			ТипПриложения = СписокВыбораТипПриложения[0].Значение;
			Если ТипПриложения = ПредопределенноеЗначение("Перечисление.ТипыПриложенийЗадач.Документы") Тогда
				НовыеПараметрыВыбора.Добавить(Новый ПараметрВыбора("ПолныйТип", "СправочникСсылка.ВидыДокументов"));
			ИначеЕсли ТипПриложения = ПредопределенноеЗначение("Перечисление.ТипыПриложенийЗадач.Мероприятия") Тогда
				НовыеПараметрыВыбора.Добавить(Новый ПараметрВыбора("ПолныйТип", "СправочникСсылка.ВидыМероприятий"));
			ИначеЕсли ТипПриложения = ПредопределенноеЗначение("Перечисление.ТипыПриложенийЗадач.Проекты") Тогда
				НовыеПараметрыВыбора.Добавить(Новый ПараметрВыбора("ПолныйТип", "СправочникСсылка.ВидыПроектов"));
			Иначе
				НовыеПараметрыВыбора.Добавить(Новый ПараметрВыбора("ПолныйТип", ""));
			КонецЕсли;
		Иначе
			НовыеПараметрыВыбора.Добавить(Новый ПараметрВыбора("ПолныйТип", ТекущиеДанные.Тип));
		КонецЕсли;
		НовыеПараметрыВыбора.Добавить(Новый ПараметрВыбора("Отбор.ПометкаУдаления", Ложь));
		
	КонецЕсли;
	ЯчейкаЗначение.ПараметрыВыбора = Новый ФиксированныйМассив(НовыеПараметрыВыбора);
	
	ТекущаяСтрокаБыстрогоПоиска = ТаблицаБыстрыйПоиск.ТекущаяСтрока;
	
КонецПроцедуры

// Быстрый поиск значение обработка выбора.
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения
//  ТаблицаБыстрыйПоиск - ТаблицаФормы
//  Элемент - ПолеФормы
//  ВыбранноеЗначение - Произвольный
//  СтандартнаяОбработка - Булево
//
Процедура БыстрыйПоискЗначениеОбработкаВыбора(Форма, ТаблицаБыстрыйПоиск, Элемент, ВыбранноеЗначение, СтандартнаяОбработка) Экспорт
	
	ТекущиеДанные = ТаблицаБыстрыйПоиск.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено
		Или (ТекущиеДанные.Параметр <> "Исполнитель" И ТекущиеДанные.Параметр <> "Состояние")
		Или (Элемент.РежимВыбораИзСписка И ТекущиеДанные.Параметр <> "Состояние") Тогда
		Возврат;
	КонецЕсли;
	
	Если ТекущиеДанные.Параметр = "Состояние" Тогда     
		
		Если ВыбранноеЗначение = НСтр("ru = 'Выбрать несколько...'") Тогда   
			
			СтандартнаяОбработка = Ложь;      
			
			ДопПараметры = Новый Структура("Форма, ТаблицаБыстрыйПоиск", Форма, ТаблицаБыстрыйПоиск);
			ОписаниеОповещения = Новый ОписаниеОповещения("БыстрыйПоискВыборСостоянияЗавершение", ЭтотОбъект, ДопПараметры);
			
			ПараметрыОткрытия = Новый Структура;
			ПараметрыОткрытия.Вставить("ТекущееЗначение", ТекущиеДанные.Значение); // сюда добавим тек значения
			ПараметрыОткрытия.Вставить("Состояния", Форма.СпискиВыбораБыстрогоПоиска["Состояние"]);
			
			ОткрытьФорму("ОбщаяФорма.БыстрыйПоискВыборСостояния",
				ПараметрыОткрытия,,,,,
				ОписаниеОповещения,
				РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс);
			
		КонецЕсли;	
		
		Возврат;
			
	КонецЕсли;
	
	Если Не РаботаСАдреснойКнигойКлиент.ОбработатьВыборУчастника(
			Форма,
			Элемент,
			ВыбранноеЗначение,
			СтандартнаяОбработка) Тогда
		Возврат;
	КонецЕсли;
	
	// Если значение требует уточнения, будет вызвана форма для ввода уточняющих данных.
	// Обработка выбора не должна выполняться в этом случае.
	ТипыДляУточнения = Новый Массив;
	ТипыДляУточнения.Добавить(Тип("Структура"));
	ТипыДляУточнения.Добавить(Тип("СправочникСсылка.РолиИсполнителей"));
	Если ТипыДляУточнения.Найти(ТипЗнч(ВыбранноеЗначение)) <> Неопределено Тогда
		
		СотрудникиКлиент.ОбработкаВыбораКонтейнера(
			Элемент,
			ВыбранноеЗначение,
			СтандартнаяОбработка);
		
		// Повторная проверка (значение могло быть изменено).
		Если ТипыДляУточнения.Найти(ТипЗнч(ВыбранноеЗначение)) <> Неопределено Тогда
			
			СтандартнаяОбработка = Ложь;
			
			Возврат;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

// Обработка авто-подбора в быстром поиске.
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения.
//  ТаблицаБыстрыйПоиск - ТаблицаФормы.
//  Элемент - ПолеФормы.
//  Текст - Строка.
//  ДанныеВыбора - Неопределено.
//  ПараметрыПолученияДанных - Структура.
//  Ожидание - Число.
//  СтандартнаяОбработка - Булево.
//
Процедура БыстрыйПоискЗначениеАвтоПодбор(Форма, ТаблицаБыстрыйПоиск, Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка) Экспорт
	
	Если Элемент.СписокВыбора.Количество() = 0 Тогда
		
		ТекущиеДанные = ТаблицаБыстрыйПоиск.ТекущиеДанные;
		Если ТекущиеДанные = Неопределено
			Или ТекущиеДанные.Параметр <> "Исполнитель" Тогда
			Возврат;
		КонецЕсли;
		
		ПараметрыПолученияДанных = Новый Структура("ПравилаКоммуникаций", Ложь);
		
		РаботаСАдреснойКнигойКлиент.ПодобратьУчастника(
			Форма,
			Элемент,
			Текст,
			ДанныеВыбора, 
			ПараметрыПолученияДанных,
			Ожидание,
			СтандартнаяОбработка,
			ТекущиеДанные.Значение);
			
	Иначе
		
		Если Не ЗначениеЗаполнено(Текст) Тогда
			
			// Стандартная обработка - показ списка выбора.
			
		Иначе
			
			СтандартнаяОбработка = Ложь;
		
			ДанныеВыбора = Новый СписокЗначений;
			
			Для Каждого СтрокаВыбора Из Элемент.СписокВыбора Цикл 
				
				НайденнаяСтрока = СтрНайтиИВыделитьОформлением(Строка(СтрокаВыбора.Значение), Текст);
				Если НайденнаяСтрока = Неопределено Тогда
					Продолжить;
				КонецЕсли;
				
				ДанныеВыбора.Добавить(СтрокаВыбора.Значение, НайденнаяСтрока,, СтрокаВыбора.Картинка);
				
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЕсли;
		
КонецПроцедуры

// Обработки окончания ввода текста в быстром поиске.
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения.
//  ТаблицаБыстрыйПоиск - ТаблицаФормы.
//  Элемент - ПолеФормы.
//  Текст - Строка.
//  ДанныеВыбора - Неопределено.
//  ПараметрыПолученияДанных - Структура.
//  СтандартнаяОбработка - Булево.
//
Процедура БыстрыйПоискЗначениеОкончаниеВводаТекста(Форма, ТаблицаБыстрыйПоиск, Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка) Экспорт
	
	Если Элемент.СписокВыбора.Количество() = 0 Тогда
		
		ТекущиеДанные = ТаблицаБыстрыйПоиск.ТекущиеДанные;
		Если ТекущиеДанные = Неопределено
			Или ТекущиеДанные.Параметр <> "Исполнитель" Тогда
			Возврат;
		КонецЕсли;
		
		ПараметрыПолученияДанных = Новый Структура("ПравилаКоммуникаций", Ложь);
			
		РаботаСАдреснойКнигойКлиент.ПодобратьУчастника(
			Форма,
			Элемент,
			Текст,
			ДанныеВыбора,
			ПараметрыПолученияДанных,
			0,
			СтандартнаяОбработка,
			ТекущиеДанные.Значение);
		
	Иначе
		
		СтандартнаяОбработка = Ложь;
		
		ДанныеВыбора = Новый СписокЗначений;
		
		Для Каждого СтрокаВыбора Из Элемент.СписокВыбора Цикл 
			
			НайденнаяСтрока = СтрНайтиИВыделитьОформлением(Строка(СтрокаВыбора.Значение), Текст);
			Если НайденнаяСтрока = Неопределено Тогда
				Продолжить;
			КонецЕсли;
			
			ДанныеВыбора.Добавить(СтрокаВыбора.Значение, НайденнаяСтрока,, СтрокаВыбора.Картинка);
			
		КонецЦикла;
		
	КонецЕсли;
		
КонецПроцедуры

// Быстрый поиск значение начало выбора.
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения.
//  ТаблицаБыстрыйПоиск - ТаблицаФормы.
//  Элемент - ПолеФормы.
//  ДанныеВыбора - Неопределено.
//  СтандартнаяОбработка - Булево.
//
Процедура БыстрыйПоискЗначениеНачалоВыбора(Форма, ТаблицаБыстрыйПоиск, Элемент, ДанныеВыбора, СтандартнаяОбработка) Экспорт
	
	Если Элемент.СписокВыбора.Количество() <> 0 И Не ЗначениеЗаполнено(Элемент.СписокВыбора[0].Значение) Тогда
		Элемент.СписокВыбора.Очистить();
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные = ТаблицаБыстрыйПоиск.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено
		Или (ТекущиеДанные.Параметр <> "Исполнитель" И ТекущиеДанные.Параметр <> "Состояние")
		Или Элемент.РежимВыбораИзСписка Тогда
		Возврат;
	КонецЕсли;
	
	Если ТекущиеДанные.Параметр = "Состояние" Тогда 
		
		СтандартнаяОбработка = Ложь;
		
		ДопПараметры = Новый Структура("Форма, ТаблицаБыстрыйПоиск", Форма, ТаблицаБыстрыйПоиск);
		ОписаниеОповещения = Новый ОписаниеОповещения("БыстрыйПоискВыборСостоянияЗавершение", ЭтотОбъект, ДопПараметры);

		ПараметрыОткрытия = Новый Структура;
		ПараметрыОткрытия.Вставить("ТекущееЗначение", ТекущиеДанные.Значение); // сюда добавим тек значения
		ПараметрыОткрытия.Вставить("Состояния", Форма.СпискиВыбораБыстрогоПоиска["Состояние"]);
		
		ОткрытьФорму("ОбщаяФорма.БыстрыйПоискВыборСостояния",
			ПараметрыОткрытия,,,,,
			ОписаниеОповещения,
			РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс);
		
		Возврат;
	КонецЕсли;
	
	ПараметрыПолученияДанных = Новый Структура("ПравилаКоммуникаций", Ложь);
	
	РаботаСАдреснойКнигойКлиент.ВыбратьУчастника(
		Форма,
		Элемент,
		СтандартнаяОбработка,
		ТекущиеДанные.Значение,,,
		ПараметрыПолученияДанных);
	
КонецПроцедуры

// Быстрый поиск значение обработка выбора.
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения.
//  ТаблицаБыстрыйПоиск - ТаблицаФормы.
//  Элемент - ПолеФормы.
//  СтандартнаяОбработка - Булево.
//
Процедура БыстрыйПоискЗначениеОчистка(Форма, ТаблицаБыстрыйПоиск, Элемент, СтандартнаяОбработка) Экспорт
	
	СтандартнаяОбработка = Ложь;
	
	ТекущиеДанные = ТаблицаБыстрыйПоиск.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ТекущиеДанные.Фиксированный Тогда
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные.Значение = ТекущиеДанные.ЗначениеПоУмолчанию;
	
	Форма.БыстрыйПоискПриИзмененииНаКлиенте();
	
КонецПроцедуры

Процедура БыстрыйПоискВыборСостоянияЗавершение(НовыйБыстрыйПоиск, ДополнительныеПараметры) Экспорт
	
	Если НовыйБыстрыйПоиск = Неопределено Или НовыйБыстрыйПоиск = КодВозвратаДиалога.Отмена Тогда
		Возврат;
	КонецЕсли;
	
	ТаблицаБыстрыйПоиск = ДополнительныеПараметры.ТаблицаБыстрыйПоиск;
	
	ТекущиеДанные = ТаблицаБыстрыйПоиск.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		ТекущиеДанные.Значение = НовыйБыстрыйПоиск;
	КонецЕсли;  
	
	ДополнительныеПараметры.Форма.БыстрыйПоискПриИзмененииНаКлиенте();
	
КонецПроцедуры

#КонецОбласти