Процедура СтороныКонтактноеЛицоНачалоВыбора(Форма, Элемент, ДанныеВыбора, СтандартнаяОбработка,
	ЭлементСтороныКонтактноеЛицо) Экспорт

	СтандартнаяОбработка = Ложь;
	
	ТекущиеДанные = Форма.Элементы.Стороны.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ТекущиеДанные.Сторона)
		И РаботаСПодписямиДокументовКлиентСервер.ЭтоКонтрагент(ТекущиеДанные.Сторона) Тогда
		
			Отбор = Новый Структура;
			Отбор.Вставить("Владелец", ТекущиеДанные.Сторона);
			Отбор.Вставить("ПометкаУдаления", Ложь);
			
			ПараметрыФормы = Новый Структура;
			ПараметрыФормы.Вставить("Отбор", Отбор);
			ПараметрыФормы.Вставить("ТекущаяСтрока", ТекущиеДанные.КонтактноеЛицо);
			
			ОткрытьФорму("Справочник.КонтактныеЛица.ФормаВыбора", ПараметрыФормы, Элемент,,,,,
				РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
			
	Иначе
	
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("РежимРаботыФормы", 1);
		ПараметрыФормы.Вставить("УпрощенныйИнтерфейс", Истина);
		Если РаботаСПодписямиДокументовКлиентСервер.ЭтоОрганизация(ТекущиеДанные.Сторона) Тогда
			ПараметрыФормы.Вставить("ОтображатьСотрудников", Истина);
			ПараметрыФормы.Вставить("ПодменятьПользователейСотрудниками", Истина);
		Иначе
			ПараметрыФормы.Вставить("ОтображатьКонтрагентов", Истина);
		КонецЕсли;
		ПараметрыФормы.Вставить("ЗаголовокФормы", НСтр("ru = 'Выбор контактного лица стороны'"));
		
		Если ЗначениеЗаполнено(ТекущиеДанные.КонтактноеЛицо) Тогда
			ПараметрыФормы.Вставить("ВыбранныеАдресаты", ТекущиеДанные.КонтактноеЛицо);
		ИначеЕсли ЗначениеЗаполнено(ТекущиеДанные.Сторона) Тогда
			ПараметрыФормы.Вставить("ВыбранныеАдресаты", ТекущиеДанные.Сторона);
		КонецЕсли;
		
		ОткрытьФорму("Справочник.АдреснаяКнига.Форма.ФормаСписка",
			ПараметрыФормы,
			ЭлементСтороныКонтактноеЛицо,,,,,
			РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		
	КонецЕсли;
		
КонецПроцедуры

Процедура СтороныПодписалНачалоВыбора(Форма, Элемент, ДанныеВыбора, СтандартнаяОбработка,
	ЭлементСтороныПодписал, ИмяРеквизита = "Подписал") Экспорт

	СтандартнаяОбработка = Ложь;
	
	ТекущиеДанные = Форма.Элементы.Стороны.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ТекущиеДанные.Сторона)
		И РаботаСПодписямиДокументовКлиентСервер.ЭтоКонтрагент(ТекущиеДанные.Сторона) Тогда
		
			Отбор = Новый Структура;
			Отбор.Вставить("Владелец", ТекущиеДанные.Сторона);
			Отбор.Вставить("ПометкаУдаления", Ложь);
			
			ПараметрыФормы = Новый Структура;
			ПараметрыФормы.Вставить("Отбор", Отбор);
			ПараметрыФормы.Вставить("ТекущаяСтрока", ТекущиеДанные.КонтактноеЛицо);
			
			ОткрытьФорму("Справочник.КонтактныеЛица.ФормаВыбора", ПараметрыФормы, Элемент,,,,,
				РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);

	Иначе

		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("РежимРаботыФормы", 1);
		ПараметрыФормы.Вставить("УпрощенныйИнтерфейс", Истина);
		Если РаботаСПодписямиДокументовКлиентСервер.ЭтоОрганизация(ТекущиеДанные.Сторона) Тогда
			ПараметрыФормы.Вставить("ОтображатьСотрудников", Истина);
			ПараметрыФормы.Вставить("ПодменятьПользователейСотрудниками", Истина);
			
			Если Форма.ЕстьДействияПодписания Тогда
				Если Форма.ПодписантыИзДействий.Количество() Тогда
					ПараметрыФормы.Вставить("СписокОтбора", Форма.ПодписантыИзДействий);
				Иначе
					Форма.Элементы.ГруппаСтраницы.ТекущаяСтраница = Форма.Элементы.СтраницаОбработка;
					Возврат;
				КонецЕсли;
			КонецЕсли;

		Иначе
			ПараметрыФормы.Вставить("ОтображатьКонтрагентов", Истина);
		КонецЕсли;
		ПараметрыФормы.Вставить("ЗаголовокФормы", НСтр("ru = 'Выбор подписанта стороны'"));
		
		Если ЗначениеЗаполнено(ТекущиеДанные[ИмяРеквизита]) Тогда
			ПараметрыФормы.Вставить("ВыбранныеАдресаты", ТекущиеДанные[ИмяРеквизита]);
		ИначеЕсли ЗначениеЗаполнено(ТекущиеДанные.Сторона) Тогда
			ПараметрыФормы.Вставить("ВыбранныеАдресаты", ТекущиеДанные.Сторона);
		КонецЕсли;
		
		ОткрытьФорму("Справочник.АдреснаяКнига.Форма.ФормаСписка",
			ПараметрыФормы,
			ЭлементСтороныПодписал,,,,,
			РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
			
	КонецЕсли;

КонецПроцедуры

Процедура СтороныПриАктивизацииЯчейки(Форма, Элементы, ТекущиеДанные) Экспорт

	Если Форма.ЭтоПолноправныйПользователь Тогда
		Возврат;
	КонецЕсли;
	
	// доступность по запрету
	МассивПолей = Новый Массив;
	МассивПолей.Добавить("Сторона");
	МассивПолей.Добавить("Наименование");
	МассивПолей.Добавить("КонтактноеЛицо");
	МассивПолей.Добавить("Подписал");
	МассивПолей.Добавить("Подписан");
	МассивПолей.Добавить("ДатаПодписи");
	МассивПолей.Добавить("Комментарий");
	
	Для Каждого Поле Из МассивПолей Цикл
		
		Элементы["Стороны" + Поле].ТолькоПросмотр =
			(Форма.ЗапретитьИзменятьРеквизитыИзШаблона
			И ПолеСтороныЕстьВШаблоне(Форма.СтороныШаблона, ТекущиеДанные, Поле))
			Или ЗапрещеноРедактироватьПолеСтороныПоСостоянию(Форма.НедоступныеПоля, ТекущиеДанные, Поле);
		
	КонецЦикла;
	
	// доступность ранее установленной подписи
	УстановитьДоступностьРеквизитовПодписанияСтороны(Форма);
	
	ЕстьЗапретРедактированияСторон = Ложь;
	
	Для Каждого Поле Из МассивПолей Цикл
		Если Элементы["Стороны" + Поле].ТолькоПросмотр И Не ЕстьЗапретРедактированияСторон Тогда
			ЕстьЗапретРедактированияСторон = Истина;
		КонецЕсли;
	КонецЦикла;
	
	Элементы["СтороныКонтекстноеМенюУдалить"].Доступность = Не ЕстьЗапретРедактированияСторон;
	
КонецПроцедуры

// Открывает форму выбора организации для подписания при исполнении задачи подписания
// 
// Параметры:
//  Документ - СправочникСсылка.ДокументыПредприятия - Подписываемый предмет
//  Исполнитель - СправочникСсылка.Сотрудники - Исполнитель задачи подписания
//  Организации - СписокЗначений Из СправочникСсылка.Организации - Организации для выбора
//  Оповещение - ОписаниеОповещения - Оповещение для вызова после закрытия формы
//  ОднаЗадача - Булево - Режим выполнения единственной задачи подписания
Процедура ВыбратьОрганизациюДляПодписания(Знач Документ, Знач Исполнитель, Знач Организации,
	Знач Оповещение, Знач ОднаЗадача) Экспорт
	
	Параметры = Новый Структура;
	Параметры.Вставить("Документ", Документ);
	Параметры.Вставить("Подписант", Исполнитель);
	Параметры.Вставить("Организации", Организации);
	Параметры.Вставить("ОднаЗадача", ОднаЗадача);
	
	ОткрытьФорму("Справочник.ДействияПодписания.Форма.ФормаВыбораОрганизацииДляПодписания",
		Параметры, , , , , Оповещение, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);

КонецПроцедуры

#Область СлужебныеПроцедурыИФункции

Процедура УстановитьДоступностьРеквизитовПодписанияСтороны(Форма)
	
	ТекущиеДанные = Форма.Элементы.Стороны.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено
		Или Форма.ПодписантыПоОрганизациям = Неопределено
		Или Не Форма.ПодписантыПоОрганизациям.Количество() Тогда
		Возврат;
	КонецЕсли;
	
	МассивПолей = Новый Массив;
	МассивПолей.Добавить("Подписал");
	МассивПолей.Добавить("Подписан");
	МассивПолей.Добавить("ДатаПодписи");
	МассивПолей.Добавить("Комментарий");
	
	ДокументПодписанЭП = Форма.Объект.ПодписанЭП;
	
	Если Не РаботаСПодписямиДокументовКлиентСервер.ЭтоОрганизация(ТекущиеДанные.Сторона) Тогда
		
		Для Каждого Поле Из МассивПолей Цикл
			Форма.Элементы["Стороны" + Поле].ТолькоПросмотр =
				ДокументПодписанЭП
				Или Форма.ЗапретитьИзменятьРеквизитыИзШаблона
					И ПолеСтороныЕстьВШаблоне(Форма.СтороныШаблона, ТекущиеДанные, Поле);
		КонецЦикла;
		
		Возврат;
	КонецЕсли;
	
	РедактированиеПоПодписаниюРазрешено = Ложь;
	
	Если Форма.ПодписантыПоОрганизациям.Получить(ТекущиеДанные.Сторона) <> Неопределено Тогда
		
		Для Каждого Подписант Из Форма.ПодписантыПоОрганизациям[ТекущиеДанные.Сторона] Цикл
			Если ДействияСерверПовтИсп.ЕстьПравоРедактированияСторонПоПодписантуДокумента(
					Подписант.Ключ, Подписант.Значение, Форма.Объект.Ссылка) Тогда
				РедактированиеПоПодписаниюРазрешено = Истина;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
	КонецЕсли;
	
	Если ТекущиеДанные.Подписан = Истина
		И ЗначениеЗаполнено(ТекущиеДанные.Подписал)
		И Не СотрудникиВызовСервера.ЭтоСотрудникПользователя(ТекущиеДанные.Подписал)
		И ЗначениеЗаполнено(ТекущиеДанные.Установил)
		И Не ТекущиеДанные.Установил = Форма.ТекущийПользователь
		И Не РедактированиеПоПодписаниюРазрешено Тогда
		
		Для Каждого Поле Из МассивПолей Цикл
			Форма.Элементы["Стороны" + Поле].ТолькоПросмотр = Истина;
		КонецЦикла;
		
	Иначе
		
		Для Каждого Поле Из МассивПолей Цикл
			Форма.Элементы["Стороны" + Поле].ТолькоПросмотр =
				Не РедактированиеПоПодписаниюРазрешено
				Или ДокументПодписанЭП
				Или (Форма.ЗапретитьИзменятьРеквизитыИзШаблона
					И ПолеСтороныЕстьВШаблоне(Форма.СтороныШаблона, ТекущиеДанные, Поле));
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

Функция ПолеСтороныЕстьВШаблоне(СтороныШаблона, ТекущиеДанные, Поле)
	
	Если ЗначениеЗаполнено(ТекущиеДанные[Поле]) Тогда
		
		Попытка
			Если СтороныШаблона.НайтиСтроки(Новый Структура(Поле, ТекущиеДанные[Поле])).Количество() > 0 Тогда
				Возврат Истина;
			Иначе
				Возврат Ложь;
			КонецЕсли;
		Исключение
			// поле отсутствует в таблице сторон шаблона
			Возврат Ложь;
		КонецПопытки;
		
	Иначе
		Возврат Ложь;
	КонецЕсли;

КонецФункции

Функция ЗапрещеноРедактироватьПолеСтороныПоСостоянию(НедоступныеПоля, ТекущиеДанные, Поле)
	
	Попытка
		Если НедоступныеПоля.Свойство("Стороны" + Поле) Тогда
			Возврат Истина;
		Иначе
			Возврат Ложь;
		КонецЕсли;
	Исключение
		// поле отсутствует в списке недоступных полей по состоянию
		Возврат Ложь;
	КонецПопытки;
	
КонецФункции

#КонецОбласти
