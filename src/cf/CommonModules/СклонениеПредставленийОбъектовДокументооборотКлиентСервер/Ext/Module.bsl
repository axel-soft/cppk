#Область ПрограммныйИнтерфейс

// Заполняет структуру параметров склонения представлений в переданной форме в зависимости от типа объекта формы.
// После заполнения структура будет иметь два свойства - ЭтоФИО и Пол, заполненных 
// в соответствии с физлицом при его наличии.
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма для заполнения в ней параметров склонения.
//		Должна иметь реквизит "ПараметрыСклонения" с типом "Произвольный".
//
Процедура ЗаполнитьПараметрыСклоненияФормы(Форма) Экспорт
	
	Объект = Форма.Объект;
	ПараметрыСклонения = СклонениеПредставленийОбъектовКлиентСервер.ПараметрыСклонения();
	
	ПараметрыСклонения.ЭтоФИО = Ложь;
	ПараметрыСклонения.Пол = Неопределено;
	
	Типы = Новый ОписаниеТипов("СправочникСсылка.КонтактныеЛица, СправочникСсылка.Пользователи, СправочникСсылка.ФизическиеЛица");
	Если Типы.СодержитТип(ТипЗнч(Объект.Ссылка)) Тогда
		
		ПараметрыСклонения.ЭтоФИО = Истина;
		
		Если ТипЗнч(Объект.Ссылка) = Тип("СправочникСсылка.ФизическиеЛица") Тогда
			ФизЛицо	= Объект.Ссылка;
		Иначе
			ФизЛицо = Объект.ФизЛицо;
		КонецЕсли;
		
		ПолФизЛица = ОбщегоНазначенияДокументооборотВызовСервера.ЗначениеРеквизитаОбъекта(ФизЛицо, "Пол");
		
		Если ПолФизЛица = ПредопределенноеЗначение("Перечисление.ПолФизическогоЛица.Мужской") Тогда
			ПараметрыСклонения.Пол = 1;
		ИначеЕсли ПолФизЛица = ПредопределенноеЗначение("Перечисление.ПолФизическогоЛица.Женский") Тогда
			ПараметрыСклонения.Пол = 2;
		КонецЕсли;
		
	КонецЕсли;
	
	Форма.ПараметрыСклонения = ПараметрыСклонения;
	
КонецПроцедуры

#КонецОбласти
