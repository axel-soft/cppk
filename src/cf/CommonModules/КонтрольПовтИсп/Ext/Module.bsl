
#Область СлужебныйПрограммныйИнтерфейс

// Получает контрольную карточку подчиненного исполнения.
// 
// Параметры:
//  Действие - СправочникСсылка.ДействияИсполнения
//  ИдентификаторВышестоящегоЭтапа - Строка
// 
// Возвращаемое значение:
//  СправочникСсылка.Контроль
// 
Функция КонтрольнаяКарточкаПодчиненногоИсполнения(Действие, ИдентификаторВышестоящегоЭтапа) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	Контроль.Ссылка
		|ИЗ
		|	Справочник.Контроль КАК Контроль
		|ГДЕ
		|	Контроль.Предмет = &Предмет
		|	И Контроль.ИдентификаторВышестоящегоЭтапа = &ИдентификаторВышестоящегоЭтапа
		|	И Не Контроль.ПометкаУдаления
		|	И Не Контроль.СнятСКонтроля";
	
	Запрос.УстановитьПараметр("Предмет", Действие);
	Запрос.УстановитьПараметр("ИдентификаторВышестоящегоЭтапа",
		Новый УникальныйИдентификатор(ИдентификаторВышестоящегоЭтапа));
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат Справочники.Контроль.ПустаяСсылка();
	КонецЕсли;
	
	Выборка = РезультатЗапроса.Выбрать();
	Выборка.Следующий();
	
	Возврат Выборка.Ссылка;
	
КонецФункции

// Получает контролера из контрольной карточки подчиненного исполнения.
// 
// Параметры:
//  Действие - СправочникСсылка.ДействияИсполнения
//  ИдентификаторВышестоящегоЭтапа - Строка
// 
// Возвращаемое значение:
//  - СправочникСсылка.Сотрудники
//  - СправочникСсылка.ПолныеРоли
// 
Функция КонтролерПодчиненногоИсполнения(Действие, ИдентификаторВышестоящегоЭтапа) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	Контроль.Контролер
		|ИЗ
		|	Справочник.Контроль КАК Контроль
		|ГДЕ
		|	Контроль.Предмет = &Предмет
		|	И Контроль.ИдентификаторВышестоящегоЭтапа = &ИдентификаторВышестоящегоЭтапа
		|	И Не Контроль.ПометкаУдаления
		|	И Не Контроль.СнятСКонтроля";
	
	Запрос.УстановитьПараметр("Предмет", Действие);
	Запрос.УстановитьПараметр("ИдентификаторВышестоящегоЭтапа",
		Новый УникальныйИдентификатор(ИдентификаторВышестоящегоЭтапа));
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат Справочники.Сотрудники.ПустаяСсылка();
	КонецЕсли;
	
	Выборка = РезультатЗапроса.Выбрать();
	Выборка.Следующий();
	
	Если ЗначениеЗаполнено(Выборка.Контролер) Тогда
		Возврат Выборка.Контролер;
	Иначе
		Возврат Справочники.Сотрудники.ПустаяСсылка();
	КонецЕсли;
	
КонецФункции

#КонецОбласти
