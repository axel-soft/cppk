#Область ПрограммныйИнтерфейс

// Открывает файл или версию файла
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма версий файлов ознакомления или согласования
//  ФайлВерсияФайла - СправочникСсылка.ВерсииФайлов, СправочникСсылка.Файлы - Ссылка на файл или версию файла
//
Процедура ОткрытьВерсиюНаКлиенте(Форма, ФайлВерсияФайла) Экспорт
	
	Если ТипЗнч(ФайлВерсияФайла) = Тип("СправочникСсылка.Файлы") Тогда
		РаботаСФайламиКлиент.Открыть(
			РаботаСФайламиВызовСервера.ДанныеФайлаДляОткрытия(
				ФайлВерсияФайла, Неопределено, Форма.УникальныйИдентификатор),
			Форма.УникальныйИдентификатор);
	Иначе
		РаботаСФайламиКлиент.Открыть(
			РаботаСФайламиВызовСервера.ДанныеФайлаДляОткрытия(
				Неопределено, ФайлВерсияФайла, Форма.УникальныйИдентификатор),
			Форма.УникальныйИдентификатор);
	КонецЕсли;
	
КонецПроцедуры

// Показывает сравнение версий файлов
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма версий файлов ознакомления или согласования
//  ДеревоВерсий - ДанныеФормыДерево - 
//
Процедура СравнитьВерсииФайловВДереве(Форма, ДеревоВерсий) Экспорт
	
	УникальныйИдентификатор = Форма.УникальныйИдентификатор;
	ЭлементДеревоВерсий = Форма.Элементы.ДеревоВерсий;
	ЧислоВыделенныхСтрок = ЭлементДеревоВерсий.ВыделенныеСтроки.Количество();
	
	Если ЧислоВыделенныхСтрок = 2 ИЛИ ЧислоВыделенныхСтрок = 1 Тогда
		
		#Если НЕ ВебКлиент Тогда	
			
			Если ЧислоВыделенныхСтрок = 2 Тогда
				Ссылка1 = ДеревоВерсий.НайтиПоИдентификатору(ЭлементДеревоВерсий.ВыделенныеСтроки[0]).Ссылка;
				Ссылка2 = ДеревоВерсий.НайтиПоИдентификатору(ЭлементДеревоВерсий.ВыделенныеСтроки[1]).Ссылка;
			ИначеЕсли ЧислоВыделенныхСтрок = 1 Тогда
				ПоказатьПредупреждение(, НСтр("ru = 'Выберите 2 версии файла для сравнения'"));
				Возврат;
			КонецЕсли;
			
			СпособСравненияВерсийФайлов = Неопределено;
			Расширение = НРег(ЭлементДеревоВерсий.ТекущиеДанные.Расширение);
			
			Если Не РаботаСФайламиКлиент.РасширениеПоддерживаетсяДляСравнения(Расширение) Тогда
				Возврат;
			КонецЕсли;
			
			Если СпособСравненияВерсийФайлов = Неопределено Тогда
				СпособСравненияВерсийФайлов = ФайловыеФункцииСлужебныйКлиентСервер.ПерсональныеНастройкиРаботыСФайлами().СпособСравненияВерсийФайлов;
				Если СпособСравненияВерсийФайлов = Неопределено Тогда // первый вызов - еще не инициализирована настройка
					ПараметрыФормы = Новый Структура;
					ОткрытьФорму("Справочник.ВерсииФайлов.Форма.ФормаВыбораСпособаСравненияВерсий", ПараметрыФормы, Форма);
					Возврат;
				КонецЕсли;
			КонецЕсли;
			
			Если Расширение = "txt" И СпособСравненияВерсийФайлов = "Р7Builder" Тогда
				
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Сравнение файлов txt не поддерживается с помощью Р7 Builder.'"));
				Возврат;
				
			КонецЕсли;	
			
			ПродолжитьСравнениеВерсий(УникальныйИдентификатор, СпособСравненияВерсийФайлов, Ссылка1, Ссылка2);
		#Иначе
			ПоказатьПредупреждение(, НСтр("ru = 'Сравнение версий в веб-клиенте не поддерживается!'"));
		#КонецЕсли
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Продолжение процедуры (см. выше).
//
Процедура ПослеПолучитьФайлВерсииВРабочийКаталог1(Результат, ПараметрыВыполнения) Экспорт
	
	УникальныйИдентификатор = ПараметрыВыполнения.УникальныйИдентификатор;
	Если Результат.ФайлПолучен Тогда
		ПараметрыВыполнения.ПолноеИмяФайла1 = Результат.ПолноеИмяФайла;
		Обработчик = Новый ОписаниеОповещения(
			"ПослеПолучитьФайлВерсииВРабочийКаталог2", ЭтотОбъект, ПараметрыВыполнения);
		РаботаСФайламиКлиент.ПолучитьФайлВерсииВРабочийКаталог(Обработчик, 
			ПараметрыВыполнения.ДанныеФайла2, ПараметрыВыполнения.ПолноеИмяФайла2, УникальныйИдентификатор);
	КонецЕсли;
	
КонецПроцедуры

// Продолжение процедуры (см. выше).
//
Процедура ПослеПолучитьФайлВерсииВРабочийКаталог2(Результат, ПараметрыВыполнения) Экспорт
	
	Если Результат.ФайлПолучен Тогда
		
		ПараметрыВыполнения.ПолноеИмяФайла2 = Результат.ПолноеИмяФайла;
		
		ПутьКФайлу1 = "";
		ПутьКФайлу2 = "";
		
		Если ПараметрыВыполнения.ДанныеФайла1.НомерВерсии < ПараметрыВыполнения.ДанныеФайла2.НомерВерсии Тогда
			ПутьКФайлу1 = ПараметрыВыполнения.ПолноеИмяФайла1;
			ПутьКФайлу2 = ПараметрыВыполнения.ПолноеИмяФайла2;
		Иначе
			ПутьКФайлу1 = ПараметрыВыполнения.ПолноеИмяФайла2;
			ПутьКФайлу2 = ПараметрыВыполнения.ПолноеИмяФайла1;
		КонецЕсли;
		
		РаботаСФайламиКлиент.СравнитьФайлы(
			ПутьКФайлу1, ПутьКФайлу2, ПараметрыВыполнения.СпособСравненияВерсийФайлов);
		
	КонецЕсли;
	
	Состояние();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции
 
// Продолжение процедуры
//
// Параметры:
//  УникальныИдентификатор - УникальныИдентификатор - УИ формы 
//  СпособСравненияВерсийФайлов - Строка - программа, которой сравниваются файлы
//  Ссылка1 - СправочникСсылка.ВерсииФайлов, СправочникСсылка.Файлы - первый файл, участвующий в сравнении
//  Ссылка2 - СправочникСсылка.ВерсииФайлов, СправочникСсылка.Файлы - второй файл, участвующий в сравнении
//
Процедура ПродолжитьСравнениеВерсий(УникальныйИдентификатор, СпособСравненияВерсийФайлов, Ссылка1, Ссылка2)
	
	#Если НЕ ВебКлиент Тогда
		
		Если СпособСравненияВерсийФайлов = Неопределено Тогда
			Возврат;
		КонецЕсли;
		
		Если ТипЗнч(Ссылка1) = Тип("СправочникСсылка.Файлы") Тогда
			ДанныеФайла1 = РаботаСФайламиВызовСервера.ДанныеФайлаДляОткрытия(
				Ссылка1, Неопределено, УникальныйИдентификатор);
		Иначе
			ДанныеФайла1 = РаботаСФайламиВызовСервера.ДанныеФайлаДляОткрытия(
				Неопределено, Ссылка1, УникальныйИдентификатор);
		КонецЕсли; 
			
		Если ТипЗнч(Ссылка2) = Тип("СправочникСсылка.Файлы") Тогда
			ДанныеФайла2 = РаботаСФайламиВызовСервера.ДанныеФайлаДляОткрытия(
				Ссылка2, Неопределено, УникальныйИдентификатор);
		Иначе
			ДанныеФайла2 = РаботаСФайламиВызовСервера.ДанныеФайлаДляОткрытия(
				Неопределено, Ссылка2, УникальныйИдентификатор);
		КонецЕсли;
		
		Если ДанныеФайла1.Версия = ДанныеФайла2.Версия Тогда
			ПоказатьПредупреждение(, НСтр("ru = 'Версии не отличаются.'"));
			Возврат;
		КонецЕсли;	
		
		ТекстСостояния = СтрШаблон(
			НСтр("ru = 'Выполняется сравнение версий файла ""%1""...'"), ДанныеФайла1.Ссылка);
		Состояние(ТекстСостояния);
		
		ПолноеИмяФайла1 = "";
		ПолноеИмяФайла2 = "";
		
		ПараметрыВыполнения = Новый Структура(
			"УникальныйИдентификатор,ДанныеФайла1, ДанныеФайла2, СпособСравненияВерсийФайлов, ПолноеИмяФайла1, ПолноеИмяФайла2", 
			УникальныйИдентификатор, ДанныеФайла1, ДанныеФайла2, СпособСравненияВерсийФайлов, ПолноеИмяФайла1, ПолноеИмяФайла2);
		Обработчик = Новый ОписаниеОповещения(
			"ПослеПолучитьФайлВерсииВРабочийКаталог1", ЭтотОбъект, ПараметрыВыполнения);
		РаботаСФайламиКлиент.ПолучитьФайлВерсииВРабочийКаталог(Обработчик, 
			ПараметрыВыполнения.ДанныеФайла1, ПараметрыВыполнения.ПолноеИмяФайла1, УникальныйИдентификатор);
		
	#КонецЕсли
	
КонецПроцедуры 

#КонецОбласти
