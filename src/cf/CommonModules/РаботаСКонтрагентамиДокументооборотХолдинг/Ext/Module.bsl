// @strict-types


#Область ПрограммныйИнтерфейс

// Вызывается при создании на сервере карточки контрагента
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - Форма карточки контрагента:
//    * Объект - СправочникОбъект.Контрагенты
//  Отказ - Булево
//  СтандартнаяОбработка - Булево
Процедура КарточкаКонтрагентаПриСозданииНаСервере(Форма, Отказ, СтандартнаяОбработка) Экспорт
	
	Если ОбщегоНазначенияДокументооборот.ЭтоДокументооборотХолдинга()
			Или Не ПолучитьФункциональнуюОпцию("ИспользоватьКОД") Тогда
		
		Возврат;
	КонецЕсли;
	
	УзелОбработки = Форма.Объект.УзелОбработки;
	Если Не ЗначениеЗаполнено(УзелОбработки) Тогда
		УзелОбработки = КОДСервер.ЦентральныйУзел().УникальныйИдентификатор();
	КонецЕсли;
	
	Если УзелОбработки = КОДПовтИсп.ИдентификаторТекущегоУзла() Тогда
		НастроитьКарточкуКонтрагентаДляТекущегоУзла(Форма);
	КонецЕсли;
	
КонецПроцедуры

// Вызывается при чтении на сервере карточки контрагента
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - Форма карточки контрагента:
//    * Объект - СправочникОбъект.Контрагенты
//  ТекущийОбъект - СправочникОбъект.Контрагенты
//
Процедура КарточкаКонтрагентаПриЧтенииНаСервере(Форма, ТекущийОбъект) Экспорт
	
	Если ОбщегоНазначенияДокументооборот.ЭтоДокументооборотХолдинга()
			Или Не ПолучитьФункциональнуюОпцию("ИспользоватьКОД") Тогда
		
		Возврат;
	КонецЕсли;
	
	ИмяРеквизита = РаботаСКонтрагентамиДокументооборотХолдингКлиентСервер.ИмяРеквизитаУзлаОбмена();
	
	Если Не РаботаСФормами.РеквизитСуществует(Форма, ИмяРеквизита) Тогда
		// Форма ещё не инициализирована. Будет заполнено при создании.
		Возврат;
	КонецЕсли;
	
	УзелОбработки = Форма.Объект.УзелОбработки;
	Если Не ЗначениеЗаполнено(УзелОбработки) Тогда
		УзелОбработки = КОДСервер.ЦентральныйУзел().УникальныйИдентификатор();
	КонецЕсли;
	
	Форма[ИмяРеквизита] = КОДПовтИсп.УзелПредставлениеПоИдентификатору(Строка(УзелОбработки));
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура НастроитьКарточкуКонтрагентаДляТекущегоУзла(Форма)
	
	ИмяЭлемента = РаботаСКонтрагентамиДокументооборотХолдингКлиентСервер.ИмяЭлементаУзлаОбмена();
	ИмяРеквизита = РаботаСКонтрагентамиДокументооборотХолдингКлиентСервер.ИмяРеквизитаУзлаОбмена();
	
	УзелОбработки = Форма.Объект.УзелОбработки;
	Если Не ЗначениеЗаполнено(УзелОбработки) Тогда
		УзелОбработки = КОДСервер.ЦентральныйУзел().УникальныйИдентификатор();
	КонецЕсли;
	
	РаботаСФормами.ДобавитьРеквизит(Форма, ИмяРеквизита, Тип("Строка"));
	Форма[ИмяРеквизита] = КОДПовтИсп.УзелПредставлениеПоИдентификатору(Строка(УзелОбработки));
	
	Элемент = РаботаСФормами.ДобавитьПолеНадписи(Форма.Элементы, ИмяЭлемента,
		Форма.Элементы.ГруппаОсновныеСведения,
		ИмяРеквизита);
	Элемент.Заголовок = Метаданные.ОбщиеРеквизиты.УзелОбработки.Синоним;
	Элемент.Гиперссылка = Истина;
	Элемент.УстановитьДействие("Нажатие", "Подключаемый_ДокументооборотНажатие");
	
КонецПроцедуры

#КонецОбласти