
#Область ПрограммныйИнтерфейс

// Обработчик завершения, вызываемый из формы инициализации узла.
//
// Параметры:
//	Параметры - Структура Из КлючИЗначение.
//	ДополнительныеПараметры - Структура Из КлючИЗначение.
//
Процедура ОткрытьФормуИнициализацииУзла(Параметры, ДополнительныеПараметры) Экспорт
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ОкрытьФормуИнициализацииУзлаЗавершение", ЭтотОбъект, Параметры);
	Форма = ОткрытьФорму(
		"Справочник.УзлыКОД.Форма.ИнициализацияУзла",,,,,,
		ОписаниеОповещения,
		РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс);
		
	Если Форма = Неопределено Тогда
		ВыполнитьОбработкуОповещения(Параметры.ОбработкаПродолжения);
	КонецЕсли;
	
КонецПроцедуры

// Обработчик завершения, вызываемый из формы инициализации узла.
//
// Параметры:
//	Результат - Булево - Истина, если узел инициализирован.
//	ДополнительныеПараметры - Структура Из КлючИЗначение.
//
Процедура ОкрытьФормуИнициализацииУзлаЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Ложь Тогда
		ДополнительныеПараметры.Отказ = Истина;
	КонецЕсли;
	
	ОписаниеОповещения = Новый ОписаниеОповещения(
		ДополнительныеПараметры.ОбработкаПродолжения.ИмяПроцедуры,
		ДополнительныеПараметры.ОбработкаПродолжения.Модуль,
		ДополнительныеПараметры);
	
	ВыполнитьОбработкуОповещения(ОписаниеОповещения, ДополнительныеПараметры);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// См. ОбщегоНазначенияКлиентПереопределяемый.ПередНачаломРаботыСистемы.
Процедура ПередНачаломРаботыСистемы(Параметры) Экспорт
	
	ПараметрыКлиента = СтандартныеПодсистемыКлиент.ПараметрыРаботыКлиентаПриЗапуске();
	
	Если Не ПараметрыКлиента.Свойство("НеобходимоОткрытьФормуИнициализацииУзла") Тогда
		Возврат;
	КонецЕсли;
	
	Параметры.ИнтерактивнаяОбработка = Новый ОписаниеОповещения(
		"ОткрытьФормуИнициализацииУзла", ОбщегоНазначенияДокументооборотХолдингКлиент);
	
КонецПроцедуры

#КонецОбласти
