
#Область ПрограммныйИнтерфейс

// Выполняет обработку результата процедуры создания файла инициализации узла.
//
// Параметры:
//	Результат - см. ДлительныеОперации.ПараметрыВыполненияФункции
//	Контекст - Произвольный - Значение, которое было указано при начале процедуры создания файла.
//
Процедура СоздатьФайлНастроекУзлаОбработатьРезультат(Результат, Контекст) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Результат.Статус = "Выполнено" Тогда
		СохранитьФайлИнициализацииУзла(Результат.АдресРезультата, Контекст.Узел);
	Иначе
		ТекстОшибки = СтрШаблон(
			Нстр("ru = 'Во время создания файла настроек узла произошла ошибка:
						|%1'", ОбщегоНазначенияКлиент.КодОсновногоЯзыка()),
			Результат.ПодробноеПредставлениеОшибки);
	
		ВызватьИсключение ТекстОшибки;
	КонецЕсли;
	
КонецПроцедуры

// Выполняет отображение диалога выбора файла настроек узла после установки расширения работы с файлами.
//
// Параметры:
//	РасширениеПодключено - Булево - Результат проверки расширения работы с файлами.
//	Контекст - см. КонтекстВыбораФайлаНастроекУзла.
//
Процедура ВыбратьФайлНастроекПослеПроверкиРасширенияРаботыСФайлами(РасширениеПодключено, Контекст) Экспорт

	Если РасширениеПодключено Тогда
		ПоказатьДиалогВыбораФайлаНастроекУзла(Контекст);
	Иначе
		ТекстОшибки = НСтр("ru = 'Не удалось установить расширение для работы с 1С:Предприятием.'");
		ПредупреждениеАсинх(
			ТекстОшибки, , Нстр("ru = 'Внимание'", ОбщегоНазначенияКлиент.КодОсновногоЯзыка()));
	КонецЕсли;
	
КонецПроцедуры

// Выполняет загрузку файла настроек узла.
//
// Параметры:
//	ДанныеФайла - ДвоичныеДанные - Двоичные данные файла настроек узла.
//	Контекст - Произвольный - Значение, которое было указано при начале процедуры загрузки.
//
Процедура ЗагрузитьНастройкиУзлаПослеСозданияДвоичныхДанных(ДанныеФайла, Контекст) Экспорт

	ПараметрыОперации = Новый Структура;
	ПараметрыОперации.Вставить("ДанныеФайла", ДанныеФайла);
	ПараметрыОперации.Вставить("ВключитьРегламентныеЗаданияОбмена", Контекст.ВключитьРегламентныеЗаданияОбмена);
	
	ДлительнаяОперация = КОДВызовСервера.НачатьЗагрузкуНастроекУзла(ПараметрыОперации);
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(Контекст.Форма);
	
	ДлительныеОперацииКлиент.ОжидатьЗавершение(
		ДлительнаяОперация,
		Новый ОписаниеОповещения("ЗагрузитьНастройкиОбработатьРезультат", КОДСлужебныйКлиент, Контекст),
		ПараметрыОжидания);

КонецПроцедуры

// Выполняет обработку результата процедуры загрузки настроек из файла.
//
// Параметры:
//	РезультатЗагрузки - см. ДлительныеОперации.ПараметрыВыполненияФункции
//	Контекст - Произвольный - Значение, которое было указано при начале процедуры загрузки.
//
Процедура ЗагрузитьНастройкиОбработатьРезультат(РезультатЗагрузки, Контекст) Экспорт
	
	Если РезультатЗагрузки = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ВыполнитьОбработкуОповещения(Контекст.ОбработчикЗавершения, РезультатЗагрузки);
		
КонецПроцедуры

// Выполняет обработку результата процедуры подготовки к отложенной настройке.
//
// Параметры:
//	РезультатПодготовки - см. ДлительныеОперации.ПараметрыВыполненияФункции
//	Контекст - Произвольный - Значение, которое было указано при начале процедуры инициализации.
//
Процедура ПодготовитьУзелКОтложеннойНастройкеОбработатьРезультат(РезультатПодготовки, Контекст) Экспорт
	
	Если РезультатПодготовки = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ВыполнитьОбработкуОповещения(Контекст.ОбработчикЗавершения, РезультатПодготовки);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Выполняет сохранение файла настроек узла в локальную файловую систему пользователя.
//
// Параметры:
//	АдресДанных - Строка - Адрес файла во временном хранилище на сервере.
//	Узел - СправочникСсылка.УзлыКОД - Узел, настройки которого сохраняются.
//
Асинх Процедура СохранитьФайлИнициализацииУзла(АдресДанных, Узел)
	
	КодУзла = ОбщегоНазначенияДокументооборотВызовСервера.ЗначениеРеквизитаОбъекта(Узел, "Код");
	
	ПараметрыДиалога = Новый ПараметрыДиалогаПолученияФайлов;
	ПараметрыДиалога.ВыборКаталога = Истина;
	ПараметрыДиалога.Заголовок =
		Нстр("ru = 'Укажите каталог для сохранения файла настроек узла'", ОбщегоНазначенияКлиент.КодОсновногоЯзыка());
	
	Ждать ПолучитьФайлССервераАсинх(
		АдресДанных, СтрШаблон("node%1.init", КодУзла), ПараметрыДиалога);
	
	УдалитьИзВременногоХранилища(АдресДанных);
	
КонецПроцедуры

// Выполняет отображение пользователю диалога выбора файла.
//
// Параметры:
//	Контекст - см. КонтекстВыбораФайлаНастроекУзла.
//
Асинх Процедура ПоказатьДиалогВыбораФайлаНастроекУзла(Контекст)
	
	ДиалогВыбораФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	ДиалогВыбораФайла.Фильтр =
		Нстр("ru = 'Выберите файл настроек'", ОбщегоНазначенияКлиент.КодОсновногоЯзыка());
	ДиалогВыбораФайла.Фильтр = СтрШаблон(
		Нстр("ru = 'Файл настроек узла %1'", ОбщегоНазначенияКлиент.КодОсновногоЯзыка()),
		"(*.init)|*.init");
	ДиалогВыбораФайла.МножественныйВыбор = Ложь;
	ДиалогВыбораФайла.ПолноеИмяФайла = Контекст.ИмяФайла;
	
	РезультатВыбора = Ждать ДиалогВыбораФайла.ВыбратьАсинх();
	Если РезультатВыбора = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ВыполнитьОбработкуОповещения(Контекст.Оповещение, РезультатВыбора[0]);
	
КонецПроцедуры

#КонецОбласти
