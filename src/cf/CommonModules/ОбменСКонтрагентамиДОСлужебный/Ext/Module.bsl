
#Область ПрограммныйИнтерфейс

#Область ПодпискиНаСобытия

// Подписка на событие. Синхронизирует состояния в РС СостоянияВерсийЭД и СостоянияЭДОДокументооборот
//
// Параметры:
//  Источник - РегистрСведенийНаборЗаписей.СостоянияПоОбъектамУчетаЭДО - Набор, из которого необходимо получить
//                                                                       сведения о состоянии.
//  Отказ - Булево
//  Замещение - Булево
//
Процедура СинхронизироватьСостоянияЭДДляДО(Источник, Отказ, Замещение) Экспорт
	
	Возврат;
	
	Если Источник.ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	ДанныеДляСинхронизации = ДанныеДляСинхронизацииСостояния(Источник);
	
	Для Каждого ДанныеСостояния Из ДанныеДляСинхронизации Цикл
		
		Записывать = Ложь;
		ДанныеСостоянияДляЗаписи = РегистрыСведений.СостояниеДокументовПоЭДО.НовыеДанныеСостоянияДляДобавления();
		
		ДанныеСостоянияДляЗаписи.Направление = ДанныеСостояния.Направление;
		ДанныеСостоянияДляЗаписи.УчетнаяЗаписьЭДО = ДанныеСостояния.УчетнаяЗаписьЭДО;
		
		Если ДанныеСостояния.СостояниеЭДО = Перечисления.СостоянияДокументовЭДО.ОжидаетсяПодтверждение
			ИЛИ ДанныеСостояния.СостояниеЭДО = Перечисления.СостоянияДокументовЭДО.ОжидаетсяПодтверждениеОператора
			ИЛИ ДанныеСостояния.СостояниеЭДО = Перечисления.СостоянияДокументовЭДО.ОжидаетсяПередачаОператору Тогда
			
			Записывать = Истина;
			ДанныеСостоянияДляЗаписи.Состояние = Перечисления.СостоянияЭДОДокументооборот.Отправлен;
			
		ИначеЕсли ДанныеСостояния.СостояниеЭДО = Перечисления.СостоянияДокументовЭДО.ТребуетсяУтверждение
			ИЛИ (ДанныеСостояния.СостояниеЭДО = Перечисления.СостоянияДокументовЭДО.ТребуетсяПодписание
				И ДанныеСостояния.Направление <> Перечисления.НаправленияЭДО.Исходящий) Тогда
			
			Записывать = Истина;
			ДанныеСостоянияДляЗаписи.Состояние = Перечисления.СостоянияЭДОДокументооборот.НаПодписи;
			
		ИначеЕсли ДанныеСостояния.СостояниеЭДО = Перечисления.СостоянияДокументовЭДО.ТребуетсяОтправкаИзвещения
			И Не ДанныеСостояния.ТребуетсяПодтверждение Тогда
			
			Записывать = Истина;
			ДанныеСостоянияДляЗаписи.Состояние = Перечисления.СостоянияЭДОДокументооборот.ОбменЗавершен;
			
		ИначеЕсли ДанныеСостояния.СостояниеЭДО = Перечисления.СостоянияДокументовЭДО.ТребуетсяОтправкаИзвещения
			И ДанныеСостояния.СостояниеЗаписанное <> Перечисления.СостоянияЭДОДокументооборот.ОбменЗавершен
			И ДанныеСостояния.Направление <> Перечисления.НаправленияЭДО.Исходящий Тогда
			
			Записывать = Истина;
			ДанныеСостоянияДляЗаписи.Состояние = Перечисления.СостоянияЭДОДокументооборот.НаПодписи;
			
		ИначеЕсли ДанныеСостояния.СостояниеЭДО = Перечисления.СостоянияДокументовЭДО.ТребуетсяОтправкаИзвещения
			И ДанныеСостояния.СостояниеЗаписанное <> Перечисления.СостоянияЭДОДокументооборот.ОбменЗавершен
			И ДанныеСостояния.ТребуетсяПодтверждение
			И ДанныеСостояния.Направление <> Перечисления.НаправленияЭДО.Исходящий Тогда
			
			Записывать = Истина;
			ДанныеСостоянияДляЗаписи.Состояние = Перечисления.СостоянияЭДОДокументооборот.НаПодписи;
			
		ИначеЕсли ДанныеСостояния.СостояниеЭДО = Перечисления.СостоянияДокументовЭДО.ТребуетсяОтправкаОтклонения Тогда
			
			Записывать = Истина;
			ДанныеСостоянияДляЗаписи.Состояние = Перечисления.СостоянияЭДОДокументооборот.ОжидаетсяИсправление;
			
		ИначеЕсли ДанныеСостояния.СостояниеЭДО = Перечисления.СостоянияДокументовЭДО.ТребуетсяОтправка
			И ДанныеСостояния.СостояниеЗаписанное = Перечисления.СостоянияЭДОДокументооборот.НаПодписи Тогда
			
			Записывать = Истина;
			ДанныеСостоянияДляЗаписи.Состояние = Перечисления.СостоянияЭДОДокументооборот.Подписан;
			
		Иначе
			
			ИндексЗначения = Перечисления.СостоянияДокументовЭДО.Индекс(ДанныеСостояния.СостояниеЭДО);
			СостояниеЭД = Метаданные.Перечисления.СостоянияДокументовЭДО.ЗначенияПеречисления[ИндексЗначения].Имя;
				
			Если Не Метаданные.Перечисления.СостоянияЭДОДокументооборот.ЗначенияПеречисления.Найти(СостояниеЭД) = Неопределено Тогда
				
				Записывать = Истина;
				ДанныеСостоянияДляЗаписи.Состояние = Перечисления.СостоянияЭДОДокументооборот[СостояниеЭД];
					
			КонецЕсли; 
			
		КонецЕсли;
		
		Если Записывать Тогда
			РегистрыСведений.СостояниеДокументовПоЭДО.Добавить(
				ДанныеСостояния.ДокументДО,
				ДанныеСостояния.Контрагент,
				ДанныеСостоянияДляЗаписи);
			
			РегистрыСведений.ПредпросмотрФайлов.Удалить(ДанныеСостояния.ФайлДО);
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ОбновитьВизуализациюФайлаДОДокументаЭДО(ДокументЭДО) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	Файлы.Ссылка КАК ФайлДО
		|ИЗ
		|	РегистрСведений.ОбъектыУчетаДокументовЭДО КАК ОбъектыУчетаДокументовЭДО
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Файлы КАК Файлы
		|		ПО ОбъектыУчетаДокументовЭДО.ОбъектУчета = Файлы.ТекущаяВерсия
		|ГДЕ
		|	ОбъектыУчетаДокументовЭДО.ЭлектронныйДокумент = &ДокументЭДО";
	Запрос.УстановитьПараметр("ДокументЭДО", ДокументЭДО);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		РегистрыСведений.ПредпросмотрФайлов.Удалить(Выборка.ФайлДО);
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

// Находит ссылку на справочник по переданному реквизиту.
//
// Параметры:
//  ИмяСправочника - Строка - Имя справочника, объект которого надо найти.
//  ИмяРеквизита - Строка - Имя реквизита, по которому будет проведен поиск.
//  ЗначРеквизита - Произвольный - значение реквизита, по которому будет проведен поиск.
//  Результат - СправочникСсылка - Ссылка на найденный объект.
//  Владелец - СправочникСсылка - Ссылка на владельца для поиска в иерархическом справочнике.
//  ТолькоНеПомеченные - Булево - Исключить из поиска помеченные на удаление.
//
Процедура НайтиСсылкуНаОбъектПоРеквизиту(
	ИмяСправочника, 
	ИмяРеквизита, 
	ЗначРеквизита, 
	Результат, 
	Владелец = Неопределено,
	ТолькоНеПомеченные = Ложь) Экспорт
	
	Результат = Неопределено;
	
	ОбъектМетаданных = Метаданные.Справочники[ИмяСправочника];
	Если НЕ ОбщегоНазначения.ЭтоСтандартныйРеквизит(ОбъектМетаданных.СтандартныеРеквизиты, ИмяРеквизита)
		И НЕ ОбъектМетаданных.Реквизиты.Найти(ИмяРеквизита) <> Неопределено Тогда
		
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ИскСправочник.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник." + ИмяСправочника + " КАК ИскСправочник
		|ГДЕ
		|	ИскСправочник." + ИмяРеквизита + " = &ЗначРеквизита";
	
	Если ЗначениеЗаполнено(Владелец) Тогда
		Запрос.Текст = Запрос.Текст + " И ИскСправочник.Владелец = &Владелец";
		Запрос.УстановитьПараметр("Владелец", Владелец);
	КонецЕсли;
	
	Если ТолькоНеПомеченные Тогда
		Запрос.Текст = Запрос.Текст + " И ИскСправочник.ПометкаУдаления = Ложь";
	КонецЕсли;
	
	Запрос.УстановитьПараметр("ЗначРеквизита", ЗначРеквизита);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Результат = Выборка.Ссылка;
	КонецЕсли;
	
КонецПроцедуры

// Выполняет поиск контактного лица контрагента по данным сертификата ЭП, при отсутствии добавляет новое.
//
Функция НайтиДобавитьКонтактноеЛицоИзСертификата(Контрагент, Сертификат) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	КонтактноеЛицо = Справочники.КонтактныеЛица.ПустаяСсылка();
	
	Если Не ЗначениеЗаполнено(Сертификат) Тогда
		Возврат КонтактноеЛицо;
	КонецЕсли;
	
	Если ТипЗнч(Сертификат) = Тип("ХранилищеЗначения") Тогда
		ДвоичныеДанныеСертификата = Сертификат.Получить();
	Иначе
		ДвоичныеДанныеСертификата = Сертификат;
	КонецЕсли;
	
	Субъект = ЭлектроннаяПодписьКлиентСервер.СвойстваСубъектаСертификата(
		Новый СертификатКриптографии(ДвоичныеДанныеСертификата));
		
	Если Не Субъект.Свойство("ОбщееИмя") Тогда
		Возврат КонтактноеЛицо;		
	КонецЕсли;
	
	ФИОКонтактногоЛица = Новый Массив;
	Если ЗначениеЗаполнено(Субъект.Фамилия) Тогда
		ФИОКонтактногоЛица.Добавить(Субъект.Фамилия);
	КонецЕсли;
	Если ЗначениеЗаполнено(Субъект.Имя) Тогда
		ФИОКонтактногоЛица.Добавить(Субъект.Имя);
		
		Если ЗначениеЗаполнено(Субъект.Отчество) Тогда
			ФИОКонтактногоЛица.Добавить(Субъект.Отчество);
		КонецЕсли;	
	КонецЕсли;
	Если Не ФИОКонтактногоЛица.Количество() Тогда
		ФИОКонтактногоЛица.Добавить(Субъект.ОбщееИмя);
	КонецЕсли;
	НаименованиеКонтактногоЛица = СтрСоединить(ФИОКонтактногоЛица, " ");
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	КонтактныеЛица.Ссылка КАК КонтактноеЛицо
		|ИЗ
		|	Справочник.КонтактныеЛица КАК КонтактныеЛица
		|ГДЕ
		|	КонтактныеЛица.Владелец = &Владелец
		|	И НЕ КонтактныеЛица.ПометкаУдаления
		|	И КонтактныеЛица.Наименование = &Наименование";
		
	Запрос.УстановитьПараметр("Владелец", Контрагент);
	Запрос.УстановитьПараметр("Наименование", НаименованиеКонтактногоЛица);
		
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		
		// Добавляем новое контактное лицо
		КонтактноеЛицо = Справочники.КонтактныеЛица.СоздатьЭлемент();
		КонтактноеЛицо.Владелец = Контрагент;
		КонтактноеЛицо.Наименование = НаименованиеКонтактногоЛица;
		Субъект.Свойство("Должность", КонтактноеЛицо.Должность);
		КонтактноеЛицо.Комментарий = НСтр("ru = 'Создан при загрузке ЭД.'");
		КонтактноеЛицо.ОбменДанными.Загрузка = Истина;
		КонтактноеЛицо.Записать();
		
		КонтактноеЛицо = КонтактноеЛицо.Ссылка;
		
	Иначе
		
		Выборка = РезультатЗапроса.Выбрать();
		Выборка.Следующий();
		
		КонтактноеЛицо = Выборка.КонтактноеЛицо; 
		
	КонецЕсли;
			
	Возврат КонтактноеЛицо;
	
КонецФункции

// Определяет параметры электронного документа по типу владельца.
//
// Параметры:
//  Источник                       - Объект, ЛюбаяСсылка - документ или справочник источника.
//  ПараметрыЭД - ТаблицаЗначений - параметры источника, необходимые для определения настроек обмена электронными документами: 
//    * Тип                - Произвольный - значение элемента структуры возвращаемой
//                           см. ОбменСКонтрагентами.ТипыДокументов. Используются все ключи, кроме УПД, УКД. Обязательный.
//                           Необходимость формирования УПД, УКД определяется на основании нескольких критериев
//                           (указанный тип, вариант формирования универсальных документов, настройки отправки
//                           электронных документов). 
//    * ФормированиеУниверсальногоДокумента - Произвольный - значение элемента структуры, возвращаемой
//                           см. ОбменСКонтрагентами.ВариантыФормированияУниверсальныхДокументов. Обязательный при указании типов:
//                            - ТоварнаяНакладная
//                            - АктВыполненныхРабот
//                            - АктНаПередачуПрав
//                            - СоглашениеОбИзмененииСтоимости
//                            - СчетФактура
//                            - КорректировочныйСчетФактура
//    * Направление        - Произвольный - значение элемента структуры возвращаемой
//                           см. ОбменСКонтрагентами.НаправленияДокументов(). Обязательный.
//    * Организация        - ОпределяемыйТип.Организация - организация документа.
//    * Контрагент         - ОпределяемыйТип.КонтрагентБЭД - контрагент документа.
//    * ДоговорКонтрагента - ОпределяемыйТип.ДоговорСКонтрагентомЭДО - договор контрагента.
//
Процедура ЗаполнитьПараметрыЭДПоИсточнику(Знач Источник, ПараметрыЭД, ФорматCML = Ложь) Экспорт
	
	Если Не ДелопроизводствоКлиентСервер.ЭтоДокументПредприятия(Источник) 
			И Не (ТипЗнч(Источник) = Тип("ДанныеФормыСтруктура") И Источник.Свойство("Ссылка") 
					И ДелопроизводствоКлиентСервер.ЭтоДокументПредприятия(Источник.Ссылка))
			И Не (ТипЗнч(Источник) = Тип("СправочникСсылка.ВерсииФайлов") 
					Или ТипЗнч(Источник) = Тип("СправочникОбъект.ВерсииФайлов")) Тогда
		
		Возврат;
	КонецЕсли;
	
	СтрокаПараметров = ПараметрыЭД.Добавить();
	
	Если ОбщегоНазначения.ЭтоСсылка(ТипЗнч(Источник)) Тогда
		ЗаполнитьПараметрыЭДПоСсылке(Источник, СтрокаПараметров, ФорматCML);
	Иначе
		ЗаполнитьПараметрыЭДПоОбъекту(Источник, СтрокаПараметров, ФорматCML);
	КонецЕсли;
		
КонецПроцедуры

// Выполняется при получении описания основания электронного документа,
// которое используется для представления данных прикладного объекта в подсистеме.
//
// Параметры:
//  ОснованиеОбъект - ОпределяемыйТип.ОснованияЭлектронныхДокументов - объект или ссылка на основание электронного документа.
//  Описание - Структура - данные, описывающие основание электронного документа:
//   * Вид - Строка - представление вида объекта. По умолчанию синоним объекта метаданных.
//   * Организация - ОпределяемыйТип.Организация - организация основания.
//   * Контрагент - ОпределяемыйТип.КонтрагентБЭД - контрагент основания.
//   * Дата - Дата - дата основания.
//   * Номер - Строка - номер основания.
//   * СуммаДокумента - Число - сумма основания.
//  СтандартнаяОбработка - Булево - признак формирования описания по умолчанию. 
//                                  Если Ложь, то используются данные из параметра Описание. По умолчанию Истина.
//
Процедура ПолучитьОписаниеОснованияЭлектронногоДокумента(Знач ОснованиеОбъект, Описание, СтандартнаяОбработка) Экспорт
	
	Если Не ДелопроизводствоКлиентСервер.ЭтоДокумент(ОснованиеОбъект) 
			И Не ТипЗнч(ОснованиеОбъект) = Тип("СправочникОбъект.ВерсииФайлов") Тогда
			
		Возврат;
	КонецЕсли;
	
	СтандартнаяОбработка = Ложь;
	
	Если ТипЗнч(ОснованиеОбъект) = Тип("СправочникОбъект.ВерсииФайлов") Тогда 
		ВладелецОбъекта =  ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ОснованиеОбъект.Владелец, "ВладелецФайла");
		ОснованиеОбъект = ВладелецОбъекта.ПолучитьОбъект();
	КонецЕсли;
	
	Описание.Вид = СтрШаблон(НСтр("ru = '%1'"), ОснованиеОбъект.ВидДокумента);
	Описание.Контрагент = ОснованиеОбъект.Контрагент;
	Описание.СуммаДокумента = ОснованиеОбъект.Сумма;
	Описание.Дата = 
		?(ЗначениеЗаполнено(ОснованиеОбъект.ДатаРегистрации), ОснованиеОбъект.ДатаРегистрации, ОснованиеОбъект.ДатаСоздания);
	Описание.Номер = 
		?(ЗначениеЗаполнено(ОснованиеОбъект.РегистрационныйНомер), ОснованиеОбъект.РегистрационныйНомер, ОснованиеОбъект.Код);
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьУчетПоОрганизациям") Тогда
		Описание.Организация = ОснованиеОбъект.Организация;
	Иначе
		Описание.Организация = Справочники.Организации.ОрганизацияПоУмолчанию();
	КонецЕсли;
	
КонецПроцедуры

// Возврат признака физического лица.
//
// Параметры:
//  ДанныеКонтрагента - СправочникСсылка - ссылка на элемент справочника.
//  ПризнакФизЛица - Булево - Истина если физическое лицо.
//
Процедура ЭтоФизЛицо(ДанныеКонтрагента, ПризнакФизЛица) Экспорт
		
	ЮрФизЛицо = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДанныеКонтрагента, "ЮрФизЛицо");
	Если ЮрФизЛицо = Перечисления.ЮрФизЛицо.ФизЛицо
		Или ЮрФизЛицо = Перечисления.ЮрФизЛицо.ИндивидуальныйПредприниматель Тогда
		
		ПризнакФизЛица = Истина;
	КонецЕсли;

КонецПроцедуры

// Возвращает, возможно ли корректировка данного вида ЭД непосредственно, т.е. без создания отдельного документа.
//
// Параметры:
//  ВидДокумента - СправочникСсылка.ВидыДокуметовЭДО - Вид документа, для которого необходимо выполнить проверку.
//
Функция ВидДокументаКорректируетсяНепосредственно(ВидДокумента) Экспорт
	
	МассивТиповДокументов = Новый Массив;
	
	МассивТиповДокументов.Добавить(Перечисления.ТипыДокументовЭДО.ЗаказТовара);
	МассивТиповДокументов.Добавить(Перечисления.ТипыДокументовЭДО.КаталогТоваров);
	МассивТиповДокументов.Добавить(Перечисления.ТипыДокументовЭДО.ОтветНаЗаказ);
	МассивТиповДокументов.Добавить(Перечисления.ТипыДокументовЭДО.ПрайсЛист);
	МассивТиповДокументов.Добавить(Перечисления.ТипыДокументовЭДО.СчетНаОплату);
	МассивТиповДокументов.Добавить(Перечисления.ТипыДокументовЭДО.ОтчетОПродажахКомиссионногоТовара);
	МассивТиповДокументов.Добавить(Перечисления.ТипыДокументовЭДО.ОтчетОСписанииКомиссионногоТовара);
	
	МассивТиповДокументов.Добавить(Перечисления.ТипыДокументовЭДО.АктВзаимозачета);
	МассивТиповДокументов.Добавить(Перечисления.ТипыДокументовЭДО.АктСверки);
	МассивТиповДокументов.Добавить(Перечисления.ТипыДокументовЭДО.Ведомость);
	МассивТиповДокументов.Добавить(Перечисления.ТипыДокументовЭДО.ГарантийноеПисьмо);
	МассивТиповДокументов.Добавить(Перечисления.ТипыДокументовЭДО.Договор);
	МассивТиповДокументов.Добавить(Перечисления.ТипыДокументовЭДО.ДополнительноеСоглашение);
	МассивТиповДокументов.Добавить(Перечисления.ТипыДокументовЭДО.КС11);
	МассивТиповДокументов.Добавить(Перечисления.ТипыДокументовЭДО.КС2);
	МассивТиповДокументов.Добавить(Перечисления.ТипыДокументовЭДО.КС3);
	МассивТиповДокументов.Добавить(Перечисления.ТипыДокументовЭДО.Отчет);
	МассивТиповДокументов.Добавить(Перечисления.ТипыДокументовЭДО.ПлатежноеПоручение);
	МассивТиповДокументов.Добавить(Перечисления.ТипыДокументовЭДО.ПриложениеКАкту);
	МассивТиповДокументов.Добавить(Перечисления.ТипыДокументовЭДО.СоглашениеОбЭДО);
	МассивТиповДокументов.Добавить(Перечисления.ТипыДокументовЭДО.Спецификация);
	МассивТиповДокументов.Добавить(Перечисления.ТипыДокументовЭДО.Уведомление);
	МассивТиповДокументов.Добавить(Перечисления.ТипыДокументовЭДО.Прочее);
	
	ТипДокументаВида = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ВидДокумента, "ТипДокумента");
	
	Возврат (МассивТиповДокументов.Найти(ТипДокументаВида) <> Неопределено);
	
КонецФункции

// Возвращает массив видов документов для которых доступна отправка по ЭДО
// 
// Возвращаемое значение:
//   Массив из Справочкик.ВидыДокументов - Массив видов документов,
//                                                   для которых доступна отправка по ЭДО
Функция ВидыДокументовДляОтправкиПоЭДО() Экспорт
	
	Запрос = Новый Запрос;
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ВидыДокументов.Ссылка КАК ВидДокумента
		|ИЗ
		|	Справочник.ВидыДокументов КАК ВидыДокументов
		|ГДЕ
		|	НЕ ВидыДокументов.ПометкаУдаления
		|	И ВидыДокументов.ВестиУчетСторон";
	
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("ВидДокумента");
	
КонецФункции

// Проверяет, является ли выбранный документ документом аннулирования по ЭДО
// 
// Параметры:
//  Документ - ОпределяемыйТип.ДокументДОДляЭДО - проверяемый документ ДО.
//  ДанныеАннулирования - Структура из:
//      * Документ - В данное поле запишется аннулируемый документ ДО
//      * ЭД - В данное поле запишется аннулируемый ЭД
//      * ФайлЭДАннулирования - В данное поле запишется файл Предложения Об Аннулировании
//      * НаправлениеЭД - В данное поле запишется направление предложкния об аннулировании
//      * СостояниеЭДО - В данное поле запишется состояние Предложения Об Аннулировании
// 
// Возвращаемое значение:
//  Булево - Является ли переданный документ документом аннулирования ЭДО
// 
Функция ЭтоДокументАннулированияЭДО(Документ, ДанныеАннулирования = Неопределено) Экспорт
	
	// Аннулирование по ЭДО добавим позже
	Возврат Ложь;
	
КонецФункции

#Область ДополнительнаяОбработкаСобытийБЭД

Процедура ЗагрузитьОтветнуюПодписьВФайлДО(СообщениеОбъект, ОтветныеПодписи) Экспорт
	
	ДанныеДокументовДО = ДанныеСвязанныхДокументовДОПоСообщениюЭДО(СообщениеОбъект.Ссылка);
	
	Для Каждого Элемент Из ДанныеДокументовДО Цикл
		
		Документ = Элемент.Ключ;
		ДанныеДокумента = Элемент.Значение;
		
		ПодписанныеДанные = Новый Массив;
		
		Контрагент = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(
			СообщениеОбъект.ЭлектронныйДокумент, "Контрагент");
		
		ПараметрыПодписания = Новый Структура;
		ПараметрыПодписания.Вставить("РазрешитьЗаписьОбъектаИзДругойСистемы", Истина);
		
		УстановленныеПодписи = ЭлектроннаяПодпись.УстановленныеПодписи(ДанныеДокумента.ВерсияФайла);
		Для Каждого СвойстваПодписи Из ОтветныеПодписи.ПодписиОсновныхДанных Цикл
			Если ЭтоНоваяПодпись(УстановленныеПодписи, СвойстваПодписи) Тогда
				
				СвойстваПодписи.Вставить("УстановившийПодпись", Неопределено);
				СвойстваПодписи.Вставить("ПодписьВерна", Истина);
				
				Элемент = Новый Структура;
				Элемент.Вставить("ПодписанныйОбъект", ДанныеДокумента.ВерсияФайла);
				Элемент.Вставить("СвойстваПодписи", СвойстваПодписи);
				Элемент.Вставить("Контрагент", Контрагент);
				Элемент.Вставить("ПараметрыВыполнения", ПараметрыПодписания);
				ПодписанныеДанные.Добавить(Элемент);
			КонецЕсли;
		КонецЦикла;
		
		НачатьТранзакцию(РежимУправленияБлокировкойДанных.Управляемый);
		Попытка
			Блокировка = Новый БлокировкаДанных;
			ЭлементБлокировки = Блокировка.Добавить(
				СтрШаблон("Справочник.%1", ОбменСКонтрагентамиДОСлужебныйКлиентСервер.ИмяСправочникаДокументовДО()));
			ЭлементБлокировки.УстановитьЗначение("Ссылка", Документ);
			Блокировка.Заблокировать();
			
			ЗаблокироватьДанныеДляРедактирования(Документ);
			
			РаботаСЭП.ЗанестиИнформациюОПодписях(ПодписанныеДанные);
			
			ДокументОбъект = Документ.ПолучитьОбъект();
			Если ДокументОбъект.Стороны.Количество() > 0 Тогда
				Для Каждого Элемент Из ПодписанныеДанные Цикл
					СтрокаКонтрагента = ДокументОбъект.Стороны.Найти(Элемент.Контрагент, "Сторона");
					Если Не СтрокаКонтрагента = Неопределено Тогда
						ПодписантОтКонтрагента = НайтиДобавитьКонтактноеЛицоИзСертификата(
							Элемент.Контрагент, Элемент.СвойстваПодписи.Сертификат);
						ДатаПодписанияКонтрагентом = Элемент.СвойстваПодписи.ДатаПроверкиПодписи;
						
						СтрокаКонтрагента.Подписан = Истина;
						СтрокаКонтрагента.ДатаПодписи = ДатаПодписанияКонтрагентом;
						СтрокаКонтрагента.Подписал = ПодписантОтКонтрагента;
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
			
			ДокументОбъект.ДополнительныеСвойства.Вставить("ЗаписьПодписанногоОбъекта", Истина);
			ДокументОбъект.ДополнительныеСвойства.Вставить("РазрешитьЗаписьОбъектаИзДругойСистемы", Истина);
			ДокументОбъект.Записать();
			
			РазблокироватьДанныеДляРедактирования(Документ);
			
			ЗафиксироватьТранзакцию();
			
		Исключение
			
			ОтменитьТранзакцию();
			
			ТекстСообщения = СтрШаблон(
				НСтр("ru = 'Не удалось прикрепить ответные подписи к документу ""%1"" (%2). Причина: %3'"), 
				Документ,
				ПолучитьНавигационнуюСсылку(Документ),
				ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
				
			ВызватьИсключение ТекстСообщения;
			
		КонецПопытки;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаписатьЗаписьОДоверенностиВРегистрыДО(ПодписанныйОбъект, ХешПодписи, Доверенность) Экспорт
	
	Если Не ЗначениеЗаполнено(Доверенность) Тогда
		Возврат;
	КонецЕсли;
	
	УстановленныеПодписи = ЭлектроннаяПодпись.УстановленныеПодписи(ПодписанныйОбъект);
	
	СвойстваПодписи = Неопределено;
	
	Для Каждого Подпись Из УстановленныеПодписи Цикл
		ХешУстановленнойПодписи = КриптографияБЭД.ХешПодписи(Подпись.Подпись);
		
		Если ХешУстановленнойПодписи = ХешПодписи Тогда
			СвойстваПодписи = Подпись;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Если Не ЗначениеЗаполнено(СвойстваПодписи) Тогда
		Возврат;
	КонецЕсли;
	
	РаботаСЭП.ЗанестиИнформациюОДоверенностиЭП(
		СвойстваПодписи.ИдентификаторПодписи, Доверенность, СвойстваПодписи);
	
КонецПроцедуры

// Событие возникает при загрузке документа.
// Выполняется в одной транзакции.
// 
// Параметры:
// 	Сообщение - ДокументСсылка.СообщениеЭДО - ссылка на сообщение ЭДО.
// 	ДанныеСообщения - СтрокаТаблицыЗначений из См. СинхронизацияЭДО.НовыеДанныеОбъектов - данные документа для загрузки.
// 	КонтекстДиагностики - См. ОбработкаНеисправностейБЭД.НовыйКонтекстДиагностики.
Процедура ПриЗагрузкеСообщенияЭДО(Сообщение, ДанныеСообщения, КонтекстДиагностики) Экспорт
	
	Если ДанныеСообщения.ТипЭлементаРегламента = Перечисления.ТипыЭлементовРегламентаЭДО.ИнформацияПолучателя Тогда
		ОбработатьЗагрузкуОтветногоТитула(Сообщение, ДанныеСообщения, КонтекстДиагностики);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ДанныеДокументовПоЭДО

// Возвращает состояние документа 1С:Документоборот в ЭДО.
//
// Параметры:
//  Документ - ОпределяемыйТип.ДокументДОДляЭДО - Документ, для которого необходимо получить сведения о состоянии.
//  Дата - Дата - Дата, на которую необходимо получить сведения.
//  Контрагент - СправочникСсылка.Контрагенты - Контрагент, для которого необходимо получить сведения.
// 
// Возвращаемое значение:
//  см. РегистрыСведений.СостояниеДокументовПоЭДО.ПолучитьСостояниеДокумента
//
Функция СостояниеДокумента(Документ, Дата = Неопределено, Контрагент = Неопределено) Экспорт

	Возврат РегистрыСведений.СостояниеДокументовПоЭДО.ПолучитьСостояниеДокумента(Документ, Дата, Контрагент);

КонецФункции 

// Новые данные состояния для установки.
// 
// Возвращаемое значение:
//  см. РегистрыСведений.СостояниеДокументовПоЭДО.НовыеДанныеСостоянияДляДобавления
Функция НовыеДанныеСостоянияДляУстановки() Экспорт
	
	Возврат РегистрыСведений.СостояниеДокументовПоЭДО.НовыеДанныеСостоянияДляДобавления();
	
КонецФункции

// Устанавливает состояние документа документооборота по ЭДО
// 
// Параметры:
//  Документ - ОпределяемыйТип.ДокументДОДляЭДО - Документ для которого необходимо установить состояние
//  Контрагент - СправочникСсылка.Контрагенты - Контрагент для которого необходимо установить состояние
//  ДанныеСостояния - см. НовыеДанныеСостоянияДляУстановки
Процедура УстановитьСостояниеДокументаЭДО(Документ, Контрагент, ДанныеСостояния) Экспорт
	
	РегистрыСведений.СостояниеДокументовПоЭДО.Добавить(Документ, Контрагент, ДанныеСостояния);
	
КонецПроцедуры

// Возвращает параметры документа по ЭДО
// 
// Параметры:
//   ДокументДО - ОпределяемыйТип.ДокументДОДляЭДО - Документ, для которого необходимо выяснить состояния по ЭДО
// 
// Возвращаемое значение:
//  Неопределено, Структура - Параметры документа по ЭДО, если документ не может учавствовать в обмене ЭДО,
//                            тогда Неопределено:
//    * ДоступнаКорректировкаЭД - Булево - Доступна ли для вида документа непосредственная корректировка
//    * СостояниеДО - ПеречислениеСсылка.СостоянияЭДОДокументооборот - Состояние документа ДО по ЭДО
//    * Подписан - Булево - Подписан ли документ
//    * ТребуетсяПодтверждение - Булево - Требуется ли ответная подпись по документу
//    * Состояние - ПеречислениеСсылка.СостоянияДокументовЭДО - Состояние электронного документа, если он есть
//    * ТипДокументаЭДО - ПеречислениеСсылка.ТипыДокументовЭДО - Тип документа ЭДО
//    * ВидДокументаЭДО - СправочникСсылка.ВидыДокументовЭДО - Вид документа ЭДО
//    * Направление - ПеречислениеСсылка.НаправленияЭДО - Направление обмена ЭДО
//    * ЭлектронныйДокумент - ДокументСсылка.ЭлектронныйДокументВходящийЭДО - Документ ЭДО входящий
//                          - ДокументСсылка.ЭлектронныйДокументИсходящийЭДО - Документ ЭДО исходящий
//    * ЕстьЭД - Булево - Есть ли с документом связанный документ ЭДО
//    * ДокументВПакетеЭДО - Булево - Состоит ли документ в пакете по ЭДО
//    * ПакетЭДО - УникальныйИдентификатор, Неопределено - УИД пакета ЭДО, если документ состоит в пакете,
//                                                         Неопределено, если документ в пакете не состоит
//    * ДоступноИзменениеПакетаЭДО - Булево - Доступно ли изменение состава пакета ЭДО.
//    * ПакетЭДОНеОтражен - Булево - Истина, если во входящем пакете не по всем документам ЭДО созданы документы ДО
//    * ДоступныДействияПоДокументу - Булево - Доступны ли действия по одиночному документу.
//                                             Для исходящих документов доступны одиночные действия только вне пакета.
//                                             Для входящих - зависит от оператора ЭДО контрагента.
//    * ПоддерживаетсяПакетнаОбработка - Булево - Поддерживается ли пакетная обработка по данному документу.
//    * УчетнаяЗаписьЭДО - Строка - Идентификатор учетной записи ЭДО
//
Функция ПараметрыДокументаПоЭДО(ДокументДО) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	СтруктураВозврата = Новый Структура;
	СтруктураВозврата.Вставить("ЕстьЭД", Ложь);
	СтруктураВозврата.Вставить("ЭлектронныйДокумент", Неопределено);
	СтруктураВозврата.Вставить("Направление", Перечисления.НаправленияЭДО.ПустаяСсылка());
	СтруктураВозврата.Вставить("ВидДокументаЭДО", Справочники.ВидыДокументовЭДО.ПустаяСсылка());
	СтруктураВозврата.Вставить("ТипДокументаЭДО", Перечисления.ТипыДокументовЭДО.ПустаяСсылка());
	СтруктураВозврата.Вставить("Состояние", Перечисления.СостоянияДокументовЭДО.ПустаяСсылка());
	СтруктураВозврата.Вставить("ТребуетсяПодтверждение", Истина);
	СтруктураВозврата.Вставить("Подписан", Ложь);
	СтруктураВозврата.Вставить("СостояниеДО", Перечисления.СостоянияЭДОДокументооборот.НеСформирован);
	СтруктураВозврата.Вставить("ДоступнаКорректировкаЭД", Ложь);
	СтруктураВозврата.Вставить("ДокументВПакетеЭДО", Ложь);
	СтруктураВозврата.Вставить("ПакетЭДО", Неопределено);
	СтруктураВозврата.Вставить("ДоступноИзменениеПакетаЭДО", Ложь);
	СтруктураВозврата.Вставить("ДоступныДействияПоДокументу", Истина);
	СтруктураВозврата.Вставить("ПакетЭДОНеОтражен", Ложь);
	СтруктураВозврата.Вставить("ПоддерживаетсяПакетнаОбработка", Истина);
	СтруктураВозврата.Вставить("УчетнаяЗаписьЭДО", "");
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	Файлы.Ссылка КАК Файл,
		|	Файлы.ТекущаяВерсия КАК ТекущаяВерсия
		|ПОМЕСТИТЬ ФайлыДокумента
		|ИЗ
		|	Справочник.Файлы КАК Файлы
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СлужебныеФайлыДокументов КАК СлужебныеФайлыДокументов
		|		ПО (СлужебныеФайлыДокументов.Файл = Файлы.Ссылка)
		|ГДЕ
		|	Файлы.ВладелецФайла = &Документ
		|	И СлужебныеФайлыДокументов.Файл ЕСТЬ NULL
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ФайлыДокумента.Файл КАК Файл,
		|	ФайлыДокумента.ТекущаяВерсия КАК ТекущаяВерсия,
		|	ВидыДокументовЭДО.ТипДокумента КАК ТипДокумента,
		|	ОбъектыУчетаДокументовЭДО.ЭлектронныйДокумент.ВидДокумента КАК ВидЭлектронногоДокумента,
		|	ОбъектыУчетаДокументовЭДО.ЭлектронныйДокумент КАК ЭлектронныйДокумент,
		|	СостоянияДокументовЭДО.Состояние КАК Состояние,
		|	ОбъектыУчетаДокументовЭДО.ЭлектронныйДокумент.ТребуетсяПодтверждение КАК ТребуетсяПодтверждение,
		|	ОбъектыУчетаДокументовЭДО.ЭлектронныйДокумент.СпособОбмена КАК СпособОбмена,
		|	ОбъектыУчетаДокументовЭДО.ЭлектронныйДокумент.ИдентификаторОрганизации КАК УчетнаяЗаписьЭДО
		|ИЗ
		|	РегистрСведений.ОбъектыУчетаДокументовЭДО КАК ОбъектыУчетаДокументовЭДО
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ФайлыДокумента КАК ФайлыДокумента
		|		ПО ФайлыДокумента.ТекущаяВерсия = ОбъектыУчетаДокументовЭДО.ОбъектУчета
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ВидыДокументовЭДО КАК ВидыДокументовЭДО
		|		ПО ОбъектыУчетаДокументовЭДО.ЭлектронныйДокумент.ВидДокумента = ВидыДокументовЭДО.Ссылка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СостоянияДокументовЭДО КАК СостоянияДокументовЭДО
		|		ПО ОбъектыУчетаДокументовЭДО.ЭлектронныйДокумент = СостоянияДокументовЭДО.ЭлектронныйДокумент
		|ГДЕ
		|	ОбъектыУчетаДокументовЭДО.Актуальный
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СостояниеДокументовПоЭДОСрезПоследних.СостояниеВерсииДокументаПоЭДО КАК СостояниеДОПоЭДО
		|ИЗ
		|	РегистрСведений.СостояниеДокументовПоЭДО.СрезПоследних КАК СостояниеДокументовПоЭДОСрезПоследних
		|ГДЕ
		|	СостояниеДокументовПоЭДОСрезПоследних.ДокументДО = &Документ";
	Запрос.УстановитьПараметр("Документ", ДокументДО);
	
	МассивРезультатов = Запрос.ВыполнитьПакет();
	
	РезультатПоСвязаннымДокументамЭДО = МассивРезультатов[1];
	ВыборкаСостоянийДО = МассивРезультатов[2].Выбрать();
	
	Если Не РезультатПоСвязаннымДокументамЭДО.Пустой() Тогда
		Выборка = РезультатПоСвязаннымДокументамЭДО.Выбрать();
		Выборка.Следующий();
		
		СтруктураВозврата.ЕстьЭД = Истина;
		СтруктураВозврата.ЭлектронныйДокумент = Выборка.ЭлектронныйДокумент;
		СтруктураВозврата.Направление = 
			?(ТипЗнч(Выборка.ЭлектронныйДокумент) = Тип("ДокументСсылка.ЭлектронныйДокументВходящийЭДО"),
				Перечисления.НаправленияЭДО.Входящий,
				Перечисления.НаправленияЭДО.Исходящий);
		СтруктураВозврата.ВидДокументаЭДО = Выборка.ВидЭлектронногоДокумента;
		СтруктураВозврата.ТипДокументаЭДО = Выборка.ТипДокумента;
		СтруктураВозврата.Состояние = Выборка.Состояние;
		СтруктураВозврата.ТребуетсяПодтверждение = Выборка.ТребуетсяПодтверждение;
		СтруктураВозврата.ДоступнаКорректировкаЭД =
			ВидДокументаКорректируетсяНепосредственно(Выборка.ВидЭлектронногоДокумента);
		СтруктураВозврата.УчетнаяЗаписьЭДО = Выборка.УчетнаяЗаписьЭДО;
		
		Если ВыборкаСостоянийДО.Следующий() Тогда
			СостояниеДО = ВыборкаСостоянийДО.СостояниеДОПоЭДО;
			
			СтруктураВозврата.СостояниеДО = СостояниеДО;
			СтруктураВозврата.Подписан = Истина;
		КонецЕсли;
		
		ПакетЭДО = ПакетДокумента(ДокументДО);
		
		СтруктураВозврата.ПоддерживаетсяПакетнаОбработка =
			СпособОбменаПоддерживаетПакетнуюОбработку(Выборка.СпособОбмена);
		
		Если ПакетЭДО = Неопределено Тогда
			СтруктураВозврата.ДокументВПакетеЭДО = Ложь;
			СтруктураВозврата.ДоступныДействияПоДокументу = Истина;
		Иначе
			СтруктураВозврата.ДокументВПакетеЭДО = Истина;
			СтруктураВозврата.ПакетЭДО = ПакетЭДО;
			
			ДанныеПакетаЭДО = ДанныеПакетаДокументов(ПакетЭДО);
			
			СтруктураВозврата.ДоступноИзменениеПакетаЭДО = 
				Не ЗначениеЗаполнено(ДанныеПакетаЭДО.ИдентификаторПакетаБЭД)
				И ДанныеПакетаЭДО.Направление = Перечисления.НаправленияЭДО.Исходящий;
			
			Если ДанныеПакетаЭДО.Направление = Перечисления.НаправленияЭДО.Входящий Тогда
				СтруктураВозврата.ДоступныДействияПоДокументу = СинхронизацияЭДО.ТребуетсяОднородностьОтвета(
					ОбщегоНазначения.ЗначениеРеквизитаОбъекта(
						СтруктураВозврата.ЭлектронныйДокумент, "ИдентификаторКонтрагента"));
			Иначе
				СтруктураВозврата.ДоступныДействияПоДокументу = Ложь;
			КонецЕсли;
			
			ЕстьНеотраженныеДокументы = (ДанныеНеотраженныхДокументовЭДОПакета(ПакетЭДО).Количество() > 0);
			СтруктураВозврата.ПакетЭДОНеОтражен = ЕстьНеотраженныеДокументы;
			
		КонецЕсли;
		
		Возврат СтруктураВозврата;
		
	КонецЕсли;
	
	РеквизитыДокумента = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДокументДО,
		"Организация, Контрагент, ВидДокумента, ФормаДокумента");
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьУчетПоОрганизациям") Тогда
		РеквизитыДокумента.Организация = Справочники.Организации.ОрганизацияПоУмолчанию();
	КонецЕсли;
	
	НастройкиОтправки = РегистрыСведений.НастройкиОтправкиДокументовПоЭДО.НастройкиОтправкиВидаДокумента(
		РеквизитыДокумента.Организация,
		РеквизитыДокумента.Контрагент,
		РеквизитыДокумента.ВидДокумента);
	
	Если НастройкиОтправки <> Неопределено 
		И РеквизитыДокумента.ФормаДокумента <> Перечисления.ВариантыФормДокументов.Бумажная Тогда

		СтруктураВозврата.ЕстьЭД = Ложь;
		СтруктураВозврата.ЭлектронныйДокумент = Неопределено;
		СтруктураВозврата.Направление = Перечисления.НаправленияЭДО.Исходящий;
		СтруктураВозврата.ВидДокументаЭДО = НастройкиОтправки.ВидДокументаЭДО;
		СтруктураВозврата.ТипДокументаЭДО = НастройкиОтправки.ТипДокумента;
		СтруктураВозврата.Состояние = Перечисления.СостоянияДокументовЭДО.НеСформирован;
		СтруктураВозврата.ТребуетсяПодтверждение = НастройкиОтправки.ТребуетсяОтветнаяПодпись;
		СтруктураВозврата.ДоступнаКорректировкаЭД =
			ВидДокументаКорректируетсяНепосредственно(НастройкиОтправки.ВидДокументаЭДО);
		
		Если ВыборкаСостоянийДО.Следующий() Тогда
			
			СостояниеДО = ВыборкаСостоянийДО.СостояниеДОПоЭДО;
			
			СтруктураВозврата.СостояниеДО = СостояниеДО;
			СтруктураВозврата.Подписан =
				Не (СостояниеДО = Перечисления.СостоянияЭДОДокументооборот.НеСформирован
					Или СостояниеДО = Перечисления.СостоянияЭДОДокументооборот.ЗакрытПринудительно);
			
		Иначе
			
			СтруктураВозврата.СостояниеДО = Перечисления.СостоянияЭДОДокументооборот.НеСформирован;
			СтруктураВозврата.Подписан = Ложь;
			
		КонецЕсли;
		
		СпособыОбмена = СпособыОбменаЭДОПоДокументамДО(
			ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ДокументДО));
		
		СтруктураВозврата.ПоддерживаетсяПакетнаОбработка =
			СпособОбменаПоддерживаетПакетнуюОбработку(СпособыОбмена[ДокументДО]);
		
		ПакетЭДО = ПакетДокумента(ДокументДО);
		
		Если ПакетЭДО = Неопределено Тогда
			СтруктураВозврата.ДокументВПакетеЭДО = Ложь;
			СтруктураВозврата.ДоступныДействияПоДокументу = Истина;
		Иначе
			СтруктураВозврата.ДокументВПакетеЭДО = Истина;
			СтруктураВозврата.ПакетЭДО = ПакетЭДО;
			
			ДанныеПакетаЭДО = ДанныеПакетаДокументов(ПакетЭДО);
			
			СтруктураВозврата.ДоступноИзменениеПакетаЭДО = 
				Не ЗначениеЗаполнено(ДанныеПакетаЭДО.ИдентификаторПакетаБЭД)
				И ДанныеПакетаЭДО.Направление = Перечисления.НаправленияЭДО.Исходящий;
			СтруктураВозврата.ДоступныДействияПоДокументу = Ложь;
		КонецЕсли;
		
		Возврат СтруктураВозврата;
		
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

// Получает идентификаторы обмена ЭДО для документов ДО
// 
// Параметры:
//  ДокументыДО - Массив Из ОпределяемыйТип.ДокументДОДляЭДО
// 
// Возвращаемое значение:
//  Соответствие Из КлючИЗначение:
//    * Ключ - Массив Из ОпределяемыйТип.ДокументДОДляЭДО
//    * Значение - см. НовыеИдентификаторыЭДОДокументаДО
//
Функция ИдентификаторыЭДОДокументовДО(ДокументыДО) Экспорт
	
	ИдентификаторыЭДОДокументов = Новый Соответствие();
	Для Каждого Документ Из ДокументыДО Цикл
		ИдентификаторыЭДОДокументов[Документ] = НовыеИдентификаторыЭДОДокументаДО();
	КонецЦикла;
	
	ТаблицыИдентификаторов = ТаблицыДляОпределенияИдентификаторовЭДО(ДокументыДО);
	
	ОбработанныеДокументы = Новый Соответствие();
	
	ЗаполнитьИдентификаторыЭДОПоТаблицам(ИдентификаторыЭДОДокументов, ТаблицыИдентификаторов, ОбработанныеДокументы);
	
	Возврат ИдентификаторыЭДОДокументов;
	
КонецФункции

#КонецОбласти

#Область СведенияОЭДИзФайла

// Сведения о формализованном электронном документе из файла
// 
// Параметры:
//  АдресВременногоХранилищаФайла - Строка - Адрес во временном хранилище на двоичные данные файла
// 
// Возвращаемое значение:
//  Структура, Неопределено - Неопределено, если файл не является формализованным. В ином случае:
// * ДатаДокумента - Дата - Дата документа
// * НомерДокумента - Строка - Номер документа
// * СуммаДокумента - Число - Сумма документа
// * ИмяСоздания - Строка - Имя для создания файла формата <Вид документа> №<Номер> от <Дата>
// * Валюта - СправочникСсылка.Валюты - Валюта документа
// * ТипДокументаЭДО - ПеречислениеСсылка.ТипыДокументовЭДО - Тип документа ЭДО
// * ВидДокументаЭДО - СправочникСсылка.ВидыДокументовЭДО - Вид документа ЭДО
Функция СведенияОЭДИзФайла(АдресВременногоХранилищаФайла) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	СтруктураВозврата = Новый Структура;
	СтруктураВозврата.Вставить("ДатаДокумента", Неопределено);
	СтруктураВозврата.Вставить("НомерДокумента", "");
	СтруктураВозврата.Вставить("СуммаДокумента", "");
	СтруктураВозврата.Вставить("ИмяСоздания", "");
	СтруктураВозврата.Вставить("Валюта", Справочники.Валюты.ПустаяСсылка());
	СтруктураВозврата.Вставить("ТипДокументаЭДО", Перечисления.ТипыДокументовЭДО.ПустаяСсылка());
	СтруктураВозврата.Вставить("ВидДокументаЭДО", Справочники.ВидыДокументовЭДО.ПустаяСсылка());
	СтруктураВозврата.Вставить("ИдентификаторДокумента", "");
	
	ОбщиеСведения = КонвертацияЭДО.ПараметрыФайлаПроизвольногоДокумента(
		ПолучитьИзВременногоХранилища(АдресВременногоХранилищаФайла));
	
	Если ОбщиеСведения = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(СтруктураВозврата, ОбщиеСведения);
	
	Попытка
		ДанныеЭДФайла = ОбменСКонтрагентами.ДанныеЭлектронногоДокументаПоФайлу(
			ПолучитьИзВременногоХранилища(АдресВременногоХранилищаФайла));
	Исключение
		Возврат Неопределено;
	КонецПопытки;
	
	Если ТипЗнч(ДанныеЭДФайла) <> Тип("Структура") Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ТипДокумента = ДанныеЭДФайла.НовыйЭД.ВидЭД;
	
	СтруктураВозврата.ТипДокументаЭДО = ТипДокумента;
	СтруктураВозврата.ВидДокументаЭДО = ЭлектронныеДокументыЭДО.ВидДокументаПоТипу(ТипДокумента);
	
	СтруктураВозврата.ИмяСоздания = СтрШаблон(НСтр("ru = '%1 №%2 от %3'"),
		ТипДокумента,
		ОбщиеСведения.НомерДокумента,
		Формат(ОбщиеСведения.ДатаДокумента, "ДФ=dd.MM.yyyy"));
	
	Форматы = ЭлектронныеДокументыЭДО.ПоддерживаемыеФорматы();
	
	Если ДанныеЭДФайла.НовыйЭД.ВерсияФормата = Форматы.CML208
		ИЛИ ДанныеЭДФайла.НовыйЭД.ВерсияФормата = Форматы.CML402 Тогда
		
		СведенияОЭД = СведенияОЭДФорматаCML(ДанныеЭДФайла);
		
	Иначе
		
		СведенияОЭД = СведенияОЭДФорматаФНС(ДанныеЭДФайла);
		
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(СтруктураВозврата, СведенияОЭД);
	
	Возврат СтруктураВозврата;
	
КонецФункции

#КонецОбласти

#Область ФормированиеПредставленияДокумента

Функция ЗапуститьФормированиеПредставленияДанныхЭДО(ДанныеДляПредставления, ИдентификаторФормы) Экспорт
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияФункции(ИдентификаторФормы);
	
	Возврат ДлительныеОперации.ВыполнитьФункцию(ПараметрыВыполнения,
		"ОбменСКонтрагентамиДОСлужебный.ПредставлениеДанныхЭДО",
		ДанныеДляПредставления);
	
КонецФункции

Функция ПредставлениеДанныхЭДО(ДанныеДляПредставления) Экспорт
	
	Файл = Неопределено;
	ВерсияФайла = Неопределено;
	ПараметрыВизуализации = Неопределено;
	
	ДанныеДляПредставления.Свойство("Файл", Файл);
	ДанныеДляПредставления.Свойство("ВерсияФайла", ВерсияФайла);
	ДанныеДляПредставления.Свойство("ПараметрыВизуализации", ПараметрыВизуализации);
	
	Если ЗначениеЗаполнено(Файл) И ЗначениеЗаполнено(ВерсияФайла) Тогда
		
		Возврат ПредставлениеДанныхПоФайлу(Файл, ВерсияФайла, ПараметрыВизуализации);
		
	ИначеЕсли ЗначениеЗаполнено(Файл) Тогда
		
		ВерсияФайла = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Файл, "ТекущаяВерсия");
		
		Возврат ПредставлениеДанныхПоФайлу(Файл, ВерсияФайла, ПараметрыВизуализации);
		
	ИначеЕсли ЗначениеЗаполнено(ВерсияФайла) Тогда
		
		Файл = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ВерсияФайла, "Владелец");
		
		Возврат ПредставлениеДанныхПоФайлу(Файл, ВерсияФайла, ПараметрыВизуализации);
		
	КонецЕсли;
	
	Документ = Неопределено;
	
	ДанныеДляПредставления.Свойство("Документ", Документ);
	
	Если ЗначениеЗаполнено(Документ) Тогда
		
		Возврат ПредставлениеДанныхПоДокументу(Документ, ПараметрыВизуализации);
		
	КонецЕсли;
	
	ДокументЭДО = Неопределено;
	
	ДанныеДляПредставления.Свойство("ДокументЭДО", ДокументЭДО);
	
	Если ЗначениеЗаполнено(ДокументЭДО) Тогда
		
		Возврат ПредставлениеДанныхПоДокументуЭДО(ДокументЭДО, ПараметрыВизуализации);
		
	КонецЕсли;
	
	Результат = НовыйРезультатПредставленияЭДО();
	Результат.Успех = Ложь;
	Результат.ОписаниеОшибки =
		НСтр("ru = 'Не переданы параметры представления данных ЭДО.'");
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область ИнтерфейсЭДО

Функция ДоступныКомандыПоДокументуЭДОИзИнтерфейсаБЭД(ДокументЭДО) Экспорт
	
	Если ТипЗнч(ДокументЭДО) = Тип("ДокументСсылка.ЭлектронныйДокументВходящийЭДО") Тогда
		
		ДокументыДО = ДокументыДОЭлектронныхДокументов(ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ДокументЭДО));
		
		ЕстьДокументДО = (ДокументыДО[ДокументЭДО] <> Неопределено);
		
		ДокументТребуетОтражения = РегистрыСведений.ДокументыЭДОКСозданиюВДО.ДокументЭДОТребуетОтраженияВДО(
			ДокументЭДО);
		
		Если ЕстьДокументДО Или ДокументТребуетОтражения Тогда
			Возврат Ложь;
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

#КонецОбласти

#Область СпискиДокументовЭДО

#Область ДокументыИсходящие

Функция ИсходящиеДокументыЭДО(Знач Отбор = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапросаИсходящихДокументовЭДО();
	
	УстановитьПараметрыЗапросаИсходящих(Запрос, Отбор);
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

Функция ЗапуститьПолучениеДанныхИсходящих(ДанныеВыделенных, ИдентификаторФормы) Экспорт
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияФункции(ИдентификаторФормы);
	
	Возврат ДлительныеОперации.ВыполнитьФункцию(ПараметрыВыполнения,
		"ОбменСКонтрагентамиДОСлужебный.ДанныеВыделенныхИсходящихДокументов",
		ДанныеВыделенных);
	
КонецФункции

// Данные выделенных исходящих документов
// 
// Параметры:
//  ДанныеВыделенныхСтрок - Массив Из Структура:
//    * Документ - ОпределяемыйТип.ДокументДОДляЭДО
//    * ИдентификаторПакета - УникальныйИдентификатор
// 
// Возвращаемое значение:
//  Структура - Данные выделенных объектов:
//    * Пакеты - Массив Из УникальныйИдентификатор - Идентификаторы пакетов документов
//    * ОдиночныеДокументы - Массив Из ОпределяемыйТип.ДокументДОДляЭДО - Документы вне пакетов
//    * ДеревоФайлов - ДеревоЗначений - Дерево файлов выделенных объектов:
//      ** ИдентификаторПакета - УникальныйИдентификатор
//      ** Документ - ОпределяемыйТип.ДокументДОДляЭДО
//      ** Файл - Структура:
//        *** Файл - СправочникСсылка.Файлы
//        *** Расширение - Строка
//      ** ПодписанЭП - Булево
//      ** ИндексКартинкиЭП - Число
//      ** ПредставлениеЭлемента - Строка
//      ** ИндексКартинкиФайла - Число
//    * Организации - Массив Из СправочникСсылка.Организации
//    * Контрагенты - Массив Из СправочникСсылка.Контрагенты
//    * ДоступныеДействия - Соответствие Из КлючИЗначение:
//      ** Ключ - Строка - Действие, см. ОбменСКонтрагентамиДОСлужебныйКлиентСервер.ДействияСпискаИсходящихДокументов
//      ** Значение - Булево - Истина, если 
//
Функция ДанныеВыделенныхИсходящихДокументов(ДанныеВыделенныхСтрок) Экспорт
	
	СтруктураВозврата = Новый Структура;
	СтруктураВозврата.Вставить("Пакеты", Новый Массив);
	СтруктураВозврата.Вставить("ОдиночныеДокументы", Новый Массив);
	СтруктураВозврата.Вставить("ДеревоФайлов", Новый ДеревоЗначений);
	СтруктураВозврата.Вставить("Организации", Новый Массив);
	СтруктураВозврата.Вставить("Контрагенты", Новый Массив);
	СтруктураВозврата.Вставить("ДоступныеДействия", Новый Соответствие);
	
	ДанныеОбъектов = ДанныеВыделенныхОбъектов(ДанныеВыделенныхСтрок);
	
	СтруктураВозврата.ДеревоФайлов = ДеревоФайловПоДаннымОбъектов(ДанныеОбъектов);
	
	Для Каждого Элемент Из ДанныеОбъектов.Пакеты Цикл
		СтруктураВозврата.Пакеты.Добавить(Элемент.Ключ);
	КонецЦикла;
	
	Для Каждого Элемент Из ДанныеОбъектов.Документы Цикл
		СтруктураВозврата.ОдиночныеДокументы.Добавить(Элемент.Ключ);
	КонецЦикла;
	
	ОрганизацииИКонтрагенты = ОрганизацииИКонтрагентыВыбранныхДанныхОбъектов(ДанныеОбъектов);
	
	ЗаполнитьЗначенияСвойств(СтруктураВозврата, ОрганизацииИКонтрагенты);
	
	ДоступныеДействия = ДоступныеДействияПоВыделеннымИсходящимОбъектам(
		ДанныеОбъектов,
		ОрганизацииИКонтрагенты.Организации,
		ОрганизацииИКонтрагенты.Контрагенты);
	
	СтруктураВозврата.ДоступныеДействия = ДоступныеДействия;
	
	Возврат СтруктураВозврата;
	
КонецФункции

#КонецОбласти

#Область ДокументыВходящие

// Запускает получение данных выбранного входящего документа ЭДО в списке.
// 
// Параметры:
//  ДокументЭДО - ДокументСсылка.ЭлектронныйДокументВходящийЭДО - Документ ЭДО, по которому необходимо получить данные
//  ИдентификаторФормы - УникальныйИдентификатор - УИД формы из которой запущено получение данных
// 
// Возвращаемое значение:
//  см. ДлительныеОперации.ВыполнитьФункцию
Функция ЗапуститьПолучениеДанныхВыбранногоВходящего(ДокументЭДО, ИдентификаторФормы) Экспорт
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияФункции(ИдентификаторФормы);
	
	Возврат ДлительныеОперации.ВыполнитьФункцию(ПараметрыВыполнения,
		"ОбменСКонтрагентамиДОСлужебный.ДанныеВыбранногоВходящегоДокумента",
		ДокументЭДО);
	
КонецФункции

// Данные входящего документа ЭДО для формы списка.
// 
// Параметры:
//  ДокументЭДО - ДокументСсылка.ЭлектронныйДокументВходящийЭДО
// 
// Возвращаемое значение:
//  см. НовыеДанныеВыделенногоВходящегоЭДО
Функция ДанныеВыбранногоВходящегоДокумента(ДокументЭДО) Экспорт
	
	Возврат ДанныеВыделенногоВходящегоДокумента(ДокументЭДО);
	
КонецФункции

#КонецОбласти

#КонецОбласти

#Область ПакетыЭДО

// Возвращает пакеты документов Документооборота
// 
// Параметры:
//  ДокументыДО - Массив Из ОпределяемыйТип.ДокументДОДляЭДО
// 
// Возвращаемое значение:
//  Соответствие Из КлючИЗначение:
//    * Ключ - ОпределяемыйТип.ДокументДОДляЭДО
//    * Значение - УникальныйИдентификатор - Идентификатор пакета документа.
//                                           Если документ не состоит в пакете - вернет пустой уникальный идентификатор.
//
Функция ПакетыДокументов(ДокументыДО) Экспорт
	
	ПакетыДокументов = Новый Соответствие();
	Для Каждого Документ Из ДокументыДО Цикл
		ПакетыДокументов[Документ] = УникальныйИдентификаторПустой();
	КонецЦикла;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	СоставПакетовЭДОДокументооборот.Документ КАК Документ,
		|	СоставПакетовЭДОДокументооборот.ИдентификаторПакета КАК ИдентификаторПакета
		|ИЗ
		|	РегистрСведений.СоставПакетовЭДОДокументооборот КАК СоставПакетовЭДОДокументооборот
		|ГДЕ
		|	СоставПакетовЭДОДокументооборот.Документ В (&Документы)";
	Запрос.УстановитьПараметр("Документы", ДокументыДО);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ПакетыДокументов[Выборка.Документ] = Выборка.ИдентификаторПакета;
	КонецЦикла;
	
	Возврат ПакетыДокументов;
	
КонецФункции

// Устарела. Следует использовать ПакетыДокументов
//  Возвращает ИД пакета ЭДО к которому принадлежит документ ДО.
//  Если документ не состоит в пакете, то возвращается Неопределено
// 
// Параметры:
// 	Документ - ОпределяемыйТип.ДокументДОДляЭДО - Документ ДО
// Возвращаемое значение:
// 	УникальныйИдентификатор, Неопределено - УИД пакета, либо Неопределено, если документ не состоит в пакете ЭДО
Функция ПакетДокумента(Документ) Экспорт
	
	ДокументыДО = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Документ);
	ИдентификаторыПакетов = ПакетыДокументов(ДокументыДО);
	
	Если ЗначениеЗаполнено(ИдентификаторыПакетов[Документ]) Тогда
		Возврат ИдентификаторыПакетов[Документ];
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

// Возвращает данные пакета ЭДО документов ДО
// 
// Параметры:
// 	ИдентификаторПакета - УникальныйИдентификатор - УИД пакета ЭДО
// Возвращаемое значение:
// 	Структура - Данные пакета:
// * ИдентификаторПакета - Неопределено, УникальныйИдентификатор - УИД пакета, если пакет существует,
//                                                                 Неопределено, если пакета ЭДО не существует
// * Организация - СправочникСсылка.Организации - Организация пакета
// * Контрагент - СправочникСсылка.Контрагенты - Контрагент пакета
// * Направление - ПеречислениеСсылка.НаправленияЭДО - Направление пакета
// * ИдентификаторПакетаБЭД - Неопределено, УникальныйИдентификатор -
//                                          Идентификартор пакета документо БЭД, если он существует
//                                          Неопределено, если пакет документов БЭД еще не создан
// * Документы - Массив из ОпределяемыйТип.ДокументДОДляЭДО - Массив документов, входящих в пакет
Функция ДанныеПакетаДокументов(ИдентификаторПакета) Экспорт
	
	СтруктураВозврата = Новый Структура;
	
	СтруктураВозврата.Вставить("ИдентификаторПакета", Неопределено);
	СтруктураВозврата.Вставить("Организация", Справочники.Организации.ПустаяСсылка());
	СтруктураВозврата.Вставить("Контрагент", Справочники.Контрагенты.ПустаяСсылка());
	СтруктураВозврата.Вставить("Направление", Перечисления.НаправленияЭДО.ПустаяСсылка());
	СтруктураВозврата.Вставить("ИдентификаторПакетаБЭД", Неопределено);
	СтруктураВозврата.Вставить("Документы", Новый Массив);
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ПакетыЭДОДокументооборот.ИдентификаторПакета,
		|	ПакетыЭДОДокументооборот.Организация,
		|	ПакетыЭДОДокументооборот.Контрагент,
		|	ПакетыЭДОДокументооборот.Направление,
		|	ПакетыЭДОДокументооборот.ИдентификаторПакетаБЭД,
		|	СоставПакетовЭДОДокументооборот.Документ
		|ИЗ
		|	РегистрСведений.ПакетыЭДОДокументооборот КАК ПакетыЭДОДокументооборот
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СоставПакетовЭДОДокументооборот КАК СоставПакетовЭДОДокументооборот
		|		ПО СоставПакетовЭДОДокументооборот.ИдентификаторПакета = ПакетыЭДОДокументооборот.ИдентификаторПакета
		|ГДЕ
		|	ПакетыЭДОДокументооборот.ИдентификаторПакета = &ИдентификаторПакета";
	Запрос.УстановитьПараметр("ИдентификаторПакета", ИдентификаторПакета);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Не Выборка.Следующий() Тогда
		Возврат СтруктураВозврата;
	КонецЕсли;
	
	СтруктураВозврата.ИдентификаторПакета = Выборка.ИдентификаторПакета;
	СтруктураВозврата.Организация = Выборка.Организация;
	СтруктураВозврата.Контрагент = Выборка.Контрагент;
	СтруктураВозврата.Направление = Выборка.Направление;
	СтруктураВозврата.ИдентификаторПакетаБЭД = Выборка.ИдентификаторПакетаБЭД;
	СтруктураВозврата.Документы.Добавить(Выборка.Документ);
	
	Пока Выборка.Следующий() Цикл
		СтруктураВозврата.Документы.Добавить(Выборка.Документ);
	КонецЦикла;
	
	Возврат СтруктураВозврата;
	
КонецФункции

Функция ОбъединитьДокументыВИсходящийПакетЭДО(Документы) Экспорт
	
	СтруктураВозврата = Новый Структура;
	СтруктураВозврата.Вставить("Успех", Ложь);
	СтруктураВозврата.Вставить("ОписаниеОшибки", "");
	СтруктураВозврата.Вставить("ИдентификаторПакета", Неопределено);
	СтруктураВозврата.Вставить("Организация", Справочники.Организации.ПустаяСсылка());
	СтруктураВозврата.Вставить("Контрагент", Справочники.Контрагенты.ПустаяСсылка());
	
	ДанныеДляПакета = ДанныеДокументовДляДобавленияВПакет(Документы);
	
	Если Не ВозможноОбъединитьДокументыВПакет(Документы, ДанныеДляПакета, СтруктураВозврата.ОписаниеОшибки) Тогда
		Возврат СтруктураВозврата;
	КонецЕсли;
	
	ОрганизацияПакета = ДанныеДляПакета.Организации[0];
	КонтрагентПакета = ДанныеДляПакета.Контрагенты[0];
	
	ИДПакета = Новый УникальныйИдентификатор;
	
	Блокировка = Новый БлокировкаДанных();
	
	Для Каждого Документ Из Документы Цикл
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.СостояниеДокументовПоЭДО");
		ЭлементБлокировки.УстановитьЗначение("ДокументДО", Документ);
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.СоставПакетовЭДОДокументооборот");
		ЭлементБлокировки.УстановитьЗначение("Документ", Документ);
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	КонецЦикла;
	
	НачатьТранзакцию();
	Попытка
		
		ЗаписьЖурналаРегистрации(
			НСтр("ru = 'Создание пакета ЭДО документов 1С:Документооборот.'"),
			УровеньЖурналаРегистрации.Информация, , ,
			СтрШаблон(НСтр("ru = 'Начало создания пакета ЭДО документов 1С:Документообот (%1).'"),
				ИДПакета));
		
		Блокировка.Заблокировать();
		
		Для Каждого Документ Из Документы Цикл
			
			Запись = РегистрыСведений.СоставПакетовЭДОДокументооборот.СоздатьМенеджерЗаписи();
			
			Запись.Документ = Документ;
			Запись.ИдентификаторПакета = ИДПакета;
			
			Запись.Записать();
			
		КонецЦикла;
		
		ЗаписьСвойствПакета = РегистрыСведений.ПакетыЭДОДокументооборот.СоздатьМенеджерЗаписи();
		
		ЗаписьСвойствПакета.ИдентификаторПакета = ИДПакета;
		ЗаписьСвойствПакета.Организация = ОрганизацияПакета;
		ЗаписьСвойствПакета.Контрагент = КонтрагентПакета;
		ЗаписьСвойствПакета.Направление = Перечисления.НаправленияЭДО.Исходящий;
		
		ЗаписьСвойствПакета.Записать();
		
		ВыборкаСведенийСостояний = ВыборкаСведенийОСостоянияхЭДОИсходящих(Документы);
		
		Пока ВыборкаСведенийСостояний.Следующий() Цикл
			
			Если ВыборкаСведенийСостояний.Состояние <>
				Перечисления.СостоянияЭДОДокументооборот.ОжидаетсяОтправка Тогда
				
				Продолжить;
			КонецЕсли;
			
			ДанныеСостояния = НовыеДанныеСостоянияДляУстановки();
			ДанныеСостояния.Состояние = Перечисления.СостоянияЭДОДокументооборот.ОжидаетСозданияПакетаЭДО;
			ДанныеСостояния.Комментарий = НСтр("ru = 'Документ добавлен в пакет ЭДО.'");
			
			УстановитьСостояниеДокументаЭДО(
				ВыборкаСведенийСостояний.ДокументДО,
				ВыборкаСведенийСостояний.Контрагент,
				ДанныеСостояния);
			
		КонецЦикла;
		
		ЗаписьЖурналаРегистрации(
			НСтр("ru = 'Создание пакета ЭДО документов 1С:Документооборот.'"),
			УровеньЖурналаРегистрации.Информация, , ,
			СтрШаблон(НСтр("ru = 'Создан пакет ЭДО документов 1С:Документообот (%1) из %2 документов.'"),
				ИДПакета,
				Документы.Количество()));
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		
		ОтменитьТранзакцию();
		
		СтруктураВозврата.ОписаниеОшибки =
			СтрШаблон(НСтр("ru = 'Не удалось создать пакет ЭДО документов 1С:Документооборот (%1) по причине:'"),
				ИДПакета)
			+ Символы.ПС
			+ ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		
		ЗаписьЖурналаРегистрации(
			НСтр("ru = 'Создание пакета ЭДО документов 1С:Документооборот.'"),
			УровеньЖурналаРегистрации.Ошибка, , ,
			СтруктураВозврата.ОписаниеОшибки);
		
		Возврат СтруктураВозврата;
		
	КонецПопытки;
	
	СтруктураВозврата.Успех = Истина;
	СтруктураВозврата.ИдентификаторПакета = ИДПакета;
	СтруктураВозврата.Организация = ОрганизацияПакета;
	СтруктураВозврата.Контрагент = КонтрагентПакета;
	
	Возврат СтруктураВозврата;
	
КонецФункции

Функция ИзменитьСоставИсходящегоПакетаЭДО(ИдентификаторПакета, ДобавляемыеДокументы, УдаляемыеДокументы) Экспорт
	
	СтруктураВозврата = Новый Структура;
	СтруктураВозврата.Вставить("Успех", Ложь);
	СтруктураВозврата.Вставить("ОписаниеОшибки", "");
	СтруктураВозврата.Вставить("ПакетУдален", Ложь);
	
	ДанныеПакета = ДанныеПакетаДокументов(ИдентификаторПакета);
	
	Если Не ВозможноИзменитьСоставПакета(ДанныеПакета, ДобавляемыеДокументы,
			УдаляемыеДокументы, СтруктураВозврата.ОписаниеОшибки) Тогда
		Возврат СтруктураВозврата;
	КонецЕсли;
	
	ДокументыВПакете = Новый Соответствие;
	
	Для Каждого Документ Из ДанныеПакета.Документы Цикл
		ДокументыВПакете.Вставить(Документ, Истина);
	КонецЦикла;
	
	ИДПакета = ДанныеПакета.ИдентификаторПакета;
	
	Блокировка = Новый БлокировкаДанных();
	
	Для Каждого Документ Из ДобавляемыеДокументы Цикл
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.СостояниеДокументовПоЭДО");
		ЭлементБлокировки.УстановитьЗначение("ДокументДО", Документ);
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.СоставПакетовЭДОДокументооборот");
		ЭлементБлокировки.УстановитьЗначение("Документ", Документ);
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	КонецЦикла;
	
	Для Каждого Документ Из УдаляемыеДокументы Цикл
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.СостояниеДокументовПоЭДО");
		ЭлементБлокировки.УстановитьЗначение("ДокументДО", Документ);
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.СоставПакетовЭДОДокументооборот");
		ЭлементБлокировки.УстановитьЗначение("Документ", Документ);
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	КонецЦикла;
	
	ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ПакетыЭДОДокументооборот");
	ЭлементБлокировки.УстановитьЗначение("ИдентификаторПакета", ИДПакета);
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	
	НачатьТранзакцию();
	Попытка
		
		ЗаписьЖурналаРегистрации(
			НСтр("ru = 'Изменение состава пакета ЭДО документов 1С:Документооборот.'"),
			УровеньЖурналаРегистрации.Информация, , ,
			СтрШаблон(НСтр("ru = 'Начало изменения состава пакета ЭДО документов 1С:Документообот (%1).'"),
				ИДПакета));
		
		Блокировка.Заблокировать();
		
		Для Каждого Документ Из УдаляемыеДокументы Цикл
			Запись = РегистрыСведений.СоставПакетовЭДОДокументооборот.СоздатьМенеджерЗаписи();
			Запись.Документ = Документ;
			Запись.Удалить();
			
			ДокументыВПакете.Удалить(Документ);
		КонецЦикла;
		
		Для Каждого Документ Из ДобавляемыеДокументы Цикл
			Запись = РегистрыСведений.СоставПакетовЭДОДокументооборот.СоздатьМенеджерЗаписи();
			Запись.Документ = Документ;
			Запись.ИдентификаторПакета = ИДПакета;
			Запись.Записать();
			
			ДокументыВПакете.Вставить(Документ, Истина);
		КонецЦикла;
		
		ВыборкаСостоянийДобавляемых = ВыборкаСведенийОСостоянияхЭДОИсходящих(ДобавляемыеДокументы);
		
		Пока ВыборкаСостоянийДобавляемых.Следующий() Цикл
			
			Если ВыборкаСостоянийДобавляемых.Состояние <>
				Перечисления.СостоянияЭДОДокументооборот.ОжидаетсяОтправка Тогда
				
				Продолжить;
			КонецЕсли;
			
			ДанныеСостояния = НовыеДанныеСостоянияДляУстановки();
			ДанныеСостояния.Состояние = Перечисления.СостоянияЭДОДокументооборот.ОжидаетСозданияПакетаЭДО;
			ДанныеСостояния.Комментарий = НСтр("ru = 'Документ добавлен в пакет ЭДО.'");
			
			УстановитьСостояниеДокументаЭДО(
				ВыборкаСостоянийДобавляемых.ДокументДО,
				ВыборкаСостоянийДобавляемых.Контрагент,
				ДанныеСостояния);
			
		КонецЦикла;
		
		ВыборкаСостоянийУдаляемых = ВыборкаСведенийОСостоянияхЭДОИсходящих(УдаляемыеДокументы);
		
		Пока ВыборкаСостоянийУдаляемых.Следующий() Цикл
			
			Если ВыборкаСостоянийУдаляемых.Состояние <>
				Перечисления.СостоянияЭДОДокументооборот.ОжидаетСозданияПакетаЭДО Тогда
				
				Продолжить;
			КонецЕсли;
			
			ДанныеСостояния = НовыеДанныеСостоянияДляУстановки();
			ДанныеСостояния.Состояние = Перечисления.СостоянияЭДОДокументооборот.ОжидаетсяОтправка;
			ДанныеСостояния.Комментарий = НСтр("ru = 'Документ удален из пакета ЭДО.'");
			
			УстановитьСостояниеДокументаЭДО(
				ВыборкаСостоянийУдаляемых.ДокументДО,
				ВыборкаСостоянийУдаляемых.Контрагент,
				ДанныеСостояния);
			
		КонецЦикла;
		
		Если ДокументыВПакете.Количество() <= 1 Тогда
			
			Если ДокументыВПакете.Количество() = 0 Тогда
				КомментарийЖР = СтрШаблон(
					НСтр("ru = 'Пакет ЭДО (%1) документов 1С:Документооборот удален,
						|поскольку все документы из него удалены.'"),
					ИДПакета);
			Иначе
				КомментарийЖР = СтрШаблон(
					НСтр("ru = 'Пакет ЭДО (%1) документов 1С:Документооборот удален,
						|поскольку теперь содержит только один документ.'"),
					ИДПакета);
				
				Для Каждого Элемент Из ДокументыВПакете Цикл
					Запись = РегистрыСведений.СоставПакетовЭДОДокументооборот.СоздатьМенеджерЗаписи();
					Запись.Документ = Элемент.Ключ;
					Запись.Удалить();
				КонецЦикла;
			КонецЕсли;
			
			Запись = РегистрыСведений.ПакетыЭДОДокументооборот.СоздатьМенеджерЗаписи();
			Запись.ИдентификаторПакета = ИДПакета;
			Запись.Удалить();
			
			СтруктураВозврата.ПакетУдален = Истина;
			
			ЗаписьЖурналаРегистрации(
				НСтр("ru = 'Удаление пакета ЭДО документов 1С:Документооборот.'"),
				УровеньЖурналаРегистрации.Информация, , ,
				КомментарийЖР);
			
		Иначе
			
			ЗаписьЖурналаРегистрации(
				НСтр("ru = 'Изменение состава пакета ЭДО документов 1С:Документооборот.'"),
				УровеньЖурналаРегистрации.Информация, , ,
				СтрШаблон(НСтр("ru = 'Начало изменения состава пакета ЭДО документов 1С:Документообот (%1).'"),
					ИДПакета));
			
		КонецЕсли;
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		
		ОтменитьТранзакцию();
		
		СтруктураВозврата.ОписаниеОшибки =
			СтрШаблон(НСтр("ru = 'Не удалось изменить состав пакета ЭДО документов 1С:Документооборот (%1) по причине:'"),
				ИДПакета)
			+ Символы.ПС
			+ ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		
		ЗаписьЖурналаРегистрации(
			НСтр("ru = 'Изменение состава пакета ЭДО документов 1С:Документооборот.'"),
			УровеньЖурналаРегистрации.Ошибка, , ,
			СтруктураВозврата.ОписаниеОшибки);
		
		Возврат СтруктураВозврата;
		
	КонецПопытки;
	
	СтруктураВозврата.Успех = Истина;
	
	Возврат СтруктураВозврата;
	
КонецФункции

// Данные документов для добавления их в пакет
// 
// Параметры:
// 	Документы - Массив из ОпределяемыйТип.ДокументДОДляЭДО
// Возвращаемое значение:
// 	Структура - Данные документов:
// * Организации - Массив из СправочникСсылка.Организации - Организации документов
// * Контрагенты - Массив из СправочникСсылка.Контрагенты - Контрагенты документов
// * Направления - Массив из ПеречислениеСсылка.НаправленияЭДО - Направления документов по ЭДО
// * Состояния - Массив из ПеречислениеСсылка.СостоянияЭДОДокументооборот - Состояния документов по ЭДО
// * ИдентификаторыПакетов - Массив из УникальныйИдентификатор - УИДы пакетов ЭДО в которых уже учавствуют документы
Функция ДанныеДокументовДляДобавленияВПакет(Документы) Экспорт
	
	СтруктураВозврата = Новый Структура;
	СтруктураВозврата.Вставить("Организации", Новый Массив);
	СтруктураВозврата.Вставить("Контрагенты", Новый Массив);
	СтруктураВозврата.Вставить("Направления", Новый Массив);
	СтруктураВозврата.Вставить("Состояния", Новый Массив);
	СтруктураВозврата.Вставить("ИдентификаторыПакетов", Новый Массив);
	
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	СостояниеДокументовПоЭДОСрезПоследних.ДокументДО КАК Документ,
		|	СостояниеДокументовПоЭДОСрезПоследних.СостояниеВерсииДокументаПоЭДО КАК Состояние,
		|	СостояниеДокументовПоЭДОСрезПоследних.НаправлениеЭД КАК Направление,
		|	ДокументыДО.Организация,
		|	ДокументыДО.Контрагент,
		|	ЕСТЬNULL(СоставПакетовЭДОДокументооборот.ИдентификаторПакета, НЕОПРЕДЕЛЕНО) КАК ИдентификаторПакета
		|ИЗ
		|	РегистрСведений.СостояниеДокументовПоЭДО.СрезПоследних(, ДокументДО В (&Документы)) КАК
		|		СостояниеДокументовПоЭДОСрезПоследних
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.%ИмяСправочникаДокументаДО% КАК ДокументыДО
		|		ПО СостояниеДокументовПоЭДОСрезПоследних.ДокументДО = ДокументыДО.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СоставПакетовЭДОДокументооборот КАК СоставПакетовЭДОДокументооборот
		|		ПО СостояниеДокументовПоЭДОСрезПоследних.ДокументДО = СоставПакетовЭДОДокументооборот.Документ";
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
		"%ИмяСправочникаДокументаДО%",
		ОбменСКонтрагентамиДОСлужебныйКлиентСервер.ИмяСправочникаДокументовДО());
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("Документы", Документы);
	
	УникальныеОрганизации = Новый Соответствие;
	УникальныеКонтрагенты = Новый Соответствие;
	УникальныеНаправления = Новый Соответствие;
	УникальныеСостояния = Новый Соответствие;
	УникальныеУИДПакетов = Новый Соответствие;
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		Если УникальныеОрганизации[Выборка.Организация] = Неопределено Тогда
			УникальныеОрганизации.Вставить(Выборка.Организация, Истина);
		КонецЕсли;
		
		Если УникальныеКонтрагенты[Выборка.Контрагент] = Неопределено Тогда
			УникальныеКонтрагенты.Вставить(Выборка.Контрагент, Истина);
		КонецЕсли;
		
		Если УникальныеНаправления[Выборка.Направление] = Неопределено Тогда
			УникальныеНаправления.Вставить(Выборка.Направление, Истина);
		КонецЕсли;
		
		Если УникальныеСостояния[Выборка.Состояние] = Неопределено Тогда
			УникальныеСостояния.Вставить(Выборка.Состояние, Истина);
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Выборка.ИдентификаторПакета)
			И УникальныеУИДПакетов[Выборка.ИдентификаторПакета] = Неопределено Тогда
			
			УникальныеУИДПакетов.Вставить(Выборка.ИдентификаторПакета, Истина);
		КонецЕсли;
		
	КонецЦикла;
	
	Для Каждого Элемент Из УникальныеОрганизации Цикл
		СтруктураВозврата.Организации.Добавить(Элемент.Ключ);
	КонецЦикла;
	
	Для Каждого Элемент Из УникальныеКонтрагенты Цикл
		СтруктураВозврата.Контрагенты.Добавить(Элемент.Ключ);
	КонецЦикла;
	
	Для Каждого Элемент Из УникальныеНаправления Цикл
		СтруктураВозврата.Направления.Добавить(Элемент.Ключ);
	КонецЦикла;
	
	Для Каждого Элемент Из УникальныеСостояния Цикл
		СтруктураВозврата.Состояния.Добавить(Элемент.Ключ);
	КонецЦикла;
	
	Для Каждого Элемент Из УникальныеУИДПакетов Цикл
		СтруктураВозврата.ИдентификаторыПакетов.Добавить(Элемент.Ключ);
	КонецЦикла;
	
	Возврат СтруктураВозврата;
	
КонецФункции

// Определяет, поддерживает ли способ обмена ЭДО пакетную обработку документов
// 
// Параметры:
//  СпособОбмена - ПеречислениеСсылка.СпособыОбменаЭД - Способ обмена ЭДО
// 
// Возвращаемое значение:
//  Булево - Поддерживает ли способ обмена пакетную обработку документов.
Функция СпособОбменаПоддерживаетПакетнуюОбработку(СпособОбмена) Экспорт
	
	Возврат (СпособОбмена = Перечисления.СпособыОбменаЭД.ЧерезСервис1СЭДО);
	
КонецФункции

Функция ДанныеНеотраженныхДокументовЭДОПакета(ИдентификаторПакетаЭДО) Экспорт
	
	НеотраженныеДокументыЭДО = Новый Соответствие;
	
	Если Не ЗначениеЗаполнено(ИдентификаторПакетаЭДО) Тогда
		Возврат НеотраженныеДокументыЭДО;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	СоставПакетовДокументовЭДО.ЭлектронныйДокумент,
		|	ЭлектронныйДокументВходящийЭДО.ВидДокумента,
		|	ЭлектронныйДокументВходящийЭДО.ДатаДокумента КАК Дата,
		|	ЭлектронныйДокументВходящийЭДО.НомерДокумента КАК Номер
		|ИЗ
		|	РегистрСведений.СоставПакетовДокументовЭДО КАК СоставПакетовДокументовЭДО
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ПакетыЭДОДокументооборот КАК ПакетыЭДОДокументооборот
		|		ПО СоставПакетовДокументовЭДО.ИдентификаторПакета = ПакетыЭДОДокументооборот.ИдентификаторПакетаБЭД
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОбъектыУчетаДокументовЭДО КАК ОбъектыУчетаДокументовЭДО
		|		ПО СоставПакетовДокументовЭДО.ЭлектронныйДокумент = ОбъектыУчетаДокументовЭДО.ЭлектронныйДокумент
		|		И ОбъектыУчетаДокументовЭДО.Актуальный
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЭлектронныйДокументВходящийЭДО КАК ЭлектронныйДокументВходящийЭДО
		|		ПО СоставПакетовДокументовЭДО.ЭлектронныйДокумент = ЭлектронныйДокументВходящийЭДО.Ссылка
		|ГДЕ
		|	ПакетыЭДОДокументооборот.ИдентификаторПакета = &ИдентификаторПакета
		|	И ОбъектыУчетаДокументовЭДО.ЭлектронныйДокумент ЕСТЬ NULL";
	Запрос.УстановитьПараметр("ИдентификаторПакета", ИдентификаторПакетаЭДО);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		ДанныеДокументаЭДО = Новый Структура;
		ДанныеДокументаЭДО.Вставить("ВидДокумента", Выборка.ВидДокумента);
		ДанныеДокументаЭДО.Вставить("Номер", Выборка.Номер);
		ДанныеДокументаЭДО.Вставить("Дата", Выборка.Дата);
		
		НеотраженныеДокументыЭДО.Вставить(Выборка.ЭлектронныйДокумент, ДанныеДокументаЭДО);
	КонецЦикла;
	
	Возврат НеотраженныеДокументыЭДО;
	
КонецФункции

// Состояния документов пакетов ЭДО.
// 
// Параметры:
//  ИдентификаторыПакетов - Массив Из УникальныйИдентификатор
// 
// Возвращаемое значение:
//  Соответствие Из КлючИЗначение:
//    * Ключ - УникальныйИдентификатор - Идентификатор пакета
//    * Значение - Соответствие из КлючИЗначение:
//      ** Ключ - ОпределяемыйТип.ДокументДОДляЭДО
//      ** Значение - ПеречислениеСсылка.СостоянияЭДОДокументооборот
//
Функция СостоянияДокументовПакетовЭДО(ИдентификаторыПакетов) Экспорт
	
	ДокументыПакетов = Новый Соответствие();
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	СоставПакетовЭДОДокументооборот.ИдентификаторПакета КАК ИдентификаторПакета,
		|	СоставПакетовЭДОДокументооборот.Документ КАК Документ,
		|	ЕСТЬNULL(СостояниеДокументовПоЭДОСрезПоследних.СостояниеВерсииДокументаПоЭДО,
		|		ЗНАЧЕНИЕ(Перечисление.СостоянияЭДОДокументооборот.ПустаяСсылка)) КАК Состояние
		|ИЗ
		|	РегистрСведений.СоставПакетовЭДОДокументооборот КАК СоставПакетовЭДОДокументооборот
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СостояниеДокументовПоЭДО.СрезПоследних КАК СостояниеДокументовПоЭДОСрезПоследних
		|		ПО СостояниеДокументовПоЭДОСрезПоследних.ДокументДО = СоставПакетовЭДОДокументооборот.Документ
		|ГДЕ
		|	СоставПакетовЭДОДокументооборот.ИдентификаторПакета В (&ИдентификаторыПакетов)";
	Запрос.УстановитьПараметр("ИдентификаторыПакетов", ИдентификаторыПакетов);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		ДанныеПакета = ДокументыПакетов.Получить(Выборка.ИдентификаторПакета);
		Если ДанныеПакета = Неопределено Тогда
			ДанныеПакета = Новый Соответствие();
			ДокументыПакетов.Вставить(Выборка.ИдентификаторПакета, ДанныеПакета);
		КонецЕсли;
		
		ДанныеПакета.Вставить(Выборка.Документ, Выборка.Состояние);
		
	КонецЦикла;
	
	Возврат ДокументыПакетов;
	
КонецФункции

// Возвращает состав пакетов ЭДО документов Документооборота
// 
// Параметры:
//  ИдентификаторыПакетов - Массив Из УникальныйИдентификатор
// 
// Возвращаемое значение:
//  Соответствие Из КлючИЗначение:
//    * Ключ - УникальныйИдентификатор
//    * Значение - Массив Из ОпределяемыйТип.ДокументДОДляЭДО
//
Функция СоставПакетовЭДОДокументов(ИдентификаторыПакетов) Экспорт
	
	СоставПакетов = Новый Соответствие();
	Для Каждого ИдентификаторПакета Из ИдентификаторыПакетов Цикл
		СоставПакетов[ИдентификаторПакета] = Новый Массив();
	КонецЦикла;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	СоставПакетовЭДОДокументооборот.Документ КАК Документ,
		|	СоставПакетовЭДОДокументооборот.ИдентификаторПакета КАК ИдентификаторПакета
		|ИЗ
		|	РегистрСведений.СоставПакетовЭДОДокументооборот КАК СоставПакетовЭДОДокументооборот
		|ГДЕ
		|	СоставПакетовЭДОДокументооборот.ИдентификаторПакета В (&ИдентификаторыПакетов)";
	Запрос.УстановитьПараметр("ИдентификаторыПакетов", ИдентификаторыПакетов);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		СоставПакета = СоставПакетов[Выборка.ИдентификаторПакета];
		СоставПакета.Добавить(Выборка.Документ);
	КонецЦикла;
	
	Возврат СоставПакетов;
	
КонецФункции

#КонецОбласти

#Область ОтражениеДокументовВДО

// Возвращает документы ДО для документов ЭДО
// 
// Параметры:
//  ЭлектронныеДокументы - Массив - Документы ЭДО для которых необходимо получить документы ДО
// 
// Возвращаемое значение:
//  Соответствие Из КлючИЗначение:
//    * Ключ - ДокументСсылка.ЭлектронныйДокументВходящийЭДО, ДокументСсылка.ЭлектронныйДокументИсходящийЭДО - Документы ЭДО
//    * Значение - ОпределяемыйТип.ДокументДОДляЭДО - Документ ДО для документа ЭДО из ключа
Функция ДокументыДОЭлектронныхДокументов(ЭлектронныеДокументы) Экспорт
	
	ДокументыДО = Новый Соответствие;
	Для Каждого ДокументЭДО Из ЭлектронныеДокументы Цикл
		ДокументыДО.Вставить(ДокументЭДО, Неопределено);
	КонецЦикла;
	
	Запрос = Новый Запрос(ИнтеграцияЭДО.ТекстЗапросаОбъектовУчетаЭлектронныхДокументов(Истина));
	Запрос.УстановитьПараметр("ЭлектронныйДокумент", ЭлектронныеДокументы);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Если ЗначениеЗаполнено(Выборка.ОбъектУчета)
			И ТипЗнч(Выборка.ОбъектУчета) = ОбменСКонтрагентамиДОСлужебныйКлиентСервер.ТипДокументаДО()
			И Выборка.Актуальный = Истина Тогда
			
			ДокументыДО[Выборка.ЭлектронныйДокумент] = Выборка.ОбъектУчета;
		КонецЕсли;
	КонецЦикла;
	
	Возврат ДокументыДО;
	
КонецФункции

// Виды документов доступные для отражения входящих ЭДО.
// 
// Возвращаемое значение:
//  Массив Из СправочникСсылка.ВидыДокументов - Виды документов доступные для отражения входящих ЭДО
Функция ВидыДокументовДоступныеДляОтраженияВходящихЭДО() Экспорт
	
	ВидыДокументов = Новый Массив;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ВидыДокументов.Ссылка
		|ИЗ
		|	Справочник.ВидыДокументов КАК ВидыДокументов
		|ГДЕ
		|	ВидыДокументов.ВестиУчетСторон
		|	И ВидыДокументов.УчитыватьНедействующиеДокументы";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		ВидыДокументов.Добавить(Выборка.Ссылка);
	КонецЦикла;
	
	Возврат ВидыДокументов;
	
КонецФункции

// Возвращает, доступен ли вид документа ДО для отражения входящего ЭДО
// 
// Параметры:
//  ВидДокумента - СправочникСсылка.ВидыДокументов - Вид документа
// 
// Возвращаемое значение:
//  Булево - Вид документа ДО доступен для отражения входящего ЭДО
Функция ВидДокументаДоступенДляОтраженияВходящегоЭДО(ВидДокумента) Экспорт
	
	РеквизитыВидаДокумента = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ВидДокумента,
		"ВестиУчетСторон, УчитыватьНедействующиеДокументы");
	
	Возврат РеквизитыВидаДокумента.ВестиУчетСторон = Истина
		И РеквизитыВидаДокумента.УчитыватьнедействующиеДокументы = Истина;
	
КонецФункции

// Сообщает пользователю ошибки, возникшие при автоматическом отражении входящего документа ЭДО
// 
// Параметры:
//  КонтекстОтражения - Структура - см. ОбменСКонтрагентамиДОСлужебныйКлиентСервер.НовыйКонтекстОтраженияДокументовВДО()
Процедура СообщитьПользователюОшибкиОтраженияВходящегоЭДО(КонтекстОтражения) Экспорт
	
	Если КонтекстОтражения.АвтоматическоеВыполнение Тогда
		Возврат;
	КонецЕсли;
	
	ДокументЭДО = КонтекстОтражения.ДокументЭДО;
	
	Для Каждого Ошибка Из КонтекстОтражения.Ошибки Цикл
		
		ТекстСообщения = СтрШаблон(
			НСтр("ru = 'При создании документа 1С:Документооборот по входящему ЭДО %1 возникла проблема:'"),
			ДокументЭДО)
			+ Символы.ПС + Ошибка.Описание;
		
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
		
	КонецЦикла;
	
КонецПроцедуры

#Область ЗаполнениеКонтекстаОтражения

// Заполняет контекст отражения входящего документа ЭДО
// 
// Параметры:
//  КонтекстОтражения - Структура - см. ОбменСКонтрагентамиДОСлужебныйКлиентСервер.НовыйКонтекстОтраженияДокументовВДО()
Процедура ЗаполнитьКонтекстОтраженияДокументовВДО(КонтекстОтражения) Экспорт
	
	ДанныеДляЗаполнения = ДанныеДляЗаполненияКонтекстаОтражения();
	
	СобратьДанныеДляЗаполненияКонтекстаОтражения(ДанныеДляЗаполнения, КонтекстОтражения);
	Если КонтекстОтражения.Отказ Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьКонтекстОтраженияПоСобраннымДанным(ДанныеДляЗаполнения, КонтекстОтражения);
	Если КонтекстОтражения.Отказ Тогда
		Возврат;
	КонецЕсли;
	
	Если КонтекстОтражения.АвтоматическоеВыполнение Тогда
		ПроверитьЗаполнениеКонтекстаДляАвтоматическогоОтражения(КонтекстОтражения);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ИнтерфейсДокументаДО

// Заполняет реквизиты документа ДО по контексту отражения входящего документа ЭДО
// 
// Параметры:
//  ДокументДО - ОпределяемыйТип.ДокументДООбъектДляЭДО - Объект документа, у котогого необходимо заполнить реквизиты
//  КонтекстОтражения - Структура - Контекст отражения входящего по ЭДО документа.
//                                  см. ОбменСКонтрагентамиДОСлужебныйКлиентСервер.НовыйКонтекстОтраженияДокументовВДО
Процедура ЗаполнитьРеквизитыДокументаПоКонтекстуОтраженияЭДО(ДокументДО, КонтекстОтражения) Экспорт
	
	ОснованиеЗаполнения = КонтекстОтражения.ОснованиеЗаполненияДокумента.ДанныеДляОтраженияПоЭДО;
	
	Если Не ЗначениеЗаполнено(ДокументДО.Заголовок) Тогда
		ДокументДО.Заголовок = ОснованиеЗаполнения.Заголовок;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ДокументДО.Содержание) Тогда
		ДокументДО.Содержание = ОснованиеЗаполнения.Содержание;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ДокументДО.ВидДокумента) Тогда
		
		ДокументДО.ВидДокумента = ОснованиеЗаполнения.ВидДокумента;
		
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ДокументДО.Тематика)
		И ЗначениеЗаполнено(ОснованиеЗаполнения.Тематика) Тогда
		
		ДокументДО.Тематика = ОснованиеЗаполнения.Тематика;
		
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ДокументДО.Комментарий) Тогда
		ДокументДО.Комментарий = ОснованиеЗаполнения.Комментарий;
	КонецЕсли;
	
	ДокументДО.Организация = ОснованиеЗаполнения.Организация;
	ДокументДО.Контрагент = ОснованиеЗаполнения.Контрагент;
	ДокументДО.Сумма = ОснованиеЗаполнения.Сумма;
	ДокументДО.Валюта = ОснованиеЗаполнения.Валюта;
	
	Если Не ЗначениеЗаполнено(ДокументДО.Папка) Тогда
		ДокументДО.Папка = ОснованиеЗаполнения.Папка;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ДокументДО.ГрифДоступа) Тогда
		ДокументДО.ГрифДоступа = ОснованиеЗаполнения.ГрифДоступа;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ДокументДО.Ответственный) Тогда
		ДокументДО.Ответственный = ОснованиеЗаполнения.Ответственный;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ДокументДО.ВопросДеятельности) Тогда
		ДокументДО.ВопросДеятельности = ОснованиеЗаполнения.ВопросДеятельности;
	КонецЕсли;
	
	ДокументДО.ДатаСоздания = ОснованиеЗаполнения.ДатаСоздания;
	ДокументДО.ФормаДокумента = ОснованиеЗаполнения.ФормаДокумента;
	
	Если Не ЗначениеЗаполнено(ДокументДО.Важность) Тогда
		ДокументДО.Важность = ОснованиеЗаполнения.Важность;
	КонецЕсли;
	
	ДокументДО.Стороны.Очистить();
	Для Каждого Сторона Из ОснованиеЗаполнения.Стороны Цикл
		НоваяСтрока = ДокументДО.Стороны.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Сторона);
	КонецЦикла;
	
КонецПроцедуры

// Заполняет таблицу файлов в форме документа ДО при отражении входящего ЭДО
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - Форма документа ДО
Процедура ЗаполнитьТаблицуФайловПриОтраженииВходящегоЭДО(Форма) Экспорт
	
	ФайлыДобавленные = Форма.ФайлыДобавленные;
	КонтекстОтражения = Форма.КонтекстОтраженияВходящегоЭДО;
	
	ФайлыКСозданию = КонтекстОтражения.ФайлыДляСоздания;
	
	ФайлыДобавленные.Очистить();
	
	Для Каждого Файл Из ФайлыКСозданию Цикл
		
		СведенияОФайле = Файл.СведенияОФайле;
		НоваяСтрока = ФайлыДобавленные.Добавить();
		
		НоваяСтрока.Наименование = СведенияОФайле.ИмяБезРасширения;
		НоваяСтрока.Расширение = СведенияОФайле.РасширениеБезТочки;
		НоваяСтрока.Адрес = СведенияОФайле.АдресВременногоХранилищаФайла;
		НоваяСтрока.Размер = СведенияОФайле.Размер;
		НоваяСтрока.ИндексКартинки =
			ФайловыеФункцииСлужебныйКлиентСервер.ПолучитьИндексПиктограммыФайла(СведенияОФайле.РасширениеБезТочки);
		НоваяСтрока.ДобавленИзШаблона = Ложь;
		НоваяСтрока.ФайлОтраженияЭДО = Истина;
		
	КонецЦикла;
	
	Элементы = Форма.Элементы;
	
	Элементы.ГруппаСпискиФайлов.ТекущаяСтраница = Элементы.ГруппаТаблица;
	
	КоличествоФайловТаблицы = ФайлыКСозданию.Количество();
	Форма.КоличествоФайлов = КоличествоФайловТаблицы;
	Элементы.НаименованиеФайла.Заголовок = ДелопроизводствоКлиентСервер.КоличествоФайловВЗаголовок(
		КоличествоФайловТаблицы);
	
КонецПроцедуры

// Удаляет файлы, касающиеся отражения ЭДО после записи в форме документа ДО
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - Форма документа ДО
Процедура УдалитьФайлыЭДОПриОтражении(Форма) Экспорт
	
	ФайлыДобавленные = Форма.ФайлыДобавленные;
	
	КоличествоСтрок = ФайлыДобавленные.Количество();
	
	ИндексСтроки = 0;
	Для Итератор = 0 По КоличествоСтрок-1 Цикл
		Строка = ФайлыДобавленные[ИндексСтроки];
		Если Строка.ФайлОтраженияЭДО Тогда
			ФайлыДобавленные.Удалить(ИндексСтроки);
		Иначе
			ИндексСтроки = ИндексСтроки + 1;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

// Проверяет корректность заполнения документа ДО и соответствие реквизитов входящему ЭДО перед записью в базе
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - Форма документа ДО
//  Отказ - Булево - Отказать ли в записи
Процедура ПроверитьДокументДОПередОтражениемВходящегоЭДО(Форма, Отказ) Экспорт
	
	ДокументОбъект = Форма.Объект;
	ВидДокумента = ДокументОбъект.ВидДокумента;
	
	Если Не ВидДокументаДоступенДляОтраженияВходящегоЭДО(ВидДокумента) Тогда
		Отказ = Истина;
		ОбщегоНазначения.СообщитьПользователю(
			НСтр("ru = 'Вид документа не может быть выбран для отражения входящего документа по ЭДО.
				|В виде документа должен быть включен учет сторон и учет недействующих документов.'"), ,
			"ВидДокумента");
	КонецЕсли;
	
	КонтекстОтражения = Форма.КонтекстОтраженияВходящегоЭДО;
	
	ОснованиеЗаполнения = КонтекстОтражения.ОснованиеЗаполненияДокумента.ДанныеДляОтраженияПоЭДО;
	СтороныКЗаполнению = ОснованиеЗаполнения.Стороны;
	
	СтороныДокумента = ДокументОбъект.Стороны;
	
	Если СтороныДокумента.Количество() <> СтороныКЗаполнению.Количество() Тогда
		Отказ = Истина;
		ОбщегоНазначения.СообщитьПользователю(
			НСтр("ru = 'Количество сторон документа изменилось. У созданного по входящему ЭДО документа должно быть две стороны:
				|Первая сторона - наша организация, вторая сторона - контрагент, от которого поступил документ'"), ,
			"Стороны");
	КонецЕсли;
	
	Если Отказ = Истина Тогда
		Возврат;
	КонецЕсли;
	
	Для Итератор = 0 По СтороныКЗаполнению.ВГраница() Цикл
		
		СторонаКЗаполнению = СтороныКЗаполнению[Итератор];
		СторонаДокумента = СтороныДокумента[Итератор];
		
		Если СторонаКЗаполнению.Сторона <> СторонаДокумента.Сторона Тогда
			Отказ = Истина;
			ОбщегоНазначения.СообщитьПользователю(
				НСтр("ru = 'Сторона документа изменилась. У созданного по входящему ЭДО документа должно быть две стороны:
					|Первая сторона - наша организация, вторая сторона - контрагент, от которого поступил документ'"), ,
				"Объект.Строны["+Итератор+"]");
		КонецЕсли;
		
		Если НачалоДня(СторонаКЗаполнению.ДатаПодписи) <> СторонаДокумента.ДатаПодписи Тогда
			Отказ = Истина;
			ОбщегоНазначения.СообщитьПользователю(
				НСтр("ru = 'Дата подписания у стороны изменилась.'"), ,
				"Объект.Строны["+Итератор+"].ДатаПодписи");
		КонецЕсли;
		
		Если СторонаКЗаполнению.Подписан <> СторонаДокумента.Подписан Тогда
			Отказ = Истина;
			ОбщегоНазначения.СообщитьПользователю(
				НСтр("ru = 'Признак подписания у стороны изменился.'"), ,
				"Объект.Строны["+Итератор+"].Подписан");
		КонецЕсли;
		
		Если ТипЗнч(СторонаДокумента.Сторона) = Тип("СправочникСсылка.Организации") Тогда
			
			Продолжить;
			
		КонецЕсли;
		
		Если СторонаКЗаполнению.Подписал <> СторонаДокумента.Подписал Тогда
			Отказ = Истина;
			ОбщегоНазначения.СообщитьПользователю(
				НСтр("ru = 'Подписант у стороны изменился.'"), ,
				"Объект.Строны["+Итератор+"].Подписал");
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

// Данные по отражению входящего документа ЭДО.
// 
// Параметры:
//  ДокументЭДО - ДокументСсылка.ЭлектронныйДокументВходящийЭДО - Документ ЭДО
// 
// Возвращаемое значение:
//  Структура:
// * ТребуетОтражения - Булево - Стоит ли документ в списке к созданию документов ДО
// * ОжидаетАвтоматическогоОтражения - Булево - Должен ли документ обрабатываться регламентным заданием
// * ЕстьДокументДО - Булево - Создан ли документ ДО по входящему ЭДО
Функция ДанныеПоОтражениюДокументаЭДО(ДокументЭДО) Экспорт
	
	ДанныеПоОтражению = Новый Структура;
	ДанныеПоОтражению.Вставить("ТребуетОтражения", Ложь);
	ДанныеПоОтражению.Вставить("ОжидаетАвтоматическогоОтражения", Ложь);
	ДанныеПоОтражению.Вставить("ЕстьДокументДО", Ложь);
	
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	ДокументыЭДОКСозданиюВДО.АвтоматическоеСоздание
		|ИЗ
		|	РегистрСведений.ДокументыЭДОКСозданиюВДО КАК ДокументыЭДОКСозданиюВДО
		|ГДЕ
		|	ДокументыЭДОКСозданиюВДО.ДокументЭДО = &ДокументЭДО
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ Первые 1
		|	ДокументыДО.Ссылка
		|ИЗ
		|	Справочник.Файлы КАК Файлы
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ОбъектыУчетаДокументовЭДО КАК ОбъектыУчетаДокументовЭДО
		|		ПО ОбъектыУчетаДокументовЭДО.ОбъектУчета = Файлы.ТекущаяВерсия
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.%ИмяСправочникаДокументаДО% КАК ДокументыДО
		|		ПО Файлы.ВладелецФайла = ДокументыДО.Ссылка
		|ГДЕ
		|	ОбъектыУчетаДокументовЭДО.ЭлектронныйДокумент = &ДокументЭДО
		|	И ОбъектыУчетаДокументовЭДО.Актуальный";
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
		"%ИмяСправочникаДокументаДО%",
		ОбменСКонтрагентамиДОСлужебныйКлиентСервер.ИмяСправочникаДокументовДО());
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("ДокументЭДО", ДокументЭДО);
	
	РезультатыЗапроса = Запрос.ВыполнитьПакет();
	
	КолвоРезультатов = РезультатыЗапроса.Количество();
	
	ВыборкаДокументовКСозданию = РезультатыЗапроса[КолвоРезультатов - 2].Выбрать();
	ВыборкаДокументовДО = РезультатыЗапроса[КолвоРезультатов - 1].Выбрать();
	
	Если ВыборкаДокументовКСозданию.Следующий() Тогда
		
		ДанныеПоОтражению.ТребуетОтражения = Истина;
		ДанныеПоОтражению.ОжидаетАвтоматическогоОтражения = ВыборкаДокументовКСозданию.АвтоматическоеСоздание;
		
	КонецЕсли;
	
	Если ВыборкаДокументовДО.Следующий() Тогда
		ДанныеПоОтражению.ЕстьДокументДО = Истина;
	КонецЕсли;
	
	Возврат ДанныеПоОтражению;
	
КонецФункции

#КонецОбласти

#Область ОперацииПослеСозданияДокументаДО

// Выполняет сопровождающие функции по отражению входящего ЭДО в ДО.
// 
// Параметры:
//  КонтекстОтражения - Структура - см. ОбменСКонтрагентамиДОСлужебныйКлиентСервер.НовыйКонтекстОтраженияДокументовВДО()
Процедура ВыполнитьОтражениеПриСозданииДокументаДО(КонтекстОтражения) Экспорт
	
	СоздатьФайлыОтраженияВходящегоЭДО(КонтекстОтражения);
	Если КонтекстОтражения.Отказ Тогда
		Возврат;
	КонецЕсли;
	
	ОтразитьСозданиеДокументаВоВходящемПакетеЭДО(КонтекстОтражения);
	Если КонтекстОтражения.Отказ Тогда
		Возврат;
	КонецЕсли;
	
	ОтметитьОтражениеВходящегоЭДОВДО(КонтекстОтражения);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область ПереопределениеБЭД

// Получает данные о юридическом (физическом) лице по ссылке.
//
// Параметры:
//  ЮрФизЛицо - СправочникСсылка - ссылка на элемент справочника, по которому получаются данные.
//  Сведения - см. ЭлектронноеВзаимодействие.СтруктураДанныхЮрФизЛица
//
Процедура ПолучитьДанныеЮрФизЛица(ЮрФизЛицо, Сведения) Экспорт
	
	Если ЗначениеЗаполнено(ЮрФизЛицо)
		И (ТипЗнч(ЮрФизЛицо) = Тип("СправочникСсылка.Организации")
		ИЛИ ТипЗнч(ЮрФизЛицо) = Тип("СправочникСсылка.Контрагенты")) Тогда
		
		ЭтоОрганизация = (ТипЗнч(ЮрФизЛицо) = Тип("СправочникСсылка.Организации"));
		
		ПолучаемыеРеквизиты = Новый Массив(); // Массив Из Строка
		ПолучаемыеРеквизиты.Добавить("ЮрФизЛицо");
		ПолучаемыеРеквизиты.Добавить("Наименование");
		ПолучаемыеРеквизиты.Добавить("НаименованиеПолное");
		ПолучаемыеРеквизиты.Добавить("ИНН");
		ПолучаемыеРеквизиты.Добавить("КПП");
		Если ЭтоОрганизация Тогда
			ПолучаемыеРеквизиты.Добавить("ОГРН");
		Иначе
			ПолучаемыеРеквизиты.Добавить("РегистрационныйНомер");
		КонецЕсли;
		
		РеквизитыЮрФизЛица = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ЮрФизЛицо,
			СтрСоединить(ПолучаемыеРеквизиты, ", "));
		
		Сведения.ЮрФизЛицо = РеквизитыЮрФизЛица.ЮрФизЛицо;
		Сведения.ПолноеНаименование = РеквизитыЮрФизЛица.НаименованиеПолное;
		Сведения.ОфициальноеНаименование = РеквизитыЮрФизЛица.Наименование;
		Сведения.Наименование = РеквизитыЮрФизЛица.Наименование;
		Сведения.ИНН = РеквизитыЮрФизЛица.ИНН;
		Сведения.КПП = РеквизитыЮрФизЛица.КПП;
		Если ЭтоОрганизация Тогда
			Сведения.ОГРН = РеквизитыЮрФизЛица.ОГРН;
		Иначе
			Сведения.ОГРН = РеквизитыЮрФизЛица.РегистрационныйНомер;
		КонецЕсли;
		Сведения.Телефоны = "";
		Сведения.КодПоОКПО = "";
		
		Запрос = Новый Запрос;
		Запрос.Текст =
		"ВЫБРАТЬ
		|	БанковскиеСчета.НомерСчета,
		|	БанковскиеСчета.Банк.Наименование КАК Банк,
		|	БанковскиеСчета.Банк.Код КАК БИК
		|ИЗ
		|	Справочник.БанковскиеСчета КАК БанковскиеСчета
		|ГДЕ
		|	БанковскиеСчета.Владелец = &Владелец
		|	И НЕ БанковскиеСчета.ПометкаУдаления";
		Запрос.УстановитьПараметр("Владелец", ЮрФизЛицо);
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			ЗаполнитьЗначенияСвойств(Сведения, Выборка);
		КонецЕсли;
		
		Если РеквизитыЮрФизЛица.ЮрФизЛицо = Перечисления.ЮрФизЛицо.ИндивидуальныйПредприниматель
			ИЛИ РеквизитыЮрФизЛица.ЮрФизЛицо = Перечисления.ЮрФизЛицо.ФизЛицо Тогда
			
			ФИО = ФизическиеЛицаКлиентСервер.ЧастиИмени(РеквизитыЮрФизЛица.Наименование);
			Сведения.Фамилия = ФИО.Фамилия;
			Сведения.Имя = ФИО.Имя;
			Сведения.Отчество = ФИО.Отчество;
		КонецЕсли;
		
	КонецЕсли;
	
	ТаблицаКИ = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъекта(ЮрФизЛицо,, ТекущаяДатаСеанса(), Ложь);
	
	Для Каждого СтрокаТаблицыКИ Из ТаблицаКИ Цикл
		
		Если СтрокаТаблицыКИ.Вид = Справочники.ВидыКонтактнойИнформации.ЮрАдресОрганизации
			ИЛИ СтрокаТаблицыКИ.Вид = Справочники.ВидыКонтактнойИнформации.ЮридическийАдресКонтрагента Тогда
			
			Сведения.ЮридическийАдресXML = СтрокаТаблицыКИ.ЗначенияПолей;
			Сведения.ЮридическийАдрес = СтрокаТаблицыКИ.Представление;
			
		ИначеЕсли СтрокаТаблицыКИ.Вид = Справочники.ВидыКонтактнойИнформации.ФактАдресОрганизации
			ИЛИ СтрокаТаблицыКИ.Вид = Справочники.ВидыКонтактнойИнформации.ФактическийАдресКонтрагента Тогда
			
			Сведения.ФактическийАдресXML = СтрокаТаблицыКИ.ЗначенияПолей;
			Сведения.ФактическийАдрес = СтрокаТаблицыКИ.Представление;
			
		ИначеЕсли СтрокаТаблицыКИ.Вид = Справочники.ВидыКонтактнойИнформации.ПочтовыйАдресОрганизации
			ИЛИ СтрокаТаблицыКИ.Вид = Справочники.ВидыКонтактнойИнформации.ПочтовыйАдресКонтрагента Тогда
			
			Сведения.ПочтовыйАдресXML = СтрокаТаблицыКИ.ЗначенияПолей;
			Сведения.ПочтовыйАдрес = СтрокаТаблицыКИ.Представление;
			
		ИначеЕсли СтрокаТаблицыКИ.Вид = Справочники.ВидыКонтактнойИнформации.ТелефонОрганизации
			ИЛИ СтрокаТаблицыКИ.Вид = Справочники.ВидыКонтактнойИнформации.ТелефонКонтрагента Тогда
			
			Сведения.ТелефоныXML = СтрокаТаблицыКИ.ЗначенияПолей;
			Сведения.Телефоны = СтрокаТаблицыКИ.Представление;
			
		ИначеЕсли СтрокаТаблицыКИ.Вид = Справочники.ВидыКонтактнойИнформации.EmailОрганизации
			ИЛИ СтрокаТаблицыКИ.Вид = Справочники.ВидыКонтактнойИнформации.EmailКонтрагента Тогда
			
			Сведения.ЭлектроннаяПочта = СтрокаТаблицыКИ.Представление;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

// Получает данные о физическом лице по ссылке.
// Для использования в МЧД следует вызывать МашиночитаемыеДоверенностиПереопределяемый.ПриИзмененииДанныеФизЛица.
//
// Параметры:
//  ФизЛицо - СправочникСсылка.ФизическиеЛица - ссылка на элемент справочника, по которому получаются данные.
//  Сведения - см. ЭлектронноеВзаимодействие.СтруктураДанныхФизЛица
//
// См. ЭлектронноеВзаимодействиеПереопределяемый.ПолучитьДанныеФизЛица.
Процедура ПолучитьДанныеФизЛица(ФизЛицо, Сведения) Экспорт

	Если Не ЗначениеЗаполнено(ФизЛицо) Тогда
		Возврат
	КонецЕсли;
	
	Сведения.Ссылка = ФизЛицо;
	
	Реквизиты = "ДатаРождения, ИНН, СтраховойНомерПФР, Пол";
	
	СтруктураДанных = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ФизЛицо, Реквизиты);
	
	СтруктураДанных.Свойство("ДатаРождения", Сведения.ДатаРождения);
	СтруктураДанных.Свойство("ИНН", Сведения.ИНН);
	СтруктураДанных.Свойство("СтраховойНомерПФР", Сведения.СтраховойНомерПФР);	
	СтруктураДанных.Свойство("Гражданство", Неопределено);
	СтруктураДанных.Свойство("Пол", Сведения.Пол);
	СтруктураДанных.Свойство("МестоРождения", Неопределено);
	ФизическиеЛица.ФамилияИнициалыФизЛица(ФизЛицо, Сведения.Фамилия, Сведения.Имя, Сведения.Отчество);
	
	ТаблицаКИ = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъекта(ФизЛицо,,
		ТекущаяДатаСеанса(), Ложь);
	
	Для Каждого СтрокаТаблицыКИ Из ТаблицаКИ Цикл	
		Если СтрокаТаблицыКИ.Вид = Справочники.ВидыКонтактнойИнформации.ПочтовыйАдресАдресата Тогда		
			Сведения.АдресМестаПроживанияXML = СтрокаТаблицыКИ.ЗначенияПолей;
			Сведения.АдресМестаПроживания = СтрокаТаблицыКИ.Представление;		
		ИначеЕсли СтрокаТаблицыКИ.Вид = Справочники.ВидыКонтактнойИнформации.ДомашнийАдресФизическогоЛица Тогда		
			Сведения.АдресПоПропискеXML = СтрокаТаблицыКИ.ЗначенияПолей;
			Сведения.АдресПоПрописке = СтрокаТаблицыКИ.Представление;		
		ИначеЕсли СтрокаТаблицыКИ.Вид = Справочники.ВидыКонтактнойИнформации.ДомашнийТелефонФизическогоЛица Тогда		
			Сведения.ТелефоныXML = СтрокаТаблицыКИ.ЗначенияПолей;
			Сведения.Телефоны = СтрокаТаблицыКИ.Представление;			
		ИначеЕсли СтрокаТаблицыКИ.Вид = Справочники.ВидыКонтактнойИнформации.EmailФизическогоЛица Тогда			
			Сведения.ЭлектроннаяПочта = СтрокаТаблицыКИ.Представление;			
		КонецЕсли;	
	КонецЦикла;

КонецПроцедуры

// Переопределение процедуры ОбменСКонтрагентамиПереопределяемый.ЗаполнитьРегистрационныеДанныеОрганизации
//
// Параметры:
//  Организация - ОпределяемыйТип.Организация - ссылка на элемент справочника Организации;
//  ДанныеОрганизации - см. ИнтеграцияЭДО.НоваяСтруктураДанныхОрганизации
//
Процедура ЗаполнитьРегистрационныеДанныеОрганизации(Организация, ДанныеОрганизации) Экспорт
	
	РеквизитыОрганизации = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Организация,
		"Наименование, НаименованиеПолное, ИНН, КПП, ОГРН, ЮрФизЛицо");
	
	Если ЗначениеЗаполнено(РеквизитыОрганизации.НаименованиеПолное) Тогда
		ДанныеОрганизации.Наименование = РеквизитыОрганизации.НаименованиеПолное;
	Иначе
		ДанныеОрганизации.Наименование = РеквизитыОрганизации.Наименование;
	КонецЕсли;
	
	ДанныеОрганизации.ИНН = РеквизитыОрганизации.ИНН;
	ДанныеОрганизации.КПП = РеквизитыОрганизации.КПП;
	ДанныеОрганизации.ОГРН = РеквизитыОрганизации.ОГРН;
	
	ОрганизацияФизЛицо = (РеквизитыОрганизации.ЮрФизЛицо = Перечисления.ЮрФизЛицо.ИндивидуальныйПредприниматель
		ИЛИ РеквизитыОрганизации.ЮрФизЛицо = Перечисления.ЮрФизЛицо.ФизЛицо);
	
	Если ОрганизацияФизЛицо Тогда
		ДанныеОрганизации.ЮрФизЛицо = "ФизЛицо";
	Иначе
		ДанныеОрганизации.ЮрФизЛицо = "ЮрЛицо";
	КонецЕсли;
	
	ЗаполнитьДанныеРуководителя(Организация, ДанныеОрганизации);
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	КонтактнаяИнформация.Значение
		|ИЗ
		|	Справочник.Организации.КонтактнаяИнформация КАК КонтактнаяИнформация
		|ГДЕ
		|	КонтактнаяИнформация.Ссылка = &Ссылка
		|	И КонтактнаяИнформация.Вид = &Вид";
	Запрос.УстановитьПараметр("Ссылка", Организация);
	Запрос.УстановитьПараметр("Вид",    Справочники.ВидыКонтактнойИнформации.ЮрАдресОрганизации);
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		
		АдресСтруктурой = РаботаСАдресами.СведенияОбАдресе(Выборка.Значение);
		Если АдресСтруктурой.Свойство("Индекс") Тогда
			ДанныеОрганизации.Индекс = АдресСтруктурой.Индекс;
		КонецЕсли;
		Если АдресСтруктурой.Свойство("Регион") Тогда
			ДанныеОрганизации.Регион = АдресСтруктурой.Регион;
		КонецЕсли;
		Если АдресСтруктурой.Свойство("КодРегиона") Тогда
			ДанныеОрганизации.КодРегиона = АдресСтруктурой.КодРегиона;
		КонецЕсли;
		Если АдресСтруктурой.Свойство("Район") Тогда
			ДанныеОрганизации.Район = АдресСтруктурой.Район;
		КонецЕсли;
		Если АдресСтруктурой.Свойство("Город") Тогда
			ДанныеОрганизации.Город = АдресСтруктурой.Город;
		КонецЕсли;
		Если АдресСтруктурой.Свойство("НаселенныйПункт") Тогда
			ДанныеОрганизации.НаселенныйПункт = АдресСтруктурой.НаселенныйПункт;
		КонецЕсли;
		Если АдресСтруктурой.Свойство("Улица") Тогда
			ДанныеОрганизации.Улица = АдресСтруктурой.Улица;
		КонецЕсли;
		Если АдресСтруктурой.Свойство("Здание") И ЗначениеЗаполнено(АдресСтруктурой.Здание) Тогда
			ДанныеОрганизации.Дом = АдресСтруктурой.Здание.Номер;
		КонецЕсли;
		Если АдресСтруктурой.Свойство("Корпуса") И ЗначениеЗаполнено(АдресСтруктурой.Корпуса) Тогда
			ДанныеОрганизации.Корпус = АдресСтруктурой.Корпуса[0].Номер;
		КонецЕсли;
		Если АдресСтруктурой.Свойство("Помещения") И ЗначениеЗаполнено(АдресСтруктурой.Помещения) Тогда
			ДанныеОрганизации.Квартира = АдресСтруктурой.Помещения[0].Номер;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

// Создает контрагента в информационной базе по реквизитам.
//
// Параметры:
//   РеквизитыКонтрагента - Структура:
//    * ИНН - Строка
//    * КПП - Строка
//    * Наименование - Строка
//   Контрагент - ОпределяемыйТип.КонтрагентБЭД
//   Отказ - Булево
//
Процедура СоздатьКонтрагентаПоРеквизитам(Знач РеквизитыКонтрагента, Контрагент, Отказ = Ложь) Экспорт
	
	Если Не ПравоДоступа("Добавление", Метаданные.Справочники.Контрагенты) Тогда
		Отказ = Истина;
		ТекстСообщения = НСтр("ru = 'Недостаточно прав для создания контрагента.'");
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,,,, Отказ);
		ЗаписьЖурналаРегистрации(НСтр("ru = 'Создание контрагента'", ОбщегоНазначения.КодОсновногоЯзыка()),
			УровеньЖурналаРегистрации.Ошибка, Метаданные.Справочники.Контрагенты,,
			ТекстСообщения);
		Возврат;
	КонецЕсли;
	
	Попытка
		
		Объект = Справочники.Контрагенты.СоздатьЭлемент();
		Объект.ИНН = РеквизитыКонтрагента.ИНН;
		Объект.КПП = РеквизитыКонтрагента.КПП;
		Объект.НаименованиеПолное = РеквизитыКонтрагента.Наименование;
		Объект.Наименование = РеквизитыКонтрагента.Наименование;
		
		Если СтрДлина(РеквизитыКонтрагента.ИНН) = 10 Тогда
			Объект.ЮрФизЛицо = Перечисления.ЮрФизЛицо.ЮрЛицо;
		ИначеЕсли СтрДлина(РеквизитыКонтрагента.ИНН) = 12 Тогда
			Объект.ЮрФизЛицо = Перечисления.ЮрФизЛицо.ИндивидуальныйПредприниматель;
		КонецЕсли;
	
		Объект.Записать();
		Контрагент = Объект.Ссылка;
	Исключение
		ТекстСообщения = НСтр("ru = 'Ошибка при записи нового элемента справочника Контрагенты.'")
			+ Символы.ПС + НСтр("ru ='Подробности см. в журнале регистрации.'",
			ОбщегоНазначения.КодОсновногоЯзыка());
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,,,, Отказ);
		ЗаписьЖурналаРегистрации(НСтр("ru = 'Создание контрагента'", ОбщегоНазначения.КодОсновногоЯзыка()),
			УровеньЖурналаРегистрации.Ошибка, Метаданные.Справочники.Контрагенты,,
			ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
	КонецПопытки;
	
КонецПроцедуры

#КонецОбласти

#Область УстаревшиеПроцедурыИФункции

// Устарела. Следует использовать ОбменЭДОДокументооборот.ДокументыЭДОДокументовДО
// Возвращает электронный документ, связанный с документом 1С:Документооборот
// 
// Параметры:
//  ДокументДО - ОпределяемыйТип.ДокументДОДляЭДО - Документ ДО для которого необходимо получить документ ЭДО
// 
// Возвращаемое значение:
//  ДокументСсылка.ЭлектронныйДокументВходящийЭДО, ДокументСсылка.ЭлектронныйДокументИсходящийЭДО, Неопределено -
//              Документ ЭДО с которым связан документ ДО, неопределено, если документа ЭДО не найдено.
Функция ЭлектронныйДокументДокументаДО(ДокументДО) Экспорт
	
	ДокументыДО = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ДокументДО);
	ДокументыЭДОПоДокументамДО = ОбменЭДОДокументооборот.ДокументыЭДОДокументовДО(ДокументыДО);
	
	ДокументыЭДО = ДокументыЭДОПоДокументамДО[ДокументДО];
	
	Если ДокументыЭДО.Количество() > 0 Тогда
		Возврат ДокументыЭДО[0];
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ЗаполнитьПараметрыЭДПоСсылке(Знач Источник, ПараметрыЭД, ФорматCML = Ложь)
	
	Если Не ЗначениеЗаполнено(Источник) Тогда
		Возврат;
	КонецЕсли;
	
	ВерсияФайла = Неопределено;
	Если ТипЗнч(Источник) = Тип("СправочникСсылка.ВерсииФайлов") Тогда 
		ВерсияФайла = Источник;
		Источник = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Источник, "Владелец.ВладелецФайла");
	КонецЕсли;
	
	ВариантыФормированияУниверсальныхДокументов = ОбменСКонтрагентами.ВариантыФормированияУниверсальныхДокументов();
	
	ПараметрыЭД.Контрагент = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Источник, "Контрагент");
	
	ПараметрыИсточника = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Источник, "ВидДокумента, Организация");	
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьУчетПоОрганизациям") Тогда
		ПараметрыЭД.Организация = ПараметрыИсточника.Организация;
	Иначе
		ПараметрыЭД.Организация = Справочники.Организации.ОрганизацияПоУмолчанию();
	КонецЕсли;
	
	ТипВходящегоЭД = ТипВходящегоЭД(Источник);
	
	Если ТипВходящегоЭД <> Неопределено Тогда
		
		ПараметрыЭД.Тип = ТипВходящегоЭД;
		
		ПараметрыЭД.ДоговорКонтрагента = Неопределено;
		ПараметрыЭД.Направление        = Перечисления.НаправленияЭДО.Входящий;
		
		Возврат;
		
	КонецЕсли;
	
	НастройкиОтправки = РегистрыСведений.НастройкиОтправкиДокументовПоЭДО.НастройкиОтправкиВидаДокумента(
		ПараметрыЭД.Организация,
		ПараметрыЭД.Контрагент, 
		ПараметрыИсточника.ВидДокумента);
	
	Если НастройкиОтправки <> Неопределено Тогда
		
		ТипДокумента = Неопределено;
		Если ТипЗнч(НастройкиОтправки) <> Тип("Структура") Тогда
			ТипДокумента = Неопределено;
		Иначе
			НастройкиОтправки.Свойство("ТипДокумента", ТипДокумента);
			Если Не ЗначениеЗаполнено(ТипДокумента) Тогда
				ТипДокументаИзФайла = ТипДокументаПоВерсииФайла(ВерсияФайла);
				Если ЗначениеЗаполнено(ТипДокументаИзФайла) Тогда
					ТипДокумента = ТипДокументаИзФайла;
				Иначе
					ТипДокумента = Перечисления.ТипыДокументовЭДО.Прочее;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
		ПараметрыЭД.Тип = ТипДокумента;
		
		ПараметрыЭД.ДоговорКонтрагента = Неопределено;
		ПараметрыЭД.Направление        = Перечисления.НаправленияЭДО.Исходящий;
		
		Если ТипДокумента = Перечисления.ТипыДокументовЭДО.УПД
			Или ТипДокумента = Перечисления.ТипыДокументовЭДО.УКД Тогда
			
			ПараметрыЭД.ФормированиеУниверсальногоДокумента = 
				ВариантыФормированияУниверсальныхДокументов.Обязательно;
			
		Иначе
			
			ПараметрыЭД.ФормированиеУниверсальногоДокумента = 
				ВариантыФормированияУниверсальныхДокументов.Запрещено;
			
		КонецЕсли;
		
		Возврат;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьПараметрыЭДПоОбъекту(Знач Источник, ПараметрыЭД, ФорматCML = Ложь)
	
	Если ТипЗнч(Источник) = Тип("СправочникОбъект.ВерсииФайлов") Тогда 
		
		ВладелецФайла = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Источник.Владелец, "ВладелецФайла");		
		Если ЗначениеЗаполнено(ВладелецФайла)
			И ТипЗнч(ВладелецФайла) = ОбменСКонтрагентамиДОСлужебныйКлиентСервер.ТипДокументаДО() Тогда
			
			ЗаполнитьПараметрыЭДПоСсылке(ВладелецФайла, ПараметрыЭД, ФорматCML);
		КонецЕсли;
		
		Возврат;
		
	КонецЕсли;
	
	Если ТипЗнч(Источник) <> Тип("СправочникОбъект.ДокументыПредприятия") Тогда
		Возврат;
	КонецЕсли;
	
	ВариантыФормированияУниверсальныхДокументов = ОбменСКонтрагентами.ВариантыФормированияУниверсальныхДокументов();
	
	ПараметрыЭД.Контрагент = Источник.Контрагент;
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьУчетПоОрганизациям") Тогда
		ПараметрыЭД.Организация = Источник.Организация;
	Иначе
		ПараметрыЭД.Организация = Справочники.Организации.ОрганизацияПоУмолчанию();
	КонецЕсли;
	
	ТипВходящегоЭД = ТипВходящегоЭД(Источник.Ссылка);
	
	Если ТипВходящегоЭД <> Неопределено Тогда
		
		ПараметрыЭД.Тип = ТипВходящегоЭД;
		
		ПараметрыЭД.ДоговорКонтрагента = Неопределено;
		ПараметрыЭД.Направление        = Перечисления.НаправленияЭДО.Входящий;
		
		Возврат;
		
	КонецЕсли;
	
	НастройкиОтправки = РегистрыСведений.НастройкиОтправкиДокументовПоЭДО.НастройкиОтправкиВидаДокумента(
		ПараметрыЭД.Организация,
		ПараметрыЭД.Контрагент, 
		Источник.ВидДокумента);
	
	Если НастройкиОтправки <> Неопределено Тогда
		
		ТипДокумента = Неопределено;
		Если ТипЗнч(НастройкиОтправки) <> Тип("Структура") Тогда
			ТипДокумента = Неопределено;
		Иначе
			НастройкиОтправки.Свойство("ТипДокумента", ТипДокумента);
			Если Не ЗначениеЗаполнено(ТипДокумента) Тогда
				ТипДокумента = Перечисления.ТипыДокументовЭДО.Прочее;
			КонецЕсли;
		КонецЕсли;
		
		ПараметрыЭД.Тип = ТипДокумента;
		
		ПараметрыЭД.ДоговорКонтрагента = Неопределено;
		ПараметрыЭД.Направление        = Перечисления.НаправленияЭДО.Исходящий;
		
		Если ТипДокумента = Перечисления.ТипыДокументовЭДО.УПД
			Или ТипДокумента = Перечисления.ТипыДокументовЭДО.УКД Тогда
			
			ПараметрыЭД.ФормированиеУниверсальногоДокумента = 
				ВариантыФормированияУниверсальныхДокументов.Обязательно;
			
		Иначе
			
			ПараметрыЭД.ФормированиеУниверсальногоДокумента = 
				ВариантыФормированияУниверсальныхДокументов.Запрещено;
			
		КонецЕсли;
		
		Возврат;
		
	КонецЕсли;
	
КонецПроцедуры

// Возвращает вид входящего электронного документа, связанного с текущим документом ДО
//
// Параметры:
//   Документ - ОпределяемыйТип.ДокументДОДляЭДО - Документ ДО
//              для которого необходимо определить вид входящего ЭД
//
// Возвращаемое значение:
//   ПеречислениеСсылка.ТипыДокументовЭДО - Вид входящего ЭД, если таковой имеется,
//       Неопределено - если входящих ЭД по документу нет.
Функция ТипВходящегоЭД(Документ)
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	Файлы.Ссылка КАК Файл,
		|	Файлы.ТекущаяВерсия КАК ТекущаяВерсия
		|ПОМЕСТИТЬ ФайлыДокумента
		|ИЗ
		|	Справочник.Файлы КАК Файлы
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СлужебныеФайлыДокументов КАК СлужебныеФайлыДокументов
		|		ПО (СлужебныеФайлыДокументов.Файл = Файлы.Ссылка)
		|ГДЕ
		|	Файлы.ВладелецФайла = &Документ
		|	И СлужебныеФайлыДокументов.Файл ЕСТЬ NULL
		|	И НЕ Файлы.ПометкаУдаления
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ФайлыДокумента.Файл КАК Файл,
		|	ФайлыДокумента.ТекущаяВерсия КАК ТекущаяВерсия,
		|	ВидыДокументовЭДО.ТипДокумента КАК ТипДокумента
		|ИЗ
		|	РегистрСведений.ОбъектыУчетаДокументовЭДО КАК ОбъектыУчетаДокументовЭДО
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ФайлыДокумента КАК ФайлыДокумента
		|		ПО ФайлыДокумента.ТекущаяВерсия = ОбъектыУчетаДокументовЭДО.ОбъектУчета
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ВидыДокументовЭДО КАК ВидыДокументовЭДО
		|		ПО ВЫРАЗИТЬ(ОбъектыУчетаДокументовЭДО.ЭлектронныйДокумент КАК
		|			Документ.ЭлектронныйДокументВходящийЭДО).ВидДокумента = ВидыДокументовЭДО.Ссылка
		|ГДЕ
		|	ОбъектыУчетаДокументовЭДО.Актуальный
		|	И ОбъектыУчетаДокументовЭДО.ЭлектронныйДокумент ССЫЛКА Документ.ЭлектронныйДокументВходящийЭДО";
	
	Запрос.УстановитьПараметр("Документ", Документ);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		Возврат Выборка.ТипДокумента;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

Функция ТипДокументаПоВерсииФайла(ВерсияФайла)
	
	Если Не ЗначениеЗаполнено(ВерсияФайла) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Файл = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ВерсияФайла, "Владелец");
	
	ДанныеФайлаИДвоичныеДанные = РаботаСФайламиВызовСервера.ДанныеФайлаИДвоичныеДанные(
		Файл,
		ВерсияФайла);
	
	Если ДанныеФайлаИДвоичныеДанные = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Если ВРег(ДанныеФайлаИДвоичныеДанные.ДанныеФайла.Расширение) <> "XML" Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ДанныеЭДФайла = ОбменСКонтрагентами.ДанныеЭлектронногоДокументаПоФайлу(ДанныеФайлаИДвоичныеДанные.ДвоичныеДанные);
	
	ПолучитьСообщенияПользователю(Истина);
	
	Если ТипЗнч(ДанныеЭДФайла) <> Тип("Структура") Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Возврат ДанныеЭДФайла.НовыйЭД.ВидЭД;
	
КонецФункции

// Данные для синхронизации состояний
// 
// Параметры:
//  Источник - РегистрСведенийНаборЗаписей.СостоянияПоОбъектамУчетаЭДО - Источник события записи состояний в БЭД
// 
// Возвращаемое значение:
//  Массив Из Структура:
//    * ДокументДО - ОпределяемыйТип.ДокументДОДляЭДО
//    * ДокументЭДО - ДокументСсылка.ЭлектронныйДокументВходящийЭДО, ДокументСсылка.ЭлектронныйДокументИсходящийЭДО -
//    * ФайлДО - СправочникСсылка.Файлы
//    * Контрагент - СправочникСсылка.Контрагенты
//    * Направление - ПеречислениеСсылка.НаправленияЭДО
//    * СостояниеЭДО - ПеречислениеСсылка.СостоянияДокументовЭДО - Состояние устанавливаемое
//    * СостояниеЗаписанное - ПеречислениеСсылка.СостоянияДокументовЭДО - Состояние записанное
//    * ТребуетсяПодтверждение - Булево
//    * УчетнаяЗаписьЭДО - Строка
//
Функция ДанныеДляСинхронизацииСостояния(Источник)
	
	ОбъектыУчета = Источник.ВыгрузитьКолонку("СсылкаНаОбъект");
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	Файлы.ВладелецФайла КАК ДокументДО,
		|	Файлы.Ссылка КАК ФайлДО,
		|	ОбъектыУчетаДокументовЭДО.ОбъектУчета КАК ВерсияФайла,
		|	ОбъектыУчетаДокументовЭДО.ЭлектронныйДокумент КАК ЭлектронныйДокумент,
		|	ОбъектыУчетаДокументовЭДО.ЭлектронныйДокумент.Контрагент КАК Контрагент,
		|	ВЫБОР
		|		КОГДА ОбъектыУчетаДокументовЭДО.ЭлектронныйДокумент ССЫЛКА Документ.ЭлектронныйДокументВходящийЭДО
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.НаправленияЭДО.Входящий)
		|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.НаправленияЭДО.Исходящий)
		|	КОНЕЦ КАК Направление,
		|	СостояниеДокументовПоЭДО.СостояниеВерсииДокументаПоЭДО КАК СостояниеЗаписанное,
		|	ОбъектыУчетаДокументовЭДО.ЭлектронныйДокумент.ТребуетсяПодтверждение КАК ТребуетсяПодтверждение,
		|	ОбъектыУчетаДокументовЭДО.ЭлектронныйДокумент.ИдентификаторОрганизации КАК УчетнаяЗаписьЭДО
		|ИЗ
		|	Справочник.Файлы КАК Файлы
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ОбъектыУчетаДокументовЭДО КАК ОбъектыУчетаДокументовЭДО
		|		ПО (ОбъектыУчетаДокументовЭДО.ОбъектУчета = Файлы.ТекущаяВерсия
		|		И ОбъектыУчетаДокументовЭДО.Актуальный)
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СостояниеДокументовПоЭДО КАК СостояниеДокументовПоЭДО
		|		ПО Файлы.ВладелецФайла = СостояниеДокументовПоЭДО.ДокументДО
		|ГДЕ
		|	ОбъектыУчетаДокументовЭДО.ОбъектУчета В (&ОбъектыУчета)";
	
	Запрос.УстановитьПараметр("ОбъектыУчета", ОбъектыУчета);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	ДанныеДляСинхронизации = Новый Массив;
	
	Для Каждого Запись Из Источник.ЭтотОбъект Цикл
		СтруктураПоиска = Новый Структура("ВерсияФайла", Запись.СсылкаНаОбъект);
		
		Выборка.НайтиСледующий(СтруктураПоиска);
		
		Если Не ЗначениеЗаполнено(Выборка.ДокументДО)
			Или ТипЗнч(Выборка.ДокументДО) <> ОбменСКонтрагентамиДОСлужебныйКлиентСервер.ТипДокументаДО() Тогда
			
			Продолжить;
		КонецЕсли;
		
		ДанныеСостояния = Новый Структура;
		ДанныеСостояния.Вставить("ДокументДО", Выборка.ДокументДО);
		ДанныеСостояния.Вставить("ДокументЭДО", Выборка.ЭлектронныйДокумент);
		ДанныеСостояния.Вставить("ФайлДО", Выборка.ФайлДО);
		ДанныеСостояния.Вставить("Контрагент", Выборка.Контрагент);
		ДанныеСостояния.Вставить("Направление", Выборка.Направление);
		ДанныеСостояния.Вставить("СостояниеЭДО", Запись.СостояниеЭДО);
		ДанныеСостояния.Вставить("СостояниеЗаписанное", Выборка.СостояниеЗаписанное);
		ДанныеСостояния.Вставить("ТребуетсяПодтверждение", Выборка.ТребуетсяПодтверждение);
		ДанныеСостояния.Вставить("УчетнаяЗаписьЭДО", Выборка.УчетнаяЗаписьЭДО);
		
		ДанныеДляСинхронизации.Добавить(ДанныеСостояния);
		
	КонецЦикла;
	
	Возврат ДанныеДляСинхронизации;
	
КонецФункции

Функция РольФайловДляОтправкиПоЭДО(ВидДокумента) Экспорт
	
	ТаблицаРолей = Делопроизводство.РолиФайловДляВидаДокументов(ВидДокумента);
	
	Для Каждого СтрРоли Из ТаблицаРолей Цикл
		Если СтрРоли.ФайлЭлектронногоДокумента Тогда
			Возврат СтрРоли.Роль;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Справочники.РолиФайлов.ПустаяСсылка();
	
КонецФункции

#Область ЗагрузкаОтветныхПодписей

Процедура ОбработатьЗагрузкуОтветногоТитула(Сообщение, ДанныеСообщения, КонтекстДиагностики)
	
	ДанныеДокументовДО = ДанныеСвязанныхДокументовДОПоСообщениюЭДО(Сообщение);
	
	РеквизитыСообщения = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Сообщение,
		"ЭлектронныйДокумент.Контрагент, ОсновнойФайл");
	
	Контрагент = РеквизитыСообщения.ЭлектронныйДокументКонтрагент;
	ОсновнойФайлСообщения = РеквизитыСообщения.ОсновнойФайл;
	
	Для Каждого Элемент Из ДанныеДокументовДО Цикл
		
		Документ = Элемент.Ключ;
		
		ПодписиОсновногоФайла = ЭлектроннаяПодпись.УстановленныеПодписи(ОсновнойФайлСообщения);
		
		ДокументОбъект = Документ.ПолучитьОбъект();
		Если ДокументОбъект.Стороны.Количество() > 0 Тогда
			Для Каждого СвойстваПодписи Из ПодписиОсновногоФайла Цикл
				СтрокаКонтрагента = ДокументОбъект.Стороны.Найти(Контрагент, "Сторона");
				Если Не СтрокаКонтрагента = Неопределено Тогда
					ПодписантОтКонтрагента = НайтиДобавитьКонтактноеЛицоИзСертификата(
						Контрагент, СвойстваПодписи.Сертификат);
					ДатаПодписанияКонтрагентом = СвойстваПодписи.ДатаПроверкиПодписи;
					
					СтрокаКонтрагента.Подписан = Истина;
					СтрокаКонтрагента.ДатаПодписи = ДатаПодписанияКонтрагентом;
					СтрокаКонтрагента.Подписал = ПодписантОтКонтрагента;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
		ДокументОбъект.ДополнительныеСвойства.Вставить("ЗаписьПодписанногоОбъекта", Истина);
		ДокументОбъект.ДополнительныеСвойства.Вставить("РазрешитьЗаписьОбъектаИзДругойСистемы", Истина);
		ДокументОбъект.Записать();
		
	КонецЦикла;
	
КонецПроцедуры

// Данные связанных документов ДО по сообщению ЭДО.
// 
// Параметры:
//  Сообщение - ДокументСсылка.СообщениеЭДО
// 
// Возвращаемое значение:
//  Соответствие Из КлючИЗначение:
//    * Ключ - ОпределяемыйТип.ДокументДОДляЭДО
//    * Значение - Структура:
//      ** Файл - СправочникСсылка.Файлы
//      ** ВерсияФайла - СправочникСсылка.ВерсииФайлов
//
Функция ДанныеСвязанныхДокументовДОПоСообщениюЭДО(Сообщение)
	
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	ВерсииФайлов.Ссылка КАК ВерсияФайла,
		|	ВерсииФайлов.Владелец КАК Файл
		|ПОМЕСТИТЬ ФайлыДО
		|ИЗ
		|	РегистрСведений.ОбъектыУчетаДокументовЭДО КАК ОбъектыУчетаДокументовЭДО
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ВерсииФайлов КАК ВерсииФайлов
		|		ПО ОбъектыУчетаДокументовЭДО.ОбъектУчета = ВерсииФайлов.Ссылка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.СообщениеЭДО КАК СообщениеЭДО
		|		ПО ОбъектыУчетаДокументовЭДО.ЭлектронныйДокумент = СообщениеЭДО.ЭлектронныйДокумент
		|ГДЕ
		|	СообщениеЭДО.Ссылка = &СообщениеЭДО
		|	И ОбъектыУчетаДокументовЭДО.Актуальный
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДокументыДО.Ссылка КАК Документ,
		|	ФайлыДО.Файл КАК Файл,
		|	ФайлыДО.ВерсияФайла КАК ВерсияФайла
		|ИЗ
		|	Справочник.Файлы КАК Файлы
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ФайлыДО КАК ФайлыДО
		|		ПО Файлы.Ссылка = ФайлыДО.Файл
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.%ИмяСправочникаДокументаДО% КАК ДокументыДО
		|		ПО Файлы.ВладелецФайла = ДокументыДО.Ссылка";
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
		"%ИмяСправочникаДокументаДО%",
		ОбменСКонтрагентамиДОСлужебныйКлиентСервер.ИмяСправочникаДокументовДО());
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("СообщениеЭДО", Сообщение);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	ДанныеСвязанныхДокументов = Новый Соответствие();
	
	Пока Выборка.Следующий() Цикл
		
		Документ = Выборка.Документ;
		
		ДанныеДокумента = ДанныеСвязанныхДокументов[Документ];
		Если ДанныеДокумента = Неопределено Тогда
			
			ДанныеДокумента = Новый Структура;
			ДанныеДокумента.Вставить("Файл", Справочники.Файлы.ПустаяСсылка());
			ДанныеДокумента.Вставить("ВерсияФайла", Справочники.ВерсииФайлов.ПустаяСсылка());
			
			ДанныеСвязанныхДокументов[Документ] = ДанныеДокумента;
			
		КонецЕсли;
		
		ДанныеДокумента.Файл = Выборка.Файл;
		ДанныеДокумента.ВерсияФайла = Выборка.ВерсияФайла;
		
	КонецЦикла;
	
	Возврат ДанныеСвязанныхДокументов;
	
КонецФункции

Функция ЭтоНоваяПодпись(УстановленныеПодписи, СвойстваПодписи)
	
	Если НЕ ЗначениеЗаполнено(УстановленныеПодписи) Тогда
		Возврат Истина;
	КонецЕсли;
	
	Для Каждого СвойстваУстановленнойПодписи Из УстановленныеПодписи Цикл
		Если СвойстваУстановленнойПодписи.Подпись = СвойстваПодписи.Подпись Тогда
			Возврат Ложь;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Истина;
	
КонецФункции

#КонецОбласти

#Область ДанныеДокументовПоЭДО

#Область ОпределениеИдентификаторовЭДО

// Новые идентификаторы ЭДОДокумента ДО.
// 
// Возвращаемое значение:
//  Структура:
//   * ИдентификаторОтправителя - Строка
//   * ИдентификаторПолучателя - Строка
Функция НовыеИдентификаторыЭДОДокументаДО()
	
	Идентификаторы = Новый Структура;
	Идентификаторы.Вставить("ИдентификаторОтправителя", "");
	Идентификаторы.Вставить("ИдентификаторПолучателя", "");
	
	Возврат Идентификаторы;
	
КонецФункции

// Возвращает таблицы идентификаторов обмена ЭДО для документов ДО по различным параметрам
// 
// Параметры:
//  ДокументыДО - Массив Из ОпределяемыйТип.ДокументДОДляЭДО
// 
// Возвращаемое значение:
//  Структура:
//    * ПоДокументамЭДО - ТаблицаЗначений - Самый надежный источник. Идентификаторы в актуальном документе ЭДО:
//      ** ДокументДО - ОпределяемыйТип.ДокументДОДляЭДО
//      ** Направление - ПеречислениеСсылка.НаправленияЭДО
//      ** ИдентификаторОтправителя - Строка
//      ** ИдентификаторПолучателя - Строка
//    * ПоФиксированномуВиду - ТаблицаЗначений - Согласно настройкам отправки, если в настройках указан вид ЭДО:
//      ** ДокументДО - ОпределяемыйТип.ДокументДОДляЭДО
//      ** ИдентификаторОтправителя - Строка
//      ** ИдентификаторПолучателя - Строка
//    * ПоАвтоопределениюВида - ТаблицаЗначений - Согласно настройкам отправки, если в настройках указан вид ЭДО:
//      ** ДокументДО - ОпределяемыйТип.ДокументДОДляЭДО
//      ** ВидДокументаЭДО - СправочникСсылка.ВидыДокументовЭДО
//      ** ИдентификаторОтправителя - Строка
//      ** ИдентификаторПолучателя - Строка
//
Функция ТаблицыДляОпределенияИдентификаторовЭДО(ДокументыДО)
	
	ТекстыЗапросов = Новый Массив;
	ТекстыЗапросов.Добавить(
		"ВЫБРАТЬ
		|	Файлы.ВладелецФайла КАК ДокументДО,
		|	ОбъектыУчетаДокументовЭДО.ЭлектронныйДокумент КАК ДокументЭДО
		|ПОМЕСТИТЬ АктуальныеДокументыЭДОДокументовДО
		|ИЗ
		|	Справочник.Файлы КАК Файлы
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ОбъектыУчетаДокументовЭДО КАК ОбъектыУчетаДокументовЭДО
		|		ПО Файлы.ТекущаяВерсия = ОбъектыУчетаДокументовЭДО.ОбъектУчета
		|ГДЕ
		|	Файлы.ВладелецФайла В (&ДокументыДО)
		|	И ОбъектыУчетаДокументовЭДО.Актуальный");
	ТекстыЗапросов.Добавить(
		"ВЫБРАТЬ
		|	АктуальныеДокументыЭДОДокументовДО.ДокументДО КАК ДокументДО,
		|	ЭлектронныйДокументВходящийЭДО.ИдентификаторКонтрагента КАК ИдентификаторОтправителя,
		|	ЭлектронныйДокументВходящийЭДО.ИдентификаторОрганизации КАК ИдентификаторПолучателя,
		|	ЗНАЧЕНИЕ(Перечисление.НаправленияЭДО.Входящий) КАК Направление
		|ПОМЕСТИТЬ ИдентификаторыПоАктуальнымДокументамЭДО
		|ИЗ
		|	АктуальныеДокументыЭДОДокументовДО КАК АктуальныеДокументыЭДОДокументовДО
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЭлектронныйДокументВходящийЭДО КАК ЭлектронныйДокументВходящийЭДО
		|		ПО АктуальныеДокументыЭДОДокументовДО.ДокументЭДО = ЭлектронныйДокументВходящийЭДО.Ссылка
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	АктуальныеДокументыЭДОДокументовДО.ДокументДО,
		|	ЭлектронныйДокументИсходящийЭДО.ИдентификаторОрганизации,
		|	ЭлектронныйДокументИсходящийЭДО.ИдентификаторКонтрагента,
		|	ЗНАЧЕНИЕ(Перечисление.НаправленияЭДО.Исходящий)
		|ИЗ
		|	АктуальныеДокументыЭДОДокументовДО КАК АктуальныеДокументыЭДОДокументовДО
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЭлектронныйДокументИсходящийЭДО КАК ЭлектронныйДокументИсходящийЭДО
		|		ПО АктуальныеДокументыЭДОДокументовДО.ДокументЭДО = ЭлектронныйДокументИсходящийЭДО.Ссылка");
	ТекстыЗапросов.Добавить(
		СтрЗаменить(
			"ВЫБРАТЬ
			|	ДокументыДокументооборота.Ссылка КАК ДокументДО,
			|	НастройкиОтправкиДокументовПоЭДО.Отправитель КАК Организация,
			|	НастройкиОтправкиДокументовПоЭДО.Получатель КАК Контрагент,
			|	НастройкиОтправкиДокументовПоЭДО.ВидДокументаЭДО КАК ВидДокументаЭДО
			|ПОМЕСТИТЬ НастройкиОтправкиДО
			|ИЗ
			|	Справочник.%ИмяСправочникаДокументаДО% КАК ДокументыДокументооборота
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.НастройкиОтправкиДокументовПоЭДО КАК НастройкиОтправкиДокументовПоЭДО
			|		ПО НастройкиОтправкиДокументовПоЭДО.Отправитель = ДокументыДокументооборота.Организация
			|		И НастройкиОтправкиДокументовПоЭДО.Получатель = ДокументыДокументооборота.Контрагент
			|		И НастройкиОтправкиДокументовПоЭДО.ВидДокумента = ДокументыДокументооборота.ВидДокумента
			|ГДЕ
			|	ДокументыДокументооборота.Ссылка В (&ДокументыДО)",
			"%ИмяСправочникаДокументаДО%", ОбменСКонтрагентамиДОСлужебныйКлиентСервер.ИмяСправочникаДокументовДО()));
	ТекстыЗапросов.Добавить(
		"ВЫБРАТЬ
		|	НастройкиОтправкиДО.ДокументДО КАК ДокументДО,
		|	НастройкиОтправкиДО.ВидДокументаЭДО КАК ВидДокументаЭДО,
		|	НастройкиОтправкиЭлектронныхДокументовПоВидам.ИдентификаторОтправителя КАК ИдентификаторОтправителя,
		|	НастройкиОтправкиЭлектронныхДокументовПоВидам.ИдентификаторПолучателя КАК ИдентификаторПолучателя
		|ПОМЕСТИТЬ ИдентификаторыПоАвтоопределениюВида
		|ИЗ
		|	НастройкиОтправкиДО КАК НастройкиОтправкиДО
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.НастройкиОтправкиЭлектронныхДокументовПоВидам КАК
		|			НастройкиОтправкиЭлектронныхДокументовПоВидам
		|		ПО НастройкиОтправкиДО.Организация = НастройкиОтправкиЭлектронныхДокументовПоВидам.Отправитель
		|		И НастройкиОтправкиДО.Контрагент = НастройкиОтправкиЭлектронныхДокументовПоВидам.Получатель
		|ГДЕ
		|	НастройкиОтправкиДО.ВидДокументаЭДО = &ПустойВидДокументаЭДО");
	ТекстыЗапросов.Добавить(
		"ВЫБРАТЬ
		|	НастройкиОтправкиДО.ДокументДО КАК ДокументДО,
		|	НастройкиОтправкиЭлектронныхДокументовПоВидам.ИдентификаторОтправителя КАК ИдентификаторОтправителя,
		|	НастройкиОтправкиЭлектронныхДокументовПоВидам.ИдентификаторПолучателя КАК ИдентификаторПолучателя
		|ПОМЕСТИТЬ ИдентификаторыПоФиксированномуВиду
		|ИЗ
		|	НастройкиОтправкиДО КАК НастройкиОтправкиДО
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.НастройкиОтправкиЭлектронныхДокументовПоВидам КАК
		|			НастройкиОтправкиЭлектронныхДокументовПоВидам
		|		ПО НастройкиОтправкиДО.Организация = НастройкиОтправкиЭлектронныхДокументовПоВидам.Отправитель
		|		И НастройкиОтправкиДО.Контрагент = НастройкиОтправкиЭлектронныхДокументовПоВидам.Получатель
		|		И НастройкиОтправкиДО.ВидДокументаЭДО = НастройкиОтправкиЭлектронныхДокументовПоВидам.ВидДокумента
		|ГДЕ
		|	НастройкиОтправкиДО.ВидДокументаЭДО <> &ПустойВидДокументаЭДО");
	
	НеобходимыеТаблицы = Новый Структура;
	НеобходимыеТаблицы.Вставить("ПоДокументамЭДО", "ИдентификаторыПоАктуальнымДокументамЭДО");
	НеобходимыеТаблицы.Вставить("ПоФиксированномуВиду", "ИдентификаторыПоФиксированномуВиду");
	НеобходимыеТаблицы.Вставить("ПоАвтоопределениюВида", "ИдентификаторыПоАвтоопределениюВида");
	
	Для Каждого Элемент Из НеобходимыеТаблицы Цикл
		ТекстыЗапросов.Добавить(
			"ВЫБРАТЬ
			|	*
			|ИЗ
			|	" + Элемент.Значение + " КАК Таблица");
	КонецЦикла;
	
	ТекстЗапроса = СтрСоединить(ТекстыЗапросов, ОбщегоНазначения.РазделительПакетаЗапросов());
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("ДокументыДО", ДокументыДО);
	Запрос.УстановитьПараметр("ПустойВидДокументаЭДО", Справочники.ВидыДокументовЭДО.ПустаяСсылка());
	РезультатыЗапроса = Запрос.ВыполнитьПакет();
	
	КоличествоПакетов = РезультатыЗапроса.Количество();
	КоличествоНеобходимыхТаблиц = НеобходимыеТаблицы.Количество();
	
	ИтераторТаблицы = 0;
	ВозвращаемыеТаблицы = Новый Структура;
	ВозвращаемыеТаблицы.Вставить("ПоДокументамЭДО", НоваяТаблицаИдентификаторовЭДОПоДокументамЭДО());
	ВозвращаемыеТаблицы.Вставить("ПоФиксированномуВиду", НоваяТаблицаИдентификаторовЭДОПоФиксированномуВиду());
	ВозвращаемыеТаблицы.Вставить("ПоАвтоопределениюВида", НоваяТаблицаИдентификаторовЭДОПоАвтоопределениюВида());
	
	Для Каждого Элемент Из НеобходимыеТаблицы Цикл
		
		ИмяТаблицы = Элемент.Ключ;
		ИндексПакетаТаблицы = КоличествоПакетов - КоличествоНеобходимыхТаблиц + ИтераторТаблицы;
		
		Выборка = РезультатыЗапроса[ИндексПакетаТаблицы].Выбрать();
		
		Пока Выборка.Следующий() Цикл
			НоваяСтрока = ВозвращаемыеТаблицы[ИмяТаблицы].Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
		КонецЦикла;
		
		ИтераторТаблицы = ИтераторТаблицы + 1;
		
	КонецЦикла;
	
	Возврат ВозвращаемыеТаблицы;
	
КонецФункции

Функция НоваяТаблицаИдентификаторовЭДОПоДокументамЭДО()
	
	Таблица = Новый ТаблицаЗначений();
	Таблица.Колонки.Добавить("ДокументДО", ОписаниеТипаДокументаДО());
	Таблица.Колонки.Добавить("Направление", ОписаниеТипаНаправленияЭДО());
	Таблица.Колонки.Добавить("ИдентификаторОтправителя", ОписаниеТипаИдентификатораЭДО());
	Таблица.Колонки.Добавить("ИдентификаторПолучателя", ОписаниеТипаИдентификатораЭДО());
	
	Возврат Таблица;
	
КонецФункции

Функция НоваяТаблицаИдентификаторовЭДОПоФиксированномуВиду()
	
	Таблица = Новый ТаблицаЗначений();
	Таблица.Колонки.Добавить("ДокументДО", ОписаниеТипаДокументаДО());
	Таблица.Колонки.Добавить("ИдентификаторОтправителя", ОписаниеТипаИдентификатораЭДО());
	Таблица.Колонки.Добавить("ИдентификаторПолучателя", ОписаниеТипаИдентификатораЭДО());
	
	Возврат Таблица;
	
КонецФункции

Функция НоваяТаблицаИдентификаторовЭДОПоАвтоопределениюВида()
	
	Таблица = Новый ТаблицаЗначений();
	Таблица.Колонки.Добавить("ДокументДО", ОписаниеТипаДокументаДО());
	Таблица.Колонки.Добавить("ВидДокументаЭДО", ОписаниеТипаВидаДокументаЭДО());
	Таблица.Колонки.Добавить("ИдентификаторОтправителя", ОписаниеТипаИдентификатораЭДО());
	Таблица.Колонки.Добавить("ИдентификаторПолучателя", ОписаниеТипаИдентификатораЭДО());
	
	Возврат Таблица;
	
КонецФункции

Функция ОписаниеТипаДокументаДО()
	
	ТипыДокументаДО = Новый Массив(); // Массив Из Тип
	ТипыДокументаДО.Добавить(ОбменСКонтрагентамиДОСлужебныйКлиентСервер.ТипДокументаДО());
	
	Описание = Новый ОписаниеТипов(ТипыДокументаДО);
	Возврат Описание;
	
КонецФункции

Функция ОписаниеТипаИдентификатораЭДО()
	
	Описание = Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(70, ДопустимаяДлина.Переменная));
	Возврат Описание;
	
КонецФункции

Функция ОписаниеТипаВидаДокументаЭДО()
	
	Описание = Новый ОписаниеТипов("СправочникСсылка.ВидыДокументовЭДО");
	Возврат Описание;
	
КонецФункции

Функция ОписаниеТипаНаправленияЭДО()
	
	Описание = Новый ОписаниеТипов("ПеречислениеСсылка.НаправленияЭДО");
	Возврат Описание;
	
КонецФункции

// Заполняет идентификаторы обмена ЭДО документов согласно таблицам
// 
// Параметры:
//  ИдентификаторыДокументов - Соответствие Из КлючИЗначение:
//    * Ключ - ОпределяемыйТип.ДокументДОДляЭДО
//    * Значение см. НовыеИдентификаторыЭДОДокументаДО
//  ТаблицыИдентификаторов - см. ТаблицыДляОпределенияИдентификаторовЭДО
//  ОбработанныеДокументы - Соответствие Из КлючИЗначение:
//    * Ключ - ОпределяемыйТип.ДокументДОДляЭДО
//    * Значение - Булево
Процедура ЗаполнитьИдентификаторыЭДОПоТаблицам(ИдентификаторыДокументов, ТаблицыИдентификаторов, ОбработанныеДокументы)
	
	Для Каждого Строка Из ТаблицыИдентификаторов.ПоДокументамЭДО Цикл
		
		Если ОбработанныеДокументы[Строка.ДокументДО] = Истина Тогда
			Продолжить;
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(Строка.ИдентификаторОтправителя)
			Или Не ЗначениеЗаполнено(Строка.ИдентификаторПолучателя) Тогда
			
			Продолжить;
		КонецЕсли;
		
		ИдентификаторыДокумента = ИдентификаторыДокументов[Строка.ДокументДО];
		ИдентификаторыДокумента.ИдентификаторОтправителя = Строка.ИдентификаторОтправителя;
		ИдентификаторыДокумента.ИдентификаторПолучателя = Строка.ИдентификаторПолучателя;
		
		ОбработанныеДокументы[Строка.ДокументДО] = Истина;
		
	КонецЦикла;
	
	Для Каждого Строка Из ТаблицыИдентификаторов.ПоФиксированномуВиду Цикл
		
		Если ОбработанныеДокументы[Строка.ДокументДО] = Истина Тогда
			Продолжить;
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(Строка.ИдентификаторОтправителя)
			Или Не ЗначениеЗаполнено(Строка.ИдентификаторПолучателя) Тогда
			
			Продолжить;
		КонецЕсли;
		
		ИдентификаторыДокумента = ИдентификаторыДокументов[Строка.ДокументДО];
		ИдентификаторыДокумента.ИдентификаторОтправителя = Строка.ИдентификаторОтправителя;
		ИдентификаторыДокумента.ИдентификаторПолучателя = Строка.ИдентификаторПолучателя;
		
		ОбработанныеДокументы[Строка.ДокументДО] = Истина;
		
	КонецЦикла;
	
	НаборыИдентификаторовПоДокументам = Новый Соответствие;
	
	Для Каждого Строка Из ТаблицыИдентификаторов.ПоАвтоопределениюВида Цикл
		
		Если ОбработанныеДокументы[Строка.ДокументДО] = Истина Тогда
			Продолжить;
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(Строка.ИдентификаторОтправителя)
			Или Не ЗначениеЗаполнено(Строка.ИдентификаторПолучателя) Тогда
			
			Продолжить;
		КонецЕсли;
		
		НаборИдентификаторовПоДокументу = НаборыИдентификаторовПоДокументам[Строка.ДокументДО];
		Если НаборИдентификаторовПоДокументу = Неопределено Тогда
			НаборИдентификаторовПоДокументу = Новый Соответствие();
			НаборыИдентификаторовПоДокументам.Вставить(Строка.ДокументДО, НаборИдентификаторовПоДокументу);
		КонецЕсли;
		
		КлючНабора = СтрШаблон("%1_%2", Строка.ИдентификаторОтправителя, Строка.ИдентификаторПолучателя);
		Идентификаторы = НовыеИдентификаторыЭДОДокументаДО();
		Идентификаторы.ИдентификаторОтправителя = Строка.ИдентификаторОтправителя;
		Идентификаторы.ИдентификаторПолучателя = Строка.ИдентификаторПолучателя;
		
		НаборИдентификаторовПоДокументу.Вставить(КлючНабора, Идентификаторы);
		
	КонецЦикла;
	
	Для Каждого Элемент Из НаборыИдентификаторовПоДокументам Цикл
		
		Документ = Элемент.Ключ;
		НаборИдентификаторовПоДокументу = Элемент.Значение;
		
		Если ОбработанныеДокументы[Документ] = Истина Тогда
			Продолжить;
		КонецЕсли;
		
		Если НаборИдентификаторовПоДокументу.Количество() <> 1 Тогда
			Продолжить;
		КонецЕсли;
		
		Для Каждого ЭлементНабора Из НаборИдентификаторовПоДокументу Цикл
			
			Идентификаторы = ЭлементНабора.Значение;
			
			ИдентификаторыДокумента = ИдентификаторыДокументов[Строка.ДокументДО];
			ИдентификаторыДокумента.ИдентификаторОтправителя = Идентификаторы.ИдентификаторОтправителя;
			ИдентификаторыДокумента.ИдентификаторПолучателя = Идентификаторы.ИдентификаторПолучателя;
			
		КонецЦикла;
		
		ОбработанныеДокументы[Строка.ДокументДО] = Истина;
		
	КонецЦикла;
	
КонецПроцедуры


#КонецОбласти

#КонецОбласти

#Область СвойстваЭДИзФайла

Функция СведенияОЭДФорматаФНС(ДанныеЭДФайла)
	
	СтруктураВозврата = Новый Структура;
	СтруктураВозврата.Вставить("Валюта", Справочники.Валюты.ПустаяСсылка());
	
	ДеревоДанных = ДанныеЭДФайла.НовыйЭД.ЗначениеРеквизита;
	
	Если ДанныеЭДФайла.НовыйЭД.ВидЭД = Перечисления.ТипыДокументовЭДО.ДокументПредприятия Тогда
		Если ДеревоДанных.ИнформацияДокумента[0].Валюта <> Неопределено Тогда
			КодВалюты = ДеревоДанных.Валюта[0].Код;
		КонецЕсли;	
	Иначе	
		КодВалюты = ДеревоЭлектронногоДокументаБЭД.ЗначениеРеквизитаВДереве(ДеревоДанных, "ВалютаКод", Ложь);
	КонецЕсли;
	
	Если КодВалюты <> Неопределено Тогда
		
		Валюта = Справочники.Валюты.НайтиПоКоду(КодВалюты);
		
		Если ЗначениеЗаполнено(Валюта) Тогда
			СтруктураВозврата.Валюта = Валюта;
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат СтруктураВозврата;
	
КонецФункции

Функция СведенияОЭДФорматаCML(ДанныеЭДФайла)
	
	СтруктураВозврата = Новый Структура;
	СтруктураВозврата.Вставить("Валюта", Справочники.Валюты.ПустаяСсылка());
	
	ДанныеДереваРазбора = ОбменСКонтрагентамиДО.ДанныеДереваРазбораCML(ДанныеЭДФайла.ДеревоРазбора);
	
	Если ДанныеДереваРазбора = Неопределено Тогда
		Возврат СтруктураВозврата;
	КонецЕсли;
	
	ДанныеДокумента = ДанныеДереваРазбора.Документы.Получить(ДанныеЭДФайла.НовыйЭД.ИД);
	
	Если ДанныеДокумента.Свойство("Валюта") Тогда
		
		ДанныеВалюты = ОбменСКонтрагентамиДО.ДанныеВалютыПоДаннымCML(ДанныеДереваРазбора, ДанныеДокумента.Валюта);
		
		Если ДанныеВалюты <> Неопределено Тогда
			Если ЗначениеЗаполнено(ДанныеВалюты.Ссылка) Тогда
				СтруктураВозврата.Валюта = ДанныеВалюты.Ссылка;
			ИначеЕсли ЗначениеЗаполнено(ДанныеВалюты.Код) Тогда
				Валюта = Справочники.Валюты.НайтиПоКоду(ДанныеВалюты.Код);
				
				Если ЗначениеЗаполнено(Валюта) Тогда
					СтруктураВозврата.Валюта = Валюта;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат СтруктураВозврата;
	
КонецФункции

#КонецОбласти

#Область ФормированиеПредставленияДокумента

Функция ПредставлениеДанныхПоФайлу(Файл, Версия, ПараметрыВизуализации) Экспорт
	
	Результат = НовыйРезультатПредставленияЭДО();
	
	Результат.Файл = Файл;
	Результат.Версия = Версия;
	
	Результат.Документ = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Файл, "ВладелецФайла");
	Результат.ДокументЭДО = ЭлектронныйДокументДокументаДО(Результат.Документ);
	
	ДанныеФайлаИДвоичныеДанные = РаботаСФайламиВызовСервера.ДанныеФайлаИДвоичныеДанные(Файл, Версия);
	
	Расширение = ДанныеФайлаИДвоичныеДанные.ДанныеФайла.Расширение;
	
	Результат.ИмяФайла = ОбщегоНазначенияКлиентСервер.ПолучитьИмяСРасширением(
		ДанныеФайлаИДвоичныеДанные.ДанныеФайла.ПолноеНаименованиеВерсии, Расширение);
	
	Результат.ИндексКартинкиФайла = ФайловыеФункцииКлиентСервер.ПолучитьИндексПиктограммыФайла(Расширение);
	
	// Если это не xml файл, то он не формализованный и выводим результат просто так.
	Если ВРег(Расширение) <> "XML" Тогда
		Результат.Успех = Истина;
		Возврат Результат;
	КонецЕсли;
	
	СведенияОФайле = СведенияОЭДИзФайла(ПоместитьВоВременноеХранилище(ДанныеФайлаИДвоичныеДанные.ДвоичныеДанные));
	
	// Если не смогли прочитать -- это не является неуспехом. Просто он не формализованный. Выводим как файл.
	Если ТипЗнч(СведенияОФайле) <> Тип("Структура") Тогда
		Результат.Успех = Истина;
		Возврат Результат;
	КонецЕсли;
	
	
	ВидДокумента = СведенияОФайле.ВидДокументаЭДО;
	ДвоичныеДанныеФайла = ДанныеФайлаИДвоичныеДанные.ДвоичныеДанные;
	
	ДвоичныеДанныеФайлаОтвета = ДвоичныеДанныеФайлаОтвета(Версия);
	
	РезультатПредставленияТабличногоДокумента =
		ЭлектронныеДокументыЭДО.ПредставлениеДанныхСообщения(
			ВидДокумента, ДвоичныеДанныеФайла, ДвоичныеДанныеФайлаОтвета, ПараметрыВизуализации);
	
	Если ТипЗнч(РезультатПредставленияТабличногоДокумента) <> Тип("Структура")
		Или Не РезультатПредставленияТабличногоДокумента.Успех Тогда
		
		Возврат Результат;
	КонецЕсли;
	
	ТабличныйДокумент = РезультатПредставленияТабличногоДокумента.ПредставлениеДокумента;
	
	ДополнитьВизуализациюШтампамиПодписей(ТабличныйДокумент, Файл, Версия);
	
	Результат.Успех = Истина;
	Результат.ТабличныйДокумент = ТабличныйДокумент;
	
	Возврат Результат;
	
КонецФункции

Функция ПредставлениеДанныхПоДокументу(Документ, ПараметрыВизуализации)
	
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	Файлы.ВладелецФайла КАК Документ,
		|	ДокументыДО.ВидДокумента,
		|	Файлы.Ссылка КАК Файл,
		|	ЕСТЬNULL(РолиФайловДокументов.Роль, ЗНАЧЕНИЕ(Справочник.РолиФайлов.ПустаяСсылка)) КАК РольФайла,
		|	Файлы.ТекущаяВерсия КАК Версия
		|ПОМЕСТИТЬ ФайлыДокумента
		|ИЗ
		|	Справочник.Файлы КАК Файлы
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СлужебныеФайлыДокументов КАК СлужебныеФайлыДокументов
		|		ПО СлужебныеФайлыДокументов.Файл = Файлы.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РолиФайловДокументов КАК РолиФайловДокументов
		|		ПО Файлы.Ссылка = РолиФайловДокументов.Файл
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.%ИмяСправочникаДокументаДО% КАК ДокументыДО
		|		ПО Файлы.ВладелецФайла = ДокументыДО.Ссылка
		|ГДЕ
		|	Файлы.ВладелецФайла = &Документ
		|	И СлужебныеФайлыДокументов.Файл ЕСТЬ NULL
		|	И НЕ Файлы.ПометкаУдаления
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ФайлыДокумента.Документ,
		|	ФайлыДокумента.Файл,
		|	ФайлыДокумента.Версия,
		|	ОбъектыУчетаДокументовЭДО.ЭлектронныйДокумент
		|ИЗ
		|	ФайлыДокумента КАК ФайлыДокумента
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ОбъектыУчетаДокументовЭДО КАК ОбъектыУчетаДокументовЭДО
		|		ПО (ОбъектыУчетаДокументовЭДО.ОбъектУчета = ФайлыДокумента.Версия
		|		И ОбъектыУчетаДокументовЭДО.Актуальный)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ФайлыДокумента.Документ,
		|	ФайлыДокумента.Файл,
		|	ФайлыДокумента.Версия
		|ИЗ
		|	ФайлыДокумента КАК ФайлыДокумента
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ВидыДокументов.РолиФайлов КАК ВидыДокументовРолиФайлов
		|		ПО ФайлыДокумента.РольФайла = ВидыДокументовРолиФайлов.Роль
		|		И ФайлыДокумента.ВидДокумента = ВидыДокументовРолиФайлов.Ссылка
		|ГДЕ
		|	ВидыДокументовРолиФайлов.ФайлЭлектронногоДокумента
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ФайлыДокумента.Документ,
		|	ФайлыДокумента.Файл,
		|	ФайлыДокумента.Версия
		|ИЗ
		|	ФайлыДокумента КАК ФайлыДокумента
		|ГДЕ
		|	ФайлыДокумента.РольФайла = ЗНАЧЕНИЕ(Справочник.РолиФайлов.ПустаяСсылка)";
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
		"%ИмяСправочникаДокументаДО%",
		ОбменСКонтрагентамиДОСлужебныйКлиентСервер.ИмяСправочникаДокументовДО());
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("Документ", Документ);
	
	РезультатыЗапроса = Запрос.ВыполнитьПакет();
	КолвоРезультатов =  РезультатыЗапроса.Количество();
	
	ВыборкаПоСвязаннымДокументамЭДО = РезультатыЗапроса[КолвоРезультатов - 2].Выбрать();
	ВыборкаПоФайламКОтправке = РезультатыЗапроса[КолвоРезультатов - 1].Выбрать();
	
	КоличествоСвязанныхДокументовЭДО = ВыборкаПоСвязаннымДокументамЭДО.Количество();
	Если КоличествоСвязанныхДокументовЭДО > 0 Тогда
		
		Если КоличествоСвязанныхДокументовЭДО > 1 Тогда
			
			Результат = НовыйРезультатПредставленияЭДО();
			Результат.Документ = Документ;
			Результат.Успех = Ложь;
			Результат.ОписаниеОшибки = СтрШаблон(
				НСтр("ru = 'С документом %1 по ЭДО.
					|В обмене по ЭДО может учавствовать документ только с одним файлом.'"),
				СтроковыеФункцииКлиентСервер.СтрокаСЧисломДляЛюбогоЯзыка(
					НСтр("ru = ';связан %1 файл;;связано %1 файла;связано %1 файлов;связано %1 файлов'"),
					КоличествоСвязанныхДокументовЭДО));
			
			Возврат Результат;
			
		КонецЕсли;
		
		ВыборкаПоСвязаннымДокументамЭДО.Следующий();
		
		Возврат ПредставлениеДанныхПоФайлу(
			ВыборкаПоСвязаннымДокументамЭДО.Файл,
			ВыборкаПоСвязаннымДокументамЭДО.Версия,
			ПараметрыВизуализации);
		
	КонецЕсли;
	
	КоличествоФайловДляОтправки = ВыборкаПоФайламКОтправке.Количество();
	
	Если КоличествоФайловДляОтправки > 1 Тогда
		
		Результат = НовыйРезультатПредставленияЭДО();
		Результат.Документ = Документ;
		Результат.Успех = Ложь;
		Результат.ОписаниеОшибки = СтрШаблон(
			НСтр("ru = 'К документу %1 для отправки по ЭДО.
				|В обмене по ЭДО может учавствовать документ только с одним файлом.'"),
			СтроковыеФункцииКлиентСервер.СтрокаСЧисломДляЛюбогоЯзыка(
				НСтр("ru = ';прикреплен %1 файл;;прикреплено %1 файла;прикреплено %1 файлов;прикреплено %1 файлов'"),
				КоличествоФайловДляОтправки));
		
		Возврат Результат;
		
	ИначеЕсли КоличествоФайловДляОтправки <=0 Тогда
		
		Результат = НовыйРезультатПредставленияЭДО();
		Результат.Документ = Документ;
		Результат.Успех = Ложь;
		Результат.ОписаниеОшибки = 
			НСтр("ru = 'К документу не прикреплено файлов доступных к отправке по ЭДО.
				|Для обмена по ЭДО к документу должен быть прикреплен файл.'");
		
		Возврат Результат;
		
	КонецЕсли;
	
	ВыборкаПоФайламКОтправке.Следующий();
	
	Возврат ПредставлениеДанныхПоФайлу(
		ВыборкаПоФайламКОтправке.Файл,
		ВыборкаПоФайламКОтправке.Версия,
		ПараметрыВизуализации);
	
КонецФункции

Функция ПредставлениеДанныхПоДокументуЭДО(ДокументЭДО, ПараметрыВизуализации)
	
	Результат = НовыйРезультатПредставленияЭДО();
	
	Результат.ДокументЭДО = ДокументЭДО;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	СообщениеЭДО.Ссылка КАК СообщениеЭДО,
		|	СообщениеЭДОПрисоединенныеФайлы.Ссылка КАК ФайлЭДО,
		|	СообщениеЭДОПрисоединенныеФайлы.Расширение,
		|	СообщениеЭДОПрисоединенныеФайлы.ПолноеИмяФайла
		|ИЗ
		|	Документ.СообщениеЭДО КАК СообщениеЭДО
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.СообщениеЭДОПрисоединенныеФайлы КАК СообщениеЭДОПрисоединенныеФайлы
		|		ПО СообщениеЭДО.ОсновнойФайл = СообщениеЭДОПрисоединенныеФайлы.Ссылка
		|ГДЕ
		|	СообщениеЭДО.ЭлектронныйДокумент = &ДокументЭДО
		|	И СообщениеЭДО.ТипЭлементаРегламента В (&ТипыЭлементовРегламента)";
	Запрос.УстановитьПараметр("ДокументЭДО", ДокументЭДО);
	
	ТипыЭлементовРегламента = Новый Массив;
	ТипыЭлементовРегламента.Добавить(Перечисления.ТипыЭлементовРегламентаЭДО.ИнформацияОтправителя);
	
	Запрос.УстановитьПараметр("ТипыЭлементовРегламента", ТипыЭлементовРегламента);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Не Выборка.Следующий() Тогда
		Результат.ОписаниеОшибки = НСтр("ru = 'Не удалось получить файл ЭДО для визуализации.'");
		Возврат Результат;
	КонецЕсли;
	
	Расширение = Выборка.Расширение;
	СообщениеЭДО = Выборка.СообщениеЭДО;
	ФайлЭДО = Выборка.ФайлЭДО;
	
	Результат.Успех = Истина;
	Результат.ЭтоВизуализацияФайлаЭДО = Истина;
	Результат.ФайлЭДО = ФайлЭДО;
	Результат.ИмяФайла = Выборка.ПолноеИмяФайла;
	Результат.ИндексКартинкиФайла = ФайловыеФункцииКлиентСервер.ПолучитьИндексПиктограммыФайла(Расширение);
	
	Если ВРег(Расширение) <> "XML" Тогда
		Возврат Результат;
	КонецЕсли;
	
	РезультатВизуализацииЭДО =
		ЭлектронныеДокументыЭДО.ПредставлениеДанныхСообщенияПоСсылке(СообщениеЭДО, , ПараметрыВизуализации);
	
	Если ТипЗнч(РезультатВизуализацииЭДО) <> Тип("Структура")
		Или Не РезультатВизуализацииЭДО.Успех Тогда
		
		Возврат Результат;
	КонецЕсли;
	
	Результат.ТабличныйДокумент = РезультатВизуализацииЭДО.ПредставлениеДокумента;
	Возврат Результат;
	
КонецФункции

Функция НовыйРезультатПредставленияЭДО()
	
	РезультатПредставления = Новый Структура;
	РезультатПредставления.Вставить("Успех", Ложь);
	РезультатПредставления.Вставить("ОписаниеОшибки", "");
	РезультатПредставления.Вставить("Документ", ОбменСКонтрагентамиДОСлужебныйКлиентСервер.ПустойДокументДО());
	РезультатПредставления.Вставить("Файл", Справочники.Файлы.ПустаяСсылка());
	РезультатПредставления.Вставить("Версия", Справочники.ВерсииФайлов.ПустаяСсылка());
	РезультатПредставления.Вставить("ДокументЭДО", Неопределено);
	РезультатПредставления.Вставить("ФайлЭДО", Неопределено);
	РезультатПредставления.Вставить("ТабличныйДокумент", Неопределено);
	РезультатПредставления.Вставить("ИндексКартинкиФайла", 0);
	РезультатПредставления.Вставить("ИмяФайла", "");
	РезультатПредставления.Вставить("ЭтоВизуализацияФайлаЭДО", Ложь);
	
	Возврат РезультатПредставления;
	
КонецФункции

Функция ДанныеОтветногоТитула(ВерсияФайлаДО)
	
	ДанныеОтвета = Новый Структура;
	ДанныеОтвета.Вставить("Сообщение", Документы.СообщениеЭДО.ПустаяСсылка());
	ДанныеОтвета.Вставить("Файл", Справочники.СообщениеЭДОПрисоединенныеФайлы.ПустаяСсылка());
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ОбъектыУчетаДокументовЭДО.ЭлектронныйДокумент,
		|	СообщениеЭДО.Ссылка КАК СообщениеОтветногоТитула,
		|	СообщениеЭДО.ОсновнойФайл КАК ФайлОтветногоТитула
		|ИЗ
		|	РегистрСведений.ОбъектыУчетаДокументовЭДО КАК ОбъектыУчетаДокументовЭДО
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.СообщениеЭДО КАК СообщениеЭДО
		|		ПО СообщениеЭДО.ЭлектронныйДокумент = ОбъектыУчетаДокументовЭДО.ЭлектронныйДокумент
		|ГДЕ
		|	ОбъектыУчетаДокументовЭДО.ОбъектУчета = &ОбъектУчета
		|	И ОбъектыУчетаДокументовЭДО.Актуальный
		|	И СообщениеЭДО.ТипЭлементаРегламента = &ТипЭлементаРегламента";
	Запрос.УстановитьПараметр("ОбъектУчета", ВерсияФайлаДО);
	Запрос.УстановитьПараметр("ТипЭлементаРегламента", Перечисления.ТипыЭлементовРегламентаЭДО.ИнформацияПолучателя);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		ДанныеОтвета.Сообщение = Выборка.СообщениеОтветногоТитула;
		ДанныеОтвета.Файл = Выборка.ФайлОтветногоТитула;
	КонецЕсли;
	
	Возврат ДанныеОтвета;
	
КонецФункции

Функция ДвоичныеДанныеФайлаОтвета(ВерсияФайлаДО)
	
	ДанныеОтвета = ДанныеОтветногоТитула(ВерсияФайлаДО);
	ФайлОтвета = ДанныеОтвета.Файл;
	
	Если Не ЗначениеЗаполнено(ФайлОтвета) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ДвоичныеДанныеФайлаОтвета = РаботаСФайлами.ДвоичныеДанныеФайла(ФайлОтвета);
	
	Возврат ДвоичныеДанныеФайлаОтвета;
	
КонецФункции

Процедура ДополнитьВизуализациюШтампамиПодписей(ТабличныйДокумент, ФайлДО ,ВерсияФайлаДО)
	
	ДанныеДляШтампа = ДанныеДляШтампаЭП(ФайлДО, ВерсияФайлаДО);
	
	Если ДанныеДляШтампа = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ДанныеДляШтампа.ПодписиОтправителя.Количество() = 0
		И ДанныеДляШтампа.ПодписиПолучателя.Количество() = 0 Тогда
		
		Возврат;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	Штамп = КриптографияБЭД.ШтампЭлектроннойПодписи(ДанныеДляШтампа);
	
	ОбщегоНазначенияБЭД.ПрисоединитьОбластьКТабличномуДокументу(ТабличныйДокумент, Штамп, "Штамп");
	
КонецПроцедуры

Функция ДанныеДляШтампаЭП(ФайлДО, ВерсияФайлаДО)
	
	ДанныеДляФормированияШтампа = КриптографияБЭД.НовыеДанныеДляФормированияШтампа();
	
	ДокументДО = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ФайлДО, "ВладелецФайла");
	
	Если Не ЗначениеЗаполнено(ДокументДО) Или Не ДелопроизводствоКлиентСервер.ЭтоДокумент(ДокументДО) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ПараметрыДокумента = ПараметрыДокументаПоЭДО(ДокументДО);
	Если ПараметрыДокумента <> Неопределено Тогда
		Направление = ПараметрыДокумента.Направление;
		СостояниеДО = ПараметрыДокумента.СостояниеДО;
	Иначе
		Направление = Перечисления.НаправленияЭДО.Исходящий;
		СостояниеДО = Перечисления.СостоянияЭДОДокументооборот.НеСформирован;
	КонецЕсли;
	
	РеквизитыДокумента = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДокументДО, "Организация");
	
	ДанныеФайлаИДвоичныеДанные = РаботаСФайламиВызовСервера.ДанныеФайлаИДвоичныеДанные(ФайлДО, ВерсияФайлаДО);
	СведенияОФайле = СведенияОЭДИзФайла(ПоместитьВоВременноеХранилище(ДанныеФайлаИДвоичныеДанные.ДвоичныеДанные));
	
	ДанныеДляФормированияШтампа.ИдентификаторДокумента = СведенияОФайле.ИдентификаторДокумента;
	ДанныеДляФормированияШтампа.ЭтоИнформацияОтправителя = Истина;
	ДанныеДляФормированияШтампа.Организация = РеквизитыДокумента.Организация;
	ДанныеДляФормированияШтампа.ЭтоИсходящийДокумент =
		(Направление = Перечисления.НаправленияЭДО.Исходящий);
	
	ОсновноеСостояние = КриптографияБЭД.ОсновныеСостоянияДокументов().Подписан;
	Если Направление = Перечисления.НаправленияЭДО.Входящий
		И СостояниеДО = Перечисления.СостоянияЭДОДокументооборот.Получен Тогда
		
		ОсновноеСостояние = КриптографияБЭД.ОсновныеСостоянияДокументов().Получен;
		
	ИначеЕсли Направление = Перечисления.НаправленияЭДО.Исходящий
		И СостояниеДО = Перечисления.СостоянияЭДОДокументооборот.Отправлен Тогда
		
		ОсновноеСостояние = КриптографияБЭД.ОсновныеСостоянияДокументов().Отправлен;
		
	КонецЕсли;
	
	ДанныеДляФормированияШтампа.ОсновноеСостояние = ОсновноеСостояние;
	
	ДополнительноеСостояние = Неопределено;
	Если СостояниеДО = Перечисления.СостоянияЭДОДокументооборот.ОжидаетсяПодтверждениеАннулирования
		Или СостояниеДО = Перечисления.СостоянияЭДОДокументооборот.ТребуетсяАннулировать Тогда
		
		ДополнительноеСостояние = КриптографияБЭД.ДополнительныеСостоянияДокументов().ВПроцессеАннулирования;
		
	ИначеЕсли СостояниеДО = Перечисления.СостоянияЭДОДокументооборот.Аннулирован Тогда 
		
		ДополнительноеСостояние = КриптографияБЭД.ДополнительныеСостоянияДокументов().Аннулирован;
		
	ИначеЕсли СостояниеДО = Перечисления.СостоянияЭДОДокументооборот.ОжидаетсяИсправление
		Или СостояниеДО = Перечисления.СостоянияЭДОДокументооборот.ТребуетсяУточнение Тогда 
		
		ДополнительноеСостояние = КриптографияБЭД.ДополнительныеСостоянияДокументов().Отклонен;
		
	КонецЕсли;
	
	ДанныеДляФормированияШтампа.ДополнительноеСостояние = ДополнительноеСостояние;
	
	ПодписиФайла = ПодписиСУчетомДоверенностиВФорматеБЭД(ВерсияФайлаДО);
	ПодписиОтветногоТитула = Новый Массив;
	
	ДанныеОтвета = ДанныеОтветногоТитула(ВерсияФайлаДО);
	Если ЗначениеЗаполнено(ДанныеОтвета.Сообщение) Тогда
		ПодписиОтветногоТитула = ЭлектронныеДокументыЭДО.УстановленныеПодписиСУчетомДоверенностей(ДанныеОтвета.Сообщение);
		ИсключитьПодписиОтветногоТитула(ПодписиФайла, ПодписиОтветногоТитула);
	КонецЕсли;
	
	ДанныеДляФормированияШтампа.ПодписиОтправителя = ПодписиФайла;
	ДанныеДляФормированияШтампа.ПодписиПолучателя = ПодписиОтветногоТитула;
	
	ДанныеДляФормированияШтампа.ЕстьОтветнаяПодпись = ЗначениеЗаполнено(ДанныеДляФормированияШтампа.ПодписиПолучателя)
		Или (Направление = Перечисления.НаправленияЭДО.Входящий
			И СостояниеДО <> Перечисления.СостоянияЭДОДокументооборот.НаПодписи);
	
	Возврат ДанныеДляФормированияШтампа;
	
КонецФункции

Функция ПодписиСУчетомДоверенностиВФорматеБЭД(ПодписанныйОбъект)
	
	ДанныеПодписиейСУчетомДоверенностей = Новый Массив;
	
	УстановленныеПодписи = ЭлектроннаяПодпись.УстановленныеПодписи(ПодписанныйОбъект);
	
	УникальныеИдентификаторы = Новый Массив;
	Для Каждого Подпись Из УстановленныеПодписи Цикл
		УникальныеИдентификаторы.Добавить(Подпись.ИдентификаторПодписи);
	КонецЦикла;
	
	ДанныеДоверенностей = РаботаСЭП.ДанныеДоверенностейПодписей(УникальныеИдентификаторы);
	
	Доверенности = Новый Массив;
	Для Каждого Элемент Из ДанныеДоверенностей Цикл
		Доверенности.Добавить(Элемент.Значение.Доверенность);
	КонецЦикла;
	
	СвойстваДоверенностей = МашиночитаемыеДоверенности.ОбщиеСвойстваДоверенностей(Доверенности);
	
	Для Каждого Подпись Из УстановленныеПодписи Цикл
		
		ДанныеПодписи = ЭлектронныеДокументыЭДОКлиентСервер.НовыеДанныеПодписиСУчетомДоверенности();
		
		СвойстваПодписи = Новый Структура;
		СвойстваПодписи.Вставить("Подпись");
		СвойстваПодписи.Вставить("ВидПодписи");
		СвойстваПодписи.Вставить("УстановившийПодпись");
		СвойстваПодписи.Вставить("Комментарий");
		СвойстваПодписи.Вставить("ИмяФайлаПодписи");
		СвойстваПодписи.Вставить("ДатаПодписи");
		СвойстваПодписи.Вставить("ДатаПроверкиПодписи");
		СвойстваПодписи.Вставить("ПодписьВерна");
		СвойстваПодписи.Вставить("ПорядковыйНомер");
		СвойстваПодписи.Вставить("Сертификат");
		СвойстваПодписи.Вставить("Отпечаток");
		СвойстваПодписи.Вставить("Владелец");
		СвойстваПодписи.Вставить("Должность");
		СвойстваПодписи.Вставить("ИдентификаторПодписи");
		
		ЗаполнитьЗначенияСвойств(СвойстваПодписи, Подпись);
		СвойстваПодписи.ВидПодписи = Перечисления.ВидыЭлектронныхПодписей.УсиленнаяКвалифицированная;
		СвойстваПодписи.Владелец = Подпись.КомуВыданСертификат;
		
		ДанныеПодписи.СвойстваПодписи = СвойстваПодписи;
		
		ДанныеДоверенности = ДанныеДоверенностей[Подпись.ИдентификаторПодписи];
		Если ДанныеДоверенности = Неопределено Тогда
			ДанныеПодписи.ЭтоПодписьПоДоверенности = Ложь;
			ДанныеПодписиейСУчетомДоверенностей.Добавить(ДанныеПодписи);
			Продолжить;
		КонецЕсли;
		
		ДанныеПодписи.ЭтоПодписьПоДоверенности = Истина;
		
		РезультатПроверкиПодписи = МашиночитаемыеДоверенности.НовыйРезультатПроверкиПодписи();
		РезультатПроверкиПодписи.ДатаПроверки = ДанныеДоверенности.ДатаПроверкиДоверенности;
		РезультатПроверкиПодписи.Доверенность = ДанныеДоверенности.Доверенность;
		РезультатПроверкиПодписи.ПодписьВерна = ДанныеДоверенности.ДоверенностьВерна;
		РезультатПроверкиПодписи.ПротоколПроверки = ДанныеДоверенности.ПротоколПроверкиДоверенности;
		
		ДанныеПодписи.РезультатПроверкиПоМЧД = РезультатПроверкиПодписи;
		ДанныеПодписи.СвойстваДоверенности = СвойстваДоверенностей[ДанныеДоверенности.Доверенность];
		
		ДанныеПодписиейСУчетомДоверенностей.Добавить(ДанныеПодписи);
		
	КонецЦикла;
	
	Возврат ДанныеПодписиейСУчетомДоверенностей;
	
КонецФункции

Процедура ИсключитьПодписиОтветногоТитула(ПодписиФайла, ПодписиОтветногоТитула)
	
	КлючиПодписейОтветногоТитула = Новый Соответствие;
	
	Для Каждого ДанныеПодписи Из ПодписиОтветногоТитула Цикл
		Ключ = КлючПодписиСДоверенностью(ДанныеПодписи);
		КлючиПодписейОтветногоТитула.Вставить(Ключ, Истина);
	КонецЦикла;
	
	Счетчик = 0;
	Пока Счетчик < ПодписиФайла.Количество() Цикл
		
		ДанныеПодписи = ПодписиФайла[Счетчик];
		Ключ = КлючПодписиСДоверенностью(ДанныеПодписи);
		Если КлючиПодписейОтветногоТитула[Ключ] = Истина Тогда
			ПодписиФайла.Удалить(Счетчик);
		Иначе
			Счетчик = Счетчик + 1;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Функция КлючПодписиСДоверенностью(ДанныеПодписи)
	
	Отпечаток = ДанныеПодписи.СвойстваПодписи.Отпечаток;
	
	Если ДанныеПодписи.ЭтоПодписьПоДоверенности Тогда
		НомерДоверенности = ДанныеПодписи.СвойстваДоверенности.НомерДоверенности;
	Иначе
		НомерДоверенности = УникальныйИдентификаторПустой();
	КонецЕсли;
	
	Возврат СтрШаблон("%1_%2", Отпечаток, НомерДоверенности);
	
КонецФункции

#КонецОбласти

#Область СпискиДокументовЭДО

#Область ДокументыИсходящие

Функция ТекстЗапросаИсходящихДокументовЭДО()
	
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	СостояниеДокументовПоЭДОСрезПоследних.ДокументДО,
		|	СостояниеДокументовПоЭДОСрезПоследних.СостояниеВерсииДокументаПоЭДО КАК Состояние
		|ПОМЕСТИТЬ ДокументыИсходящие
		|ИЗ
		|	РегистрСведений.СостояниеДокументовПоЭДО.СрезПоследних(,
		|		НаправлениеЭД = ЗНАЧЕНИЕ(Перечисление.НаправленияЭДО.Исходящий)) КАК СостояниеДокументовПоЭДОСрезПоследних
		|ГДЕ
		|	СостояниеДокументовПоЭДОСрезПоследних.СостояниеВерсииДокументаПоЭДО В (&СостоянияДляСписка)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ДокументыИсходящие.ДокументДО,
		|	ДокументыИсходящие.Состояние,
		|	ДокументыДО.Организация,
		|	ДокументыДО.Контрагент,
		|	ДокументыДО.ВидДокумента,
		|	ЕСТЬNULL(СоставПакетовЭДОДокументооборот.ИдентификаторПакета, &ПустойИдентификатор) КАК ИдентификаторПакета
		|ПОМЕСТИТЬ ДокументыБезОтборов
		|ИЗ
		|	Справочник.%ИмяСправочникаДокументаДО% КАК ДокументыДО
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДокументыИсходящие КАК ДокументыИсходящие
		|		ПО ДокументыИсходящие.ДокументДО = ДокументыДО.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СоставПакетовЭДОДокументооборот КАК СоставПакетовЭДОДокументооборот
		|		ПО ДокументыДО.Ссылка = СоставПакетовЭДОДокументооборот.Документ
		|ГДЕ
		|	&ОтборОрганизации
		|	И &ОтборКонтрагента
		|	И Не ДокументыДО.ПометкаУдаления
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДокументыБезОтборов.ДокументДО,
		|	ДокументыБезОтборов.Организация,
		|	ДокументыБезОтборов.Контрагент,
		|	ДокументыБезОтборов.ВидДокумента,
		|	ДокументыБезОтборов.Состояние,
		|	ДокументыБезОтборов.ИдентификаторПакета
		|ПОМЕСТИТЬ ДокументыСОтборами
		|ИЗ
		|	ДокументыБезОтборов КАК ДокументыБезОтборов
		|ГДЕ
		|	&ОтборВидаДокумента
		|	И &ОтборСостояния
		|	И &ОтборТипаОбъектов
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДокументыСОтборами.ИдентификаторПакета
		|ПОМЕСТИТЬ ПакетыСодержащиеДокументыСОтборами
		|ИЗ
		|	ДокументыСОтборами КАК ДокументыСОтборами
		|ГДЕ
		|	ДокументыСОтборами.ИдентификаторПакета <> &ПустойИдентификатор
		|СГРУППИРОВАТЬ ПО
		|	ДокументыСОтборами.ИдентификаторПакета
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДокументыСОтборами.ВидДокумента,
		|	ДокументыСОтборами.ДокументДО,
		|	ДокументыСОтборами.ИдентификаторПакета,
		|	ДокументыСОтборами.Контрагент,
		|	ДокументыСОтборами.Организация,
		|	ДокументыСОтборами.Состояние
		|ПОМЕСТИТЬ ОдиночныеДокументыСОтборами
		|ИЗ
		|	ДокументыСОтборами КАК ДокументыСОтборами
		|ГДЕ
		|	ДокументыСОтборами.ИдентификаторПакета = &ПустойИдентификатор
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПакетыСодержащиеДокументыСОтборами.ИдентификаторПакета,
		|	ДокументыБезОтборов.ДокументДО,
		|	ДокументыБезОтборов.Организация,
		|	ДокументыБезОтборов.Контрагент,
		|	ДокументыБезОтборов.ВидДокумента,
		|	ДокументыБезОтборов.Состояние,
		|	ДокументыБезОтборов.Состояние <> ЗНАЧЕНИЕ(Перечисление.СостоянияЭДОДокументооборот.НеСформирован) КАК ПодписанЭП
		|ИЗ
		|	ПакетыСодержащиеДокументыСОтборами КАК ПакетыСодержащиеДокументыСОтборами
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДокументыБезОтборов КАК ДокументыБезОтборов
		|		ПО ПакетыСодержащиеДокументыСОтборами.ИдентификаторПакета = ДокументыБезОтборов.ИдентификаторПакета
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ОдиночныеДокументыСОтборами.ИдентификаторПакета,
		|	ОдиночныеДокументыСОтборами.ДокументДО,
		|	ОдиночныеДокументыСОтборами.Организация,
		|	ОдиночныеДокументыСОтборами.Контрагент,
		|	ОдиночныеДокументыСОтборами.ВидДокумента,
		|	ОдиночныеДокументыСОтборами.Состояние,
		|	ОдиночныеДокументыСОтборами.Состояние <> ЗНАЧЕНИЕ(Перечисление.СостоянияЭДОДокументооборот.НеСформирован)
		|ИЗ
		|	ОдиночныеДокументыСОтборами КАК ОдиночныеДокументыСОтборами";
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
		"%ИмяСправочникаДокументаДО%",
		ОбменСКонтрагентамиДОСлужебныйКлиентСервер.ИмяСправочникаДокументовДО());
	
	Возврат ТекстЗапроса;
	
КонецФункции

Процедура УстановитьПараметрыЗапросаИсходящих(Запрос, Отбор)
	
	СостоянияДляСписка = Новый Массив;
	СостоянияДляСписка.Добавить(Перечисления.СостоянияЭДОДокументооборот.НеСформирован);
	СостоянияДляСписка.Добавить(Перечисления.СостоянияЭДОДокументооборот.Подписан);
	СостоянияДляСписка.Добавить(Перечисления.СостоянияЭДОДокументооборот.ПоставленВОчередьНаОтправку);
	СостоянияДляСписка.Добавить(Перечисления.СостоянияЭДОДокументооборот.ОжидаетсяОтправка);
	СостоянияДляСписка.Добавить(Перечисления.СостоянияЭДОДокументооборот.ОжидаетСозданияПакетаЭДО);
	
	Запрос.УстановитьПараметр("СостоянияДляСписка", СостоянияДляСписка);
	
	Если ТипЗнч(Отбор) = Тип("Структура")
		И Отбор.Свойство("Действия")
		И Отбор.Действия = "Подписание" Тогда
		
		СостоянияПоДействию = Новый Массив;
		СостоянияПоДействию.Добавить(Перечисления.СостоянияЭДОДокументооборот.НеСформирован);
		
		Запрос.Текст = СтрЗаменить(Запрос.Текст,
			"&ОтборСостояния",
			"ДокументыБезОтборов.Состояние В (&СостоянияПоДействию)");
		
		Запрос.УстановитьПараметр("СостоянияПоДействию", СостоянияПоДействию);
		
	ИначеЕсли ТипЗнч(Отбор) = Тип("Структура")
		И Отбор.Свойство("Действия")
		И Отбор.Действия = "Отправка" Тогда
		
		СостоянияПоДействию = Новый Массив;
		СостоянияПоДействию.Добавить(Перечисления.СостоянияЭДОДокументооборот.Подписан);
		СостоянияПоДействию.Добавить(Перечисления.СостоянияЭДОДокументооборот.ПоставленВОчередьНаОтправку);
		СостоянияПоДействию.Добавить(Перечисления.СостоянияЭДОДокументооборот.ОжидаетсяОтправка);
		СостоянияПоДействию.Добавить(Перечисления.СостоянияЭДОДокументооборот.ОжидаетСозданияПакетаЭДО);
		
		Запрос.Текст = СтрЗаменить(Запрос.Текст,
			"&ОтборСостояния",
			"ДокументыБезОтборов.Состояние В (&СостоянияПоДействию)");
		
		Запрос.УстановитьПараметр("СостоянияПоДействию", СостоянияПоДействию);
		
	Иначе
		
		Запрос.Текст = СтрЗаменить(Запрос.Текст,
			"&ОтборСостояния",
			"Истина");
		
	КонецЕсли;
	
	Если ТипЗнч(Отбор) = Тип("Структура")
		И Отбор.Свойство("Организация")
		И ЗначениеЗаполнено(Отбор.Организация) Тогда
		
		Запрос.Текст = СтрЗаменить(Запрос.Текст,
			"&ОтборОрганизации",
			"ДокументыДО.Организация = &Организация");
		
		Запрос.УстановитьПараметр("Организация", Отбор.Организация);
		
	Иначе
		
		Запрос.Текст = СтрЗаменить(Запрос.Текст,
			"&ОтборОрганизации",
			"Истина");
		
	КонецЕсли;
	
	Если ТипЗнч(Отбор) = Тип("Структура")
		И Отбор.Свойство("Контрагент")
		И ЗначениеЗаполнено(Отбор.Контрагент) Тогда
		
		Запрос.Текст = СтрЗаменить(Запрос.Текст,
			"&ОтборКонтрагента",
			"ДокументыДО.Контрагент = &Контрагент");
		
		Запрос.УстановитьПараметр("Контрагент", Отбор.Контрагент);
		
	Иначе
		
		Запрос.Текст = СтрЗаменить(Запрос.Текст,
			"&ОтборКонтрагента",
			"Истина");
		
	КонецЕсли;
	
	Если ТипЗнч(Отбор) = Тип("Структура")
		И Отбор.Свойство("ВидДокумента")
		И ЗначениеЗаполнено(Отбор.ВидДокумента) Тогда
		
		Запрос.Текст = СтрЗаменить(Запрос.Текст,
			"&ОтборВидаДокумента",
			"ДокументыБезОтборов.ВидДокумента = &ВидДокумента");
		
		Запрос.УстановитьПараметр("ВидДокумента", Отбор.ВидДокумента);
		
	Иначе
		
		Запрос.Текст = СтрЗаменить(Запрос.Текст,
			"&ОтборВидаДокумента",
			"Истина");
		
	КонецЕсли;
	
	Если ТипЗнч(Отбор) = Тип("Структура")
		И Отбор.Свойство("Объекты")
		И Отбор.Объекты = "Документы" Тогда
		
		Запрос.Текст = СтрЗаменить(Запрос.Текст,
			"&ОтборТипаОбъектов",
			"ДокументыБезОтборов.ИдентификаторПакета = &ПустойИдентификатор");
		
		Запрос.УстановитьПараметр("ПустойИдентификатор", УникальныйИдентификаторПустой());
		
	ИначеЕсли ТипЗнч(Отбор) = Тип("Структура")
		И Отбор.Свойство("Объекты")
		И Отбор.Объекты = "Пакеты" Тогда
		
		Запрос.Текст = СтрЗаменить(Запрос.Текст,
			"&ОтборТипаОбъектов",
			"ДокументыБезОтборов.ИдентификаторПакета <> &ПустойИдентификатор");
		
		Запрос.УстановитьПараметр("ПустойИдентификатор", УникальныйИдентификаторПустой());
		
	Иначе
		
		Запрос.Текст = СтрЗаменить(Запрос.Текст,
			"&ОтборТипаОбъектов",
			"Истина");
		
		Запрос.УстановитьПараметр("ПустойИдентификатор", УникальныйИдентификаторПустой());
		
	КонецЕсли;
	
КонецПроцедуры

Функция ДанныеВыделенныхОбъектов(ДанныеВыделенныхСтрок)
	
	СтруктураВозврата = Новый Структура;
	СтруктураВозврата.Вставить("Пакеты", Новый Соответствие);
	СтруктураВозврата.Вставить("Документы", Новый Соответствие);
	
	ПакетыВыделенные = Новый Соответствие;
	ДокументыВыделенные = Новый Соответствие;
	
	Для Каждого ДанныеСтроки Из ДанныеВыделенныхСтрок Цикл
		Если ЗначениеЗаполнено(ДанныеСтроки.ИдентификаторПакета) Тогда
			ПакетыВыделенные.Вставить(ДанныеСтроки.ИдентификаторПакета, Истина);
		Иначе
			ДокументыВыделенные.Вставить(ДанныеСтроки.Документ, Истина)
		КонецЕсли;
	КонецЦикла;
	
	УникальныеПакеты = Новый Массив;
	УникальныеДокументы = Новый Массив;
	
	Для Каждого Элемент Из ПакетыВыделенные Цикл
		УникальныеПакеты.Добавить(Элемент.Ключ);
	КонецЦикла;
	
	Для Каждого Элемент Из ДокументыВыделенные Цикл
		УникальныеДокументы.Добавить(Элемент.Ключ);
	КонецЦикла;
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапросаДанныхВыбранныхИсходящихДокументов();
	Запрос.УстановитьПараметр("Пакеты", УникальныеПакеты);
	Запрос.УстановитьПараметр("Документы", УникальныеДокументы);
	
	МассивРезультатов = Запрос.ВыполнитьПакет();
	КолвоПакетов = МассивРезультатов.Количество();
	
	ВыборкаДокументов = МассивРезультатов[КолвоПакетов - 4].Выбрать();
	ВыборкаСостояний = МассивРезультатов[КолвоПакетов - 3].Выбрать();
	ВыборкаФайлов = МассивРезультатов[КолвоПакетов - 2].Выбрать();
	ВыборкаОрганизацийИКонтрагентов = МассивРезультатов[КолвоПакетов - 1].Выбрать();
	
	ДанныеПоДокументам = Новый Соответствие;
	
	Пока ВыборкаСостояний.Следующий() Цикл
		
		ДанныеДокумента = ДанныеПоДокументам.Получить(ВыборкаСостояний.Документ);
		
		Если ДанныеДокумента = Неопределено Тогда
			ДанныеДокумента = НовыеДанныеДокументаСпискаИсходящих();
			ДанныеПоДокументам.Вставить(ВыборкаСостояний.Документ, ДанныеДокумента);
		КонецЕсли;
		
		ДанныеДокумента.Состояние = ВыборкаСостояний.Состояние;
		
	КонецЦикла;
	
	Пока ВыборкаФайлов.Следующий() Цикл
		
		Если Не ЗначениеЗаполнено(ВыборкаФайлов.Файл) Тогда
			Продолжить;
		КонецЕсли;
		
		ДанныеДокумента = ДанныеПоДокументам.Получить(ВыборкаФайлов.Документ);
		
		Если ДанныеДокумента = Неопределено Тогда
			ДанныеДокумента = НовыеДанныеДокументаСпискаИсходящих();
			ДанныеПоДокументам.Вставить(ВыборкаФайлов.Документ, ДанныеДокумента);
		КонецЕсли;
		
		ДанныеФайла = Новый Структура;
		ДанныеФайла.Вставить("Файл", ВыборкаФайлов.Файл);
		ДанныеФайла.Вставить("Расширение", ВыборкаФайлов.Расширение);
		
		ДанныеДокумента.Файлы.Добавить(ДанныеФайла);
		
	КонецЦикла;
	
	Пока ВыборкаОрганизацийИКонтрагентов.Следующий() Цикл
		
		ДанныеДокумента = ДанныеПоДокументам.Получить(ВыборкаОрганизацийИКонтрагентов.Документ);
		
		Если ДанныеДокумента = Неопределено Тогда
			ДанныеДокумента = НовыеДанныеДокументаСпискаИсходящих();
			ДанныеПоДокументам.Вставить(ВыборкаОрганизацийИКонтрагентов.Документ, ДанныеДокумента);
		КонецЕсли;
		
		ДанныеДокумента.Организация = ВыборкаОрганизацийИКонтрагентов.Организация;
		ДанныеДокумента.Контрагент = ВыборкаОрганизацийИКонтрагентов.Контрагент;
		
	КонецЦикла;
	
	СпособыОбмена = СпособыОбменаЭДОПоДокументамДО(УникальныеДокументы);
	
	Для Каждого Элемент Из СпособыОбмена Цикл
		
		Документ = Элемент.Ключ;
		СпособОбмена = Элемент.Значение;
		
		ДанныеДокумента = ДанныеПоДокументам.Получить(Документ);
		
		Если ДанныеДокумента = Неопределено Тогда
			ДанныеДокумента = НовыеДанныеДокументаСпискаИсходящих();
			ДанныеПоДокументам.Вставить(ВыборкаОрганизацийИКонтрагентов.Документ, ДанныеДокумента);
		КонецЕсли;
		
		ДанныеДокумента.СпособОбмена = СпособОбмена;
		
	КонецЦикла;
	
	Пока ВыборкаДокументов.Следующий() Цикл
		
		Если Не ЗначениеЗаполнено(ВыборкаДокументов.ИдентификаторПакета) Тогда
			
			СтруктураВозврата.Документы.Вставить(ВыборкаДокументов.Документ,
				ДанныеПоДокументам[ВыборкаДокументов.Документ]);
			
		Иначе
			
			ДокументыПакета = СтруктураВозврата.Пакеты.Получить(ВыборкаДокументов.ИдентификаторПакета);
			
			Если ДокументыПакета = Неопределено Тогда
				ДокументыПакета = Новый Соответствие;
				СтруктураВозврата.Пакеты.Вставить(ВыборкаДокументов.ИдентификаторПакета, ДокументыПакета);
			КонецЕсли;
			
			ДокументыПакета.Вставить(ВыборкаДокументов.Документ,
				ДанныеПоДокументам[ВыборкаДокументов.Документ]);
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат СтруктураВозврата;
	
КонецФункции

Функция НовыеДанныеДокументаСпискаИсходящих()
	
	ДанныеДокумента = Новый Структура;
	ДанныеДокумента.Вставить("Организация", Справочники.Организации.ПустаяСсылка());
	ДанныеДокумента.Вставить("Контрагент", Справочники.Контрагенты.ПустаяСсылка());
	ДанныеДокумента.Вставить("Состояние", Перечисления.СостоянияЭДОДокументооборот.ПустаяСсылка());
	ДанныеДокумента.Вставить("СпособОбмена", Перечисления.СпособыОбменаЭД.ПустаяСсылка());
	ДанныеДокумента.Вставить("Файлы", Новый Массив);
	
	Возврат ДанныеДокумента;
	
КонецФункции

Функция ТекстЗапросаДанныхВыбранныхИсходящихДокументов()
	
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	СоставПакетовЭДОДокументооборот.ИдентификаторПакета,
		|	ДокументыДО.ВидДокумента,
		|	СоставПакетовЭДОДокументооборот.Документ
		|ПОМЕСТИТЬ Документы
		|ИЗ
		|	РегистрСведений.СоставПакетовЭДОДокументооборот КАК СоставПакетовЭДОДокументооборот
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.%ИмяСправочникаДокументаДО% КАК ДокументыДО
		|		ПО СоставПакетовЭДОДокументооборот.Документ = ДокументыДО.Ссылка
		|ГДЕ
		|	СоставПакетовЭДОДокументооборот.ИдентификаторПакета В (&Пакеты)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	НЕОПРЕДЕЛЕНО,
		|	ДокументыДО.ВидДокумента,
		|	ДокументыДО.Ссылка
		|ИЗ
		|	Справочник.%ИмяСправочникаДокументаДО% КАК ДокументыДО
		|ГДЕ
		|	ДокументыДО.Ссылка В (&Документы)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Документы.Документ,
		|	ЕСТЬNULL(СостояниеДокументовПоЭДОСрезПоследних.СостояниеВерсииДокументаПоЭДО,
		|		ЗНАЧЕНИЕ(Перечисление.СостоянияЭДОДокументооборот.ПустаяСсылка)) КАК Состояние
		|ПОМЕСТИТЬ СостоянияДокументов
		|ИЗ
		|	Документы КАК Документы
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СостояниеДокументовПоЭДО.СрезПоследних КАК СостояниеДокументовПоЭДОСрезПоследних
		|		ПО Документы.Документ = СостояниеДокументовПоЭДОСрезПоследних.ДокументДО
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Документы.Документ,
		|	Файлы.Ссылка КАК Файл,
		|	ЕСТЬNULL(РолиФайловДокументов.Роль, ЗНАЧЕНИЕ(Справочник.РолиФайлов.ПустаяСсылка)) КАК РольФайла,
		|	Документы.ВидДокумента,
		|	Файлы.ТекущаяВерсия.Расширение КАК Расширение
		|ПОМЕСТИТЬ ФайлыДокументов
		|ИЗ
		|	Справочник.Файлы КАК Файлы
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СлужебныеФайлыДокументов КАК СлужебныеФайлыДокументов
		|		ПО Файлы.Ссылка = СлужебныеФайлыДокументов.Файл
		|		ПРАВОЕ СОЕДИНЕНИЕ Документы КАК Документы
		|		ПО Файлы.ВладелецФайла = Документы.Документ
		|		И НЕ Файлы.ПометкаУдаления
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РолиФайловДокументов КАК РолиФайловДокументов
		|		ПО Файлы.Ссылка = РолиФайловДокументов.Файл
		|ГДЕ
		|	СлужебныеФайлыДокументов.Файл ЕСТЬ NULL
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ФайлыДокументов.Документ,
		|	ФайлыДокументов.Файл,
		|	ФайлыДокументов.Расширение
		|ПОМЕСТИТЬ ФайлыДокументовДляОтправки
		|ИЗ
		|	ФайлыДокументов КАК ФайлыДокументов
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ВидыДокументов.РолиФайлов КАК ВидыДокументовРолиФайлов
		|		ПО ФайлыДокументов.ВидДокумента = ВидыДокументовРолиФайлов.Ссылка
		|		И ФайлыДокументов.РольФайла = ВидыДокументовРолиФайлов.Роль
		|ГДЕ
		|	ВидыДокументовРолиФайлов.ФайлЭлектронногоДокумента
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ФайлыДокументов.Документ,
		|	ФайлыДокументов.Файл,
		|	ФайлыДокументов.Расширение
		|ИЗ
		|	ФайлыДокументов КАК ФайлыДокументов
		|ГДЕ
		|	ФайлыДокументов.РольФайла = ЗНАЧЕНИЕ(Справочник.РолиФайлов.ПустаяСсылка)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Документы.Документ,
		|	ДокументыДО.Организация,
		|	ДокументыДО.Контрагент
		|ПОМЕСТИТЬ ОрганизацииИКонтрагенты
		|ИЗ
		|	Документы КАК Документы
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.%ИмяСправочникаДокументаДО% КАК ДокументыДО
		|		ПО Документы.Документ = ДокументыДО.Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Документы.ИдентификаторПакета,
		|	Документы.Документ
		|ИЗ
		|	Документы КАК Документы
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СостоянияДокументов.Документ,
		|	СостоянияДокументов.Состояние
		|ИЗ
		|	СостоянияДокументов КАК СостоянияДокументов
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ФайлыДокументовДляОтправки.Документ,
		|	ФайлыДокументовДляОтправки.Файл,
		|	ФайлыДокументовДляОтправки.Расширение
		|ИЗ
		|	ФайлыДокументовДляОтправки КАК ФайлыДокументовДляОтправки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ОрганизацииИКонтрагенты.Документ,
		|	ОрганизацииИКонтрагенты.Контрагент,
		|	ОрганизацииИКонтрагенты.Организация
		|ИЗ
		|	ОрганизацииИКонтрагенты КАК ОрганизацииИКонтрагенты";
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
		"%ИмяСправочникаДокументаДО%",
		ОбменСКонтрагентамиДОСлужебныйКлиентСервер.ИмяСправочникаДокументовДО());
	
	Возврат ТекстЗапроса;
КонецФункции

Функция ДеревоФайловПоДаннымОбъектов(ДанныеОбъектов)
	
	ДеревоФайлов = Новый ДеревоЗначений;
	ДеревоФайлов.Колонки.Добавить("ИдентификаторПакета");
	ДеревоФайлов.Колонки.Добавить("Документ");
	ДеревоФайлов.Колонки.Добавить("Файл");
	ДеревоФайлов.Колонки.Добавить("ПодписанЭП");
	ДеревоФайлов.Колонки.Добавить("ИндексКартинкиЭП");
	ДеревоФайлов.Колонки.Добавить("ПредставлениеЭлемента");
	ДеревоФайлов.Колонки.Добавить("ИндексКартинкиФайла");
	
	ВыбранОдинПакет = (ДанныеОбъектов.Пакеты.Количество() = 1 И ДанныеОбъектов.Документы.Количество() = 0);
	
	// Если выбран один пакет, то обработка происходит иначе -- просто кидаем все данные в корень.
	Если ВыбранОдинПакет Тогда
		
		Для Каждого ЭлементПакета Из ДанныеОбъектов.Пакеты Цикл
			Для Каждого ЭлементДокумента Из ЭлементПакета.Значение Цикл
				Документ = ЭлементДокумента.Ключ;
				ДанныеДокумента = ЭлементДокумента.Значение;
				
				СтрокаДокумента = ДеревоФайлов.Строки.Добавить();
				
				ЗаполнитьДанныеСтрокиДереваДокумента(
					СтрокаДокумента, Документ, ДанныеДокумента, ЭлементПакета.Ключ);
			КонецЦикла;
		КонецЦикла;
		
		Возврат ДеревоФайлов;
		
	КонецЕсли;
	
	Для Каждого ЭлементПакета Из ДанныеОбъектов.Пакеты Цикл
		
		ИдентификаторПакета = ЭлементПакета.Ключ;
		ДокументыПакета = ЭлементПакета.Значение;
		
		СтрокаПакета = ДеревоФайлов.Строки.Добавить();
		
		СтрокаПакета.ИдентификаторПакета = ИдентификаторПакета;
		СтрокаПакета.ПредставлениеЭлемента = СтрШаблон(
			НСтр("ru = 'Пакет ЭДО (%1)'"),
			СтроковыеФункцииКлиентСервер.СтрокаСЧисломДляЛюбогоЯзыка(
				НСтр("ru = ';%1 документ;;%1 документа;%1 документов;%1 документов'"),
				ДокументыПакета.Количество()));
		
		ИндексКартинкиЭППакета = Неопределено;
		
		Для Каждого ЭлементДокумента Из ДокументыПакета Цикл
			
			Документ = ЭлементДокумента.Ключ;
			ДанныеДокумента = ЭлементДокумента.Значение;
			
			СтрокаДокумента = СтрокаПакета.Строки.Добавить();
			
			ЗаполнитьДанныеСтрокиДереваДокумента(
				СтрокаДокумента, Документ, ДанныеДокумента, ИдентификаторПакета);
			
			Если ИндексКартинкиЭППакета = Неопределено Тогда
				
				ИндексКартинкиЭППакета = СтрокаДокумента.ИндексКартинкиЭП;
				
			ИначеЕсли СтрокаДокумента.ИндексКартинкиЭП <> ИндексКартинкиЭППакета Тогда 
				
				ИндексКартинкиЭППакета = 1
				
			КонецЕсли;
			
		КонецЦикла;
		
		СтрокаПакета.ИндексКартинкиЭП = ИндексКартинкиЭППакета;
		СтрокаПакета.ПодписанЭП = (ИндексКартинкиЭППакета = 2);
		СтрокаПакета.ИндексКартинкиФайла = -1;
		
	КонецЦикла;
	
	Для Каждого ЭлементДокумента Из ДанныеОбъектов.Документы Цикл
		
		Документ = ЭлементДокумента.Ключ;
		ДанныеДокумента = ЭлементДокумента.Значение;
		
		СтрокаДокумента = ДеревоФайлов.Строки.Добавить();
		
		ЗаполнитьДанныеСтрокиДереваДокумента(
			СтрокаДокумента, Документ, ДанныеДокумента);
		
	КонецЦикла;
	
	Возврат ДеревоФайлов;
	
КонецФункции

Процедура ЗаполнитьДанныеСтрокиДереваДокумента(СтрокаДокумента, Документ,
	ДанныеДокумента, ИдентификаторПакета = Неопределено)
	
	СтрокаДокумента.ИдентификаторПакета = ИдентификаторПакета;
	СтрокаДокумента.Документ = Документ;
	СтрокаДокумента.ПодписанЭП =
		(ДанныеДокумента.Состояние <> Перечисления.СостоянияЭДОДокументооборот.НеСформирован);
	СтрокаДокумента.ИндексКартинкиЭП = ?(СтрокаДокумента.ПодписанЭП, 2, 0);
	
	Если ДанныеДокумента.Файлы.Количество() = 0 Тогда
		
		СтрокаДокумента.ПредставлениеЭлемента = СтрШаблон(
			НСтр("ru = '(нет файлов) %1'"),
			Документ);
		
		СтрокаДокумента.ИндексКартинкиФайла = -1;
		
	ИначеЕсли ДанныеДокумента.Файлы.Количество() > 1 Тогда
		
		СтрокаДокумента.ПредставлениеЭлемента = СтрШаблон(
			НСтр("ru = '(%2) %1'"),
			Документ,
			СтроковыеФункцииКлиентСервер.СтрокаСЧисломДляЛюбогоЯзыка(
				НСтр("ru = ';%1 файл;;%1 файла;%1 файлов;%1 файлов'"),
				ДанныеДокумента.Файлы.Количество()));
		
		СтрокаДокумента.ИндексКартинкиФайла = -1;
		
	Иначе
		
		ДанныеФайла = ДанныеДокумента.Файлы[0];
		
		СтрокаДокумента.ПредставлениеЭлемента = Строка(ДанныеФайла.Файл);
		
		СтрокаДокумента.Файл = ДанныеФайла.Файл;
		СтрокаДокумента.ИндексКартинкиФайла =
			ФайловыеФункцииКлиентСервер.ПолучитьИндексПиктограммыФайла(ДанныеФайла.Расширение);
		
	КонецЕсли;
	
КонецПроцедуры

Функция ОрганизацииИКонтрагентыВыбранныхДанныхОбъектов(ДанныеОбъектов)
	
	СтруктураВозврата = Новый Структура;
	СтруктураВозврата.Вставить("Организации", Новый Массив);
	СтруктураВозврата.Вставить("Контрагенты", Новый Массив);
	
	УникальныеОрганизации = Новый Соответствие;
	УникальныеКонтрагенты = Новый Соответствие;
	
	Для Каждого ЭлементПакета Из ДанныеОбъектов.Пакеты Цикл
		
		Для Каждого ЭлементДокумента Из ЭлементПакета.Значение Цикл
			
			ДанныеДокумента = ЭлементДокумента.Значение;
			
			Если УникальныеОрганизации[ДанныеДокумента.Организация] = Неопределено Тогда
				УникальныеОрганизации.Вставить(ДанныеДокумента.Организация, Истина);
			КонецЕсли;
			
			Если УникальныеКонтрагенты[ДанныеДокумента.Контрагент] = Неопределено Тогда
				УникальныеКонтрагенты.Вставить(ДанныеДокумента.Контрагент, Истина);
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;
	
	Для Каждого ЭлементДокумента Из ДанныеОбъектов.Документы Цикл
		
		ДанныеДокумента = ЭлементДокумента.Значение;
		
		Если УникальныеОрганизации[ДанныеДокумента.Организация] = Неопределено Тогда
			УникальныеОрганизации.Вставить(ДанныеДокумента.Организация, Истина);
		КонецЕсли;
		
		Если УникальныеКонтрагенты[ДанныеДокумента.Контрагент] = Неопределено Тогда
			УникальныеКонтрагенты.Вставить(ДанныеДокумента.Контрагент, Истина);
		КонецЕсли;
		
	КонецЦикла;
	
	Для Каждого Элемент Из УникальныеОрганизации Цикл
		СтруктураВозврата.Организации.Добавить(Элемент.Ключ);
	КонецЦикла;
	
	Для Каждого Элемент Из УникальныеКонтрагенты Цикл
		СтруктураВозврата.Контрагенты.Добавить(Элемент.Ключ);
	КонецЦикла;
	
	Возврат СтруктураВозврата;
	
КонецФункции

Функция ДоступныеДействияПоВыделеннымИсходящимОбъектам(ДанныеОбъектов, Организации, Контрагенты)
	
	ВозможныеДействия = ОбменСКонтрагентамиДОСлужебныйКлиентСервер.ДействияСпискаИсходящихДокументов();
	ДоступныеДействия = Новый Соответствие;
	
	Если Организации.Количество() <> 1
		Или Контрагенты.Количество() <> 1 Тогда
		
		Возврат ДоступныеДействия;
	КонецЕсли;
	
	// Если в каком либо документе нет файлов или их больше 1, то действия нельзя выполнять вообще.
	Для Каждого ЭлементПакета Из ДанныеОбъектов.Пакеты Цикл
		ДокументыПакета = ЭлементПакета.Значение;
		
		Для Каждого ЭлементДокумента Из ДокументыПакета Цикл
			ДанныеДокумента = ЭлементДокумента.Значение;
			
			Если ДанныеДокумента.Файлы.Количество() = 0
				Или ДанныеДокумента.Файлы.Количество() > 1 Тогда
				
				Возврат ДоступныеДействия;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	Для Каждого ЭлементДокумента Из ДанныеОбъектов.Документы Цикл
		ДанныеДокумента = ЭлементДокумента.Значение;
		
		Если ДанныеДокумента.Файлы.Количество() = 0
			Или ДанныеДокумента.Файлы.Количество() > 1 Тогда
			
			Возврат ДоступныеДействия;
		КонецЕсли;
	КонецЦикла;
	
	Если ДанныеОбъектов.Пакеты.Количество() = 0
		И ДанныеОбъектов.Документы.Количество() > 0 Тогда
		
		ДоступныДействияСПакетом = Истина;
		
		Для Каждого Элемент Из ДанныеОбъектов.Документы Цикл
			
			ДанныеДокумента = Элемент.Значение;
			
			Если ДанныеДокумента.Состояние <> Перечисления.СостоянияЭДОДокументооборот.НеСформирован
				И ДанныеДокумента.Состояние <> Перечисления.СостоянияЭДОДокументооборот.Подписан
				И ДанныеДокумента.Состояние <> Перечисления.СостоянияЭДОДокументооборот.ОжидаетсяОтправка Тогда
				
				ДоступныДействияСПакетом = Ложь;
				Прервать;
			КонецЕсли;
			
			Если Не СпособОбменаПоддерживаетПакетнуюОбработку(ДанныеДокумента.СпособОбмена) Тогда
				ДоступныДействияСПакетом = Ложь;
				Прервать;
			КонецЕсли;
			
		КонецЦикла;
		
		Если ДоступныДействияСПакетом Тогда
			ДоступныеДействия.Вставить(ВозможныеДействия.СоздатьПакет, Истина);
			
			Если ДанныеОбъектов.Документы.Количество() = 1 Тогда
				
				ДоступныеДействия.Вставить(ВозможныеДействия.ДобавитьКПакету, Истина);
				
			КонецЕсли;
		КонецЕсли;
				
	ИначеЕсли ДанныеОбъектов.Документы.Количество() = 0
		И ДанныеОбъектов.Пакеты.Количество() = 1 Тогда
		
		ДоступноИзменениеСоставаПакета = Истина;
		
		Для Каждого ЭлементПакета Из ДанныеОбъектов.Пакеты Цикл
			
			ДокументыПакета = ЭлементПакета.Значение;
			
			Для Каждого ЭлементДокумента Из ДокументыПакета Цикл
				
				ДанныеДокумента = ЭлементДокумента.Значение;
				
				Если ДанныеДокумента.Состояние <> Перечисления.СостоянияЭДОДокументооборот.НеСформирован
					И ДанныеДокумента.Состояние <> Перечисления.СостоянияЭДОДокументооборот.Подписан
					И ДанныеДокумента.Состояние <> Перечисления.СостоянияЭДОДокументооборот.ОжидаетсяОтправка
					И ДанныеДокумента.Состояние <> Перечисления.СостоянияЭДОДокументооборот.ОжидаетСозданияПакетаЭДО Тогда
					
					ДоступноИзменениеСоставаПакета = Ложь;
					Прервать;
				КонецЕсли;
				
			КонецЦикла;
			
		КонецЦикла;
		
		Если ДоступноИзменениеСоставаПакета Тогда
			ДоступныеДействия.Вставить(ВозможныеДействия.ИзменитьСоставПакета, Истина);
		КонецЕсли;
		
	КонецЕсли;
	
	Для Каждого ЭлементПакета Из ДанныеОбъектов.Пакеты Цикл
		
		Для Каждого ЭлементДокумента Из ЭлементПакета.Значение Цикл
			
			ДанныеДокумента = ЭлементДокумента.Значение;
			
			Если ДанныеДокумента.Состояние = Перечисления.СостоянияЭДОДокументооборот.НеСформирован Тогда
				
				ДоступныеДействия.Вставить(ВозможныеДействия.Подписать, Истина);
				ДоступныеДействия.Вставить(ВозможныеДействия.Отправить, Истина);
				
				Прервать;
				
			ИначеЕсли ДанныеДокумента.Состояние = Перечисления.СостоянияЭДОДокументооборот.Подписан
				Или ДанныеДокумента.Состояние
					= Перечисления.СостоянияЭДОДокументооборот.ОжидаетСозданияПакетаЭДО
				Или ДанныеДокумента.Состояние 
					= Перечисления.СостоянияЭДОДокументооборот.ПоставленВОчередьНаОтправку
				Или ДанныеДокумента.Состояние 
					= Перечисления.СостоянияЭДОДокументооборот.ОжидаетсяОтправка Тогда
				
				ДоступныеДействия.Вставить(ВозможныеДействия.Отправить, Истина);
			КонецЕсли;
			
		КонецЦикла;
		
		Если ДоступныеДействия[ВозможныеДействия.Подписать] <> Неопределено Тогда
			Прервать;
		КонецЕсли;
		
	КонецЦикла;
	
	Для Каждого ЭлементДокумента Из ДанныеОбъектов.Документы Цикл
		
		Если ДоступныеДействия[ВозможныеДействия.Подписать] <> Неопределено Тогда
			Прервать;
		КонецЕсли;
		
		ДанныеДокумента = ЭлементДокумента.Значение;
		
		Если ДанныеДокумента.Состояние = Перечисления.СостоянияЭДОДокументооборот.НеСформирован Тогда
			
			ДоступныеДействия.Вставить(ВозможныеДействия.Подписать, Истина);
			ДоступныеДействия.Вставить(ВозможныеДействия.Отправить, Истина);
			
			Прервать;
			
		ИначеЕсли ДанныеДокумента.Состояние = Перечисления.СостоянияЭДОДокументооборот.Подписан
			Или ДанныеДокумента.Состояние
				= Перечисления.СостоянияЭДОДокументооборот.ОжидаетСозданияПакетаЭДО
			Или ДанныеДокумента.Состояние 
				= Перечисления.СостоянияЭДОДокументооборот.ПоставленВОчередьНаОтправку
			Или ДанныеДокумента.Состояние 
				= Перечисления.СостоянияЭДОДокументооборот.ОжидаетсяОтправка Тогда
			
			ДоступныеДействия.Вставить(ВозможныеДействия.Отправить, Истина);
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат ДоступныеДействия;
	
КонецФункции

#КонецОбласти

#Область ДокументыВходящие

Функция ДанныеВыделенногоВходящегоДокумента(ДокументЭДО)
	
	ДанныеДокумента = НовыеДанныеВыделенногоВходящегоЭДО();
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапросаДанныхВыделенногоВходящего();
	Запрос.УстановитьПараметр("ЭлектронныйДокумент", ДокументЭДО);
	
	РезультатыЗапроса = Запрос.ВыполнитьПакет();
	
	КолвоРезультатов = РезультатыЗапроса.Количество();
	
	ВыборкаДокументовПакета = РезультатыЗапроса[КолвоРезультатов - 2].Выбрать();
	ВыборкаДанныхДокумента = РезультатыЗапроса[КолвоРезультатов - 1].Выбрать();
	
	Если Не ВыборкаДанныхДокумента.Следующий() Тогда
		Возврат ДанныеДокумента;
	КонецЕсли;
	
	ДанныеДокумента.ДокументЭДО = ВыборкаДанныхДокумента.ДокументЭДО;
	ДанныеДокумента.Организация = ВыборкаДанныхДокумента.Организация;
	ДанныеДокумента.Контрагент = ВыборкаДанныхДокумента.Контрагент;
	ДанныеДокумента.ВидДокумента = ВыборкаДанныхДокумента.ВидДокумента;
	ДанныеДокумента.Номер = ВыборкаДанныхДокумента.Номер;
	ДанныеДокумента.Дата = ВыборкаДанныхДокумента.Дата;
	ДанныеДокумента.ТребуетсяОтветнаяПодпись = ВыборкаДанныхДокумента.ТребуетсяПодтверждение;
	ДанныеДокумента.ФайлЭДО = ВыборкаДанныхДокумента.ФайлЭДО;
	ДанныеДокумента.РасширениеФайла = ВыборкаДанныхДокумента.РасширениеФайла;
	ДанныеДокумента.ОписаниеОшибкиАвтосоздания = ВыборкаДанныхДокумента.ОписаниеОшибкиСоздания;
	ДанныеДокумента.ОжидаетАвтоматическогоСоздания = ВыборкаДанныхДокумента.ОжидаетАвтоматическогоСоздания;
	
	Пока ВыборкаДокументовПакета.Следующий() Цикл
		
		ДанныеДокументаПакета = Новый Структура;
		ДанныеДокументаПакета.Вставить("ВидДокумента", ВыборкаДокументовПакета.ВидДокумента);
		ДанныеДокументаПакета.Вставить("Номер", ВыборкаДокументовПакета.Номер);
		ДанныеДокументаПакета.Вставить("Дата", ВыборкаДокументовПакета.Дата);
		
		ДанныеДокумента.ДанныеДокументовПакета.Вставить(
			ВыборкаДокументовПакета.ЭлектронныйДокумент, ДанныеДокументаПакета);
		
	КонецЦикла;
	
	Возврат ДанныеДокумента;
	
КонецФункции

// Возвращает новые данные выделенного входящего документа ЭДО для отражения в ДО
// 
// Возвращаемое значение:
//  Структура - Данные выделенного входящего документа:
//    * ДокументЭДО - ДокументСсылка.ЭлектронныйДокументВходящийЭДО
//    * Организация - СправочникСсылка.Организации
//    * Контрагент - СправочникСсылка.Контрагенты
//    * ВидДокумента - СправочникСсылка.ВидыДокументовЭДО
//    * Номер - Строка
//    * Дата - Дата
//    * ТребуетсяОтветнаяПодпись - Булево
//    * ФайлЭДО - СправочникСсылка.СообщениеЭДОПрисоединенныеФайлы - Ссылка на файл информации отправителя
//    * РасширениеФайла - Строка
//    * ОписаниеОшибкиАвтосоздания - Строка - Описание ошибки, если была попытка создать файл автоматически,
//                                            но завершилась неудачей. Если ошибок нет, то пустая строка
//    * ОжидаетАвтоматическогоСоздания - Булево - Стоит в очереди для создания по правилам в регламентном задании
//    * ДанныеДокументовПакета - Соответствие Из КлючИЗначение:
//      ** Ключ - ДокументСсылка.ЭлектронныйДокументВходящийЭДО - Другой документ пакета
//      ** Значение - Структура:
//        *** ВидДокумента - СправочникСсылка.ВидыДокументовЭДО
//        *** Номер - Строка
//        *** Дата - Дата
//
Функция НовыеДанныеВыделенногоВходящегоЭДО()
	
	ДанныеДокумента = Новый Структура;
	ДанныеДокумента.Вставить("ДокументЭДО", Документы.ЭлектронныйДокументВходящийЭДО.ПустаяСсылка());
	ДанныеДокумента.Вставить("Организация", Справочники.Организации.ПустаяСсылка());
	ДанныеДокумента.Вставить("Контрагент", Справочники.Контрагенты.ПустаяСсылка());
	ДанныеДокумента.Вставить("ВидДокумента", Справочники.ВидыДокументовЭДО.ПустаяСсылка());
	ДанныеДокумента.Вставить("Номер", "");
	ДанныеДокумента.Вставить("Дата", Дата(1, 1, 1));
	ДанныеДокумента.Вставить("ТребуетсяОтветнаяПодпись", Ложь);
	ДанныеДокумента.Вставить("ФайлЭДО", Справочники.СообщениеЭДОПрисоединенныеФайлы.ПустаяСсылка());
	ДанныеДокумента.Вставить("РасширениеФайла", "");
	ДанныеДокумента.Вставить("ОписаниеОшибкиАвтосоздания", "");
	ДанныеДокумента.Вставить("ОжидаетАвтоматическогоСоздания", Ложь);
	
	ДанныеДокумента.Вставить("ДанныеДокументовПакета", Новый Соответствие);
	
	Возврат ДанныеДокумента;
	
КонецФункции

Функция ТекстЗапросаДанныхВыделенногоВходящего()
	
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	ЭлектронныйДокументВходящийЭДО.Ссылка КАК ДокументЭДО,
		|	ЭлектронныйДокументВходящийЭДО.Организация,
		|	ЭлектронныйДокументВходящийЭДО.Контрагент,
		|	ЭлектронныйДокументВходящийЭДО.ВидДокумента,
		|	ЭлектронныйДокументВходящийЭДО.ТребуетсяПодтверждение,
		|	СообщениеЭДОПрисоединенныеФайлы.Ссылка КАК ФайлЭДО,
		|	СообщениеЭДОПрисоединенныеФайлы.Расширение КАК РасширениеФайла,
		|	СоставПакетовДокументовЭДО.ИдентификаторПакета,
		|	ЭлектронныйДокументВходящийЭДО.НомерДокумента,
		|	ЭлектронныйДокументВходящийЭДО.ДатаДокумента
		|ПОМЕСТИТЬ ДанныеДокумента
		|ИЗ
		|	Документ.СообщениеЭДО КАК СообщениеЭДО
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЭлектронныйДокументВходящийЭДО КАК ЭлектронныйДокументВходящийЭДО
		|		ПО СообщениеЭДО.ЭлектронныйДокумент = ЭлектронныйДокументВходящийЭДО.Ссылка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.СообщениеЭДОПрисоединенныеФайлы КАК СообщениеЭДОПрисоединенныеФайлы
		|		ПО СообщениеЭДО.ОсновнойФайл = СообщениеЭДОПрисоединенныеФайлы.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СоставПакетовДокументовЭДО КАК СоставПакетовДокументовЭДО
		|		ПО СообщениеЭДО.ЭлектронныйДокумент = СоставПакетовДокументовЭДО.ЭлектронныйДокумент
		|ГДЕ
		|	ЭлектронныйДокументВходящийЭДО.Ссылка = &ЭлектронныйДокумент
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СоставПакетовДокументовЭДО.ЭлектронныйДокумент,
		|	ЭлектронныйДокументВходящийЭДО.ВидДокумента,
		|	ЭлектронныйДокументВходящийЭДО.НомерДокумента КАК Номер,
		|	ЭлектронныйДокументВходящийЭДО.ДатаДокумента КАК Дата
		|ИЗ
		|	РегистрСведений.СоставПакетовДокументовЭДО КАК СоставПакетовДокументовЭДО
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДанныеДокумента КАК ДанныеДокумента
		|		ПО СоставПакетовДокументовЭДО.ИдентификаторПакета = ДанныеДокумента.ИдентификаторПакета
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЭлектронныйДокументВходящийЭДО КАК ЭлектронныйДокументВходящийЭДО
		|		ПО СоставПакетовДокументовЭДО.ЭлектронныйДокумент = ЭлектронныйДокументВходящийЭДО.Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДанныеДокумента.ДокументЭДО,
		|	ДанныеДокумента.Организация,
		|	ДанныеДокумента.Контрагент,
		|	ДанныеДокумента.ВидДокумента,
		|	ДанныеДокумента.ТребуетсяПодтверждение,
		|	ДанныеДокумента.ФайлЭДО,
		|	ДанныеДокумента.РасширениеФайла,
		|	ДанныеДокумента.НомерДокумента КАК Номер,
		|	ДанныеДокумента.ДатаДокумента КАК Дата,
		|	ЕСТЬNULL(ОшибкиЭДОКИсправлению.ОписаниеПроблемы, """") КАК ОписаниеОшибкиСоздания,
		|	ЕСТЬNULL(ДокументыЭДОКСозданиюВДО.АвтоматическоеСоздание, Ложь) КАК ОжидаетАвтоматическогоСоздания
		|ИЗ
		|	ДанныеДокумента КАК ДанныеДокумента
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОшибкиЭДОКИсправлению КАК ОшибкиЭДОКИсправлению
		|		ПО ДанныеДокумента.ДокументЭДО = ОшибкиЭДОКИсправлению.ПредметОшибки
		|		ЛЕВОЕ соединение РегистрСведений.ДокументыЭДОКСозданиюВДО КАК ДокументыЭДОКСозданиюВДО
		|		ПО ДокументыЭДОКСозданиюВДО.ДокументЭДО = ДанныеДокумента.ДокументЭДО";
	
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#Область ПакетыЭДО

Функция ВозможноОбъединитьДокументыВПакет(Документы, ДанныеДляПакета, ОписаниеОшибки)
	
	Если ТипЗнч(Документы) <> Тип("Массив")
		Или Документы.Количество() <= 1 Тогда
		
		ОписаниеОшибки =
			НСтр("ru = 'Неверные параметры создания пакета.
				|Необходимо передать массив минимум из 2 документов 1С:Документооборот.'");
		Возврат Ложь;
	КонецЕсли;
	
	Для Каждого Элемент Из Документы Цикл
		Если ТипЗнч(Элемент) <> ОбменСКонтрагентамиДОСлужебныйКлиентСервер.ТипДокументаДО() Тогда
			
			ОписаниеОшибки =
				НСтр("ru = 'Неверные параметры создания пакета.
					|К объединению в пакет переданы элементы не являющиеся документами 1С:Документооборот.'");
			Возврат Ложь;
			
		КонецЕсли;
	КонецЦикла;
	
	Если ДанныеДляПакета.Организации.Количество() > 1 Тогда
		ОписаниеОшибки =
			НСтр("ru = 'К объединению в пакет выбраны документы по разным организациям.
				|В исходящий пакет ЭДО могут быть объединены документы только одной организации.'");
		Возврат Ложь;
	КонецЕсли;
	
	Если ДанныеДляПакета.Контрагенты.Количество() > 1 Тогда
		ОписаниеОшибки =
			НСтр("ru = 'К объединению в пакет выбраны документы по разным контрагентам.
				|В исходящий пакет ЭДО могут быть объединены документы только одного контрагента.'");
		Возврат Ложь;
	КонецЕсли;
	
	Для Каждого Направление Из ДанныеДляПакета.Направления Цикл
		Если Направление <> Перечисления.НаправленияЭДО.Исходящий Тогда
			
			ОписаниеОшибки =
				НСтр("ru = 'К объединению в пакет выбраны не исходящие документы.'");
			Возврат Ложь;
			
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого Состояние Из ДанныеДляПакета.Состояния Цикл
		Если Состояние <> Перечисления.СостоянияЭДОДокументооборот.НеСформирован
			И Состояние <> Перечисления.СостоянияЭДОДокументооборот.Подписан
			И Состояние <> Перечисления.СостоянияЭДОДокументооборот.ОжидаетсяОтправка Тогда
			
			ОписаниеОшибки =
				НСтр("ru = 'К объединению в пакет выбраны документы с неподходящими состояниями.
					|В исходящий пакет ЭДО могут быть объединены документы только в состоянии ""Не начат"" и ""Подписан"".'");
			Возврат Ложь;
			
		КонецЕсли;
	КонецЦикла;
	
	Если ДанныеДляПакета.ИдентификаторыПакетов.Количество() > 0 Тогда
		ОписаниеОшибки =
			НСтр("ru = 'К объединению в пакет выбраны документы уже состоящие в пакетах ЭДО.'");
		Возврат Ложь;
	КонецЕсли;
	
	СпособыОбмена = СпособыОбменаЭДОПоДокументамДО(Документы);
	
	Для Каждого Элемент Из СпособыОбмена Цикл
		Если Не СпособОбменаПоддерживаетПакетнуюОбработку(Элемент.Значение) Тогда
			ОписаниеОшибки =
				НСтр("ru = 'К объединению в пакет выбраны документы с неподходящим способом обмена.
					|Пакетную обработку поддерживает только сервис 1С-ЭДО, при настройке обмена напрямую с оператором, пакеты не поддерживаются.'");
			Возврат Ложь;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Истина;
	
КонецФункции

Функция ВозможноИзменитьСоставПакета(ДанныеПакета, ДобавляемыеДокументы, УдаляемыеДокументы, ОписаниеОшибки)
	
	Если ДанныеПакета.ИдентификаторПакета = Неопределено Тогда
		ОписаниеОшибки = СтрШаблон(
			НСтр("ru = 'Не найден пакет документов (%1) в которых необходимо добавить документы.'"),
			ДанныеПакета.ИдентификаторПакета);
		Возврат Ложь;
	КонецЕсли;
	
	Если ДанныеПакета.Направление <> Перечисления.НаправленияЭДО.Исходящий Тогда
		ОписаниеОшибки = СтрШаблон(
			НСтр("ru = 'Пакет документов (%1) не исходящий.
				|Возможно менять состав только исходящих по ЭДО пакетов.'"),
			ДанныеПакета.ИдентификаторПакета);
		Возврат Ложь;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ДанныеПакета.ИдентификаторПакетаБЭД) Тогда
		ОписаниеОшибки = СтрШаблон(
			НСтр("ru = 'Пакет документов (%1) запущен в отправку. Нельзя менять состав этого пакета.'"),
			ДанныеПакета.ИдентификаторПакета);
		Возврат Ложь;
	КонецЕсли;
	
	// Создадим соответствия, чтобы эффективнее искать
	ДокументыВПакете = Новый Соответствие;
	Для Каждого Документ Из ДанныеПакета.Документы Цикл
		ДокументыВПакете.Вставить(Документ, Истина);
	КонецЦикла;
	
	НовыеДокументы = Новый Соответствие;
	Для Каждого Документ Из ДобавляемыеДокументы Цикл
		НовыеДокументы.Вставить(Документ, Истина);
	КонецЦикла;
	
	Для Каждого Документ Из УдаляемыеДокументы Цикл
		
		Если ДокументыВПакете[Документ] = Неопределено Тогда
			ОписаниеОшибки = СтрШаблон(
				НСтр("ru = 'Попытка удаления документа %1 из пакета ЭДО (%2), который не содержит удаляемого документа.'"),
				Документ,
				ДанныеПакета.ИдентификаторПакета);
			Возврат Ложь;
		КонецЕсли;
		
		Если НовыеДокументы[Документ] <> Неопределено Тогда
			ОписаниеОшибки = СтрШаблон(
				НСтр("ru = 'Ошибка изменения состава пакета ЭДО (%2):
					|Документ %1 присутствует и в удаляемых и в добавляемых документах.'"),
				Документ,
				ДанныеПакета.ИдентификаторПакета);
			Возврат Ложь;
		КонецЕсли;
		
	КонецЦикла;
	
	Для Каждого Документ Из ДобавляемыеДокументы Цикл
		
		Если ДокументыВПакете[Документ] <> Неопределено Тогда
			ОписаниеОшибки = СтрШаблон(
				НСтр("ru = 'Попытка добавления документа %1 в пакет ЭДО (%2), который уже содержит этот документ.'"),
				Документ,
				ДанныеПакета.ИдентификаторПакета);
			Возврат Ложь;
		КонецЕсли;
		
	КонецЦикла;
	
	Если ДобавляемыеДокументы.Количество() > 0 Тогда
		
		ДанныеНовыхДокументов = ДанныеДокументовДляДобавленияВПакет(ДобавляемыеДокументы);
		
		Если ДанныеНовыхДокументов.Организации.Количество() > 1 Тогда
			ОписаниеОшибки =
				НСтр("ru = 'К добавлению в пакет выбраны документы по разным организациям.
					|В исходящий пакет ЭДО могут быть добавлены документы только одной организации.'");
			Возврат Ложь;
		КонецЕсли;
		
		Если ДанныеНовыхДокументов.Организации[0] <> ДанныеПакета.Организация Тогда
			ОписаниеОшибки =
				НСтр("ru = 'К добавлению в пакет выбраны документы по организации, отличной от пакета.'");
			Возврат Ложь;
		КонецЕсли;
		
		Если ДанныеНовыхДокументов.Контрагенты.Количество() > 1 Тогда
			ОписаниеОшибки =
				НСтр("ru = 'К добавлению в пакет выбраны документы по разным контрагентам.
					|В исходящий пакет ЭДО могут быть добавлены документы только одного контрагента.'");
			Возврат Ложь;
		КонецЕсли;
		
		Если ДанныеНовыхДокументов.Контрагенты[0] <> ДанныеПакета.Контрагент Тогда
			ОписаниеОшибки =
				НСтр("ru = 'К добавлению в пакет выбраны документы по контрагенту, отличному от пакета.'");
			Возврат Ложь;
		КонецЕсли;
		
		Для Каждого Направление Из ДанныеНовыхДокументов.Направления Цикл
			Если Направление <> Перечисления.НаправленияЭДО.Исходящий Тогда
				
				ОписаниеОшибки =
					НСтр("ru = 'К добавлению в пакет выбраны не исходящие документы.'");
				Возврат Ложь;
				
			КонецЕсли;
		КонецЦикла;
		
		Для Каждого Состояние Из ДанныеНовыхДокументов.Состояния Цикл
			Если Состояние <> Перечисления.СостоянияЭДОДокументооборот.НеСформирован
				И Состояние <> Перечисления.СостоянияЭДОДокументооборот.Подписан
				И Состояние <> Перечисления.СостоянияЭДОДокументооборот.ОжидаетсяОтправка Тогда
				
				ОписаниеОшибки =
					НСтр("ru = 'К добавлению в пакет выбраны документы с неподходящими состояниями.
						|В исходящий пакет ЭДО могут быть добавлены документы только в состоянии ""Не начат"" и ""Подписан"".'");
				Возврат Ложь;
				
			КонецЕсли;
		КонецЦикла;
		
		Если ДанныеНовыхДокументов.ИдентификаторыПакетов.Количество() > 0 Тогда
			ОписаниеОшибки =
				НСтр("ru = 'К добавлению в пакет выбраны документы уже состоящие в пакетах ЭДО.'");
			Возврат Ложь;
		КонецЕсли;
		
	КонецЕсли;
	
	СпособыОбмена = СпособыОбменаЭДОПоДокументамДО(ДобавляемыеДокументы);
	
	Для Каждого Элемент Из СпособыОбмена Цикл
		Если Не СпособОбменаПоддерживаетПакетнуюОбработку(Элемент.Значение) Тогда
			ОписаниеОшибки =
				НСтр("ru = 'К добавлению в пакет выбраны документы с неподходящим способом обмена.
					|Пакетную обработку поддерживает только сервис 1С-ЭДО, при настройке обмена напрямую с оператором, пакеты не поддерживаются.'");
			Возврат Ложь;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Истина;
	
КонецФункции

// Выборка сведений о состояниях исходящих по ЭДО документов.
// 
// Параметры:
//  Документы - Массив Из ОпределяемыйТип.ДокументДОДляЭДО
// 
// Возвращаемое значение:
//  ВыборкаИзРезультатаЗапроса:
//    * ДокументДО - ОпределяемыйТип.ДокументДОДляЭДО
//    * Контрагент - СправочникСсылка.Контрагенты
//    * Состояние - ПеречислениеСсылка.СостоянияЭДОДокументооборот
//
Функция ВыборкаСведенийОСостоянияхЭДОИсходящих(Документы)
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	СостояниеДокументовПоЭДОСрезПоследних.ДокументДО КАК ДокументДО,
		|	СостояниеДокументовПоЭДОСрезПоследних.Контрагент КАК Контрагент,
		|	СостояниеДокументовПоЭДОСрезПоследних.СостояниеВерсииДокументаПоЭДО КАК Состояние
		|ИЗ
		|	РегистрСведений.СостояниеДокументовПоЭДО.СрезПоследних(, ДокументДО В (&Документы)
		|	И НаправлениеЭД = ЗНАЧЕНИЕ(Перечисление.НаправленияЭДО.Исходящий)) КАК СостояниеДокументовПоЭДОСрезПоследних";
	Запрос.УстановитьПараметр("Документы", Документы);
	
	Возврат Запрос.Выполнить().Выбрать();
	
КонецФункции

#КонецОбласти

Функция СпособыОбменаЭДОПоДокументамДО(ДокументыДО)
	
	СпособыОбмена = Новый Соответствие;
	
	Для Каждого Документ Из ДокументыДО Цикл
		СпособыОбмена.Вставить(Документ, Перечисления.СпособыОбменаЭД.ПустаяСсылка());
	КонецЦикла;
	
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	Файлы.ВладелецФайла КАК Документ,
		|	ОбъектыУчетаДокументовЭДО.ЭлектронныйДокумент
		|ПОМЕСТИТЬ СуществующиеДокументыЭДО
		|ИЗ
		|	Справочник.Файлы КАК Файлы
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СлужебныеФайлыДокументов КАК СлужебныеФайлыДокументов
		|		ПО Файлы.Ссылка = СлужебныеФайлыДокументов.Файл
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ОбъектыУчетаДокументовЭДО КАК ОбъектыУчетаДокументовЭДО
		|		ПО (Файлы.ТекущаяВерсия = ОбъектыУчетаДокументовЭДО.ОбъектУчета
		|		И ОбъектыУчетаДокументовЭДО.Актуальный)
		|ГДЕ
		|	СлужебныеФайлыДокументов.Файл ЕСТЬ NULL
		|	И Файлы.ВладелецФайла В (&Документы)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДокументыДО.Ссылка КАК Документ,
		|	НастройкиОтправкиДокументовПоЭДО.Отправитель,
		|	НастройкиОтправкиДокументовПоЭДО.Получатель,
		|	НастройкиОтправкиДокументовПоЭДО.ВидДокументаЭДО
		|ПОМЕСТИТЬ ВидыДокументовЭДОДокументов
		|ИЗ
		|	Справочник.%ИмяСправочникаДокументаДО% КАК ДокументыДО
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.НастройкиОтправкиДокументовПоЭДО КАК НастройкиОтправкиДокументовПоЭДО
		|		ПО ДокументыДО.Организация = НастройкиОтправкиДокументовПоЭДО.Отправитель
		|		И ДокументыДО.Контрагент = НастройкиОтправкиДокументовПоЭДО.Получатель
		|		И ДокументыДО.ВидДокумента = НастройкиОтправкиДокументовПоЭДО.ВидДокумента
		|ГДЕ
		|	ДокументыДО.Ссылка В (&Документы)
		|	И НастройкиОтправкиДокументовПоЭДО.Отправлять
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВидыДокументовЭДОДокументов.Документ,
		|	НастройкиОтправкиЭлектронныхДокументовПоВидам.СпособОбменаЭД КАК СпособОбмена
		|ИЗ
		|	ВидыДокументовЭДОДокументов КАК ВидыДокументовЭДОДокументов
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.НастройкиОтправкиЭлектронныхДокументовПоВидам КАК
		|			НастройкиОтправкиЭлектронныхДокументовПоВидам
		|		ПО ВидыДокументовЭДОДокументов.Отправитель = НастройкиОтправкиЭлектронныхДокументовПоВидам.Отправитель
		|		И ВидыДокументовЭДОДокументов.Получатель = НастройкиОтправкиЭлектронныхДокументовПоВидам.Получатель
		|		И ВидыДокументовЭДОДокументов.ВидДокументаЭДО <> ЗНАЧЕНИЕ(Справочник.ВидыДокументовЭДО.ПустаяСсылка)
		|		И ВидыДокументовЭДОДокументов.ВидДокументаЭДО = НастройкиОтправкиЭлектронныхДокументовПоВидам.ВидДокумента
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВидыДокументовЭДОДокументов.Документ,
		|	НастройкиОтправкиЭлектронныхДокументовПоВидам.СпособОбменаЭД
		|ИЗ
		|	ВидыДокументовЭДОДокументов КАК ВидыДокументовЭДОДокументов
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.НастройкиОтправкиЭлектронныхДокументовПоВидам КАК
		|			НастройкиОтправкиЭлектронныхДокументовПоВидам
		|		ПО ВидыДокументовЭДОДокументов.Отправитель = НастройкиОтправкиЭлектронныхДокументовПоВидам.Отправитель
		|		И ВидыДокументовЭДОДокументов.Получатель = НастройкиОтправкиЭлектронныхДокументовПоВидам.Получатель
		|		И ВидыДокументовЭДОДокументов.ВидДокументаЭДО = ЗНАЧЕНИЕ(Справочник.ВидыДокументовЭДО.ПустаяСсылка)
		|		И НастройкиОтправкиЭлектронныхДокументовПоВидам.ВидДокумента = &ВидДокументаПрочее
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СуществующиеДокументыЭДО.Документ,
		|	ЭлектронныйДокументВходящийЭДО.СпособОбмена,
		|	NULL
		|ИЗ
		|	СуществующиеДокументыЭДО КАК СуществующиеДокументыЭДО
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЭлектронныйДокументВходящийЭДО КАК ЭлектронныйДокументВходящийЭДО
		|		ПО СуществующиеДокументыЭДО.ЭлектронныйДокумент = ЭлектронныйДокументВходящийЭДО.Ссылка
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	СуществующиеДокументыЭДО.Документ,
		|	NULL,
		|	ЭлектронныйДокументИсходящийЭДО.СпособОбмена
		|ИЗ
		|	СуществующиеДокументыЭДО КАК СуществующиеДокументыЭДО
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЭлектронныйДокументИсходящийЭДО КАК ЭлектронныйДокументИсходящийЭДО
		|		ПО СуществующиеДокументыЭДО.ЭлектронныйДокумент = ЭлектронныйДокументИсходящийЭДО.Ссылка";
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
		"%ИмяСправочникаДокументаДО%",
		ОбменСКонтрагентамиДОСлужебныйКлиентСервер.ИмяСправочникаДокументовДО());
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("Документы", ДокументыДО);
	Запрос.УстановитьПараметр("ВидДокументаПрочее",
		ЭлектронныеДокументыЭДО.ВидДокументаПоТипу(Перечисления.ТипыДокументовЭДО.Прочее));
	
	РезультатыЗапроса = Запрос.ВыполнитьПакет();
	КолвоРезультатов = РезультатыЗапроса.Количество();
	
	ВыборкаПоНастройкамОтправки = РезультатыЗапроса[КолвоРезультатов - 2].Выбрать();
	ВыборкаПоСуществующимДокументамЭДО = РезультатыЗапроса[КолвоРезультатов - 1].Выбрать();
	
	Пока ВыборкаПоНастройкамОтправки.Следующий() Цикл
		Если ЗначениеЗаполнено(ВыборкаПоНастройкамОтправки.СпособОбмена) Тогда
			СпособыОбмена.Вставить(ВыборкаПоНастройкамОтправки.Документ,
				ВыборкаПоНастройкамОтправки.СпособОбмена);
		КонецЕсли;
	КонецЦикла;
	
	Пока ВыборкаПоСуществующимДокументамЭДО.Следующий() Цикл
		Если ЗначениеЗаполнено(ВыборкаПоСуществующимДокументамЭДО.СпособОбмена) Тогда
			СпособыОбмена.Вставить(ВыборкаПоСуществующимДокументамЭДО.Документ,
				ВыборкаПоСуществующимДокументамЭДО.СпособОбмена);
		КонецЕсли;
	КонецЦикла;
	
	Возврат СпособыОбмена;
	
КонецФункции

#Область ОтражениеДокументовВДО

#Область ЗаполнениеКонтекстаОтражения

Функция ДанныеДляЗаполненияКонтекстаОтражения()
	
	ДанныеДляЗаполнения = Новый Структура;
	ДанныеДляЗаполнения.Вставить("ДанныеДокументооборотаБЭД", Неопределено);
	ДанныеДляЗаполнения.Вставить("СтруктураРазбораФайлаЭДО", Неопределено);
	ДанныеДляЗаполнения.Вставить("ПравилаОтраженияВДО", Неопределено);
	ДанныеДляЗаполнения.Вставить("ДанныеОФайлах", Новый Массив);
	ДанныеДляЗаполнения.Вставить("СтроковыеПоляПоШаблонам", Неопределено);
	ДанныеДляЗаполнения.Вставить("ДанныеШаблонаПравилОтражения", Неопределено);
	
	Возврат ДанныеДляЗаполнения;
	
КонецФункции

Процедура СобратьДанныеДляЗаполненияКонтекстаОтражения(ДанныеДляЗаполнения, КонтекстОтражения)
	
	ЗаполнитьДанныеДокументооборотаБЭДДляОтражения(ДанныеДляЗаполнения, КонтекстОтражения);
	Если КонтекстОтражения.Отказ Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьСтруктуруРазбораФайлаЭДО(ДанныеДляЗаполнения, КонтекстОтражения);
	Если КонтекстОтражения.Отказ Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьПравилаОтраженияВДО(ДанныеДляЗаполнения, КонтекстОтражения);
	Если КонтекстОтражения.Отказ Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьСписокФайловКСозданиюВДО(ДанныеДляЗаполнения, КонтекстОтражения);
	Если КонтекстОтражения.Отказ Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьСтроковыеПоляПоШаблонамДляОтраженияВДО(ДанныеДляЗаполнения, КонтекстОтражения);
	Если КонтекстОтражения.Отказ Тогда
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьДанныеДокументооборотаБЭДДляОтражения(ДанныеДляЗаполнения, КонтекстОтражения)
	
	ДанныеБЭД = Новый Структура;
	ДанныеБЭД.Вставить("ДокументЭДО", Документы.ЭлектронныйДокументВходящийЭДО.ПустаяСсылка());
	ДанныеБЭД.Вставить("СообщениеЭДО", Документы.СообщениеЭДО.ПустаяСсылка());
	ДанныеБЭД.Вставить("ФайлЭДО", Справочники.СообщениеЭДОПрисоединенныеФайлы.ПустаяСсылка());
	ДанныеБЭД.Вставить("ВидДокументаЭДО", Справочники.ВидыДокументовЭДО.ПустаяСсылка());
	ДанныеБЭД.Вставить("Номер", "");
	ДанныеБЭД.Вставить("Дата", Дата(1, 1, 1));
	ДанныеБЭД.Вставить("НомерЭДО", "");
	ДанныеБЭД.Вставить("ДатаЭДО", Дата(1, 1, 1));
	ДанныеБЭД.Вставить("Организация", Справочники.Организации.ПустаяСсылка());
	ДанныеБЭД.Вставить("Контрагент", Справочники.Контрагенты.ПустаяСсылка());
	ДанныеБЭД.Вставить("СуммаДокумента", 0);
	ДанныеБЭД.Вставить("ИмяФайла", "");
	ДанныеБЭД.Вставить("РасширениеФайла", "");
	
	ДокументЭДО = КонтекстОтражения.ДокументЭДО;
	
	ДанныеБЭД.ДокументЭДО = ДокументЭДО;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ЭлектронныйДокументВходящийЭДО.Ссылка КАК ДокументЭДО,
		|	СообщениеЭДО.Ссылка КАК СообщениеЭДО,
		|	СообщениеЭДОПрисоединенныеФайлы.Ссылка КАК ФайлЭДО,
		|	ЭлектронныйДокументВходящийЭДО.ВидДокумента КАК ВидДокументаЭДО,
		|	ЭлектронныйДокументВходящийЭДО.НомерДокумента КАК Номер,
		|	ЭлектронныйДокументВходящийЭДО.ДатаДокумента КАК Дата,
		|	ЭлектронныйДокументВходящийЭДО.Номер КАК НомерЭДО,
		|	ЭлектронныйДокументВходящийЭДО.Дата КАК ДатаЭДО,
		|	ЭлектронныйДокументВходящийЭДО.Организация КАК Организация,
		|	ЭлектронныйДокументВходящийЭДО.Контрагент КАК Контрагент,
		|	ЭлектронныйДокументВходящийЭДО.СуммаДокумента КАК СуммаДокумента,
		|	СообщениеЭДОПрисоединенныеФайлы.Наименование КАК ИмяФайла,
		|	СообщениеЭДОПрисоединенныеФайлы.Расширение КАК РасширениеФайла
		|ИЗ
		|	Документ.СообщениеЭДО КАК СообщениеЭДО
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЭлектронныйДокументВходящийЭДО КАК ЭлектронныйДокументВходящийЭДО
		|		ПО СообщениеЭДО.ЭлектронныйДокумент = ЭлектронныйДокументВходящийЭДО.Ссылка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.СообщениеЭДОПрисоединенныеФайлы КАК СообщениеЭДОПрисоединенныеФайлы
		|		ПО СообщениеЭДО.ОсновнойФайл = СообщениеЭДОПрисоединенныеФайлы.Ссылка
		|ГДЕ
		|	ЭлектронныйДокументВходящийЭДО.Ссылка = &ЭлектронныйДокумент
		|	И СообщениеЭДО.ТипЭлементаРегламента В (&ТипыЭлементовРегламента)";
	Запрос.УстановитьПараметр("ЭлектронныйДокумент", ДокументЭДО);
	
	ТипыЭлементовРегламентаПервичныхЭД = Новый Массив;
	ТипыЭлементовРегламентаПервичныхЭД.Добавить(Перечисления.ТипыЭлементовРегламентаЭДО.ИнформацияОтправителя);
	
	Запрос.УстановитьПараметр("ТипыЭлементовРегламента", ТипыЭлементовРегламентаПервичныхЭД);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Не Выборка.Следующий() Тогда
		
		ОписаниеПроблемы = 
			СтрШаблон(НСтр("ru = 'Не удалось получить данные документооборота ЭДО по входящему документу.
				|Проверьте существование сообщения ЭДО информации отправителя по документу %1'"),
			ДокументЭДО);
		
		ДобавитьОшибкуКонтекстаОтражения(КонтекстОтражения, ОписаниеПроблемы, ДокументЭДО, Истина);
		
		ДанныеДляЗаполнения.ДанныеДокументооборотаБЭД = ДанныеБЭД;
		
		Возврат;
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(ДанныеБЭД, Выборка);
	
	ДанныеДляЗаполнения.ДанныеДокументооборотаБЭД = ДанныеБЭД;
	
	Возврат;
	
КонецПроцедуры

Процедура ЗаполнитьСтруктуруРазбораФайлаЭДО(ДанныеДляЗаполнения, КонтекстОтражения)
	
	РасширениеФайла = ДанныеДляЗаполнения.ДанныеДокументооборотаБЭД.РасширениеФайла;
	Если НРег(РасширениеФайла) <> "xml" Тогда
		ДанныеДляЗаполнения.СтруктураРазбораФайлаЭДО = Неопределено;
		Возврат;
	КонецЕсли;
	
	ФайлЭДО = ДанныеДляЗаполнения.ДанныеДокументооборотаБЭД.ФайлЭДО;
	
	Попытка
		ДвоичныеДанныеФайлаЭДО = РаботаСФайлами.ДвоичныеДанныеФайла(ФайлЭДО);
	Исключение
		
		ОписаниеПроблемы = НСтр("ru = 'Не удалось получить двоичные данные файла ЭДО по причине:'")
			+ Символы.ПС + ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		
		ДобавитьОшибкуКонтекстаОтражения(КонтекстОтражения, ОписаниеПроблемы, ФайлЭДО, Истина);
		
		Возврат;
		
	КонецПопытки;
	
	СсылкаНаДвоичныеДанные = ПоместитьВоВременноеХранилище(ДвоичныеДанныеФайлаЭДО, Новый УникальныйИдентификатор);
	
	ДанныеДляЗаполнения.СтруктураРазбораФайлаЭДО = СведенияОЭДИзФайла(СсылкаНаДвоичныеДанные);
	
	УдалитьИзВременногоХранилища(СсылкаНаДвоичныеДанные);
	
КонецПроцедуры

Процедура ЗаполнитьПравилаОтраженияВДО(ДанныеДляЗаполнения, КонтекстОтражения)
	
	ДанныеБЭД = ДанныеДляЗаполнения.ДанныеДокументооборотаБЭД;
	
	КлючПравилОтражения = РегистрыСведений.ПравилаУчетаВидовЭДДО.КлючПравилОтраженияВДО(
			ДанныеБЭД.ВидДокументаЭДО,
			ДанныеБЭД.Организация,
			ДанныеБЭД.Контрагент);
	
	ПравилаОтраженияВДО = РегистрыСведений.ПравилаУчетаВидовЭДДО.ПараметрыДокументаПоВидуЭДО(КлючПравилОтражения);
	
	Если КонтекстОтражения.АвтоматическоеВыполнение
		И Не ПравилаОтраженияПригодныДляАвтоматическогоСоздания(
			ПравилаОтраженияВДО, КлючПравилОтражения, КонтекстОтражения) Тогда
		
		Возврат;
	КонецЕсли;
	
	ДанныеДляЗаполнения.ПравилаОтраженияВДО = ПравилаОтраженияВДО;
	ДанныеДляЗаполнения.ДанныеШаблонаПравилОтражения = ДанныеШаблонаПравилОтраженияВДО(ПравилаОтраженияВДО);
	
КонецПроцедуры

Функция ПравилаОтраженияПригодныДляАвтоматическогоСоздания(ПравилаОтраженияВДО, КлючПравил, КонтекстОтражения)
	
	Если Не ПравилаОтраженияВДО.СоздаватьАвтоматически Тогда
		ОписаниеПроблемы = СтрШаблон(
			НСтр("ru = 'В настройках создания документов 1С:Документооборот по входящим ЭДО отключено автоматическое создание.
				|Настройте автоматическое создание для вида ЭДО %1 по контрагенту %2 или попробуйте создать документ вручную.'"),
			КлючПравил.ВидДокументаЭДО, КлючПравил.Контрагент);
		
		ДобавитьОшибкуКонтекстаОтражения(КонтекстОтражения, ОписаниеПроблемы,
			КонтекстОтражения.ДокументЭДО, Истина);
		Возврат Ложь;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ПравилаОтраженияВДО.ВидДокумента) Тогда
		ОписаниеПроблемы = СтрШаблон(
			НСтр("ru = 'В настройках создания документов 1С:Документооборот по входящим ЭДО не указан вид документа.
				|Проверьте корректность указания вида документа 1С:Документооборот в настройках создания для вида ЭДО %1 по контрагенту %2'"),
			КлючПравил.ВидДокументаЭДО, КлючПравил.Контрагент);
		
		ДобавитьОшибкуКонтекстаОтражения(КонтекстОтражения, ОписаниеПроблемы,
			КонтекстОтражения.ДокументЭДО, Истина);
		Возврат Ложь;
	КонецЕсли;
	
	Если Не ВидДокументаДоступенДляОтраженияВходящегоЭДО(ПравилаОтраженияВДО.ВидДокумента) Тогда
		ОписаниеПроблемы = СтрШаблон(
			НСтр("ru = 'В настройках создания документов 1С:Документооборот по входящим ЭДО указан вид документа недоступный для отражения входящих ЭДО.
				|В виде документа должен быть установлен учет сторон и учет недействующих документов.
				|Проверьте корректность указания вида документа 1С:Документооборот в настройках создания для вида ЭДО %1 по контрагенту %2'"),
			КлючПравил.ВидДокументаЭДО, КлючПравил.Контрагент);
		
		ДобавитьОшибкуКонтекстаОтражения(КонтекстОтражения, ОписаниеПроблемы,
			КонтекстОтражения.ДокументЭДО, Истина);
		Возврат Ложь;
	КонецЕсли;
	
	ВестиУчетПоТематикам =
		ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ПравилаОтраженияВДО.ВидДокумента, "ВестиУчетПоТематикам");
	
	Если ВестиУчетПоТематикам И Не ЗначениеЗаполнено(ПравилаОтраженияВДО.ТематикаДокумента) Тогда
		ОписаниеПроблемы = СтрШаблон(
			НСтр("ru = 'В настройках создания документов 1С:Документооборот по входящим ЭДО не указана тематика.
				|Проверьте корректность указания тематики и вида документа 1С:Документооборот в настройках создания для вида ЭДО %1 по контрагенту %2'"),
			КлючПравил.ВидДокументаЭДО, КлючПравил.Контрагент);
		
		ДобавитьОшибкуКонтекстаОтражения(КонтекстОтражения, ОписаниеПроблемы,
			КонтекстОтражения.ДокументЭДО, Истина);
		Возврат Ложь;
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

Функция ДанныеШаблонаПравилОтраженияВДО(ПравилаОтраженияВДО)
	
	ДанныеШаблонаПравилОтражения = Новый Структура;
	ДанныеШаблонаПравилОтражения.Вставить("ГрифДоступа", Справочники.ГрифыДоступа.ПустаяСсылка());
	ДанныеШаблонаПравилОтражения.Вставить("Папка", Справочники.ПапкиДокументов.ПустаяСсылка());
	ДанныеШаблонаПравилОтражения.Вставить("ВопросДеятельности", Справочники.ВопросыДеятельности.ПустаяСсылка());
	ДанныеШаблонаПравилОтражения.Вставить("Ответственный", Справочники.Сотрудники.ПустаяСсылка());
	
	Если Не ЗначениеЗаполнено(ПравилаОтраженияВДО.Шаблон) Тогда
		Возврат ДанныеШаблонаПравилОтражения;
	КонецЕсли;
	
	РеквизитыШаблона = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ПравилаОтраженияВДО.Шаблон,
		"Папка, ВопросДеятельности, Ответственный, ГрифДоступа");
	
	ЗаполнитьЗначенияСвойств(ДанныеШаблонаПравилОтражения, РеквизитыШаблона);
	
	Возврат ДанныеШаблонаПравилОтражения;
	
КонецФункции

Процедура ЗаполнитьСписокФайловКСозданиюВДО(ДанныеДляЗаполнения, КонтекстОтражения)
	
	СтруктураРазбораФайлаЭДО = ДанныеДляЗаполнения.СтруктураРазбораФайлаЭДО;
	ДанныеБЭД = ДанныеДляЗаполнения.ДанныеДокументооборотаБЭД;
	ПравилаОтраженияВДО = ДанныеДляЗаполнения.ПравилаОтраженияВДО;
	ФайлЭДО = ДанныеБЭД.ФайлЭДО;
	
	Попытка
		ДвоичныеДанныеФайлаЭДО = РаботаСФайлами.ДвоичныеДанныеФайла(ФайлЭДО);
	Исключение
		
		ОписаниеПроблемы = НСтр("ru = 'Не удалось получить двоичные данные файла ЭДО по причине:'")
			+ Символы.ПС + ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		
		ДобавитьОшибкуКонтекстаОтражения(КонтекстОтражения, ОписаниеПроблемы, ФайлЭДО, Истина);
		
		Возврат;
		
	КонецПопытки;
	
	ИмяФайлаПоДанннымРазбора = "";
	Если ТипЗнч(СтруктураРазбораФайлаЭДО) = Тип("Структура")
		И СтруктураРазбораФайлаЭДО.Свойство("ИмяСоздания")
		И ЗначениеЗаполнено(СтруктураРазбораФайлаЭДО.ИмяСоздания) Тогда
		
		ИмяФайлаПоДанннымРазбора = СтруктураРазбораФайлаЭДО.ИмяСоздания;
	КонецЕсли;
	ИмяФайла = ?(ИмяФайлаПоДанннымРазбора = "", ДанныеБЭД.ИмяФайла, ИмяФайлаПоДанннымРазбора);
	
	СсылкаНаДвоичныеДанные = ПоместитьВоВременноеХранилище(ДвоичныеДанныеФайлаЭДО, Новый УникальныйИдентификатор);
	
	СведенияОФайле = РаботаСФайламиКлиентСервер.СведенияОФайле("ФайлСВерсией");
	ЗаполнитьЗначенияСвойств(СведенияОФайле, ФайлЭДО);
	СведенияОФайле.АдресВременногоХранилищаФайла = СсылкаНаДвоичныеДанные;
	СведенияОФайле.ВремяИзмененияУниверсальное = ТекущаяУниверсальнаяДата();
	СведенияОФайле.ВремяИзменения = ТекущаяДатаСеанса();
	СведенияОФайле.ИмяБезРасширения = ИмяФайла;
	СведенияОФайле.РасширениеБезТочки = ДанныеБЭД.РасширениеФайла;
	СведенияОФайле.РольФайла = ПравилаОтраженияВДО.РольФайла;
	
	УстановленныеПодписи = ЭлектроннаяПодпись.УстановленныеПодписи(ФайлЭДО);
	
	ФайлКСозданию = Новый Структура;
	ФайлКСозданию.Вставить("СведенияОФайле", СведенияОФайле);
	ФайлКСозданию.Вставить("УстановленныеПодписи", УстановленныеПодписи);
	
	ДанныеДляЗаполнения.ДанныеОФайлах.Добавить(ФайлКСозданию);
	
КонецПроцедуры

Процедура ЗаполнитьСтроковыеПоляПоШаблонамДляОтраженияВДО(ДанныеДляЗаполнения, КонтекстОтражения)
	
	ПравилаОтраженияВДО = ДанныеДляЗаполнения.ПравилаОтраженияВДО;
	ДанныеБЭД = ДанныеДляЗаполнения.ДанныеДокументооборотаБЭД;
	
	ПараметрыЗаполнения = ПараметрыЗаполненияСтроковыхПолей(ДанныеДляЗаполнения);
	
	Наименование = "";
	Содержание = "";
	
	Если ЗначениеЗаполнено(ПравилаОтраженияВДО.ШаблонНаименования) Тогда
		
		РезультатЗаполнения = ОбменСКонтрагентамиДОСервер.ЗаполнитьСтроковыеПараметрыПоШаблону(
			ПравилаОтраженияВДО.ШаблонНаименования,
			ПараметрыЗаполнения,
			"ДеревоПараметровСозданияДокумента");
		
		Если Не РезультатЗаполнения.Успех Тогда
			ОписаниеПроблемы = СтрШаблон(
				НСтр("ru = 'Не удалось заполнить наименование по шаблону по причине:
					|%1
					|Документу будет присвоено стандартное наименование.'"),
				РезультатЗаполнения.Описание);
			
			ДобавитьОшибкуКонтекстаОтражения(КонтекстОтражения, ОписаниеПроблемы, КонтекстОтражения.ДокументДО, Ложь);
		Иначе
			Наименование = РезультатЗаполнения.ЗаполненнаяСтрока;
		КонецЕсли;
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ПравилаОтраженияВДО.ШаблонСодержания) Тогда
		
		РезультатЗаполнения = ОбменСКонтрагентамиДОСервер.ЗаполнитьСтроковыеПараметрыПоШаблону(
			ПравилаОтраженияВДО.ШаблонСодержания,
			ПараметрыЗаполнения,
			"ДеревоПараметровСозданияДокумента");
		
		Если Не РезультатЗаполнения.Успех Тогда
			ОписаниеПроблемы = СтрШаблон(
				НСтр("ru = 'Не удалось заполнить содержание по шаблону по причине:
					|%1'"),
				РезультатЗаполнения.Описание);
			
			ДобавитьОшибкуКонтекстаОтражения(КонтекстОтражения, ОписаниеПроблемы, КонтекстОтражения.ДокументДО, Ложь);
		Иначе
			Содержание = РезультатЗаполнения.ЗаполненнаяСтрока;
		КонецЕсли;
		
	КонецЕсли;
	
	Если Наименование = "" Тогда
		Наименование = СтрШаблон(
			НСтр("ru = 'Документ ЭДО (%1) №%2 от %3'"),
			ДанныеБЭД.ВидДокументаЭДО,
			ДанныеБЭД.Номер,
			Формат(ДанныеБЭД.Дата, "ДФ=dd.MM.yyyy"));
	КонецЕсли;
	
	СтроковыеПоляПоШаблонам = Новый Структура;
	СтроковыеПоляПоШаблонам.Вставить("Наименование", Наименование);
	СтроковыеПоляПоШаблонам.Вставить("Содержание", Содержание);
	
	ДанныеДляЗаполнения.СтроковыеПоляПоШаблонам = СтроковыеПоляПоШаблонам;
	
КонецПроцедуры

Функция ПараметрыЗаполненияСтроковыхПолей(ДанныеДляЗаполнения)
	
	ПараметрыЗаполнения = ОбменСКонтрагентамиДОСервер.НовыеПараметрыЗаполненияСтроковыхПолей();
	
	ДанныеБЭД = ДанныеДляЗаполнения.ДанныеДокументооборотаБЭД;
	ПравилаОтраженияВДО = ДанныеДляЗаполнения.ПравилаОтраженияВДО;
	
	ПараметрыЗаполнения.ВидДокументаЭДО    = ДанныеБЭД.ВидДокументаЭДО;
	ПараметрыЗаполнения.ДатаДокумента      = ДанныеБЭД.Дата;
	ПараметрыЗаполнения.НомерДокумента     = ДанныеБЭД.Номер;
	ПараметрыЗаполнения.ДатаЭД             = ДанныеБЭД.ДатаЭДО;
	ПараметрыЗаполнения.НомерЭД            = ДанныеБЭД.НомерЭДО;
	ПараметрыЗаполнения.Организация        = ДанныеБЭД.Организация;
	ПараметрыЗаполнения.Контрагент         = ДанныеБЭД.Контрагент;
	ПараметрыЗаполнения.Сумма              = СуммаДокументаПоДаннымДляЗаполнения(ДанныеДляЗаполнения);
	ПараметрыЗаполнения.Валюта             = ВалютаДокументаПоДаннымДляЗаполнения(ДанныеДляЗаполнения);
	ПараметрыЗаполнения.Шаблон             = ПравилаОтраженияВДО.Шаблон;
	ПараметрыЗаполнения.Ответственный      = ОтветственныйПоДаннымДляЗаполнения(ДанныеДляЗаполнения);
	ПараметрыЗаполнения.Папка              = ПапкаПоДаннымДляЗаполнения(ДанныеДляЗаполнения);
	ПараметрыЗаполнения.ГрифДоступа        = ГрифДоступаПоДаннымДляЗаполнения(ДанныеДляЗаполнения);
	ПараметрыЗаполнения.ВопросДеятельности = ВопросДеятельностиПоДаннымДляЗаполнения(ДанныеДляЗаполнения);
	
	ПараметрыЗаполнения.ВходящийЭД         = ДанныеБЭД.ДокументЭДО;
	ПараметрыЗаполнения.СообщениеЭДО       = ДанныеБЭД.СообщениеЭДО;
	ПараметрыЗаполнения.ФайлЭД             = ДанныеБЭД.ФайлЭДО;
	
	Возврат ПараметрыЗаполнения;
	
КонецФункции

Процедура ЗаполнитьКонтекстОтраженияПоСобраннымДанным(ДанныеДляЗаполнения, КонтекстОтражения)
	
	ЗаполнитьДанныеДляОтраженияПоЭДО(
		КонтекстОтражения.ОснованиеЗаполненияДокумента.ДанныеДляОтраженияПоЭДО,
		ДанныеДляЗаполнения);
	
	ЗаполнитьФайлыКСозданию(КонтекстОтражения.ФайлыДляСоздания, ДанныеДляЗаполнения);
	
КонецПроцедуры

Процедура ЗаполнитьДанныеДляОтраженияПоЭДО(ДанныеДляОтраженияПоЭДО, ДанныеДляЗаполнения)
	
	ДанныеБЭД = ДанныеДляЗаполнения.ДанныеДокументооборотаБЭД;
	ПравилаОтраженияВДО = ДанныеДляЗаполнения.ПравилаОтраженияВДО;
	СтроковыеПоляПоШаблонам = ДанныеДляЗаполнения.СтроковыеПоляПоШаблонам;
	
	ДанныеДляОтраженияПоЭДО.Заголовок = СтроковыеПоляПоШаблонам.Наименование;
	ДанныеДляОтраженияПоЭДО.Содержание = СтроковыеПоляПоШаблонам.Содержание;
	ДанныеДляОтраженияПоЭДО.ВидДокумента = ПравилаОтраженияВДО.ВидДокумента;
	ДанныеДляОтраженияПоЭДО.Тематика = ПравилаОтраженияВДО.ТематикаДокумента;
	ДанныеДляОтраженияПоЭДО.Комментарий = "";
	ДанныеДляОтраженияПоЭДО.Организация = ДанныеБЭД.Организация;
	ДанныеДляОтраженияПоЭДО.Контрагент = ДанныеБЭД.Контрагент;
	ДанныеДляОтраженияПоЭДО.Сумма = СуммаДокументаПоДаннымДляЗаполнения(ДанныеДляЗаполнения);
	ДанныеДляОтраженияПоЭДО.Валюта = ВалютаДокументаПоДаннымДляЗаполнения(ДанныеДляЗаполнения);
	ДанныеДляОтраженияПоЭДО.Папка = ПапкаПоДаннымДляЗаполнения(ДанныеДляЗаполнения);
	ДанныеДляОтраженияПоЭДО.ГрифДоступа = ГрифДоступаПоДаннымДляЗаполнения(ДанныеДляЗаполнения);
	ДанныеДляОтраженияПоЭДО.Ответственный = ОтветственныйПоДаннымДляЗаполнения(ДанныеДляЗаполнения);
	ДанныеДляОтраженияПоЭДО.ВопросДеятельности = ВопросДеятельностиПоДаннымДляЗаполнения(ДанныеДляЗаполнения);
	ДанныеДляОтраженияПоЭДО.ДатаСоздания = ТекущаяДатаСеанса();
	ДанныеДляОтраженияПоЭДО.ФормаДокумента = Перечисления.ВариантыФормДокументов.Электронная;
	
	ЗаполнитьСтороныДокумента(ДанныеДляОтраженияПоЭДО.Стороны, ДанныеДляЗаполнения);
	
КонецПроцедуры

Процедура ЗаполнитьСтороныДокумента(Стороны, ДанныеДляЗаполнения)
	
	ДанныеБЭД = ДанныеДляЗаполнения.ДанныеДокументооборотаБЭД;
	
	СторонаОрганизации = ОбменСКонтрагентамиДОСлужебныйКлиентСервер.НовоеОписаниеСтороныДокументаДО();
	СторонаОрганизации.Сторона = ДанныеБЭД.Организация;
	
	ДанныеОФайлах = ДанныеДляЗаполнения.ДанныеОФайлах;
	ДанныеФайла = ДанныеОФайлах[ДанныеОФайлах.ВГраница()];
	УстановленныеПодписи = ДанныеФайла.УстановленныеПодписи;
	
	СторонаКонтрагента = ОбменСКонтрагентамиДОСлужебныйКлиентСервер.НовоеОписаниеСтороныДокументаДО();
	СторонаКонтрагента.Сторона = ДанныеБЭД.Контрагент;
	
	Если УстановленныеПодписи.Количество() > 0 Тогда
		
		СвойстваПодписи = УстановленныеПодписи[УстановленныеПодписи.ВГраница()];
		
		СторонаКонтрагента.Подписан = Истина;
		СторонаКонтрагента.Подписал =
			НайтиДобавитьКонтактноеЛицоИзСертификата(ДанныеБЭД.Контрагент, СвойстваПодписи.Сертификат);
		СторонаКонтрагента.ДатаПодписи = СвойстваПодписи.ДатаПодписи;
		
	КонецЕсли;
	
	Стороны.Добавить(СторонаОрганизации);
	Стороны.Добавить(СторонаКонтрагента);
	
КонецПроцедуры

Процедура ЗаполнитьФайлыКСозданию(ФайлыДляСоздания, ДанныеДляЗаполнения)
	
	ДанныеОФайлах = ДанныеДляЗаполнения.ДанныеОФайлах;
	
	Для Каждого ДанныеФайла Из ДанныеОФайлах Цикл
		
		ФайлКСозданию = ОбменСКонтрагентамиДОСлужебныйКлиентСервер.НовоеОписаниеФайлаДОДляСоздания();
		
		ЗаполнитьЗначенияСвойств(ФайлКСозданию.СведенияОФайле, ДанныеФайла.СведенияОФайле);
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(
			ФайлКСозданию.ЭлектронныеПодписи, ДанныеФайла.УстановленныеПодписи);
		
		ФайлыДляСоздания.Добавить(ФайлКСозданию);
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ПроверитьЗаполнениеКонтекстаДляАвтоматическогоОтражения(КонтекстОтражения)
	
	ДанныеДляОтражения = КонтекстОтражения.ОснованиеЗаполненияДокумента.ДанныеДляОтраженияПоЭДО;
	ВидДокумента = ДанныеДляОтражения.ВидДокумента;
	
	Если Не ЗначениеЗаполнено(ВидДокумента) Тогда
		ОписаниеПроблемы = СтрШаблон(
			НСтр("ru = 'При автоматическом создании документа 1С:Документооборот по входящему ЭДО %1 не удалось определить вид документа.
				|Проверьте настройки автоматического создания документов или попробуйте создать документ вручную.'"),
			КонтекстОтражения.ДокументЭДО);
		
		ДобавитьОшибкуКонтекстаОтражения(КонтекстОтражения, ОписаниеПроблемы,
			КонтекстОтражения.ДокументЭДО, Истина);
		Возврат;
	КонецЕсли;
	
	РеквизитыВидаДокумента = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ВидДокумента,
		"ВестиУчетПоТематикам, ОбязательноеУказаниеОтветственного");
	
	Если Не ЗначениеЗаполнено(ДанныеДляОтражения.Тематика) И РеквизитыВидаДокумента.ВестиУчетПоТематикам Тогда
		ОписаниеПроблемы = СтрШаблон(
			НСтр("ru = 'При автоматическом создании документа 1С:Документооборот по входящему ЭДО %1 не удалось определить тематику документа.
				|Проверьте настройки автоматического создания документов или попробуйте создать документ вручную.'"),
			КонтекстОтражения.ДокументЭДО);
		
		ДобавитьОшибкуКонтекстаОтражения(КонтекстОтражения, ОписаниеПроблемы,
			КонтекстОтражения.ДокументЭДО, Истина);
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ДанныеДляОтражения.Ответственный)
		И РеквизитыВидаДокумента.ОбязательноеУказаниеОтветственного Тогда
		ОписаниеПроблемы = СтрШаблон(
			НСтр("ru = 'При автоматическом создании документа 1С:Документооборот по входящему ЭДО %1 не удалось определить ответственного документа.
				|Проверьте настройки автоматического создания документов или попробуйте создать документ вручную.'"),
			КонтекстОтражения.ДокументЭДО);
		
		ДобавитьОшибкуКонтекстаОтражения(КонтекстОтражения, ОписаниеПроблемы,
			КонтекстОтражения.ДокументЭДО, Истина);
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ДанныеДляОтражения.Папка)
		И ПолучитьФункциональнуюОпцию("ИспользоватьПапкиДокументов") Тогда
		ОписаниеПроблемы = СтрШаблон(
			НСтр("ru = 'При автоматическом создании документа 1С:Документооборот по входящему ЭДО %1 не удалось определить папку документа.
				|Проверьте настройки автоматического создания документов или попробуйте создать документ вручную.'"),
			КонтекстОтражения.ДокументЭДО);
		
		ДобавитьОшибкуКонтекстаОтражения(КонтекстОтражения, ОписаниеПроблемы,
			КонтекстОтражения.ДокументЭДО, Истина);
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ДанныеДляОтражения.ГрифДоступа)
		И ПолучитьФункциональнуюОпцию("ИспользоватьГрифыДоступа") Тогда
		ОписаниеПроблемы = СтрШаблон(
			НСтр("ru = 'При автоматическом создании документа 1С:Документооборот по входящему ЭДО %1 не удалось определить гриф доступа документа.
				|Проверьте настройки автоматического создания документов или попробуйте создать документ вручную.'"),
			КонтекстОтражения.ДокументЭДО);
		
		ДобавитьОшибкуКонтекстаОтражения(КонтекстОтражения, ОписаниеПроблемы,
			КонтекстОтражения.ДокументЭДО, Истина);
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ДанныеДляОтражения.ВопросДеятельности)
		И ПолучитьФункциональнуюОпцию("ИспользоватьВопросыДеятельности") Тогда
		ОписаниеПроблемы = СтрШаблон(
			НСтр("ru = 'При автоматическом создании документа 1С:Документооборот по входящему ЭДО %1 не удалось определить вопрос деятельности документа.
				|Проверьте настройки автоматического создания документов или попробуйте создать документ вручную.'"),
			КонтекстОтражения.ДокументЭДО);
		
		ДобавитьОшибкуКонтекстаОтражения(КонтекстОтражения, ОписаниеПроблемы,
			КонтекстОтражения.ДокументЭДО, Истина);
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

Функция ГрифДоступаПоДаннымДляЗаполнения(ДанныеДляЗаполнения)
	
	ПравилаОтраженияВДО = ДанныеДляЗаполнения.ПравилаОтраженияВДО;
	ДанныеШаблонаПравилОтражения = ДанныеДляЗаполнения.ДанныеШаблонаПравилОтражения;
	
	Если ЗначениеЗаполнено(ПравилаОтраженияВДО.ГрифДоступа) Тогда
		Возврат ПравилаОтраженияВДО.ГрифДоступа;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ДанныеШаблонаПравилОтражения.ГрифДоступа) Тогда
		Возврат ДанныеШаблонаПравилОтражения.ГрифДоступа;
	КонецЕсли;
	
	ГрифДоступаПоУмолчанию = Константы.ГрифДоступаПоУмолчанию.Получить();
	
	Если ЗначениеЗаполнено(ГрифДоступаПоУмолчанию) Тогда
		Возврат ГрифДоступаПоУмолчанию;
	КонецЕсли;
	
	Возврат Справочники.ГрифыДоступа.ПустаяСсылка();
	
КонецФункции

Функция ВопросДеятельностиПоДаннымДляЗаполнения(ДанныеДляЗаполнения)
	
	ПравилаОтраженияВДО = ДанныеДляЗаполнения.ПравилаОтраженияВДО;
	ДанныеШаблонаПравилОтражения = ДанныеДляЗаполнения.ДанныеШаблонаПравилОтражения;
	
	Если ЗначениеЗаполнено(ПравилаОтраженияВДО.ВопросДеятельности) Тогда
		Возврат ПравилаОтраженияВДО.ВопросДеятельности;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ДанныеШаблонаПравилОтражения.ВопросДеятельности) Тогда
		Возврат ДанныеШаблонаПравилОтражения.ВопросДеятельности;
	КонецЕсли;
	
	Возврат Справочники.ВопросыДеятельности.ПустаяСсылка();
	
КонецФункции

Функция ПапкаПоДаннымДляЗаполнения(ДанныеДляЗаполнения)
	
	ПравилаОтраженияВДО = ДанныеДляЗаполнения.ПравилаОтраженияВДО;
	ДанныеШаблонаПравилОтражения = ДанныеДляЗаполнения.ДанныеШаблонаПравилОтражения;
	
	Если ЗначениеЗаполнено(ПравилаОтраженияВДО.Папка) Тогда
		Возврат ПравилаОтраженияВДО.Папка;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ДанныеШаблонаПравилОтражения.Папка) Тогда
		Возврат ДанныеШаблонаПравилОтражения.Папка;
	КонецЕсли;
	
	Возврат Справочники.ПапкиДокументов.ПустаяСсылка();
	
КонецФункции

Функция СуммаДокументаПоДаннымДляЗаполнения(ДанныеДляЗаполнения)
	
	ДанныеБЭД = ДанныеДляЗаполнения.ДанныеДокументооборотаБЭД;
	СтруктураРазбораФайлаЭДО = ДанныеДляЗаполнения.СтруктураРазбораФайлаЭДО;
	
	Если СтруктураРазбораФайлаЭДО <> Неопределено Тогда
		
		Если СтруктураРазбораФайлаЭДО.Свойство("СуммаДокумента")
			И ЗначениеЗаполнено(СтруктураРазбораФайлаЭДО.СуммаДокумента) Тогда
			
			Возврат СтруктураРазбораФайлаЭДО.СуммаДокумента;
		КонецЕсли;
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ДанныеБЭД.СуммаДокумента) Тогда
		Возврат ДанныеБЭД.СуммаДокумента;
	КонецЕсли;
	
	Возврат 0;
	
КонецФункции

Функция ВалютаДокументаПоДаннымДляЗаполнения(ДанныеДляЗаполнения)
	
	СтруктураРазбораФайлаЭДО = ДанныеДляЗаполнения.СтруктураРазбораФайлаЭДО;
	
	Если СтруктураРазбораФайлаЭДО <> Неопределено Тогда
		
		Если СтруктураРазбораФайлаЭДО.Свойство("Валюта")
			И ЗначениеЗаполнено(СтруктураРазбораФайлаЭДО.Валюта) Тогда
			
			Возврат СтруктураРазбораФайлаЭДО.Валюта;
		КонецЕсли;
		
	КонецЕсли;
	
	Валюта = Константы.ОсновнаяВалюта.Получить();
	
	Если ЗначениеЗаполнено(Валюта) Тогда
		Возврат Валюта;
	КонецЕсли;
	
	Возврат Справочники.Валюты.ПустаяСсылка();
	
КонецФункции

Функция ОтветственныйПоДаннымДляЗаполнения(ДанныеДляЗаполнения)
	
	ПравилаОтраженияВДО = ДанныеДляЗаполнения.ПравилаОтраженияВДО;
	ДанныеШаблонаПравилОтражения = ДанныеДляЗаполнения.ДанныеШаблонаПравилОтражения;
	
	Если ЗначениеЗаполнено(ПравилаОтраженияВДО.Ответственный) Тогда
		Возврат ПравилаОтраженияВДО.Ответственный;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ДанныеШаблонаПравилОтражения.Ответственный) Тогда
		Возврат ДанныеШаблонаПравилОтражения.Ответственный;
	КонецЕсли;
	
	Возврат Сотрудники.ОсновнойСотрудник();
	
КонецФункции

#КонецОбласти

#Область ОперацииПослеСозданияДокументаДО

Процедура СоздатьФайлыОтраженияВходящегоЭДО(КонтекстОтражения)
	
	ДокументДО = КонтекстОтражения.ДокументДО;
	ДокументЭДО = КонтекстОтражения.ДокументЭДО;
	
	Если КонтекстОтражения.ФайлыДляСоздания.Количество() = 0 Тогда
		ОписаниеПроблемы = СтрШаблон(
			НСтр("ru = 'При отражении входящего документа ЭДО %1 не переданы файлы для отражения в 1С:Документооборот.'"),
			ДокументЭДО);
		
		ДобавитьОшибкуКонтекстаОтражения(КонтекстОтражения, ОписаниеПроблемы, ДокументЭДО, Истина);
		Возврат;
	КонецЕсли;
	
	РолиФайловПоПриоритету = РолиФайловДляОтраженияВходящегоЭДО(КонтекстОтражения);
	ТекущийИндексРоли = 0;
	
	Для Каждого ФайлКСозданию Из КонтекстОтражения.ФайлыДляСоздания Цикл
		
		ЗаполнитьРольФайлаПриОтраженииЭДО(ФайлКСозданию, РолиФайловПоПриоритету, ТекущийИндексРоли);
		
		Попытка
			ФайлДО = РаботаСФайламиВызовСервера.СоздатьФайлСВерсией(ДокументДО, ФайлКСозданию.СведенияОФайле);
		Исключение
			ОписаниеПроблемы =
				НСтр("ru = 'Не удалось записать файл 1С:Документооборот по причине:'")
				+ Символы.ПС + ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			
			ДобавитьОшибкуКонтекстаОтражения(КонтекстОтражения, ОписаниеПроблемы, ДокументЭДО, Истина);
			Возврат;
		КонецПопытки;
		
		ВерсияФайлаДО = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ФайлДО, "ТекущаяВерсия");
		
		ЗанестиИнформациюОПодписяхФайлаДО(ФайлДО, ФайлКСозданию.ЭлектронныеПодписи, КонтекстОтражения);
		Если КонтекстОтражения.Отказ Тогда
			Возврат;
		КонецЕсли;
		
		Попытка
			ИнтеграцияЭДО.УстановитьСвязьЭлектронногоДокументаСОбъектомУчета(ДокументЭДО, ВерсияФайлаДО);
		Исключение
			ОписаниеПроблемы =
				НСтр("ru = 'Не удалось установить связь документа ЭДО с документом 1С:Документооборот по причине:'")
				+ Символы.ПС + ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			
			ДобавитьОшибкуКонтекстаОтражения(КонтекстОтражения, ОписаниеПроблемы, ДокументЭДО, Истина);
			Возврат;
		КонецПопытки;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗанестиИнформациюОПодписяхФайлаДО(ФайлДО, ЭлектронныеПодписи, КонтекстОтражения)
	
	ДокументЭДО = КонтекстОтражения.ДокументЭДО;
	
	Если ЭлектронныеПодписи.Количество() > 0 Тогда
		
		ИдентификаторыПодписей = Новый Массив;
		Для Каждого СвойстваПодписи Из ЭлектронныеПодписи Цикл
			ИдентификаторыПодписей.Добавить(СвойстваПодписи.ИдентификаторПодписи);
		КонецЦикла;
		
		ДанныеДоверенностей = РаботаСЭП.ДанныеДоверенностейПодписей(ИдентификаторыПодписей);
		
		СведенияОПодписях = Новый Массив;
		
		Для Каждого СвойстваПодписи Из ЭлектронныеПодписи Цикл
			ДанныеДоверенности = ДанныеДоверенностей[СвойстваПодписи.ИдентификаторПодписи];
			
			Если ТипЗнч(ДанныеДоверенности) = Тип("Структура")
				И ЗначениеЗаполнено(ДанныеДоверенности.Доверенность) Тогда
				
				СвойстваПодписи.Вставить("Доверенность", ДанныеДоверенности.Доверенность);
			КонецЕсли;
			
			СведенияОПодписях.Добавить(
				Новый Структура("ПодписанныйОбъект, СвойстваПодписи", ФайлДО, СвойстваПодписи));
		КонецЦикла;
		
		Попытка
			РаботаСЭП.ЗанестиИнформациюОПодписях(СведенияОПодписях);
		Исключение
			ОписаниеПроблемы =
				НСтр("ru = 'Не удалось записать электронные подписи файла 1С:Документооборот по причине:'")
				+ Символы.ПС + ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			
			ДобавитьОшибкуКонтекстаОтражения(КонтекстОтражения, ОписаниеПроблемы, ДокументЭДО, Истина);
			Возврат;
		КонецПопытки;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ОтразитьСозданиеДокументаВоВходящемПакетеЭДО(КонтекстОтражения)
	
	ДокументДО = КонтекстОтражения.ДокументДО;
	ДокументЭДО = КонтекстОтражения.ДокументЭДО;
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапросаПакетовЭДОВходящегоДокумента();
	Запрос.УстановитьПараметр("ЭлектронныйДокумент", ДокументЭДО);
	
	РезультатыЗапроса = Запрос.ВыполнитьПакет();
	
	КолвоРезультатов =РезультатыЗапроса.Количество();
	ВыборкаПоСуществующимПакетам = РезультатыЗапроса[КолвоРезультатов - 2].Выбрать();
	ВыборкаДанныхПакетаБЭД = РезультатыЗапроса[КолвоРезультатов - 1].Выбрать();
	
	// Если имеются данные по существующему пакету, то просто добавляем документ к нему.
	Если ВыборкаПоСуществующимПакетам.Следующий() Тогда
		
		Попытка
			ДобавитьДокуметДОКВходящемуПакетуПриОтражении(
				ДокументДО, ВыборкаПоСуществующимПакетам.ИдентификаторПакета);
		Исключение
			ОписаниеПроблемы = НСтр("ru = 'Не удалось добавить созданы документ к входящему пакету ЭДО по причине:'")
				+ Символы.ПС + ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			
			ДобавитьОшибкуКонтекстаОтражения(КонтекстОтражения, ОписаниеПроблемы, ДокументЭДО, Истина);
		КонецПопытки;
		
		Возврат;
		
	КонецЕсли;
	
	// Если данных по существующему пакету нет, но имеются данные по пакету БЭД,
	//  то пакет нужно создать и добавить к нему документ
	Если ВыборкаДанныхПакетаБЭД.Следующий() Тогда
		
		Попытка
			ИДПакетаДО = СоздатьВходящийПакетЭДО(
				ВыборкаДанныхПакетаБЭД.Организация,
				ВыборкаДанныхПакетаБЭД.Контрагент,
				ВыборкаДанныхПакетаБЭД.ИдентификаторПакета);
			ДобавитьДокуметДОКВходящемуПакетуПриОтражении(ДокументДО, ИДПакетаДО);
		Исключение
			ОписаниеПроблемы = НСтр("ru = 'Не удалось создать входящий пакет ЭДО по причине:'")
				+ Символы.ПС + ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			
			ДобавитьОшибкуКонтекстаОтражения(КонтекстОтражения, ОписаниеПроблемы, ДокументЭДО, Истина);
		КонецПопытки;
		
		Возврат;
		
	КонецЕсли;
	
КонецПроцедуры

Функция ТекстЗапросаПакетовЭДОВходящегоДокумента()
	
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	СоставПакетовДокументовЭДО.ИдентификаторПакета,
		|	ПакетыДокументовЭДО.Организация,
		|	ПакетыДокументовЭДО.Контрагент
		|ПОМЕСТИТЬ ДанныеПакетаБЭД
		|ИЗ
		|	РегистрСведений.СоставПакетовДокументовЭДО КАК СоставПакетовДокументовЭДО
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ПакетыДокументовЭДО КАК ПакетыДокументовЭДО
		|		ПО СоставПакетовДокументовЭДО.ИдентификаторПакета = СоставПакетовДокументовЭДО.ИдентификаторПакета
		|ГДЕ
		|	СоставПакетовДокументовЭДО.ЭлектронныйДокумент = &ЭлектронныйДокумент
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДанныеПакетаБЭД.ИдентификаторПакета КАК ИдентификаторПакетаБЭД,
		|	ПакетыЭДОДокументооборот.ИдентификаторПакета КАК ИдентификаторПакета,
		|	ПакетыЭДОДокументооборот.Организация КАК Организация,
		|	ПакетыЭДОДокументооборот.Контрагент КАК Контрагент
		|ИЗ
		|	ДанныеПакетаБЭД КАК ДанныеПакетаБЭД
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ПакетыЭДОДокументооборот КАК ПакетыЭДОДокументооборот
		|		ПО ДанныеПакетаБЭД.ИдентификаторПакета = ПакетыЭДОДокументооборот.ИдентификаторПакетаБЭД
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДанныеПакетаБЭД.ИдентификаторПакета,
		|	ДанныеПакетаБЭД.Организация,
		|	ДанныеПакетаБЭД.Контрагент
		|ИЗ
		|	ДанныеПакетаБЭД КАК ДанныеПакетаБЭД";
	Возврат ТекстЗапроса;
	
КонецФункции

Процедура ДобавитьДокуметДОКВходящемуПакетуПриОтражении(ДокументДО, ИдентификаторПакета)
	
	Запись = РегистрыСведений.СоставПакетовЭДОДокументооборот.СоздатьМенеджерЗаписи();
	
	Запись.Документ = ДокументДО;
	Запись.ИдентификаторПакета = ИдентификаторПакета;
	
	Запись.Записать();
	
КонецПроцедуры

Функция СоздатьВходящийПакетЭДО(Организация, Контрагент, ИдентификаторПакетаБЭД)
	
	ИДПакетаДО = Новый УникальныйИдентификатор;
	
	Запись = РегистрыСведений.ПакетыЭДОДокументооборот.СоздатьМенеджерЗаписи();
	
	Запись.ИдентификаторПакета = ИДПакетаДО;
	Запись.Организация = Организация;
	Запись.Контрагент = Контрагент;
	Запись.ИдентификаторПакетаБЭД = ИдентификаторПакетаБЭД;
	Запись.Направление = Перечисления.НаправленияЭДО.Входящий;
	
	Запись.Записать();
	
	Возврат ИДПакетаДО;
	
КонецФункции

Процедура ОтметитьОтражениеВходящегоЭДОВДО(КонтекстОтражения)
	
	ДокументЭДО = КонтекстОтражения.ДокументЭДО;
	ДокументДО = КонтекстОтражения.ДокументДО;
	
	Попытка
		РегистрыСведений.ДокументыЭДОКСозданиюВДО.УдалитьДокументИзСпискаКОтражению(ДокументЭДО);
	Исключение
		ОписаниеПроблемы = НСтр("ru = 'Не удалось удалить документ ЭДО из списка к отражению в 1С:Документооборот по причине:'")
			+ Символы.ПС + ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		
		ДобавитьОшибкуКонтекстаОтражения(КонтекстОтражения, ОписаниеПроблемы, ДокументЭДО, Истина);
		Возврат;
	КонецПопытки;
	
	Попытка
		РегистрыСведений.ОшибкиЭДОКИсправлению.УдалитьЗаписьОбОшибке(ДокументЭДО);
	Исключение
		ОписаниеПроблемы = НСтр("ru = 'Не удалось удалить документ ЭДО из списка ошибок к исправлению по причине:'")
			+ Символы.ПС + ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		
		ДобавитьОшибкуКонтекстаОтражения(КонтекстОтражения, ОписаниеПроблемы, ДокументЭДО, Истина);
		Возврат;
	КонецПопытки;
	
	Если КонтекстОтражения.АвтоматическоеВыполнение Тогда
		Ответственный = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДокументДО, "Ответственный");
		Если Не ЗначениеЗаполнено(Ответственный) Тогда
			
			Попытка
				РегистрыСведений.ПроверкаЗагруженныхДанных.УстановитьПризнакПроверки(ДокументДО, Ложь);
			Исключение
				ОписаниеПроблемы =
					НСтр("ru = 'Не удалось установить признак проверки созданного документа 1С:Документооборот по причине:'")
					+ Символы.ПС + ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
				
				ДобавитьОшибкуКонтекстаОтражения(КонтекстОтражения, ОписаниеПроблемы, ДокументЭДО, Истина);
				Возврат;
			КонецПопытки;
			
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// Возвращает массив ролей файлов отсортированных по приоритету заполнения
// 
// Параметры:
//  КонтекстОтражения - см. ОбменСКонтрагентамиДОСлужебныйКлиентСервер.НовыйКонтекстОтраженияДокументовВДО
// 
// Возвращаемое значение:
//  Массив из см. НовоеОписаниеРолиПоПриоритетуЗаполнения
//
Функция РолиФайловДляОтраженияВходящегоЭДО(КонтекстОтражения)
	
	РолиФайловПоПриоритетуЗапролнения = Новый Массив;
	
	ДокументДО = КонтекстОтражения.ДокументДО;
	
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	ВидыДокументовРолиФайлов.Роль,
		|	ВидыДокументовРолиФайлов.Обязательная,
		|	ВидыДокументовРолиФайлов.ТолькоОдинФайл,
		|	ВидыДокументовРолиФайлов.ФайлЭлектронногоДокумента
		|ИЗ
		|	Справочник.%ИмяСправочникаДокументаДО% КАК ДокументыДО
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ВидыДокументов.РолиФайлов КАК ВидыДокументовРолиФайлов
		|		ПО ВидыДокументовРолиФайлов.Ссылка = ДокументыДО.ВидДокумента
		|ГДЕ
		|	ДокументыДО.Ссылка = &ДокументДО";
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
		"%ИмяСправочникаДокументаДО%",
		ОбменСКонтрагентамиДОСлужебныйКлиентСервер.ИмяСправочникаДокументовДО());
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("ДокументДО", ДокументДО);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		
		ПустаяРоль = НовоеОписаниеРолиПоПриоритетуЗаполнения();
		РолиФайловПоПриоритетуЗапролнения.Добавить(ПустаяРоль);
		
		Возврат РолиФайловПоПриоритетуЗапролнения;
		
	КонецЕсли;
	
	РольФайлаЭлектронногоДокумента = Справочники.РолиФайлов.ПустаяСсылка();
	РолиОбязательныеСЕдинственнымФайлом = Новый Массив;
	РолиОбязательныеСМножествомФайлов = Новый Массив;
	НеобязательныеРоли = Новый Массив;
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		Если Выборка.ФайлЭлектронногоДокумента Тогда
			РольФайлаЭлектронногоДокумента = Выборка.Роль;
		ИначеЕсли Выборка.Обязательная И Выборка.ТолькоОдинФайл Тогда
			РолиОбязательныеСЕдинственнымФайлом.Добавить(Выборка.Роль);
		ИначеЕсли Выборка.Обязательная Тогда
			РолиОбязательныеСМножествомФайлов.Добавить(Выборка.Роль);
		Иначе
			НеобязательныеРоли.Добавить(Выборка.Роль);
		КонецЕсли;
		
	КонецЦикла;
	
	Если ЗначениеЗаполнено(РольФайлаЭлектронногоДокумента) Тогда
		
		ОписаниеРоли = НовоеОписаниеРолиПоПриоритетуЗаполнения();
		ОписаниеРоли.РольФайла = РольФайлаЭлектронногоДокумента;
		ОписаниеРоли.МожноДобавитьМногоФайлов = Ложь;
		
		РолиФайловПоПриоритетуЗапролнения.Добавить(ОписаниеРоли);
		
	КонецЕсли;
	
	Для Каждого Роль Из РолиОбязательныеСЕдинственнымФайлом Цикл
		
		ОписаниеРоли = НовоеОписаниеРолиПоПриоритетуЗаполнения();
		ОписаниеРоли.РольФайла = Роль;
		ОписаниеРоли.МожноДобавитьМногоФайлов = Ложь;
		
		РолиФайловПоПриоритетуЗапролнения.Добавить(ОписаниеРоли);
		
	КонецЦикла;
	
	Для Каждого Роль Из РолиОбязательныеСМножествомФайлов Цикл
		
		ОписаниеРоли = НовоеОписаниеРолиПоПриоритетуЗаполнения();
		ОписаниеРоли.РольФайла = Роль;
		ОписаниеРоли.МожноДобавитьМногоФайлов = Ложь;
		
		РолиФайловПоПриоритетуЗапролнения.Добавить(ОписаниеРоли);
		
	КонецЦикла;
	
	Если НеобязательныеРоли.Количество() > 0 Тогда
		
		ОписаниеРоли = НовоеОписаниеРолиПоПриоритетуЗаполнения();
		ОписаниеРоли.РольФайла = НеобязательныеРоли[0];
		ОписаниеРоли.МожноДобавитьМногоФайлов = Истина;
		
		РолиФайловПоПриоритетуЗапролнения.Добавить(ОписаниеРоли);
		
	ИначеЕсли РолиОбязательныеСМножествомФайлов.Количество() > 0 Тогда
		
		ОписаниеРоли = НовоеОписаниеРолиПоПриоритетуЗаполнения();
		ОписаниеРоли.РольФайла = НеобязательныеРоли[0];
		ОписаниеРоли.МожноДобавитьМногоФайлов = Истина;
		
		РолиФайловПоПриоритетуЗапролнения.Добавить(ОписаниеРоли);
		
	Иначе
		
		ОписаниеРоли = НовоеОписаниеРолиПоПриоритетуЗаполнения();
		РолиФайловПоПриоритетуЗапролнения.Добавить(ОписаниеРоли);
		
	КонецЕсли;
	
	Возврат РолиФайловПоПриоритетуЗапролнения;
	
КонецФункции

// Новое описание роли по приоритету заполнения.
// 
// Возвращаемое значение:
//  Структура - Новое описание роли по приоритету заполнения:
//    * РольФайла - СправочникСсылка.РолиФайлов
//    * МожноДобавитьМногоФайлов - Булево
Функция НовоеОписаниеРолиПоПриоритетуЗаполнения()
	
	ОписаниеРоли = Новый Структура;
	ОписаниеРоли.Вставить("РольФайла", Справочники.РолиФайлов.ПустаяСсылка());
	ОписаниеРоли.Вставить("МожноДобавитьМногоФайлов", Истина);
	
	Возврат ОписаниеРоли;
	
КонецФункции

// Заполняет роль файла при отражении входящего ЭДО
// 
// Параметры:
//  ФайлКСозданию см. ОбменСКонтрагентамиДОСлужебныйКлиентСервер.НовоеОписаниеФайлаДОДляСоздания
//  РолиФайловДляОтраженияВходящегоЭДО см. РолиФайловДляОтраженияВходящегоЭДО
//  ИндексПриоритетаРоли - Число
//
Процедура ЗаполнитьРольФайлаПриОтраженииЭДО(ФайлКСозданию, РолиФайловДляОтраженияВходящегоЭДО, ИндексПриоритетаРоли)
	
	Если ИндексПриоритетаРоли < 0
		Или ИндексПриоритетаРоли > РолиФайловДляОтраженияВходящегоЭДО.ВГраница() Тогда
			
		Возврат;
	КонецЕсли;
	
	ЗаполненнаяРоль = ФайлКСозданию.СведенияОФайле.РольФайла;
	ТекущееОписаниеРоли = РолиФайловДляОтраженияВходящегоЭДО[ИндексПриоритетаРоли];
	
	Если ЗначениеЗаполнено(ЗаполненнаяРоль) Тогда
		
		Если ЗаполненнаяРоль = ТекущееОписаниеРоли.РольФайла
			И Не ТекущееОписаниеРоли.МожноДобавитьМногоФайлов Тогда
			
			ИндексПриоритетаРоли = ИндексПриоритетаРоли + 1;
		КонецЕсли;
		
		Возврат;
		
	КонецЕсли;
	
	ФайлКСозданию.СведенияОФайле.РольФайла = ТекущееОписаниеРоли.РольФайла;
	
	Если Не ТекущееОписаниеРоли.МожноДобавитьМногоФайлов Тогда
		ИндексПриоритетаРоли = ИндексПриоритетаРоли + 1;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

Процедура ДобавитьОшибкуКонтекстаОтражения(КонтекстОтражения, ОписаниеПроблемы, ПредметОшибки, Критическая)
	
	Ошибка = ОбменСКонтрагентамиДОСлужебныйКлиентСервер.НоваяОшибкаКонтекстаОтражения();
	
	Ошибка.Описание = ОписаниеПроблемы;
	Ошибка.ПредметОшибки = ПредметОшибки;
	Ошибка.Критическая = Критическая;
	
	КонтекстОтражения.Ошибки.Добавить(Ошибка);
	
	Если Критическая = Истина Тогда
		КонтекстОтражения.Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ПереопределениеБЭД

Процедура ЗаполнитьДанныеРуководителя(Организация, ДанныеОрганизации)
	
	Если Не ЗначениеЗаполнено(Организация) Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ФизическиеЛица.Ссылка,
		|	ФизическиеЛица.Наименование,
		|	ФизическиеЛица.ДатаРождения
		|ИЗ
		|	Справочник.Сотрудники КАК Сотрудники
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ОтветственныеЛицаОрганизаций.СрезПоследних КАК
		|			ОтветственныеЛицаОрганизацийСрезПоследних
		|		ПО Сотрудники.Ссылка = ОтветственныеЛицаОрганизацийСрезПоследних.Сотрудник
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ФизическиеЛица КАК ФизическиеЛица
		|		ПО Сотрудники.Владелец = ФизическиеЛица.Ссылка
		|ГДЕ
		|	ОтветственныеЛицаОрганизацийСрезПоследних.Организация = &Организация
		|	И ОтветственныеЛицаОрганизацийСрезПоследних.ОтветственноеЛицо = &ОтветственноеЛицо";
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("ОтветственноеЛицо", Перечисления.ОтветственныеЛицаОрганизаций.РуководительОрганизации);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Не Выборка.Следующий() Тогда
		Возврат;
	КонецЕсли;
	
	ЧастиИмени = ФизическиеЛицаКлиентСервер.ЧастиИмени(Выборка.Наименование);
	ДанныеОрганизации.Фамилия = ЧастиИмени.Фамилия;
	ДанныеОрганизации.Имя = ЧастиИмени.Имя;
	ДанныеОрганизации.Отчество = ЧастиИмени.Отчество;
	
	ДанныеОрганизации.РуководительФизЛицо = Выборка.Ссылка;
	ДанныеОрганизации.ДатаРождения = Выборка.ДатаРождения;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
