////////////////////////////////////////////////////////////////////////////////
// Модуль для переопределения особенностей конфигурации для холдинга (сервер).
//
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Проверяет, является ли этот узел центральным.
// 
// Параметры:
//  СтандартнаяОбработка - Булево.
// 
// Возвращаемое значение:
//  Булево - Это центральный узел
//
Функция ЭтоЦентральныйУзел(СтандартнаяОбработка) Экспорт
	
	СтандартнаяОбработка = Ложь;
	
	ЭтоЦентральныйУзел = КОДСервер.ЭтоЦентральныйУзел();
	
	Возврат ЭтоЦентральныйУзел;
	
КонецФункции

// Проверяет, принадлежит ли объект этому узлу.
// 
// Параметры:
//  Объект - СправочникОбъект,
//			 ДокументОбъект,
//			 БизнесПроцессОбъект,
//			 ЗадачаОбъект - Объект, который необходимо проверить.
// СтандартнаяОбработка - Булево - Признак стандартной обработки.
// 
// Возвращаемое значение:
//  Булево - Объект принадлежит этому узлу.
//
Функция ОбъектЭтогоУзла(Объект, СтандартнаяОбработка) Экспорт
	
	СтандартнаяОбработка = Ложь;
	
	ОбъектЭтогоУзла = КОДСервер.ОбъектЭтогоУзла(Объект);
	
	Возврат ОбъектЭтогоУзла;

КонецФункции

// Проверяет, что изменение объекта выполняется процессе загрузки .
//
// Параметры:
//	Объект - КонстантаМенеджерЗначения -
//		   - СправочникОбъект -
//		   - ДокументОбъект -
//		   - БизнесПроцессОбъект -
//		   - ЗадачаОбъект -
//		   - ПланВидовХарактеристикОбъект -
//		   - РегистрСведенийНаборЗаписей -
//		   - РегистрНакопленияНаборЗаписей - Объект, который необходимо проверить.
//
// Возвращаемое значение:
//	Булево - Истина, если выполняется загрузка объекта.
//
Функция ЭтоЗагрузкаОбъекта(Объект) Экспорт
	
	Возврат КОДСервер.ЭтоЗагрузкаОбъекта(Объект);
	
КонецФункции

// Увеличивает версию данных.
// 
// Параметры:
//  СтараяВерсияДанных - Число.
//  СтандартнаяОбработка - Булево.
// 
// Возвращаемое значение:
//  Число - Новая версия данных.
//
Функция УвеличитьВерсиюДанных(СтараяВерсияДанных, СтандартнаяОбработка) Экспорт
	
	СтандартнаяОбработка = Ложь;
	
	Если КОДСервер.ЭтоЦентральныйУзел() Тогда
		СдвигВерсии = 1000;
	Иначе
		ГенераторСлучайныхЧисел = Новый ГенераторСлучайныхЧисел;
		СдвигВерсии = ГенераторСлучайныхЧисел.СлучайноеЧисло(1, 999);
	КонецЕсли;
	
	НоваяВерсияДанных = СтараяВерсияДанных + СдвигВерсии;
	
	Возврат НоваяВерсияДанных;
	
КонецФункции

// Проверяет, принадлежит ли объект этому узлу и можно ли его обработку выполнить в очереди заданий.
// 
// Параметры:
//  Объект - СправочникОбъект,
//  		 СправочникСсылка,
//			 ДокументОбъект, ДокументСсылка
//			 БизнесПроцессОбъект, БизнесПроцессСсылка
//			 ЗадачаОбъект, ЗадачаСсылка - Объект, который необходимо проверить.
// 
// Возвращаемое значение:
//  Булево - Истина, если объект не принадлежит этому узлу, его можно обработать в очереди заданий
//
Функция ОбъектМожноОбработатьВОчередиЗаданий(Знач Объект) Экспорт
	
	Если Не (ОбщегоНазначенияДокументооборот.ЭтоДокументооборотХолдинга() И ПолучитьФункциональнуюОпцию(
		"ИспользоватьКОД") И ПолучитьФункциональнуюОпцию("ИспользоватьЗадания")) Тогда
			Возврат Ложь;
	КонецЕсли;
	
	ОбъектСсылка = Неопределено;
	ТекущийОбъект = Неопределено;
	
	Если ОбщегоНазначения.ЭтоСсылка(ТипЗнч(Объект)) Тогда
		ОбъектСсылка = Объект;
		ТекущийОбъект = Объект.ПолучитьОбъект();
	Иначе
		ОбъектСсылка = Объект.Ссылка;
		ТекущийОбъект = Объект;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ОбъектСсылка) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ЭтоПроцессОбработки = Ложь;
	Если ОбщегоНазначения.ЭтоБизнесПроцесс(ОбъектСсылка.Метаданные()) Тогда
		ЭтоПроцессОбработки = РаботаСБизнесПроцессамиХолдинг.ЭтоПроцессОбработки(ОбъектСсылка);
	КонецЕсли;
	
	Если ОбщегоНазначения.ЭтоЗадача(ОбъектСсылка.Метаданные()) Тогда
		ТекущийОбъект = ТекущийОбъект.БизнесПроцесс.ПолучитьОбъект();
	КонецЕсли;
	
	Возврат Не ОбщегоНазначенияДокументооборот.ОбъектЭтогоУзла(ТекущийОбъект) И Не ЭтоПроцессОбработки;
	
КонецФункции	

#КонецОбласти