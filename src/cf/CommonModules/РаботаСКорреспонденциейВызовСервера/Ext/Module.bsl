
#Область ПрограммныйИнтерфейс

// Возвращает реквизиты документа "В ответ на".
// 
// Параметры:
//  Документ - СправочникСсылка.ДокументыПредприятия - ссылка на выбранный документ.
// 
// Возвращаемое значение:
//  ДанныеВОтветНа - Структура - реквизиты документа "В ответ на".
//
Функция ДанныеВОтветНа(Знач Документ) Экспорт 
	
	ДанныеВОтветНа = Новый Структура;
	ДанныеВОтветНа.Вставить("РегистрационныйНомер", "");
	ДанныеВОтветНа.Вставить("ДатаРегистрации", Неопределено);
	ДанныеВОтветНа.Вставить("Заголовок", "");
	ДанныеВОтветНа.Вставить("ВопросДеятельности", Неопределено);
	ДанныеВОтветНа.Вставить("Проект", Неопределено);
	ДанныеВОтветНа.Вставить("Контрагенты", Неопределено);
	ДанныеВОтветНа.Вставить("Контрагент", Неопределено);
	ДанныеВОтветНа.Вставить("Адресат", Неопределено);
	ДанныеВОтветНа.Вставить("НомерКонтрагента", "");
	ДанныеВОтветНа.Вставить("ДатаКонтрагента", Неопределено);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ДокументыПредприятия.Ссылка,
		|	ДокументыПредприятия.Заголовок,
		|	ДокументыПредприятия.ДатаРегистрации,
		|	ДокументыПредприятия.РегистрационныйНомер,
		|	Корреспонденция.ДатаКонтрагента,
		|	Корреспонденция.НомерКонтрагента,
		|	ДокументыПредприятия.ВопросДеятельности,
		|	ДокументыПредприятия.Проект,
		|	ДокументыПредприятия.Контрагенты
		|ИЗ
		|	Документ.Корреспонденция КАК Корреспонденция
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ДокументыПредприятия КАК ДокументыПредприятия
		|		ПО Корреспонденция.Основание = ДокументыПредприятия.Ссылка
		|ГДЕ
		|	ДокументыПредприятия.Ссылка = &Документ";
	Запрос.Параметры.Вставить("Документ", Документ);
	УстановитьПривилегированныйРежим(Истина);
	
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		Возврат ДанныеВОтветНа;
	КонецЕсли;
	
	ВыборкаДокумент = Результат.Выбрать();
	ВыборкаДокумент.Следующий();
	ЗаполнитьЗначенияСвойств(ДанныеВОтветНа, ВыборкаДокумент,, "Контрагенты");
	
	ВыборкаКонтрагент = ВыборкаДокумент.Контрагенты.Выбрать();
	Пока ВыборкаКонтрагент.Следующий() Цикл 
		ДанныеВОтветНа.Вставить("Контрагент", ВыборкаКонтрагент.Контрагент);
		ДанныеВОтветНа.Вставить("Адресат", ВыборкаКонтрагент.КонтактноеЛицо);
		Прервать;
	КонецЦикла;

	Возврат ДанныеВОтветНа;
	
КонецФункции

// Возвращает список значений данных выбора при автоподборе текста.
// 
// Параметры:
//  ВОтветНаСтрока  - Строка - для поиска данных.
//  Контрагенты - Массив - контрагенты для поиска.
//  ТолькоИсходящаяКорреспонденция - Булево - признак выбора исходящей корреспонденции.
// 
// Возвращаемое значение:
//  СписокЗначений - Данные выбора в поле "В ответ на".
//
Функция ДанныеВыбораВОтветНа(Знач ВОтветНаСтрока, Контрагенты, ТолькоИсходящаяКорреспонденция = Ложь) Экспорт 
	
	ДанныеВыбора = Новый СписокЗначений;

	Если Лев(ВОтветНаСтрока, 1) = "№" Тогда
		ВОтветНаСтрока = СокрЛП(Прав(ВОтветНаСтрока, СтрДлина(ВОтветНаСтрока)-1));
	КонецЕсли;

	Если Не ЗначениеЗаполнено(ВОтветНаСтрока) Тогда
		Возврат ДанныеВыбора;
	КонецЕсли;
		
	// Заполнение "в ответ на". Поиск по номеру или наименованию
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ ПЕРВЫЕ 100
		|	ДокументыПредприятия.Ссылка КАК Ссылка,
		|	Корреспонденция.НомерКонтрагента,
		|	Корреспонденция.ДатаКонтрагента,
		|	ДокументыПредприятия.РегистрационныйНомер,
		|	ДокументыПредприятия.ДатаРегистрации,
		|	ДокументыПредприятия.Заголовок
		|ИЗ
		|	Справочник.ДокументыПредприятия КАК ДокументыПредприятия
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ВидыДокументов КАК ВидыДокументов
		|		ПО (ВидыДокументов.Ссылка = ДокументыПредприятия.ВидДокумента)
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ДокументыПредприятия.Контрагенты КАК ДокументыПредприятияКонтрагенты
		|		ПО (ДокументыПредприятияКонтрагенты.Ссылка = ДокументыПредприятия.Ссылка)
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.Корреспонденция КАК Корреспонденция
		|		ПО (Корреспонденция.Основание = ДокументыПредприятия.Ссылка)
		|ГДЕ
		|	(ДокументыПредприятия.РегистрационныйНомер ПОДОБНО &СтрокаПоиска
		|	ИЛИ ДокументыПредприятия.Заголовок ПОДОБНО &СтрокаПоиска
		|	ИЛИ Корреспонденция.НомерКонтрагента ПОДОБНО &СтрокаПоиска)
		|	И ВидыДокументов.ЯвляетсяИсходящейКорреспонденцией = &ТолькоИсходящаяКорреспонденция
		|	И ВидыДокументов.ЯвляетсяВходящейКорреспонденцией = &ТолькоВходящаяКорреспонденция
		|	И (НЕ &ОтборПоКонтрагентам
		|	ИЛИ ДокументыПредприятияКонтрагенты.Контрагент В (&Контрагенты))";
	
	Запрос.УстановитьПараметр("СтрокаПоиска", ВОтветНаСтрока + "%");
	Запрос.УстановитьПараметр("ОтборПоКонтрагентам", Контрагенты.Количество() > 0);
	Запрос.УстановитьПараметр("Контрагенты", Контрагенты);
	
	Если ТолькоИсходящаяКорреспонденция Тогда 
		Запрос.УстановитьПараметр("ТолькоИсходящаяКорреспонденция", Истина);
		Запрос.УстановитьПараметр("ТолькоВходящаяКорреспонденция", Ложь);
		
	Иначе
		Запрос.УстановитьПараметр("ТолькоИсходящаяКорреспонденция", Ложь);
		Запрос.УстановитьПараметр("ТолькоВходящаяКорреспонденция", Истина);
	КонецЕсли;
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ДанныеВыбора.Добавить(Выборка.Ссылка,
			РаботаСКорреспонденцией.ПредставлениеДокументаКорреспонденции(
				Выборка, НСтр("ru = '№ %1 от %2'"), ТолькоИсходящаяКорреспонденция));
	КонецЦикла;
	
	Возврат ДанныеВыбора;
	
КонецФункции

// Возвращает список значений данных выбора при автоподборе текста в поле предмета переписки.
// Ищет по рег. номеру или заголовку.
// 
// Параметры:
//  Текст  - Строка - строка для поиска данных.
// 
// Возвращаемое значение:
//  СписокЗначений - Данные выбора в поле "Предмет переписки".
//
Функция ДанныеВыбораПредметаПереписки(Знач Текст) Экспорт
	
	ДанныеВыбора = Новый СписокЗначений;

	Если Лев(Текст, 1) = "№" Тогда
		Текст = СокрЛП(Прав(Текст, СтрДлина(Текст)-1));
	КонецЕсли;

	Если Не ЗначениеЗаполнено(Текст)Тогда
		Возврат ДанныеВыбора;
	КонецЕсли;
		
	// Заполнение "в ответ на". Поиск по номеру или наименованию
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ДокументыПредприятия.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.ДокументыПредприятия КАК ДокументыПредприятия
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ВидыДокументов КАК ВидыДокументов
	|		ПО (ВидыДокументов.Ссылка = ДокументыПредприятия.ВидДокумента)
	|ГДЕ
	|	(ДокументыПредприятия.РегистрационныйНомер ПОДОБНО &СтрокаПоиска
	|			ИЛИ ДокументыПредприятия.Заголовок ПОДОБНО &СтрокаПоиска)
	|	И Не ВидыДокументов.ЯвляетсяИсходящейКорреспонденцией
	|	И Не ВидыДокументов.ЯвляетсяВходящейКорреспонденцией";
	
	Запрос.Параметры.Вставить("СтрокаПоиска", Текст + "%");
	
	Результат = Запрос.Выполнить();
	Если Не Результат.Пустой() Тогда 
		Выборка = Результат.Выбрать();
		Пока Выборка.Следующий() Цикл
			ДанныеВыбора.Добавить(Выборка.Ссылка);
		КонецЦикла;
	КонецЕсли;
	
	Возврат ДанныеВыбора;
	
КонецФункции

// Находит и возвращает ссылку на документ Корреспонденция, принадлежащий переданному владельцу
// Вернет Неопределено, если Корреспонденция не найдена
// 
// Параметры:
//  ДокументВладелец - СправочникСсылка.ДокументыПредприятия
// 
// Возвращаемое значение:
//  - ДокументСсылка.Корреспонденция, Неопределено
//
Функция КорреспонденцияДокумента(ДокументВладелец) Экспорт
	
	Возврат РаботаСКорреспонденцией.КорреспонденцияДокумента(ДокументВладелец);
	
КонецФункции

#КонецОбласти
