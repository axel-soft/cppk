// @strict-types


#Область ПрограммныйИнтерфейс

// Выполняется при определении адресатов самостоятельных данных для определения особого алгоритма.
//
// Параметры:
//   ТипДанныхОбмена - Структура - Описание типа данных КОД (см. КОДПовтИсп.ИдентификаторИТипыДанныхОбмена):
//     * ПолноеИмя - Строка - Полное имя объекта метаданных
//   КлючиДанных - Массив Из СправочникСсылка, ДокументСсылка - Ключи объектов, которые требуют определения адресатов.
//   ПараметрыРассмотрения - Структура
//   УзлыДанных - Соответствие Из КлючИЗначение - Неявно возвращаемый параметр, в него следует поместить результат.
//   СтандартнаяОбработка - Булево - Признак отключения стандартной логики определения адресатов.
//
Процедура ПриОпределенииАдресатовСамостоятельныхДанных(ТипДанныхОбмена, КлючиДанных, ПараметрыРассмотрения,
		УзлыДанных, СтандартнаяОбработка) Экспорт
	
	Если ТипДанныхОбмена.ПолноеИмя = "Справочник.ДокументыПредприятия" Тогда
		
		ПараметрыОпределения = НовыеПараметрыОпределенияАдресатовДокументов();
		
		УзлыДанных = АдресатыДокументов(КлючиДанных, ПараметрыОпределения);
		СтандартнаяОбработка = Ложь;
		
	КонецЕсли;
	
КонецПроцедуры

// Узлы адресаты документов.
// 
// Параметры:
//  ДокументыДО - Массив Из СправочникСсылка.ДокументыПредприятия
//  ПараметрыОпределения - см. НовыеПараметрыОпределенияАдресатовДокументов
// 
// Возвращаемое значение:
//  Соответствие Из КлючИЗначение:
//    * Ключ - СправочникСсылка.ДокументыПредприятия
//    * Значение - Массив Из СправочникСсылка.УзлыКОД
//
Функция АдресатыДокументов(ДокументыДО, ПараметрыОпределения) Экспорт
	
	УзлыДокументов = КОДСервер.УзлыСамостоятельныхДанныхПоПравам(ДокументыДО);
	
	ОбменСКонтрагентамиДОХолдинг.ДополнитьУзлыДокументовДляЭДО(УзлыДокументов, ПараметрыОпределения);
	
	Возврат УзлыДокументов;
	
КонецФункции

// Возвращает параметры определения адресатов документов.
//  Состав параметров может частично наполняться вызываемыми зависимыми подсистемами.
// 
// Возвращаемое значение:
//  Структура
Функция НовыеПараметрыОпределенияАдресатовДокументов() Экспорт
	
	ПараметрыОпределения = Новый Структура;
	
	ОбщегоНазначенияКлиентСервер.ДополнитьСтруктуру(ПараметрыОпределения,
		ОбменСКонтрагентамиДОХолдинг.НовыеПараметрыОпределенияАдресатовДокументовДО());
	
	Возврат ПараметрыОпределения;
	
КонецФункции

#КонецОбласти
