#Область ПрограммныйИнтерфейс

// Получить параметры нумерации.
// 
// Параметры:
//  Объект - СправочникСсылка.ДокументыПредприятия
//         - СправочникОбъект.ДокументыПредприятия
//  ОбработатьНаСервере - Булево
// 
// Возвращаемое значение:
//  Структура:
// * ЧисловойНомер - Число
// * ДатаСоздания - Дата
// * ДатаРегистрации - Дата
// * ВидДокумента - СправочникСсылка.ВидыДокументов
// * Организация - СправочникСсылка.Организации
//               - Неопределено
// * НоменклатураДел - СправочникСсылка.НоменклатураДел
// * ВопросДеятельности - СправочникСсылка.ВопросыДеятельности
// * Подразделение - СправочникСсылка.СтруктураПредприятия
// * Ответственный - СправочникСсылка.Сотрудники
//                 - СправочникСсылка.Пользователи
// * Ссылка - СправочникСсылка.ДокументыПредприятия
// * Проект - СправочникСсылка.Проекты
// * Тематика - СправочникСсылка.ТематикиДокументов
// * ГрифДоступа - СправочникСсылка.ГрифыДоступа
// * Контрагент - СправочникСсылка.Контрагенты
//              - Неопределено
// * ТипДокумента - ПеречислениеСсылка.ТипыОбъектов
//
Функция ПолучитьПараметрыНумерации(Объект, ОбработатьНаСервере = Ложь) Экспорт 
	
	Если ОбработатьНаСервере Тогда
		Возврат Нумерация.ПараметрыНумерации(Объект);
	КонецЕсли;
	
	СтруктураПараметров = Новый Структура;
	СтруктураПараметров.Вставить("ЧисловойНомер", Объект.ЧисловойНомер);
	СтруктураПараметров.Вставить("ДатаСоздания", Объект.ДатаСоздания);
	СтруктураПараметров.Вставить("ДатаРегистрации", Объект.ДатаРегистрации);
	СтруктураПараметров.Вставить("ВидДокумента", Объект.ВидДокумента);
	СтруктураПараметров.Вставить("Организация", Объект.Организация);
	СтруктураПараметров.Вставить("НоменклатураДел", Объект.НоменклатураДел);
	СтруктураПараметров.Вставить("ВопросДеятельности", Объект.ВопросДеятельности);
	СтруктураПараметров.Вставить("Подразделение", Объект.Подразделение);
	СтруктураПараметров.Вставить("Ответственный", Объект.Ответственный);
	СтруктураПараметров.Вставить("Ссылка", Объект.Ссылка);
	СтруктураПараметров.Вставить("Проект", Объект.Проект);
	СтруктураПараметров.Вставить("Тематика", Объект.Тематика);
	СтруктураПараметров.Вставить("ГрифДоступа", Объект.ГрифДоступа);
	
	Если ТипЗнч(Объект.Ссылка) = Тип("СправочникСсылка.ДокументыПредприятия") Тогда
		ПараметрыСторон = РаботаСПодписямиДокументовКлиентСервер.ПараметрыРегистрации(Объект);
		СтруктураПараметров.Вставить("Контрагент", ПараметрыСторон.Контрагент);
		СтруктураПараметров.Вставить("Организация", ПараметрыСторон.Организация);
	КонецЕсли;
	
	СтруктураПараметров.Вставить("ТипДокумента",
		ПредопределенноеЗначение("Перечисление.ТипыОбъектов.ДокументыПредприятия"));
	
	Возврат СтруктураПараметров;
	
КонецФункции

#КонецОбласти