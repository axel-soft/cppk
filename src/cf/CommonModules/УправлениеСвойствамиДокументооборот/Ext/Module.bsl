
#Область СлужебныйПрограммныйИнтерфейс

// Возвращает поддержку проверки условий доп. свойств объектом.
//
// Параметры:
//  ОписаниеОбъекта - Объект ссылочного типа или структура описывающая объект, с необходимыми полями (обязательное поле: Ссылка).
//
// Возвращаемое значение:
//  Булево
// 
Функция ОбъектПоддерживаетПроверкуУсловийДопСвойств(ОписаниеОбъекта) Экспорт
	
	СтандартнаяОбработка = Истина;
	
	Результат = Неопределено;
	УправлениеСвойствамиПереопределяемый.ПриОпределенииПоддержкиПроверкиУсловийДопСвойств(
		ОписаниеОбъекта, Результат, СтандартнаяОбработка);
	Если Не СтандартнаяОбработка Тогда
		Возврат Результат;
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

// Возвращает реквизиты отбора для условий видимости/доступности/обязательности для доп. свойств набора.
//
// Параметры:
//  НаборСвойств - СправочникСсылка.НаборыДополнительныхРеквизитовИСведений - ссылка на набор доп. свойств.
//
// Возвращаемое значение:
//  ТаблицаЗначений
//   * Реквизит - Строка
//   * Представление - Строка
//   * ТипЗначения - Тип - тип реквизита.
//   * НомерКартинки - Число
//   * РежимВыбора - Булево
//
Функция РеквизитыОтбораДляУсловийДопСвойств(НаборСвойств) Экспорт
	
	РеквизитыОбъекта = Неопределено;
	
	СтандартнаяОбработка = Истина;
	УправлениеСвойствамиПереопределяемый.ПриФормированииРеквизитовОтбораДляУсловийДопСвойств(
		НаборСвойств, РеквизитыОбъекта, СтандартнаяОбработка);
	Если Не СтандартнаяОбработка Тогда
		Возврат РеквизитыОбъекта;
	КонецЕсли;
	
	Родитель = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(НаборСвойств, "Родитель");
	Если Не ЗначениеЗаполнено(Родитель) Тогда
		Родитель = НаборСвойств;
	КонецЕсли;
	
	НаборДополнительныхРеквизитов = Родитель.ДополнительныеРеквизиты;
	
	ПредопределенныеНаборыСвойств = УправлениеСвойствамиПовтИсп.ПредопределенныеНаборыСвойств();
	ОписаниеНабора = ПредопределенныеНаборыСвойств.Получить(Родитель); // см. Справочники.НаборыДополнительныхРеквизитовИСведений.СвойстваНабора
	Если ОписаниеНабора = Неопределено Тогда
		ИмяПредопределенныхДанных = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Родитель, "ИмяПредопределенныхДанных");
	Иначе
		ИмяПредопределенныхДанных = ОписаниеНабора.Имя;
	КонецЕсли;
	
	ПозицияЗаменяемогоСимвола = СтрНайти(ИмяПредопределенныхДанных, "_");
	ПолноеИмяОбъектаМетаданных = Лев(ИмяПредопределенныхДанных, ПозицияЗаменяемогоСимвола - 1)
		+ "."
		+ Сред(ИмяПредопределенныхДанных, ПозицияЗаменяемогоСимвола + 1);
	
	РеквизитыОбъекта = СписокРеквизитовДляОтбора(ПолноеИмяОбъектаМетаданных, НаборДополнительныхРеквизитов);
	
	Возврат РеквизитыОбъекта;
	
КонецФункции

// Возвращает доп. реквизиты отбора для проверки условий видимости/доступности/обязательности.
// Реквизиты могут группировать произвольно.
//
// Параметры:
//  ОписаниеОбъекта - Объект ссылочного типа или структура описывающая объект, с необходимыми полями (обязательное поле: Ссылка).
//
// Возвращаемое значение:
//  Структура
//
Функция ДопРеквизитыОтбораДляУсловий(ОписаниеОбъекта) Экспорт
	
	ДопРеквизиты = Неопределено;
	
	СтандартнаяОбработка = Истина;
	УправлениеСвойствамиПереопределяемый.ПриОпределенииДопРеквизитовОтбораДляУсловий(
		ОписаниеОбъекта, ДопРеквизиты, СтандартнаяОбработка);
	Если Не СтандартнаяОбработка Тогда
		Возврат ДопРеквизиты;
	КонецЕсли;
	
	ДопРеквизиты = Новый Структура;
	Возврат ДопРеквизиты;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция СписокРеквизитовДляОтбора(ПолноеИмяОбъектаМетаданных, НаборДополнительныхРеквизитов)
	
	РеквизитыОбъекта = Новый ТаблицаЗначений;
	РеквизитыОбъекта.Колонки.Добавить("Реквизит");
	РеквизитыОбъекта.Колонки.Добавить("Представление", Новый ОписаниеТипов("Строка"));
	РеквизитыОбъекта.Колонки.Добавить("ТипЗначения", Новый ОписаниеТипов);
	РеквизитыОбъекта.Колонки.Добавить("НомерКартинки", Новый ОписаниеТипов("Число"));
	РеквизитыОбъекта.Колонки.Добавить("РежимВыбора", Новый ОписаниеТипов("ИспользованиеГруппИЭлементов"));
	
	ОбъектМетаданных = Метаданные.НайтиПоПолномуИмени(ПолноеИмяОбъектаМетаданных);
	
	Для Каждого ДополнительныйРеквизит Из НаборДополнительныхРеквизитов Цикл
		СвойстваОбъекта = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДополнительныйРеквизит.Свойство, "Наименование, ТипЗначения");
		СтрокаРеквизит = РеквизитыОбъекта.Добавить();
		СтрокаРеквизит.Реквизит = ДополнительныйРеквизит.Свойство;
		СтрокаРеквизит.Представление = СвойстваОбъекта.Наименование;
		СтрокаРеквизит.НомерКартинки  = 2;
		СтрокаРеквизит.ТипЗначения = СвойстваОбъекта.ТипЗначения;
	КонецЦикла;
	
	Для Каждого Реквизит Из ОбъектМетаданных.СтандартныеРеквизиты Цикл
		ДобавитьРеквизитВТаблицу(РеквизитыОбъекта, Реквизит, Истина);
	КонецЦикла;
	
	Для Каждого Реквизит Из ОбъектМетаданных.Реквизиты Цикл
		ДобавитьРеквизитВТаблицу(РеквизитыОбъекта, Реквизит, Ложь);
	КонецЦикла;
	
	РеквизитыОбъекта.Сортировать("Представление Возр");
	
	Возврат РеквизитыОбъекта;
	
КонецФункции

Процедура ДобавитьРеквизитВТаблицу(РеквизитыОбъекта, Реквизит, Стандартный)
	СтрокаРеквизит = РеквизитыОбъекта.Добавить();
	СтрокаРеквизит.Реквизит = Реквизит.Имя;
	СтрокаРеквизит.Представление = Реквизит.Представление();
	СтрокаРеквизит.НомерКартинки  = 1;
	СтрокаРеквизит.ТипЗначения = Реквизит.Тип;
	Если Стандартный Тогда
		СтрокаРеквизит.РежимВыбора = ?(Реквизит.Имя = "Родитель", ИспользованиеГруппИЭлементов.Группы, Неопределено);
	Иначе
		СтрокаРеквизит.РежимВыбора = Реквизит.ВыборГруппИЭлементов;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти
