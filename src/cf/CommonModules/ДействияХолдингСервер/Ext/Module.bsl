
#Область ПрограммныйИнтерфейс

// Добавляет задание обновления действия в очередь заданий.
//
// Параметры:
//	Действие - ОпределяемыйТип.ДействияОбъект - Действие, для которого необходимо добавить задание.
//
// Возвращаемое значение:
//	Булево - Истина - Задание добавлено.
//
Функция ДобавитьЗаданиеОбновитьДействиеОбработкиОбъекта(Действие) Экспорт
	
	Если Не Действие.ДополнительныеСвойства.ЗаписьВНеродномУзлеКод Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Действие.ДополнительныеСвойства.Вставить("ОтключитьОтметкиВремени", Истина);
	Действие.ДополнительныеСвойства.Вставить("АвторИзменения", ПользователиДокументооборот.ОлицетворяемыйПользователь());
	ЗаполнитьДанныеДействияОтправка(Действие.ДополнительныеСвойства, Действие);
	ОбработкаОчередиЗаданийСервер.ДобавитьЗаданиеОбновитьДействиеОбработки(
		Действие, Действие.ДополнительныеСвойства); 
	
	Возврат Истина;

КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Заполнить данные действия для отправки.
// 
// Параметры:
//  Действие - ОпределяемыйТип.ДействияОбъект - Действие, для которого необходимо добавить задание.
//  ДанныеДляЗаполнения - Структура - данные заполнения
//
Процедура ЗаполнитьДанныеДействияОтправка(ДанныеДляЗаполнения, Действие)
	
	ЗаписьСообщения = КОДОтправка.ИнициализироватьЗаписьСообщения(
	ОтметкиВремени.ПредставлениеОтметки(ОтметкиВремени.Текущая()), 0, , , ДанныеДляЗаполнения.ВерсияФормата);
	ПолноеИмяОбъекта = Действие.Метаданные().ПолноеИмя();
	
	ТипДанных = КОДПовтИсп.ТипДанныхОбмена(ПолноеИмяОбъекта);
	Если ТипДанных = Неопределено Тогда
		ВызватьИсключение 
		СтрШаблон(НСтр("ru = 'Объект %1 не является типом данных КОД'"), ПолноеИмяОбъекта);
	КонецЕсли;
	
	ОбъектXDTO = КОДОтправка.НовыйОбъектXDTO(ТипДанных.ПолноеИмя, ЗаписьСообщения);
	КОДОтправка.ЗаполнитьСсылочныйОбъект(ОбъектXDTO, Действие, ЗаписьСообщения);
	КОДОтправка.ЗаполнитьОдноименныеСвойстваXDTOРеквизитами(ОбъектXDTO, Действие, ЗаписьСообщения);
	
	ЗаписьJSON = Новый ЗаписьJSON;
	ЗаписьJSON.УстановитьСтроку();
	ЗаписьJSON.ЗаписатьНачалоОбъекта();
	ЗаписьJSON.ЗаписатьИмяСвойства("object");
	ЗаписьСообщения.ФабрикаXDTO.ЗаписатьJSON(ЗаписьJSON, ОбъектXDTO, НазначениеТипаXML.Явное);
	ЗаписьJSON.ЗаписатьКонецОбъекта();
	
	ДанныеДляЗаполнения.Вставить("ОбъектJSON", ЗаписьJSON.Закрыть());

КонецПроцедуры

#КонецОбласти