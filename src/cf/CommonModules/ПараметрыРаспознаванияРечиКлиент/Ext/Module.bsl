///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2022, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

&НаКлиенте
Процедура ПоказатьВыборПараметровРаспознаванияРечи(Форма, ТекущиеПараметры, ОбработчикПолученияРезультата) Экспорт
	
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("ПараметрыРаботыСРечью", ТекущиеПараметры);
	ПараметрыОткрытия.Вставить("ИмяФормы", Форма.ИмяФормы);
	
	
	Обработчик = Новый ОписаниеОповещения("ПослеПолученияРезультатаВыбораПараметровРаспознаванияРечи",
		ЭтотОбъект, ОбработчикПолученияРезультата);
	ОткрытьФорму("Обработка.ПараметрыРаспознаванияРечи.Форма", ПараметрыОткрытия , Форма, , , , Обработчик);
	
КонецПроцедуры

&НаКлиенте
Функция ПараметрыМодели(ТекущиеПараметры) Экспорт
	
#Если ТолстыйКлиентУправляемоеПриложение Тогда
	ВызватьИсключение НСтр("ru = 'Распознавание речи не поддерживается в толстом клиенте.'");
#Иначе
	ПараметрыМодели = Новый ПараметрыМоделиРаспознаванияРечи;
	ПараметрыМодели.КодЯзыка   = ТекущиеПараметры.КодЯзыка;
	ПараметрыМодели.Акустика   = ТекущиеПараметры.Акустика;
	ПараметрыМодели.Грамматика = ТекущиеПараметры.Грамматика;
	ПараметрыМодели.Версия     = ТекущиеПараметры.Версия;
	
	Возврат ПараметрыМодели;
#КонецЕсли
	
КонецФункции

&НаКлиенте
Функция ВариантИспользования(ТекущиеПараметры) Экспорт
	
#Если ТолстыйКлиентУправляемоеПриложение Тогда
	ВызватьИсключение НСтр("ru = 'Распознавание речи не поддерживается в толстом клиенте.'");
#Иначе
	Если ТекущиеПараметры.ВариантИспользования = "Локально" Тогда
		Возврат ВариантИспользованияРасположенияРаботыСРечью.ТолькоЛокальное;
	ИначеЕсли ТекущиеПараметры.ВариантИспользования = "ВОблаке" Тогда
		Возврат ВариантИспользованияРасположенияРаботыСРечью.ТолькоВнешнее;
	Иначе
		Возврат ВариантИспользованияРасположенияРаботыСРечью.Авто;
	КонецЕсли;
#КонецЕсли
	
КонецФункции

&НаКлиенте
Функция ПараметрыПотоковогоРаспознаванияРечи(ТекущиеПараметры, ОбработчикОстановкиАудиозаписи) Экспорт
	
#Если ТолстыйКлиентУправляемоеПриложение Тогда
	ВызватьИсключение НСтр("ru = 'Распознавание речи не поддерживается в толстом клиенте.'");
#Иначе
	ПараметрыРаспознавания = Новый ПараметрыПотоковогоРаспознаванияРечи;
	ПараметрыРаспознавания.ОбработчикОстановкиАудиозаписи = ОбработчикОстановкиАудиозаписи;
	ПараметрыРаспознавания.ИспользоватьТолькоДополнительныеГрамматики =
		ТекущиеПараметры.ИспользоватьТолькоДополнительныеГрамматики;
	
	ПараметрыРаспознавания.ДополнительныеГрамматики.Добавить("ОбщаяГрамматика");
	ПараметрыРаспознавания.ДополнительныеГрамматики.Добавить("ПользовательскаяГрамматика");
	
	Возврат ПараметрыРаспознавания;
#КонецЕсли
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ПослеПолученияРезультатаВыбораПараметровРаспознаванияРечи(Результат, Контекст) Экспорт
	
	Если Контекст = Неопределено Или Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ВыполнитьОбработкуОповещения(Контекст, Результат);
	
КонецПроцедуры

#КонецОбласти