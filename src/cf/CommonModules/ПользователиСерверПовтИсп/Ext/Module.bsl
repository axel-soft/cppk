////////////////////////////////////////////////////////////////////////////////
// Пользователи (сервер, повторное использование)
//
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Возвращает ссылку неуказанного пользователя.
// См. также Пользователи.СсылкаНеуказанногоПользователя.
//
// Возвращаемое значение:
//  СправочникСсылка.Пользователи - См. Пользователи.СсылкаНеуказанногоПользователя().
//
Функция СсылкаНеуказанногоПользователя() Экспорт
	
	Возврат Пользователи.СсылкаНеуказанногоПользователя();
	
КонецФункции

// Возвращает дерево - ветви - структура предприятия, листья - пользователи
Функция ПолучитьПользователейВПодразделениях() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	СправочникСтруктураПредприятия.Ссылка КАК ГруппаСсылка,
		|	СправочникСтруктураПредприятия.ПометкаУдаления КАК ГруппаПометкаУдаления,
		|	СправочникСтруктураПредприятия.Наименование КАК ГруппаНаименование,
		|	СправочникПользователи.Ссылка КАК ПользователиСсылка,
		|	СправочникПользователи.ПометкаУдаления КАК ПользователиПометкаУдаления,
		|	СправочникПользователи.ПредставлениеВПерепискеСРангом КАК ПользователиНаименование,
		|	Сотрудники.Ссылка = Сотрудники.Подразделение.Руководитель КАК ЭтоРуководитель,
		|	Сотрудники.Ссылка КАК СотрудникиСсылка
		|ПОМЕСТИТЬ ВрТаблица
		|ИЗ
		|	Справочник.СтруктураПредприятия КАК СправочникСтруктураПредприятия
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Сотрудники КАК Сотрудники
		|		ПО СправочникСтруктураПредприятия.Ссылка = Сотрудники.Подразделение
		|			И НЕ Сотрудники.ПометкаУдаления
		|			И Сотрудники.Действует
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СотрудникиПользователей КАК СотрудникиПользователей
		|		ПО Сотрудники.Ссылка = СотрудникиПользователей.Сотрудник
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Пользователи КАК СправочникПользователи
		|		ПО СотрудникиПользователей.Пользователь = СправочникПользователи.Ссылка
		|			И НЕ СправочникПользователи.Недействителен
		|			И НЕ СправочникПользователи.ПометкаУдаления
		|ГДЕ
		|	СправочникСтруктураПредприятия.ПометкаУдаления = ЛОЖЬ
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВрТаблица.ГруппаСсылка,
		|	ИсполнителиРолей.РольИсполнителя,
		|	ИсполнителиРолей.РольИсполнителя.Владелец.Наименование,
		|	ИсполнителиРолей.РольИсполнителя.ПометкаУдаления,
		|	ВрТаблица.ГруппаПометкаУдаления,
		|	ВрТаблица.ГруппаНаименование
		|ПОМЕСТИТЬ ВрТаблицаРоли
		|ИЗ
		|	ВрТаблица КАК ВрТаблица
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ИсполнителиРолей КАК ИсполнителиРолей
		|		ПО ВрТаблица.СотрудникиСсылка = ИсполнителиРолей.Исполнитель
		|		И (ИсполнителиРолей.РольИсполнителя.ПометкаУдаления = ЛОЖЬ)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВрТаблица.ГруппаСсылка,
		|	ВрТаблица.ГруппаПометкаУдаления,
		|	ВрТаблица.ГруппаНаименование,
		|	ВрТаблица.ПользователиСсылка,
		|	ВрТаблица.ПользователиПометкаУдаления,
		|	ВрТаблица.ПользователиНаименование,
		|	ВрТаблица.ЭтоРуководитель
		|ИЗ
		|	ВрТаблица КАК ВрТаблица
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ВрТаблицаРоли.ГруппаСсылка,
		|	ВрТаблицаРоли.ГруппаПометкаУдаления,
		|	ВрТаблицаРоли.ГруппаНаименование,
		|	ВрТаблицаРоли.РольИсполнителя,
		|	ВрТаблицаРоли.РольИсполнителяПометкаУдаления,
		|	ВрТаблицаРоли.РольИсполнителяВладелецНаименование,
		|	ЛОЖЬ
		|ИЗ
		|	ВрТаблицаРоли КАК ВрТаблицаРоли
		|УПОРЯДОЧИТЬ ПО
		|	ВрТаблица.ГруппаСсылка ИЕРАРХИЯ,
		|	ВрТаблица.ПользователиНаименование";
		
	ВыборкаДерево = Запрос.Выполнить().Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкамСИерархией);
	
	Возврат ВыборкаДерево;

КонецФункции	

// Возвращает дерево - ветви - группы пользователей, листья - пользователи
Функция ПолучитьПользователейВГруппах() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	РабочиеГруппы.Ссылка КАК ГруппаСсылка,
		|	РабочиеГруппы.ПометкаУдаления КАК ГруппаПометкаУдаления,
		|	РабочиеГруппы.Наименование КАК ГруппаНаименование,
		|	СправочникПользователи.Ссылка КАК ПользователиСсылка,
		|	СправочникПользователи.ПометкаУдаления КАК ПользователиПометкаУдаления,
		|	СправочникПользователи.ПредставлениеВПерепискеСРангом КАК ПользователиНаименование,
		|	ИСТИНА В
		|		(ВЫБРАТЬ ПЕРВЫЕ 1
		|			ИСТИНА
		|		ИЗ
		|			Справочник.СтруктураПредприятия КАК Руководители
		|		ГДЕ
		|			Руководители.Руководитель = Сотрудники.Ссылка
		|			И Руководители.ПометкаУдаления = ЛОЖЬ) КАК ЭтоРуководитель,
		|	Сотрудники.Ссылка КАК СотрудникиСсылка
		|ПОМЕСТИТЬ ВрТаблица_0
		|ИЗ
		|	Справочник.РабочиеГруппы КАК РабочиеГруппы
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.РабочиеГруппы.Состав КАК РабочиеГруппыСостав
		|		ПО РабочиеГруппы.Ссылка = РабочиеГруппыСостав.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Сотрудники КАК Сотрудники
		|		ПО РабочиеГруппыСостав.Участник = Сотрудники.Ссылка
		|			И НЕ Сотрудники.ПометкаУдаления
		|			И Сотрудники.Действует
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СотрудникиПользователей КАК СотрудникиПользователей
		|		ПО Сотрудники.Ссылка = СотрудникиПользователей.Сотрудник
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Пользователи КАК СправочникПользователи
		|		ПО (СправочникПользователи.Ссылка = СотрудникиПользователей.Пользователь
		|				ИЛИ РабочиеГруппы.Ссылка = ЗНАЧЕНИЕ(Справочник.РабочиеГруппы.ВсеПользователи))
		|			И (СправочникПользователи.Недействителен = ЛОЖЬ)
		|			И (СправочникПользователи.ПометкаУдаления = ЛОЖЬ)
		|ГДЕ
		|	РабочиеГруппы.ПометкаУдаления = ЛОЖЬ
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВрТаблица_0.ГруппаСсылка,
		|	ВрТаблица_0.ГруппаПометкаУдаления,
		|	ВрТаблица_0.ГруппаНаименование,
		|	ВрТаблица_0.ПользователиСсылка,
		|	ВрТаблица_0.ПользователиПометкаУдаления,
		|	ВрТаблица_0.ПользователиНаименование,
		|	МАКСИМУМ(ВрТаблица_0.ЭтоРуководитель) КАК ЭтоРуководитель,
		|	ВрТаблица_0.СотрудникиСсылка
		|ПОМЕСТИТЬ ВрТаблица
		|ИЗ
		|	ВрТаблица_0 КАК ВрТаблица_0
		|СГРУППИРОВАТЬ ПО
		|	ВрТаблица_0.ГруппаСсылка,
		|	ВрТаблица_0.ПользователиСсылка,
		|	ВрТаблица_0.ГруппаПометкаУдаления,
		|	ВрТаблица_0.ГруппаНаименование,
		|	ВрТаблица_0.ПользователиПометкаУдаления,
		|	ВрТаблица_0.ПользователиНаименование,
		|	ВрТаблица_0.СотрудникиСсылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВрТаблица.ГруппаСсылка,
		|	ИсполнителиРолей.РольИсполнителя,
		|	ИсполнителиРолей.РольИсполнителя.Владелец.Наименование,
		|	ИсполнителиРолей.РольИсполнителя.ПометкаУдаления,
		|	ВрТаблица.ГруппаПометкаУдаления,
		|	ВрТаблица.ГруппаНаименование
		|ПОМЕСТИТЬ ВрТаблицаРоли
		|ИЗ
		|	ВрТаблица КАК ВрТаблица
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ИсполнителиРолей КАК ИсполнителиРолей
		|		ПО ВрТаблица.СотрудникиСсылка = ИсполнителиРолей.Исполнитель
		|			И (ИсполнителиРолей.РольИсполнителя.ПометкаУдаления = ЛОЖЬ)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВрТаблица.ГруппаСсылка,
		|	ВрТаблица.ГруппаПометкаУдаления,
		|	ВрТаблица.ГруппаНаименование,
		|	ВрТаблица.ПользователиСсылка,
		|	ВрТаблица.ПользователиПометкаУдаления,
		|	ВрТаблица.ПользователиНаименование,
		|	ВрТаблица.ЭтоРуководитель
		|ИЗ
		|	ВрТаблица КАК ВрТаблица
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ВрТаблицаРоли.ГруппаСсылка,
		|	ВрТаблицаРоли.ГруппаПометкаУдаления,
		|	ВрТаблицаРоли.ГруппаНаименование,
		|	ВрТаблицаРоли.РольИсполнителя,
		|	ВрТаблицаРоли.РольИсполнителяПометкаУдаления,
		|	ВрТаблицаРоли.РольИсполнителяВладелецНаименование,
		|	ЛОЖЬ
		|ИЗ
		|	ВрТаблицаРоли КАК ВрТаблицаРоли
		|
		|УПОРЯДОЧИТЬ ПО
		|	ВрТаблица.ГруппаСсылка ИЕРАРХИЯ,
		|	ВрТаблица.ПользователиНаименование";
		
	ВыборкаДерево = Запрос.Выполнить().Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкамСИерархией);
	
	Возврат ВыборкаДерево;

КонецФункции	

// Возвращает таблицу - все пользователи
Функция ПолучитьПользователейСписком() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	Пользователи.Ссылка,
		|	Пользователи.ПометкаУдаления,
		|	Пользователи.ПредставлениеВПерепискеСРангом КАК Наименование,
		|	ИСТИНА В
		|		(ВЫБРАТЬ ПЕРВЫЕ 1
		|			ИСТИНА
		|		ИЗ
		|			Справочник.СтруктураПредприятия КАК Руководители
		|		ГДЕ
		|			Руководители.Руководитель = Сотрудники.Ссылка
		|			И Руководители.ПометкаУдаления = ЛОЖЬ) КАК ЭтоРуководитель
		|ИЗ
		|	Справочник.Пользователи КАК Пользователи
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СотрудникиПользователей КАК СотрудникиПользователей
		|		ПО Пользователи.Ссылка = СотрудникиПользователей.Пользователь
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Сотрудники КАК Сотрудники
		|		ПО СотрудникиПользователей.Сотрудник = Сотрудники.Ссылка
		|			И НЕ Сотрудники.ПометкаУдаления
		|			И Сотрудники.Действует
		|ГДЕ
		|	Пользователи.Недействителен = ЛОЖЬ
		|		И Пользователи.ПометкаУдаления = ЛОЖЬ
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	РолиИсполнителей.Ссылка,
		|	РолиИсполнителей.ПометкаУдаления,
		|	РолиИсполнителей.Наименование,
		|	ЛОЖЬ
		|ИЗ
		|	Справочник.РолиИсполнителей КАК РолиИсполнителей
		|ГДЕ
		|	РолиИсполнителей.ПометкаУдаления = ЛОЖЬ
		|УПОРЯДОЧИТЬ ПО
		|	Наименование";
		
	ВыборкаТаблица = Запрос.Выполнить().Выгрузить();
	
	Возврат ВыборкаТаблица;

КонецФункции	

// Функция ЭтоПолноправныйПользовательИБ проверяет,
// является ли полноправным текущий пользователь ИБ или
// пользователь ИБ заданного пользователя (обычного или внешнего).
//
//  Полноправными считается:
// а) пользователь ИБ при пустом списке пользователей ИБ,
//    если основная роль не задана или ПолныеПрава,
// б) пользователь ИБ с ролью ПолныеПрава.
//
//
// Параметры:
//  Пользователь - Неопределено (проверяется текущий пользователь ИБ),
//                 Справочник.Пользователи, Справочник.ВнешниеПользователи
//                 (осуществляется поиск пользователя ИБ по уникальному
//                  идентификатору, заданному в реквизите ИдентификаторПользователяИБ,
//                  если пользователь ИБ не найден, возвращается Ложь).
//
// Возвращаемое значение:
//  Булево.
//
Функция ЭтоПолноправныйПользовательИБ(Пользователь = Неопределено) Экспорт
	
	Возврат Пользователи.ЭтоПолноправныйПользователь(Пользователь,, Ложь);
	
КонецФункции	

// Функция ВсеРоли() возвращает таблицу значений имен всех ролей конфигурации.
//
// Параметры:
// 
// Возвращаемое значение:
//  ТаблицаЗначений
//      Имя           - Строка
//      Синоним       - Строка
//      Идентификатор - СправочникСсылка.ИдентификаторыОбъектовМетаданных
//
Функция ВсеРоли() Экспорт
	
	Таблица = Новый ТаблицаЗначений;
	Таблица.Колонки.Добавить("Имя",           Новый ОписаниеТипов("Строка"));
	Таблица.Колонки.Добавить("Синоним",       Новый ОписаниеТипов("Строка"));
	Таблица.Колонки.Добавить("Идентификатор", Новый ОписаниеТипов("СправочникСсылка.ИдентификаторыОбъектовМетаданных"));
	
	Для каждого Роль Из Метаданные.Роли Цикл
		Строка = Таблица.Добавить();
		Строка.Имя           = Роль.Имя;
		Строка.Синоним       = ?(ЗначениеЗаполнено(Роль.Синоним), Роль.Синоним, Роль.Имя);
		Строка.Идентификатор = ОбщегоНазначения.ИдентификаторОбъектаМетаданных(Роль);
	КонецЦикла;
	
	Возврат Таблица;
	
КонецФункции

// Возвращает таблицу - все пользователи
Функция ПолучитьМассивГруппПользователя(Пользователь) Экспорт
	
	// Получим все группы пользователей для пользователя
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	РабочиеГруппыСостав.Ссылка
		|ИЗ
		|	Справочник.РабочиеГруппы.Состав КАК РабочиеГруппыСостав
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СотрудникиПользователей КАК СотрудникиПользователей
		|		ПО РабочиеГруппыСостав.Участник = СотрудникиПользователей.Сотрудник
		|ГДЕ
		|	СотрудникиПользователей.Пользователь = &Пользователь
		|	И СотрудникиПользователей.Сотрудник.Действует
		|	И НЕ РабочиеГруппыСостав.Ссылка.ПометкаУдаления";

	Запрос.Параметры.Вставить("Пользователь", Пользователь);
	Результат = Запрос.Выполнить().Выгрузить();
	
	МассивГруппПользователей = Результат.ВыгрузитьКолонку("Ссылка");	
	МассивГруппПользователей.Добавить(
		ПредопределенноеЗначение("Справочник.РабочиеГруппы.ВсеПользователи"));
	
	Возврат МассивГруппПользователей;

КонецФункции

Функция ПолучитьПользователейВГруппе(Группа) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	РабочиеГруппы.Ссылка КАК ГруппаСсылка,
		|	РабочиеГруппы.ПометкаУдаления КАК ГруппаПометкаУдаления,
		|	РабочиеГруппы.Наименование КАК ГруппаНаименование,
		|	СправочникПользователи.Ссылка КАК ПользователиСсылка,
		|	СправочникПользователи.ПометкаУдаления КАК ПользователиПометкаУдаления,
		|	СправочникПользователи.ПредставлениеВПерепискеСРангом КАК ПользователиНаименование,
		|	ИСТИНА В
		|		(ВЫБРАТЬ ПЕРВЫЕ 1
		|			ИСТИНА
		|		ИЗ
		|			Справочник.СтруктураПредприятия КАК Руководители
		|		ГДЕ
		|			Руководители.Руководитель = Сотрудники.Ссылка
		|			И Руководители.ПометкаУдаления = ЛОЖЬ) КАК ЭтоРуководитель
		|ПОМЕСТИТЬ ВрТаблица_0
		|ИЗ
		|	Справочник.РабочиеГруппы КАК РабочиеГруппы
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.РабочиеГруппы.Состав КАК РабочиеГруппыСостав
		|		ПО РабочиеГруппы.Ссылка = РабочиеГруппыСостав.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Сотрудники КАК Сотрудники
		|		ПО РабочиеГруппы.Ссылка = Сотрудники.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СотрудникиПользователей КАК СотрудникиПользователей
		|		ПО Сотрудники.Ссылка = СотрудникиПользователей.Сотрудник
		|			И Сотрудники.Действует
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Пользователи КАК СправочникПользователи
		|		ПО (СправочникПользователи.Ссылка = СотрудникиПользователей.Пользователь
		|				ИЛИ РабочиеГруппы.Ссылка = ЗНАЧЕНИЕ(Справочник.РабочиеГруппы.ВсеПользователи))
		|			И (СправочникПользователи.Недействителен = ЛОЖЬ)
		|			И (СправочникПользователи.ПометкаУдаления = ЛОЖЬ)
		|ГДЕ
		|	РабочиеГруппы.ПометкаУдаления = ЛОЖЬ
		|	И НЕ СправочникПользователи.ПометкаУдаления
		|	И НЕ СправочникПользователи.Недействителен
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВрТаблица_0.ГруппаСсылка,
		|	ВрТаблица_0.ГруппаПометкаУдаления,
		|	ВрТаблица_0.ГруппаНаименование,
		|	ВрТаблица_0.ПользователиСсылка,
		|	ВрТаблица_0.ПользователиПометкаУдаления,
		|	ВрТаблица_0.ПользователиНаименование,
		|	МАКСИМУМ(ВрТаблица_0.ЭтоРуководитель) КАК ЭтоРуководитель
		|ПОМЕСТИТЬ ВрТаблица
		|ИЗ
		|	ВрТаблица_0 КАК ВрТаблица_0
		|СГРУППИРОВАТЬ ПО
		|	ВрТаблица_0.ГруппаСсылка,
		|	ВрТаблица_0.ПользователиСсылка,
		|	ВрТаблица_0.ГруппаПометкаУдаления,
		|	ВрТаблица_0.ГруппаНаименование,
		|	ВрТаблица_0.ПользователиПометкаУдаления,
		|	ВрТаблица_0.ПользователиНаименование
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВрТаблица.ГруппаСсылка,
		|	ИсполнителиРолей.РольИсполнителя,
		|	ИсполнителиРолей.РольИсполнителя.Владелец.Наименование,
		|	ИсполнителиРолей.РольИсполнителя.ПометкаУдаления,
		|	ВрТаблица.ГруппаПометкаУдаления,
		|	ВрТаблица.ГруппаНаименование
		|ПОМЕСТИТЬ ВрТаблицаРоли
		|ИЗ
		|	ВрТаблица КАК ВрТаблица
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СотрудникиПользователей КАК СотрудникиПользователей
		|		ПО ВрТаблица.ПользователиСсылка = СотрудникиПользователей.Пользователь
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ИсполнителиРолей КАК ИсполнителиРолей
		|		ПО СотрудникиПользователей.Сотрудник = ИсполнителиРолей.Исполнитель
		|			И (ИсполнителиРолей.РольИсполнителя.ПометкаУдаления = ЛОЖЬ)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВрТаблица.ГруппаСсылка,
		|	ВрТаблица.ГруппаПометкаУдаления,
		|	ВрТаблица.ГруппаНаименование,
		|	ВрТаблица.ПользователиСсылка,
		|	ВрТаблица.ПользователиПометкаУдаления,
		|	ВрТаблица.ПользователиНаименование,
		|	ВрТаблица.ЭтоРуководитель
		|ИЗ
		|	ВрТаблица КАК ВрТаблица
		|ГДЕ
		|	ВрТаблица.ГруппаСсылка = &Группа
		|
		|УПОРЯДОЧИТЬ ПО
		|	ВрТаблица.ГруппаСсылка ИЕРАРХИЯ,
		|	ВрТаблица.ПользователиНаименование";
		
	Запрос.Параметры.Вставить("Группа", Группа);
	ВыборкаДерево = Запрос.Выполнить().Выгрузить();
	
	Возврат ВыборкаДерево;

КонецФункции	

// Возвращает дерево - ветви - структура предприятия, листья - пользователи
Функция ПолучитьПользователейВПодразделении(Подразделение) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	СправочникСтруктураПредприятия.Ссылка КАК ГруппаСсылка,
		|	СправочникСтруктураПредприятия.ПометкаУдаления КАК ГруппаПометкаУдаления,
		|	СправочникСтруктураПредприятия.Наименование КАК ГруппаНаименование,
		|	СправочникПользователи.Ссылка КАК ПользователиСсылка,
		|	СправочникПользователи.ПометкаУдаления КАК ПользователиПометкаУдаления,
		|	СправочникПользователи.ПредставлениеВПерепискеСРангом КАК ПользователиНаименование,
		|	Сотрудники.Ссылка = Сотрудники.Подразделение.Руководитель КАК ЭтоРуководитель,
		|	Сотрудники.Ссылка КАК СотрудникиСсылка
		|ПОМЕСТИТЬ ВрТаблица
		|ИЗ
		|	Справочник.СтруктураПредприятия КАК СправочникСтруктураПредприятия
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Сотрудники КАК Сотрудники
		|		ПО СправочникСтруктураПредприятия.Ссылка = Сотрудники.Подразделение
		|			И НЕ Сотрудники.ПометкаУдаления
		|			И Сотрудники.Действует
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СотрудникиПользователей КАК СотрудникиПользователей
		|		ПО Сотрудники.Ссылка = СотрудникиПользователей.Сотрудник
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Пользователи КАК СправочникПользователи
		|		ПО СотрудникиПользователей.Пользователь = СправочникПользователи.Ссылка
		|			И НЕ СправочникПользователи.Недействителен
		|			И НЕ СправочникПользователи.ПометкаУдаления
		|ГДЕ
		|	СправочникСтруктураПредприятия.ПометкаУдаления = ЛОЖЬ
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВрТаблица.ГруппаСсылка,
		|	ВрТаблица.ГруппаПометкаУдаления,
		|	ВрТаблица.ГруппаНаименование,
		|	ВрТаблица.ПользователиСсылка,
		|	ВрТаблица.ПользователиПометкаУдаления,
		|	ВрТаблица.ПользователиНаименование,
		|	ВрТаблица.ЭтоРуководитель
		|ИЗ
		|	ВрТаблица КАК ВрТаблица
		|ГДЕ
		|	ВрТаблица.ГруппаСсылка = &Подразделение
		|
		|УПОРЯДОЧИТЬ ПО
		|	ВрТаблица.ГруппаСсылка ИЕРАРХИЯ,
		|	ВрТаблица.ПользователиНаименование";
		
	Запрос.Параметры.Вставить("Подразделение", Подразделение);
	ВыборкаДерево = Запрос.Выполнить().Выгрузить();
	
	Возврат ВыборкаДерево;

КонецФункции	

Функция ПользовательЯвляетсяСлужебным(Знач Пользователь = Неопределено) Экспорт
	
	Если Пользователь = Неопределено Тогда
		Пользователь = Пользователи.ТекущийПользователь();
	КонецЕсли;
	
	Возврат ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Пользователь, "Служебный");
	
КонецФункции

#КонецОбласти