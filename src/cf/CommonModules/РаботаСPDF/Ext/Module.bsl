
///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2023, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Проверяет и добавляет программно реквизит и поле ДокументPDF на форму
// Параметры:
//  Форма - Форма - Необходимая форма
//  СведенияОДокументеPDF - Структура - Сведения о документе pdf с ключами:
//   * ИмяРеквизита - Строка - Имя реквизита pdf
//   * ИмяЭлемента - Строка - Имя элемента
//   * ИмяРодителяЭлемента - Строка - Имя родителя элемента
//
Процедура ВывестиPDFДокументНаФорму(Форма, СведенияОДокументеPDF) Экспорт
	
	ДобавляемыеРеквизиты = Новый Массив;
	Если Не РаботаСФормами.РеквизитСуществует(Форма, СведенияОДокументеPDF.ИмяРеквизита) Тогда
		ДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы(СведенияОДокументеPDF.ИмяРеквизита,
			Новый ОписаниеТипов("ДокументPDF")));
		Форма.ИзменитьРеквизиты(ДобавляемыеРеквизиты);
	КонецЕсли;
	
	ЭлементРодительПоляPDFДокумента = Форма.Элементы.Найти(СведенияОДокументеPDF.ИмяРодителяЭлемента);
	Если ЭлементРодительПоляPDFДокумента <> Неопределено тогда
		ДобавитьПолеPDFДокумента(Форма,
			СведенияОДокументеPDF.ИмяЭлемента,
			ЭлементРодительПоляPDFДокумента,
			СведенияОДокументеPDF.ИмяРеквизита);
	КонецЕсли;
	
	ИмяРодителяКоманды = СведенияОДокументеPDF.ИмяРодителяКоманды;
	
	ИмяКомандыФормы = "Подключаемый_МасштабПлюс";
		
	СозданнаяКоманда = РаботаСФормами.ДобавитьКоманду(
		Форма,
		ИмяКомандыФормы,
		НСтр("ru = 'Увеличить'"),
		НСтр("ru = 'Увеличить'"),
		БиблиотекаКартинок.УвеличитьМасштаб);
		
	СозданнаяКоманда.Действие = ИмяКомандыФормы;
		
	ИмяЭлемента = "Увеличить";	
	ЭлементРодительКоманды = Форма.Элементы.Найти(ИмяРодителяКоманды);
	
	РаботаСФормами.ДобавитьКнопку(Форма, ИмяЭлемента, ЭлементРодительКоманды, 
		ИмяКомандыФормы);
	
	ИмяКомандыФормы = "Подключаемый_МасштабМинус";
		
	СозданнаяКоманда = РаботаСФормами.ДобавитьКоманду(
		Форма,
		ИмяКомандыФормы,
		НСтр("ru = 'Уменьшить'"),
		НСтр("ru = 'Уменьшить'"),
		БиблиотекаКартинок.УменьшитьМасштаб);
		
	СозданнаяКоманда.Действие = ИмяКомандыФормы;
		
	ИмяЭлемента = "Уменьшить";	
	ЭлементРодительКоманды = Форма.Элементы.Найти(ИмяРодителяКоманды);
	
	РаботаСФормами.ДобавитьКнопку(Форма, ИмяЭлемента, ЭлементРодительКоманды, ИмяКомандыФормы);
		
	ИмяКомандыФормы = "Подключаемый_ПоворотЛево";
		
	СозданнаяКоманда = РаботаСФормами.ДобавитьКоманду(
		Форма,
		ИмяКомандыФормы,
		НСтр("ru = 'Повернуть по часовой стрелке'"),
		НСтр("ru = 'Повернуть по часовой стрелке'"),
		БиблиотекаКартинок.ПовернутьПоЧасовойСтрелке);   
		
	СозданнаяКоманда.Действие = ИмяКомандыФормы;	
		
	ИмяЭлемента = "ПоворотЛево";	
	ЭлементРодительКоманды = Форма.Элементы.Найти(ИмяРодителяКоманды);
	
	РаботаСФормами.ДобавитьКнопку(Форма, ИмяЭлемента, ЭлементРодительКоманды, ИмяКомандыФормы);	
		
	ИмяКомандыФормы = "Подключаемый_ПоворотПраво";
		
	СозданнаяКоманда = РаботаСФормами.ДобавитьКоманду(
		Форма,
		ИмяКомандыФормы,
		НСтр("ru = 'Повернуть против часовой стрелки'"),
		НСтр("ru = 'Повернуть против часовой стрелки'"),
		БиблиотекаКартинок.ПовернутьПротивЧасовойСтрелки);   
		
	СозданнаяКоманда.Действие = ИмяКомандыФормы;	
		
	ИмяЭлемента = "ПоворотПраво";	
	ЭлементРодительКоманды = Форма.Элементы.Найти(ИмяРодителяКоманды);
	
	РаботаСФормами.ДобавитьКнопку(Форма, ИмяЭлемента, ЭлементРодительКоманды, ИмяКомандыФормы);

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Добавляет поле ввода формы.
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - Форма.
//  Имя - Строка - Уникальное имя добавляемого элемента.
//  Родитель - ГруппаФормы, ТаблицаФормы, ФормаКлиентскогоПриложения - Родитель для добавляемого элемента.
//  ПутьКДанным - Строка - Путь к реквизиту формы.
// 
// Возвращаемое значение:
//  ПолеФормы - Поле ввода формы.
//
Функция ДобавитьПолеPDFДокумента(Форма, Имя, Родитель, ПутьКДанным)
	
	СозданныйЭлемент = Форма.Элементы.Вставить(Имя, Тип("ПолеФормы"), Родитель);
	СозданныйЭлемент.Вид = ВидПоляФормы.ПолеPDFДокумента;
	СозданныйЭлемент.ПутьКДанным = ПутьКДанным;
	СозданныйЭлемент.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
	СозданныйЭлемент.ОтображениеПодсказки = ОтображениеПодсказки.Нет;
		
	Возврат СозданныйЭлемент;
	
КонецФункции

#КонецОбласти
