// @strict-types


#Область ПрограммныйИнтерфейс

#Область УчетныеЗаписиЭДО

// Вызывается при создании на сервере карточки учетной записи ЭДО
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - Форма карточки учетной записи
//  Отказ - Булево
//  СтандартнаяОбработка - Булево
Процедура УчетнаяЗаписьЭДОПриСозданииНаСервере(Форма, Отказ, СтандартнаяОбработка) Экспорт
	
	Если Не МиграцияДанныхИзВнешнихСистемСервер.БлокироватьОбменСВнешнимиРесурсами() Тогда
		Возврат;
	КонецЕсли;
	
	УчетнаяЗаписьЭДОУстановитьВидимостьДоступность(Форма);
	
КонецПроцедуры

// Вызывается при создании на сервере формы списка учетных записей ЭДО
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения
//  Отказ - Булево
//  СтандартнаяОбработка - Булево
//
Процедура УчетнаяЗаписьЭДОФормаСпискаПриСозданииНаСервере(Форма, Отказ, СтандартнаяОбработка) Экспорт
	
	Если Не МиграцияДанныхИзВнешнихСистемСервер.БлокироватьОбменСВнешнимиРесурсами() Тогда
		Возврат;
	КонецЕсли;
	
	СписокУчетныхЗаписейЭДОУстановитьВидимостьДоступность(Форма);
	
КонецПроцедуры

#КонецОбласти

#Область ЭлектронныеДокументыЭДО

// Вызывается при создании на сервере карточки учетной записи ЭДО
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения
//
Процедура ЭлектронныйДокументИсходящийЭДОПриНастройкеВидимостиДействий(Форма) Экспорт
	
	Если Не МиграцияДанныхИзВнешнихСистемСервер.БлокироватьОбменСВнешнимиРесурсами() Тогда
		Возврат;
	КонецЕсли;
	
	ЭлектронныйДокументИсходящийЭДОУстановитьВидимостьДоступность(Форма);
	
КонецПроцедуры

// Вызывается при настройки видимости действий исходящего документа ЭДО
// 
// Параметры:
//  ТолькоПросмотр - Булево
//
Процедура ЭлектронныйДокументВходящийЭДОПриНастройкеВидимостиДействий(ТолькоПросмотр) Экспорт
	
	Если Не МиграцияДанныхИзВнешнихСистемСервер.БлокироватьОбменСВнешнимиРесурсами() Тогда
		Возврат;
	КонецЕсли;
	
	ЭлектронныйДокументВходящийЭДОУстановитьВидимостьДоступность(ТолькоПросмотр);
	
КонецПроцедуры

// Вызывается при определении доступных по ЭДО действий для документа ЭДО
// 
// Параметры:
//  СостояниеДокумента - ПеречислениеСсылка.СостоянияДокументовЭДО - Состояние электронного документа.
//  СвойстваДокумента - ДокументОбъект.ЭлектронныйДокументВходящийЭДО, ДокументОбъект.ЭлектронныйДокументИсходящийЭДО, Структура - Свойства:
//    * Ссылка - ДокументСсылка.ЭлектронныйДокументВходящийЭДО, ДокументСсылка.ЭлектронныйДокументИсходящийЭДО - Ссылка на электронный документ.
//    * НаОзнакомлении - Булево - Признак необходимости ознакомиться с электронным документом.
//    * СпособОбмена - ПеречислениеСсылка.СпособыОбменаЭД - Способ обмена электронным документом.
//    * ВидДокумента - СправочникСсылка.ВидыДокументовЭДО
//  Действия - Соответствие Из КлючИЗначение:
//    * Ключ - ПеречислениеСсылка.ДействияПоЭДО
//    * Значение - Булево
//
Процедура ПриОпределенииДействийДоступныхПоЭДО(Знач СостояниеДокумента, Знач СвойстваДокумента, Действия) Экспорт
	
	Если Не МиграцияДанныхИзВнешнихСистемСервер.БлокироватьОбменСВнешнимиРесурсами() Тогда
		Возврат;
	КонецЕсли;
	
	// Если по миграции у нас обмен продолжается в прошлой версии, то действия нельзя выполнять ни для какого документа
	Действия.Очистить();
	
КонецПроцедуры

#КонецОбласти

#Область НастройкиЭДО

// Вызывается при создании на сервере формы настроек отражения документов в ДО
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения
//  Отказ - Булево
//  СтандартнаяОбработка - Булево
//
Процедура НастройкиОтраженияДокументовВУчетеПриСозданииНаСервере(Форма, Отказ, СтандартнаяОбработка) Экспорт
	
	Если Не МиграцияДанныхИзВнешнихСистемСервер.БлокироватьОбменСВнешнимиРесурсами() Тогда
		Возврат;
	КонецЕсли;
	
	НастройкиОтраженияДокументовВУчетеУстановитьВидимостьДоступность(Форма);
	
КонецПроцедуры

// Вызывается при создании на сервере формы настроек отправки документов
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения
//  Отказ - Булево
//  СтандартнаяОбработка - Булево
//
Процедура НастройкиОтправкиДокументовПриСозданииНаСервере(Форма, Отказ, СтандартнаяОбработка) Экспорт
	
	Если Не МиграцияДанныхИзВнешнихСистемСервер.БлокироватьОбменСВнешнимиРесурсами() Тогда
		Возврат;
	КонецЕсли;
	
	НастройкиОтправкиДокументовУстановитьВидимостьДоступность(Форма);
	
КонецПроцедуры

// Вызывается при создании на сервере формы списка настроек ЭДО
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения
//  Отказ - Булево
//  СтандартнаяОбработка - Булево
//
Процедура СписокНастроекЭДОПриСозданииНаСервере(Форма, Отказ, СтандартнаяОбработка) Экспорт
	
	Если Не МиграцияДанныхИзВнешнихСистемСервер.БлокироватьОбменСВнешнимиРесурсами() Тогда
		Возврат;
	КонецЕсли;
	
	СписокНастроекЭДОУстановитьВидимостьДоступность(Форма);
	
КонецПроцедуры

#КонецОбласти

#Область ИнтерфейсДокументовЭДО

// Вызывается при создании на сервере формы текущих дел ЭДО
// 
// Параметры:
//  Форма - см. Обработка.ИнтерфейсДокументовЭДО.Форма.ТекущиеДелаПоЭДО
//  Отказ - Булево
//  СтандартнаяОбработка - Булево
//
Процедура ТекущиеДелаЭДОПриСозданииНаСервере(Форма, Отказ, СтандартнаяОбработка) Экспорт
	
	Если Не МиграцияДанныхИзВнешнихСистемСервер.БлокироватьОбменСВнешнимиРесурсами() Тогда
		Возврат;
	КонецЕсли;
	
	ТекущиеДелаЭДОУстановитьВидимостьДоступность(Форма);
	ТекущиеДелаЭДОУстановитьНедоступныеКоманды(Форма);
	
КонецПроцедуры

#КонецОбласти

#Область РегламентыеЗадания

// Перед регламентной проверкой наличия новых документов в сервисе ЭДО.
// 
// Параметры:
//  УчетныеЗаписиЭДО - Неопределено, Массив Из Строка - Учетные записи ЭДО, по которым необходимо проверить
//                                                      наличие новых документов.
//                                                      Неопределено - отбора не будет.
//                                                      Массив - будет отбор по учетным записям в массиве.
//
Процедура ПередРегламентнойПроверкойНаличияНовыхДокументовВСервисеЭДО(УчетныеЗаписиЭДО) Экспорт
	
	Если Не МиграцияДанныхИзВнешнихСистемСервер.БлокироватьОбменСВнешнимиРесурсами() Тогда
		Возврат;
	КонецЕсли;
	
	// Если блокирован обмен с внешними ресурсами, то проверку не проводим вообще не по каким учеткам.
	УчетныеЗаписиЭДО = Новый Массив();
	
КонецПроцедуры

#КонецОбласти

// Вызывается перед проверкой контрагента в сервисе 1С-ЭДО при записи контрагента
// 
// Параметры:
//  Источник - СправочникОбъект.Контрагенты
//  Отказ - Булево
//  ПроводитьПроверку - Булево
//
Процедура ПередПроверкойКонтрагентаВ1СЭДОПриЗаписи(Источник, Отказ, ПроводитьПроверку) Экспорт
	
	Если МиграцияДанныхИзВнешнихСистемСервер.БлокироватьОбменСВнешнимиРесурсами() Тогда
		ПроводитьПроверку = Ложь;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура СоздатьНастроитьНадписьОбменаВДругойВерсии(Форма, Заголовок, СледующийЭлемент)
	
	ИмяЭлемента = ОбменСКонтрагентамиДОМиграцияКлиентСервер.ИмяЭлементаНадписьОбменВДругойВерсии();
	
	Элемент = Форма.Элементы.Найти(ИмяЭлемента);
	Если Элемент = Неопределено Тогда
		Элемент = Форма.Элементы.Вставить(ИмяЭлемента, Тип("ДекорацияФормы"),
			Форма, СледующийЭлемент);
	КонецЕсли;
	
	Элемент.Вид = ВидДекорацииФормы.Надпись;
	Элемент.Заголовок = Заголовок;
	Элемент.ЦветФона = ЦветаСтиля.ОтсутствиеЦветФона;
	Элемент.РастягиватьПоГоризонтали = Истина;
	Элемент.АвтоМаксимальнаяШирина = Ложь;
	
КонецПроцедуры

#Область УчетныеЗаписиЭДО

Процедура УчетнаяЗаписьЭДОУстановитьВидимостьДоступность(Форма)
	
	Элементы = Форма.Элементы;
	
	Форма.ТолькоПросмотр = Истина;
	
	Элементы.ИзменитьАдресОрганизации.Доступность = Ложь;
	Элементы.ИзменитьКодНалоговогоОргана.Доступность = Ложь;
	Элементы.ФормаПроверкаУчетнойЗаписи.Доступность = Ложь;
	
	ТекстПредупреждения =
		ОбменСКонтрагентамиДОМиграцияКлиентСервер.ТекстДекорацииОБлокировкеПриМиграции();
	СоздатьНастроитьНадписьОбменаВДругойВерсии(Форма, ТекстПредупреждения, Элементы.ПанельКонтекстныхНовостей);
	
КонецПроцедуры

Процедура СписокУчетныхЗаписейЭДОУстановитьВидимостьДоступность(Форма)
	
	Элементы = Форма.Элементы;
	
	Форма.ТолькоПросмотр = Истина;
	
	Элементы.СоздатьОбменЧерезСервисЭДО.Доступность = Ложь;
	Элементы.СоздатьПрямойОбмен.Доступность = Ложь;
	
	ТекстПредупреждения =
		ОбменСКонтрагентамиДОМиграцияКлиентСервер.ТекстДекорацииОБлокировкеПриМиграции();
	СоздатьНастроитьНадписьОбменаВДругойВерсии(Форма, ТекстПредупреждения, Элементы.ПанельКонтекстныхНовостей);
	
КонецПроцедуры

#КонецОбласти

#Область ЭлектронныеДокументыЭДО

Процедура ЭлектронныйДокументИсходящийЭДОУстановитьВидимостьДоступность(Форма)
	
	Элементы = Форма.Элементы;
	
	Форма.ТолькоПросмотр = Истина;
	
	ТекстПредупреждения =
		ОбменСКонтрагентамиДОМиграцияКлиентСервер.ТекстДекорацииОБлокировкеПриМиграции();
	СоздатьНастроитьНадписьОбменаВДругойВерсии(Форма, ТекстПредупреждения, Элементы.ГруппаОсновное);
	
КонецПроцедуры

Процедура ЭлектронныйДокументВходящийЭДОУстановитьВидимостьДоступность(ТолькоПросмотр)
	
	ТолькоПросмотр = Истина;
	
	ТекстПредупреждения =
		ОбменСКонтрагентамиДОМиграцияКлиентСервер.ТекстДекорацииОБлокировкеПриМиграции();
	Сообщить(ТекстПредупреждения);
	
КонецПроцедуры

#КонецОбласти

#Область НастройкиЭДО

Процедура НастройкиОтраженияДокументовВУчетеУстановитьВидимостьДоступность(Форма)
	
	Элементы = Форма.Элементы;
	
	Форма.ТолькоПросмотр = Истина;
	
	Элементы.ФормаУдалить.Доступность = Ложь;
	
	ТекстПредупреждения =
		ОбменСКонтрагентамиДОМиграцияКлиентСервер.ТекстДекорацииОБлокировкеПриМиграции();
	СоздатьНастроитьНадписьОбменаВДругойВерсии(Форма, ТекстПредупреждения, Элементы.ПанельКонтекстныхНовостей);
	
КонецПроцедуры

Процедура НастройкиОтправкиДокументовУстановитьВидимостьДоступность(Форма)
	
	Элементы = Форма.Элементы;
	
	Форма.ТолькоПросмотр = Истина;
	
	Элементы.ФормаОтправитьКаталогПоСоглашению.Доступность = Ложь;
	Элементы.ФормаСформироватьСоглашениеПоШаблону.Доступность = Ложь;
	Элементы.ФормаУдалить.Доступность = Ложь;
	
	ТекстПредупреждения =
		ОбменСКонтрагентамиДОМиграцияКлиентСервер.ТекстДекорацииОБлокировкеПриМиграции();
	СоздатьНастроитьНадписьОбменаВДругойВерсии(Форма, ТекстПредупреждения, Элементы.ПанельКонтекстныхНовостей);
	
КонецПроцедуры

Процедура СписокНастроекЭДОУстановитьВидимостьДоступность(Форма)
	
	Элементы = Форма.Элементы;
	
	Форма.ТолькоПросмотр = Истина;
	
	Элементы.СоздатьНастройкуВнутреннегоЭДО.Доступность = Ложь;
	Элементы.НастройкиОтправкиСоздатьНастройкуОтправки.Доступность = Ложь;
	Элементы.ИнтеркампаниСоздатьНастройкуИнтеркампани.Доступность = Ложь;
	Элементы.НастройкиОтправкиСоздатьНастройкуПрямогоОбмена.Доступность = Ложь;
	Элементы.НастройкиПолученияСоздатьНастройкуПолучения.Доступность = Ложь;
	Элементы.НастройкиОтправкиОбновитьСтатусыПодключения.Доступность = Ложь;
	
	ТекстПредупреждения =
		ОбменСКонтрагентамиДОМиграцияКлиентСервер.ТекстДекорацииОБлокировкеПриМиграции();
	СоздатьНастроитьНадписьОбменаВДругойВерсии(Форма, ТекстПредупреждения, Элементы.ПанельКонтекстныхНовостей);
	
КонецПроцедуры

#КонецОбласти

#Область ИнтерфейсДокументовЭДО

Процедура ТекущиеДелаЭДОУстановитьВидимостьДоступность(Форма)
	
	Элементы = Форма.Элементы;
	
	Форма.ТолькоПросмотр = Истина;
	
	Элементы.ОтправитьИПолучитьЭД.Видимость = Ложь;
	
	ТекстПредупреждения =
		ОбменСКонтрагентамиДОМиграцияКлиентСервер.ТекстДекорацииОБлокировкеПриМиграции();
	СоздатьНастроитьНадписьОбменаВДругойВерсии(Форма, ТекстПредупреждения, Элементы.ПанельКонтекстныхНовостей);
	
КонецПроцедуры

Процедура ТекущиеДелаЭДОУстановитьНедоступныеКоманды(Форма)
	
	НедоступныеКоманды = Форма.НедоступныеКоманды; // Массив Из Строка
	
	НедоступныеКоманды.Добавить("КомандыВходящиеЭД");
	НедоступныеКоманды.Добавить("КомандыИсходящиеЭД");
	НедоступныеКоманды.Добавить("КомандыСформироватьЭД");
	НедоступныеКоманды.Добавить("КомандыКонтейнерыЭД");
	НедоступныеКоманды.Добавить("КомандыПриглашенияЭД");
	НедоступныеКоманды.Добавить("Обновить");
	НедоступныеКоманды.Добавить("ЗакрытьПринудительно");
	НедоступныеКоманды.Добавить("Перенаправить");
	НедоступныеКоманды.Добавить("УтвердитьЭД");
	НедоступныеКоманды.Добавить("ПодписатьВх");
	НедоступныеКоманды.Добавить("ПодписатьИсх");
	НедоступныеКоманды.Добавить("ОтклонитьПодписаниеИсх");
	НедоступныеКоманды.Добавить("Аннулировать");
	НедоступныеКоманды.Добавить("ОтклонитьДокумент");
	НедоступныеКоманды.Добавить("ОтклонитьАннулирование");
	НедоступныеКоманды.Добавить("ИзменитьДокумент");
	НедоступныеКоманды.Добавить("СформироватьЭД");
	НедоступныеКоманды.Добавить("СоздатьПроизвольныйДокумент");
	НедоступныеКоманды.Добавить("ОтправитьВАрхив");
	НедоступныеКоманды.Добавить("ОтправитьЭД");
	НедоступныеКоманды.Добавить("ВернутьНаПодготовкуКОтправке");
	НедоступныеКоманды.Добавить("ОтменитьКонтейнеры");
	НедоступныеКоманды.Добавить("РаспаковатьКонтейнеры");
	НедоступныеКоманды.Добавить("ОтправитьПриглашения");
	НедоступныеКоманды.Добавить("ПринятьПриглашения");
	НедоступныеКоманды.Добавить("ОтклонитьПриглашения");
	НедоступныеКоманды.Добавить("УтвердитьПодписатьИОтправитьВх");
	НедоступныеКоманды.Добавить("УтвердитьПодписатьИОтправитьИсх");
	НедоступныеКоманды.Добавить("СформироватьПодписатьИОтправить");
	НедоступныеКоманды.Добавить("СопоставитьНоменклатуру");
	НедоступныеКоманды.Добавить("ОткрытьНоменклатуруКонтрагентов");
	НедоступныеКоманды.Добавить("ПровестиУчетныеДокументы");
	НедоступныеКоманды.Добавить("ВАрхивПриглашенияОтправить");
	НедоступныеКоманды.Добавить("ОтозватьПриглашение");
	НедоступныеКоманды.Добавить("ВходящиеЭДКонтекстноеМенюУтвердитьЭД");
	НедоступныеКоманды.Добавить("ВходящиеЭДКонтекстноеМенюОтклонитьАннулирование");
	НедоступныеКоманды.Добавить("ИсходящиеЭДКонтекстноеМенюОтклонитьПодписаниеИсх");
	НедоступныеКоманды.Добавить("ИсходящиеЭДКонтекстноеМенюОтклонитьАннулирование");
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
