////////////////////////////////////////////////////////////////////////////////
// Работа с быстрым поиском (сервер).
//
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Обновляет представление быстрого поиска. Возвращает представление фильтров
//
// Параметры:
//  РеквизитБыстрыйПоиск - ДанныеФормыКоллекция - Таблица "Быстрый поиск".
//  КнопкаСброситьОтбор - КнопкаФормы - Кнопка "Сбросить отбора"
//  
//  Возвращаемое значение -Строка  - представление фильтров
//
Функция ОбновитьПредставлениеБыстрогоПоиска(
	РеквизитБыстрыйПоиск, КнопкаСброситьОтбор) Экспорт
	
	ЕстьБыстрыйПоиск = Ложь;
	ПредставлениеФильтров = Новый ФорматированнаяСтрока("");
	Для Каждого Строка Из РеквизитБыстрыйПоиск Цикл
		
		Строка.ПредставлениеЗначения = ПредставлениеЗначения(Строка.Значение);

		Если ЗначениеЗаполнено(Строка.Значение) И Не Строка.Фиксированный Тогда
			
			ЕстьБыстрыйПоиск = Истина;
			
			Если Строка(ПредставлениеФильтров) <> "" Тогда
				ПредставлениеФильтров = Новый ФорматированнаяСтрока(ПредставлениеФильтров, ", ");
			КонецЕсли;
				
			ПредставлениеФильтров = Новый ФорматированнаяСтрока(
				ПредставлениеФильтров, 
				Новый ФорматированнаяСтрока(Строка.ПредставлениеПараметра, , ЦветаСтиля.ИнформационнаяНадпись),
				Новый ФорматированнаяСтрока(" " + Строка.ПредставлениеЗначения));
				 
		КонецЕсли;
		
	КонецЦикла;
	
	КнопкаСброситьОтбор.Видимость = ЕстьБыстрыйПоиск;
	
	Возврат ПредставлениеФильтров;
	
КонецФункции

// Сбрасываете быстрый поиск.
//
// Параметры:
//  РеквизитБыстрыйПоиск - ДанныеФормыКоллекция - Таблица "Быстрый поиск".
//  КнопкаСброситьОтбор - КнопкаФормы - Кнопка "Сбросить отбора"
//
Процедура СброситьОтборНаСервере(РеквизитБыстрыйПоиск, КнопкаСброситьОтбор) Экспорт
	
	Для Каждого Строка Из РеквизитБыстрыйПоиск Цикл
		
		Если Строка.Фиксированный Тогда
			Продолжить;
		КонецЕсли;
		
		Строка.Значение = Строка.ЗначениеПоУмолчанию;
		
	КонецЦикла;
	
	ОбновитьПредставлениеБыстрогоПоиска(РеквизитБыстрыйПоиск, КнопкаСброситьОтбор);
	
КонецПроцедуры

// Устанавливает условное оформление таблицы "Быстрый поиск".
// 
// Параметры:
//  УсловноеОформление - УсловноеОформлениеКомпоновкиДанных.
//  ТаблицаБыстрыйПоиск - ТаблицаФормы - Таблица "Быстрый поиск".
//  ЯчейкаЗначение - ПолеФормы - Ячейка "Значение" таблицы "Быстрый поиск".
//
Процедура УстановитьУсловноеОформление(УсловноеОформление, ТаблицаБыстрыйПоиск, ЯчейкаЗначение) Экспорт
	
	// Пустой текст значения.
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	Поля = Элемент.Поля.Элементы;
	Поля.Добавить().Поле = Новый ПолеКомпоновкиДанных(ЯчейкаЗначение.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных(ТаблицаБыстрыйПоиск.Имя + ".Значение");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Заполнено;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", "");
	
	// Фиксированная ячейка.
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	Поля = Элемент.Поля.Элементы;
	Поля.Добавить().Поле = Новый ПолеКомпоновкиДанных(ТаблицаБыстрыйПоиск.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных(ТаблицаБыстрыйПоиск.Имя + ".Фиксированный");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Истина;
	
	Элемент.Оформление.УстановитьЗначениеПараметра(
		"ЦветФона", ЦветаСтиля.ЦветФонаТолькоПросмотр);
	Элемент.Оформление.УстановитьЗначениеПараметра(
		"ТолькоПросмотр", Истина);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Формирует представление значения для быстрого поиска.
// 
// Параметры:
// 	Значение - Произвольный.
// 	
// Возвращаемое значение:
// 	Строка - Описание.
//
Функция ПредставлениеЗначения(Значение)
	
	ПредставлениеЗначения = "";
	Если ТипЗнч(Значение) = Тип("СписокЗначений") Тогда
		
		МассивПредставления = Новый Массив;
		Для Каждого ЭлементСписка Из Значение Цикл
			МассивПредставления.Добавить(ПредставлениеЗначения(ЭлементСписка.Значение));
		КонецЦикла;
		ПредставлениеЗначения = СтрСоединить(МассивПредставления, "; ");
		
	ИначеЕсли ТипЗнч(Значение) = Тип("Дата") Тогда
		
		ПредставлениеЗначения = Формат(Значение, "ДЛФ=D");
		
	ИначеЕсли ТипЗнч(Значение) = Тип("СправочникСсылка.Пользователи")
		Или ТипЗнч(Значение) = Тип("СправочникСсылка.Сотрудники") Тогда
		
		ПредставлениеЗначения = РаботаСЗадачамиПовтИсп.ПредставлениеУчастника(Строка(Значение));
		
	Иначе
		
		ПредставлениеЗначения = Строка(Значение);
		
	КонецЕсли;
	
	Возврат ПредставлениеЗначения;
	
КонецФункции

#КонецОбласти