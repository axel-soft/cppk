
#Область ПрограммныйИнтерфейс

Процедура ОбработкаКомандыЖТУНаОсновании(ПараметрКоманды, ПараметрыВыполненияКоманды, ВидЖурнала) Экспорт
	ПараметрыФормы = Новый Структура();
	
	Если ТТС_ЖурналыТУВызовСервера.ВидЖурнала(ПараметрыВыполненияКоманды.Источник.Объект.ВидДокумента).Журнал1 Тогда
		ДанныеЖурнала = ПараметрыВыполненияКоманды.Источник.Элементы.ТТС_ДанныеЖурнала;
		ТекущаяСтрокаЖурнала = ДанныеЖурнала.ТекущиеДанные;
		Если ТекущаяСтрокаЖурнала = Неопределено Тогда
			ПоказатьПредупреждение(,НСтр("ru = 'Не выбрана строка журнала'"));
			Возврат;
		КонецЕсли;
	Иначе
		ДанныеЖурнала = ПараметрыВыполненияКоманды.Источник.ТТС_ДанныеЖурнала;
		Если ДанныеЖурнала.Количество()>0 Тогда
			ТекущаяСтрокаЖурнала = ДанныеЖурнала[0];
		Иначе
			ПоказатьПредупреждение(,НСтр("ru = 'Нет данных журнала'"));
			Возврат;
		КонецЕсли;
	КонецЕсли;
	ИДСтроки = ТекущаяСтрокаЖурнала.ИДСтроки;
	
	Основание = ТТС_ЖурналыТУВызовСервера.ПолучитьЖурналОснование(ИДСтроки);
	Если НЕ ЗначениеЗаполнено(Основание) Тогда
		ПоказатьПредупреждение(,НСтр("ru = 'Не найден журнал-основание'"));
		Возврат;
	КонецЕсли;
	
	Если НЕ ТТСВызовСервера.ДокументЗарегистрирован(Основание) Тогда
		ПоказатьПредупреждение(,НСтр("ru = 'Журнал-основание не зарегистрирован'"));
		Возврат;
	КонецЕсли;
	
	ШаблонЖТУ = ТТС_ЖурналыТУВызовСервера.ПолучитьШаблонЖТУ(ВидЖурнала);
	Если ШаблонЖТУ = Неопределено Тогда
		НомерЖурнала = ?(ВидЖурнала = "ЖТУ2", "№2", "№3");
		ПоказатьПредупреждение(,"Не найден шаблон журнала ТУ-133 "+НомерЖурнала);
		Возврат;
	КонецЕсли;
	
	ПараметрыФормы.Вставить("ШаблонДокумента", ШаблонЖТУ);
	ПараметрыФормы.Вставить("ТТС_ИДСтрокиЖурнала", ИДСтроки);
	ПараметрыФормы.Вставить("Основание", Основание);
	
	Форма = Открытьформу("Справочник.ДокументыПредприятия.ФормаОбъекта", ПараметрыФормы, ПараметрыВыполненияКоманды.Источник, ПараметрыВыполненияКоманды.Уникальность);
	
КонецПроцедуры

Процедура УстановитьОтметкуОПроверке(Форма, СЗамечаниями) Экспорт
	РольПроверяющего = ТТС_ЖурналыТУВызовСервера.ПолучитьРольПроверяющего(ПользователиКлиент.ТекущийПользователь());
	Если ТТС_ЖурналыТУВызовСервера.ЖурналПроверен(Форма.Объект.Ссылка, РольПроверяющего) Тогда
		ТекстСообщения = НСтр("ru = 'Журнал уже проверен'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения,,,,);
		Форма.Элементы.ТТС_ГруппаПроверено.Видимость = Ложь;
		Возврат;
	КонецЕсли;
	Параметры = Новый Структура("Форма, СЗамечаниями, Перезаписывать", Форма, СЗамечаниями, Ложь);
	Если СЗамечаниями Тогда
		УстановитьОтметкуОПроверкеПродолжение(КодВозвратаДиалога.Да, Параметры);
	Иначе
		ОписаниеОповещения = Новый ОписаниеОповещения(
			"УстановитьОтметкуОПроверкеПродолжение",
			ЭтотОбъект, Параметры);
		ТекстВопроса = НСтр("ru = 'Оставить комментарий?'");	
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНетОтмена);
	КонецЕсли;
КонецПроцедуры

Процедура УстановитьОтметкуОПроверкеПродолжение(Результат, Параметры) Экспорт
	Если Результат = КодВозвратаДиалога.Отмена Тогда
		Возврат;
	ИначеЕсли Результат = КодВозвратаДиалога.Нет Тогда
		УстановитьОтметкуОПроверкеЗавершить(Параметры);
	Иначе
		ОписаниеОповещения = Новый ОписаниеОповещения(
			"УстановитьОтметкуОПроверкеВводЗамечания",
			ЭтотОбъект, Параметры);
		Подсказка = ?(Параметры.СЗамечаниями, НСтр("ru = 'Введите замечание'"), НСтр("ru = 'Введите комментарий'"));	
		ПоказатьВводСтроки (ОписаниеОповещения, ,Подсказка);
	КонецЕсли;
КонецПроцедуры

Процедура УстановитьОтметкуОПроверкеВводЗамечания(Строка, Параметры) Экспорт
	Если Строка = Неопределено Тогда
		Возврат;
	Иначе
		УстановитьОтметкуОПроверкеЗавершить(Параметры, Строка);
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура УстановитьОтметкуОПроверкеЗавершить(Параметры, Замечание = "")
	ТТС_ЖурналыТУВызовСервера.УстановитьОтметкуОПроверке(Параметры.Форма.Объект.Ссылка, Параметры.СЗамечаниями, Замечание);
	Параметры.Форма.Элементы.ТТС_ГруппаПроверено.Видимость = Ложь;
КонецПроцедуры

#КонецОбласти