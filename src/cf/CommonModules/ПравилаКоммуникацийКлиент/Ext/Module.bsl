
#Область ПрограммныйИнтерфейс

// Выводит предупреждение о выборе элемента недоступного по правилам с возможностью детализации по
// нарушеному правилу и точной ссылки на выбранный элемент.
//
// Параметры:
//	Значение - СправочникСсылка - Выбранное значение.
//	Правило - СправочникСсылка.ПравилаКоммуникаций - Нарушенное правило.
//
Процедура ПоказатьНарушение(Значение, Правило) Экспорт

	// Форма может быть открыта второй раз подряд, т.к. в форме-владельце могут сработать 2 обработчика.
	// (ОкончаниеВводаТекста и ОбработкаВыбора), поэтому проверка уже открытой формы:
	ИмяФормы = "Справочник.ПравилаКоммуникаций.Форма.ФормаПредупреждения";
	Форма = ОбщегоНазначенияДокументооборотКлиент.НайтиФормуПоИмени(ИмяФормы);
	Если Форма <> Неопределено Тогда
		Форма.Активизировать();
	Иначе
		ОткрытьФорму(
			ИмяФормы,
			Новый Структура("Значение, Правило", Значение, Правило), ,
			Новый УникальныйИдентификатор);
	КонецЕсли;
	
КонецПроцедуры

// Выводит предупреждение о наличии нескольких участников недоступных по правилам с возможностью детализации по
// нарушенному правилу. Подробности выводятся в виде HTML документа с гиперссылками на участников и правила.
// В зависимости от выбранной пользователем команды в результат закрытия может передаваться 1, 2 или Неопределено.
// Обработка различных сценариев закрытие производится в процедуре ОписанияОповещенияОЗакрытии.
// 
// Параметры:
//  Нарушения - Массив Из Структура - Массив структур со следующими ключами:
//   * ВидУчастника	- СправочникСсылка.ВидыУчастниковЗадач - Вид участника задачи.
//   * Участник	- ОпределяемыйТип.УчастникЗадач - Участник задачи.
//   * Правило	- СправочникСсылка.ПравилаКоммуникаций - Правило коммуникаций.
//  НаименованиеПредмета - Строка - Наименование вида предмета, по которому показывается нарушение. Например: "Задача". 
//  НаименованиеДействия - Строка - Наименование действия по предмету, Например: "Направить на исполнение".
//  ОписаниеОповещенияОЗакрытии - ОписаниеОповещения - Описание оповещения о закрытии формы.
//
Процедура ПоказатьНесколькоНарушений(Нарушения, НаименованиеПредмета, НаименованиеДействия,
	ОписаниеОповещенияОЗакрытии = Неопределено) Экспорт
	
	СписокДляПараметраФормы = Новый СписокЗначений;
	СписокДляПараметраФормы.ЗагрузитьЗначения(Нарушения);
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Нарушения", СписокДляПараметраФормы);
	ПараметрыФормы.Вставить("НаименованиеПредмета", НаименованиеПредмета);
	ПараметрыФормы.Вставить("НаименованиеДействия", НаименованиеДействия);
	
	ОткрытьФорму("Справочник.ПравилаКоммуникаций.Форма.ФормаПредупрежденияПоНескольким",
		ПараметрыФормы, , Новый УникальныйИдентификатор,,, ОписаниеОповещенияОЗакрытии);
		
КонецПроцедуры

#КонецОбласти

