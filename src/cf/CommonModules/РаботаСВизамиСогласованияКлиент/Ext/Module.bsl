#Область ПрограммныйИнтерфейс

// Выполняет проверки перед началом добавления визы
//
Процедура ДобавитьВизу(ЭтаФорма) Экспорт 
	
	Документ = ЭтаФорма.Объект;
	
	ПараметрыОповещения = Новый Структура;
	ПараметрыОповещения.Вставить("ЭтаФорма", ЭтаФорма);
	ПараметрыОповещения.Вставить("Документ", Документ);
	
	ОписаниеОповещения = Новый ОписаниеОповещения(
		"ДобавитьВизуПродолжение",
		ЭтотОбъект,
		ПараметрыОповещения);

	Если Не ЗначениеЗаполнено(Документ.Ссылка) Тогда 
		ТекстВопроса = НСтр("ru = 'Для добавления визы согласования необходимо записать документ.
			|Выполнить запись?'");
		
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
	Иначе 
		ВыполнитьОбработкуОповещения(ОписаниеОповещения, КодВозвратаДиалога.ОК);
	КонецЕсли;
		
КонецПроцедуры

Процедура ДобавитьВизуПродолжение(Результат, Параметры) Экспорт 

	Документ = Параметры.Документ;
	ЭтаФорма = Параметры.ЭтаФорма;
	
	Если Результат = КодВозвратаДиалога.Нет Тогда 
		Возврат;
	ИначеЕсли Результат = КодВозвратаДиалога.Да Тогда 	
		
		Если Не ЭтаФорма.Записать() Тогда 
			Возврат;
		КонецЕсли;
		
		ПоказатьОповещениеПользователя(
			НСтр("ru = 'Создание:'"), 
			ПолучитьНавигационнуюСсылку(Документ.Ссылка),
			Строка(Документ.Ссылка),
			БиблиотекаКартинок.Информация32);	
	КонецЕсли;	

	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Документ", Документ.Ссылка);
	ОткрытьФорму("Справочник.ВизыСогласования.Форма.ФормаЭлемента", ПараметрыФормы, Параметры.ЭтаФорма);
	
КонецПроцедуры

// Выполняет проверки перед началом изменения визы
//
Процедура ИзменитьВизу(ЭтаФорма) Экспорт 
	
	Виза = ЭтаФорма.Элементы.ВизыСогласования.ТекущиеДанные;
	Документ = ЭтаФорма.Объект;
	
	ПараметрыОповещения = Новый Структура;
	ПараметрыОповещения.Вставить("ЭтаФорма", ЭтаФорма);
	ПараметрыОповещения.Вставить("Документ", Документ);
	ПараметрыОповещения.Вставить("Виза", Виза);
	
	ОписаниеОповещения = Новый ОписаниеОповещения(
		"ИзменитьВизуПродолжение",
		ЭтотОбъект,
		ПараметрыОповещения);

	Если Не ЗначениеЗаполнено(Документ.Ссылка) Тогда 
		
		ИндексВыбраннойВизы = ЭтаФорма.Элементы.ВизыСогласования.ВыделенныеСтроки[0];
		
		ТекстВопроса = НСтр("ru = 'Для изменения визы согласования необходимо записать документ.
			|Выполнить запись?'");
		
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
	Иначе 
		ВыполнитьОбработкуОповещения(ОписаниеОповещения, КодВозвратаДиалога.ОК);
	КонецЕсли;
		
КонецПроцедуры	

Процедура ИзменитьВизуПродолжение(Результат, Параметры) Экспорт 

	Документ = Параметры.Документ;
	ЭтаФорма = Параметры.ЭтаФорма;
	Виза = Параметры.Виза;
	
	Если Результат = КодВозвратаДиалога.Нет Тогда 
		Возврат;
	ИначеЕсли Результат = КодВозвратаДиалога.Да Тогда 	
		
		Если Не ЭтаФорма.Записать() Тогда 
			Возврат;
		КонецЕсли;
		
		ПоказатьОповещениеПользователя(
			НСтр("ru = 'Создание:'"), 
			ПолучитьНавигационнуюСсылку(Документ.Ссылка),
			Строка(Документ.Ссылка),
			БиблиотекаКартинок.Информация32);	
	КонецЕсли;	

	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Ключ", Виза.Ссылка);
	ПараметрыФормы.Вставить("Документ", Документ.Ссылка);
	ОткрытьФорму("Справочник.ВизыСогласования.Форма.ФормаЭлемента", ПараметрыФормы, ЭтаФорма);
	
КонецПроцедуры

// Возвращает структуру реквизитов визы, которые будут подписаны
//
Функция ПолучитьСтруктуруВизДляПодписания() Экспорт
	
	Возврат Новый Структура("Документ,
		|ДатаИсполнения,
		|РезультатСогласования,
		|Исполнитель");
	
КонецФункции

// Открывает форму версий согласованных файлов по навигационной ссылке на визу
// 
// Параметры:
//  НавигационнаяСсылка - Строка - Навигационная ссылка на визу согласования
//  Форма - ФормаКлиентскогоПриложения - Форма-владелец из которой вызывается открытие списка версий
Процедура ОткрытьВерсииСогласованныхФайловПоСсылке(НавигационнаяСсылка, Форма) Экспорт
	
	ВизаСогласования = РаботаС_HTMLВызовСервера.СсылкаПоНавигационной(НавигационнаяСсылка);
	Если Не ЗначениеЗаполнено(ВизаСогласования) Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыОткрытия = Новый Структура();
	ПараметрыОткрытия.Вставить("ВизаСогласования", ВизаСогласования);
	ОткрытьФорму("РегистрСведений.ВерсииСогласованныхФайлов.Форма.ВерсииСогласованныхФайловПоВизе",
		ПараметрыОткрытия, Форма);
	
КонецПроцедуры

#КонецОбласти