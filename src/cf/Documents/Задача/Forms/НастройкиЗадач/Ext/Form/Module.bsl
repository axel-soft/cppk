#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗагрузитьНастройки();
	
	ЭтоМобильныйКлиент = ПараметрыСеанса.ЭтоМобильныйКлиент;
	Если ЭтоМобильныйКлиент Тогда
		МК_НастроитьЭлементыФормы();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Готово(Команда)
	
	СохранитьНастройки();
	
	ОбновитьПовторноИспользуемыеЗначения();
	
	Оповестить("ИзмененыНастройкиЗадач");
	
	Закрыть();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗагрузитьНастройки()
	
	СрокПоУмолчанию =
		РаботаСЗадачами.ПолучитьПерсональнуюНастройку("СрокПоУмолчанию");
	ОтображениеКарточкиЗадачи =
		РаботаСЗадачами.ПолучитьПерсональнуюНастройку("ОтображениеКарточкиЗадачи");
	
	ФлагПоУмолчанию = РаботаСФлагамиОбъектовСервер.ПолучитьФлагДляЗадачПоУмолчанию();
	
	ПерсональныеНастройкиУчетаВремени =
		УчетВремени.ПолучитьПерсональныеНастройкиУчетаВремениСервер();
	ДобавлятьРаботуВЕжедневныйОтчетПриВыполненииЗадачи =
		ПерсональныеНастройкиУчетаВремени.ДобавлятьРаботуВЕжедневныйОтчетПриВыполненииЗадачи;
	
	Элементы.ДобавлятьРаботуВЕжедневныйОтчетПриВыполненииЗадачи.Видимость =
		ПолучитьФункциональнуюОпцию("ВестиУчетФактическихТрудозатрат")
		И ПолучитьФункциональнуюОпцию("ИспользоватьЕжедневныеОтчеты");
	
КонецПроцедуры

&НаСервере
Процедура СохранитьНастройки()
	
	РаботаСЗадачами.УстановитьПерсональнуюНастройку(
		"СрокПоУмолчанию",
		СрокПоУмолчанию);
	РаботаСЗадачами.УстановитьПерсональнуюНастройку(
		"ОтображениеКарточкиЗадачи",
		ОтображениеКарточкиЗадачи);
	
	ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(
		"ФлагиОбъектов",
		"ФлагПоУмолчаниюДляЗадач",
		ФлагПоУмолчанию);
	
	ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(
		"НастройкиУчетаВремени",
		"ДобавлятьРаботуВЕжедневныйОтчетПриВыполненииЗадачи",
		ДобавлятьРаботуВЕжедневныйОтчетПриВыполненииЗадачи);
	
КонецПроцедуры

#Область СлужебныеПроцедурыИФункции_МобильныйКлиент

&НаСервере
Процедура МК_НастроитьЭлементыФормы()
	
	СворачиваниеЭлементовПоВажности = СворачиваниеЭлементовФормыПоВажности.НеИспользовать;
	ВертикальныйИнтервал = ИнтервалМеждуЭлементамиФормы.Половинный;
	
	Элементы.МК_НижнийБлок.Видимость = Истина;
	Элементы.Переместить(Элементы.ФормаГотово, Элементы.МК_НижнийБлок);
	Элементы.Переместить(Элементы.ФормаОтмена, Элементы.МК_НижнийБлок);
	
	МК.ОформитьАкцентнуюКнопку(Элементы.ФормаГотово);
	МК.ОформитьАкцентнуюКнопку(Элементы.ФормаОтмена);
	
	Элементы.ФормаОтмена.ЦветФона = ЦветаСтиля.ОтметкаОтрицательногоВыполненияЗадачи;
	Элементы.ФормаОтмена.ЦветРамки = ЦветаСтиля.ОтметкаОтрицательногоВыполненияЗадачи;
	
	МК.ВыровнятьЭлементФормы(Элементы.СрокПоУмолчанию);
	МК.ВыровнятьЭлементФормы(Элементы.ФлагПоУмолчанию);
	МК.ВыровнятьЭлементФормы(Элементы.ОтображениеКарточкиЗадачи);
	
	Элементы.ДобавлятьРаботуВЕжедневныйОтчетПриВыполненииЗадачи.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Лево;
	Элементы.ДобавлятьРаботуВЕжедневныйОтчетПриВыполненииЗадачи.ГоризонтальноеПоложение = ГоризонтальноеПоложениеЭлемента.Право;
	
	Элементы.СрокПоУмолчанию.КнопкаРегулирования = Ложь;
	
	Элементы.ОтображениеКарточкиЗадачи.Видимость = Ложь;

КонецПроцедуры
	
#КонецОбласти

#КонецОбласти
