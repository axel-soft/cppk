#Область ОбработчикиСобытий

&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	Если Не ЗначениеЗаполнено(ПараметрКоманды) Тогда
		ПоказатьПредупреждение(
			,НСтр("ru = 'Команда не может быть выполнена для указанного объекта.'"));
		Возврат;
	КонецЕсли;
	
	ИсточникЗадачи = ИсточникЗадачи(ПараметрКоманды);
	Если ЗначениеЗаполнено(ИсточникЗадачи) Тогда
		ВызватьИсключение НСтр("ru = 'Копирование задач процессов запрещено.'");
	КонецЕсли;
	
	Если ТипЗнч(ПараметрыВыполненияКоманды.Источник) = Тип("ФормаКлиентскогоПриложения")
		И ПараметрыВыполненияКоманды.Источник.ИмяФормы = "Документ.Задача.Форма.ФормаЭлемента" Тогда
		РежимОткрытияОкна = РежимОткрытияОкнаФормы.БлокироватьОкноВладельца;
	Иначе
		РежимОткрытияОкна = РежимОткрытияОкнаФормы.Независимый;
	КонецЕсли;
	
	ОткрытьФорму(
		"Документ.Задача.ФормаОбъекта",
		Новый Структура("Основание", Новый Структура("Основание, РежимКопирования", ПараметрКоманды, Истина)),
		ПараметрыВыполненияКоманды.Источник,
		ПараметрыВыполненияКоманды.Уникальность,
		ПараметрыВыполненияКоманды.Окно,
		ПараметрыВыполненияКоманды.НавигационнаяСсылка,,
		РежимОткрытияОкна);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ИсточникЗадачи(Задача)
	
	Возврат ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Задача, "Источник");
	
КонецФункции

#КонецОбласти