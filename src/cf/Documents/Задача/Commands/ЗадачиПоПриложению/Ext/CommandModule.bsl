#Область ОбработчикиСобытий

&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	КлючевыеОперации = "ЗадачиОткрытиеФормыЗадачиПоПриложению";
	ОценкаПроизводительностиКлиент.ЗамерВремени(КлючевыеОперации);
	
	Если ПараметрыВыполненияКоманды.Параметры <> Неопределено
		И ПараметрыВыполненияКоманды.Параметры.Свойство("ПараметрКоманды") Тогда
		
		ПараметрКоманды = ПараметрыВыполненияКоманды.Параметры.ПараметрКоманды;
		
	КонецЕсли;
	
	НавигационнаяСсылкаСПараметром = НавигационнаяСсылкаСПараметром(ПараметрКоманды);
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("КлючНазначенияИспользования", "ЗадачиПоПриложению");
	ПараметрыФормы.Вставить("Приложение", ПараметрКоманды);
	
	КлючУникальности =
		ПараметрыФормы.КлючНазначенияИспользования
		+ ПолучитьНавигационнуюСсылку(ПараметрКоманды);
	
	ОткрытьФорму("Документ.Задача.Форма.Задачи",
		ПараметрыФормы,
		ПараметрыВыполненияКоманды.Источник,
		КлючУникальности,
		ПараметрыВыполненияКоманды.Окно,
		НавигационнаяСсылкаСПараметром);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция НавигационнаяСсылкаСПараметром(Знач ПараметрКоманды)
	
	КлючОбъекта = "НавигационнаяСсылкаСПараметром";
	
	КлючНастроек = СтрШаблон(
		"ЗадачиПоПриложению/%1",
		ПолучитьНавигационнуюСсылку(ПараметрКоманды));
	
	НавигационнаяСсылкаКоманды = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
		КлючОбъекта,
		КлючНастроек);
	
	Если НавигационнаяСсылкаКоманды = Неопределено Тогда
		
		ПредставлениеНавигационнойСсылкиКоманды  = СтрШаблон(
			"%1 ""%2""",
			Метаданные.Документы.Задача.Команды.ЗадачиПоПриложению.Синоним,
			Строка(ПараметрКоманды));
		
		ВнешниеДанныеНавигационнойСсылкиКоманды = Новый Структура("ПараметрКоманды", ПараметрКоманды);
		
		НавигационнаяСсылкаКоманды = ПолучитьНавигационнуюСсылку(
			Метаданные.Документы.Задача.Команды.ЗадачиПоПриложению,
			ПараметрКоманды,
			ПредставлениеНавигационнойСсылкиКоманды,
			ВнешниеДанныеНавигационнойСсылкиКоманды);
		
		ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(
			КлючОбъекта,
			КлючНастроек,
			НавигационнаяСсылкаКоманды);
		
	КонецЕсли;
	
	Возврат НавигационнаяСсылкаКоманды;
	
КонецФункции

#КонецОбласти