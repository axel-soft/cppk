
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Объект.Ссылка.Пустая() Тогда 
		Если Параметры.Свойство("Основание") И ЗначениеЗаполнено(Параметры.Основание) Тогда 
			Объект.Основание = Параметры.Основание;
		КонецЕсли;
		
		УчетВходящейКорреспонденции = Ложь; УчетИсходящейКорреспонденции = Ложь;
		Если Параметры.Свойство("УчетВходящейКорреспонденции") И Параметры.Свойство("УчетИсходящейКорреспонденции") Тогда 
			УчетВходящейКорреспонденции = Параметры.УчетВходящейКорреспонденции;
			УчетИсходящейКорреспонденции = Параметры.УчетИсходящейКорреспонденции;
		ИначеЕсли ЗначениеЗаполнено(Объект.Основание) Тогда 
			Реквизиты = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Объект.Основание,
				"ВидДокумента.ЯвляетсяВходящейКорреспонденцией, ВидДокумента.ЯвляетсяИсходящейКорреспонденцией");
			УчетВходящейКорреспонденции = Реквизиты.ВидДокументаЯвляетсяВходящейКорреспонденцией;
			УчетИсходящейКорреспонденции = Реквизиты.ВидДокументаЯвляетсяИсходящейКорреспонденцией;
		КонецЕсли;
		
		Если УчетВходящейКорреспонденции = Истина И Не УчетИсходящейКорреспонденции = Истина Тогда 
			Объект.ВидКорреспонденции = Перечисления.ВидыКорреспонденции.Входящая;
			Элементы.ВидКорреспонденции.Доступность = Ложь;
		ИначеЕсли Не УчетВходящейКорреспонденции = Истина И УчетИсходящейКорреспонденции = Истина Тогда 
			Объект.ВидКорреспонденции = Перечисления.ВидыКорреспонденции.Исходящая;
			Элементы.ВидКорреспонденции.Доступность = Ложь;
		ИначеЕсли Не УчетВходящейКорреспонденции = Истина И Не УчетИсходящейКорреспонденции = Истина Тогда 
			СтандартнаяОбработка = Ложь;
			Возврат;
		КонецЕсли;
	Иначе 
		Элементы.ВидКорреспонденции.Доступность = Ложь;
		Элементы.Основание.Доступность = Ложь;
	КонецЕсли;
	
	Если Объект.Корреспонденты.Количество() > 0 Тогда
		ЗаполнитьЗначенияСвойств(
			ЭтаФорма,
			Объект.Корреспонденты[0],
			"Отправлен, 
			|ДатаОтправки, 
			|СпособОтправки"); 
	КонецЕсли;
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	УстановитьВидимостьЭлементов();
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	ТекущийОбъект.Корреспонденты.Очистить();
	РеквизитыОснования = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Объект.Основание,
		"ВидДокумента.ВестиУчетСторон, ВидДокумента.ВестиУчетПоКонтрагентам, Контрагенты, Стороны");
	Если РеквизитыОснования.ВидДокументаВестиУчетСторон = Истина Тогда
		ВыборкаСтороны = РеквизитыОснования.Стороны.Выбрать();
		Пока ВыборкаСтороны.Следующий() Цикл 
			Если ЗначениеЗаполнено(ВыборкаСтороны.Сторона) 
				И ТипЗнч(ВыборкаСтороны.Сторона) = Тип("СправочникСсылка.Контрагенты")
				И ЗначениеЗаполнено(ВыборкаСтороны.Сторона) Тогда 
				НоваяСтрока = ТекущийОбъект.Корреспонденты.Добавить();
				НоваяСтрока.Корреспондент = ВыборкаСтороны.Сторона;
				НоваяСтрока.СпособОтправки = СпособОтправки;
				НоваяСтрока.Отправлен = Отправлен;
				НоваяСтрока.ДатаОтправки = ДатаОтправки;
			КонецЕсли;
		КонецЦикла;
		
	ИначеЕсли РеквизитыОснования.ВидДокументаВестиУчетПоКонтрагентам = Истина Тогда
		ВыборкаКонтрагенты = РеквизитыОснования.Контрагенты.Выбрать();
		Пока ВыборкаКонтрагенты.Следующий() Цикл
			НоваяСтрока = ТекущийОбъект.Корреспонденты.Добавить();
			НоваяСтрока.Корреспондент = ВыборкаКонтрагенты.Контрагент;
			НоваяСтрока.СпособОтправки = СпособОтправки;
			НоваяСтрока.Отправлен = Отправлен;
			НоваяСтрока.ДатаОтправки = ДатаОтправки;
		КонецЦикла;
	КонецЕсли;
	
	ПараметрыЗаписи.Вставить("ЭтоНовыйОбъект", НЕ ЗначениеЗаполнено(ТекущийОбъект.Ссылка));
	
	Если ЗначениеЗаполнено(ТекущийОбъект.Ссылка) Тогда 
		РеквизитыПоСсылке = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ТекущийОбъект.Ссылка, 
			"РегистрационныйНомер, ДатаРегистрации, НомерКонтрагента, ДатаКонтрагента")
	Иначе 
		РеквизитыПоСсылке = Новый Структура;
		РеквизитыПоСсылке.Вставить("РегистрационныйНомер", "");
		РеквизитыПоСсылке.Вставить("ДатаРегистрации", Дата(1, 1, 1));
		РеквизитыПоСсылке.Вставить("НомерКонтрагента", "");
		РеквизитыПоСсылке.Вставить("ДатаКонтрагента", Дата(1, 1, 1));
	КонецЕсли;
	
	Если Не ПараметрыЗаписи.ЭтоНовыйОбъект И Модифицированность 
		И (ТекущийОбъект.РегистрационныйНомер <> РеквизитыПоСсылке.РегистрационныйНомер
			Или ТекущийОбъект.ДатаРегистрации <> РеквизитыПоСсылке.ДатаРегистрации
			Или ТекущийОбъект.НомерКонтрагента <> РеквизитыПоСсылке.НомерКонтрагента
			Или ТекущийОбъект.ДатаКонтрагента <> РеквизитыПоСсылке.ДатаКонтрагента) Тогда 
		ПараметрыЗаписи.Вставить("ИзменилисьРеквизиты", Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	Если ПараметрыЗаписи.ЭтоНовыйОбъект Или 
		ПараметрыЗаписи.Свойство("ИзменилисьРеквизиты") И ПараметрыЗаписи.ИзменилисьРеквизиты Тогда 
		СтруктураОповещения = Новый Структура("Документ, Корреспонденция", Объект.Основание, Объект.Ссылка);
		Оповестить("ЗаписанаКорреспонденция", СтруктураОповещения); 
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьВидимостьЭлементов()
	
	Если Объект.ВидКорреспонденции = ПредопределенноеЗначение("Перечисление.ВидыКорреспонденции.Входящая") Тогда 
		Элементы.НомерКонтрагента.Заголовок = НСтр("ru = 'Исходящий №'");
		Элементы.СпособПолучения.Видимость = Истина;
		Элементы.СпособОтправки.Видимость = Ложь;
		Элементы.ГруппаОтправленДата.Видимость = Ложь;
	Иначе 
		Элементы.НомерКонтрагента.Заголовок = НСтр("ru = '№ получателя'");
		Элементы.СпособПолучения.Видимость = Ложь;
		Элементы.СпособОтправки.Видимость = Истина;
		Элементы.ГруппаОтправленДата.Видимость = Истина;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВидКорреспонденцииПриИзменении(Элемент)
	
	УстановитьВидимостьЭлементов();
	
КонецПроцедуры
