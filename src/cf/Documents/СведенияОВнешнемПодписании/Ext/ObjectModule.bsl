// @strict-types

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Дата = ТекущаяДатаСеанса();
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда
		
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, ДанныеЗаполнения, , "ДополнительныеПредставления");
		
		ДополнительныеПредставленияДанных = Неопределено;
		ДанныеЗаполнения.Свойство("ДополнительныеПредставления", ДополнительныеПредставленияДанных);
		Если ДополнительныеПредставленияДанных <> Неопределено 
			И ТипЗнч(ДополнительныеПредставленияДанных) = Тип("Массив") Тогда
			
			Для Каждого Представление Из ДополнительныеПредставленияДанных Цикл // СправочникСсылка.Файлы
				ДополнительноеПредставление = ДополнительныеПредставления.Добавить();
				ДополнительноеПредставление.Представление = Представление;
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ОбщегоНазначенияДокументооборот.УстановитьДополнительноеСвойствоПредыдущиеЗначенияРеквизитов(ЭтотОбъект);
	
	// Свойство ЭтоНовый (Булево) гарантированно устанавливается в методе
	// см. ОбщегоНазначенияДокументооборот.УстановитьДополнительноеСвойствоПредыдущиеЗначенияРеквизитов
	//@skip-check property-return-type
	ЭтоНовый = ДополнительныеСвойства.ЭтоНовый; // Булево
	Если ЭтоНовый Тогда
		СсылкаНового = Документы.СведенияОВнешнемПодписании.ПолучитьСсылку();
		УстановитьСсылкуНового(СсылкаНового);
		ИдентификаторДокумента = Строка(СсылкаНового.УникальныйИдентификатор());
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	// Свойство ЭтоНовый (Булево) гарантированно устанавливается в методе:
	// см. ОбщегоНазначенияДокументооборот.УстановитьДополнительноеСвойствоЭтоНовый
	//@skip-check property-return-type
	ЭтоНовый = ДополнительныеСвойства.ЭтоНовый; // Булево
	
	// Свойство ПредыдущиеЗначенияРеквизитов (Структура) гарантированно устанавливается в методе:
	// см. ОбщегоНазначенияДокументооборот.УстановитьДополнительноеСвойствоПредыдущиеЗначенияРеквизитов
	//@skip-check property-return-type
	ПредыдущиеЗначенияРеквизитов = ДополнительныеСвойства.ПредыдущиеЗначенияРеквизитов; // Структура
	
	// Свойство ПометкаУдаления (Булево) гарантированно устанавливается в методе:
	// см. Документы.СведенияОВнешнемПодписании.ОтслеживаемыеРеквизиты
	//@skip-check property-return-type
	ПометкаУдаленияБыла = ПредыдущиеЗначенияРеквизитов.ПометкаУдаления; // Булево
	
	Если ЭтоНовый Тогда
		РаботаСВнешнимПодписанием.ПриСозданииСведенияОВнешнемПодписании(ЭтотОбъект, Отказ);
	ИначеЕсли ПометкаУдаления И Не ПометкаУдаленияБыла Тогда
		РаботаСВнешнимПодписанием.ПриПометкеУдаленияСведенияОВнешнемПодписании(ЭтотОбъект, Отказ);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли
