
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ПриСозданииНаСервереРедакцииКонфигурации();
	
	// Открытие из формы:
	Если Объект.Ссылка.Пустая() И Параметры.ЭтоПрограммноеСоздание Тогда
		
		Документ = Параметры.Документ; // ОпределяемыйТип.ПредметМЭДО
		Объект.Документ = Документ;
		Если МЭДОПереопределяемый.ЭтоИсходящийДокумент(Объект.Документ) Тогда
			Объект.ИдентификаторДокумента = "" + Объект.Документ.УникальныйИдентификатор();
			Объект.Направление = Перечисления.НаправленияСообщенийМЭДО.Исходящее;
		ИначеЕсли МЭДОПереопределяемый.ЭтоВходящийДокумент(Объект.Документ) Тогда 
			Объект.Направление = Перечисления.НаправленияСообщенийМЭДО.Входящее;
		КонецЕсли;
		Реквизиты = МЭДОПереопределяемый.ТребуемыеДанныеВходящегоДокумента(
			Объект.Документ, "Организация", МЭДОСтруктурыДанных.НовыйЛегкийОтвет());
		Объект.Организация = Реквизиты.Организация;
		
		Модифицированность = Истина;
	КонецЕсли;
	
	УправлениеДоступностьюВидимостью();
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	ОбщегоНазначенияДокументооборот.ПоказатьНадписьПометкиУдаления(ЭтотОбъект, ТекущийОбъект.ПометкаУдаления);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	ОповеститьПредметМэдоОЗаписи();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ГотовКОтправкеПриИзменении(Элемент)
	
	Элементы.ОтправитьПоМЭДО.Доступность = Объект.ГотовКОтправке;
	
КонецПроцедуры

&НаКлиенте
Процедура ГлавныйФайлАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если Объект.Ссылка.Пустая() Тогда
		Возврат;
	КонецЕсли;
	
	ДанныеВыбора = ФайлыПодходящиеДляМЭДО(Объект.Документ);
	
КонецПроцедуры

&НаКлиенте
Процедура ГлавныйФайлНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОтправитьПоМЭДО(Команда)
	
	ОчиститьСообщения();
	
	Если Не Объект.ГотовКОтправке Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(
			НСтр("ru = 'Необходимо установить флаг ""Готов к отправке""'"),
			Объект.Ссылка,
			"ГотовКОтправке",
			"Объект");
		Возврат;
	КонецЕсли;
	
	Если Объект.Ссылка.Пустая() Или Модифицированность Тогда 
		Если Не Записать() Тогда 
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	ДанныеОтвета = МЭДОКлиент.НовыйЛегкийОтвет();
	МЭДОВызовСервера.ОтправитьИсходящиеДокументы(
		Объект.Организация,
		ДанныеОтвета,
		ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Объект.Документ));
	
	МЭДОКлиент.ПоказатьРезультатОтправки(НСтр("ru = 'Документ отправлен'"), ДанныеОтвета);
	Прочитать();
	Элементы.ОтправитьПоМЭДО.Доступность = Объект.ГотовКОтправке;
	ОповеститьПредметМэдоОЗаписи();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УправлениеДоступностьюВидимостью()
	
	ЕстьПолныеПрава = Пользователи.ЭтоПолноправныйПользователь();
	Элементы.ОтправитьПоМЭДО.Доступность = Объект.ГотовКОтправке;
	Если Не ЕстьПолныеПрава Тогда
		Элементы.ГруппаТехническиеДанные.Видимость = Ложь;
		Элементы.ПредыдущаяОтправка.ТолькоПросмотр = Истина;
		Элементы.ПредыдущаяОтправка.Видимость = Объект.ПредыдущаяОтправка;
	КонецЕсли;
	
	// Эти поля изменяются только программно, данные редактируются из исходной формы документа:
	Элементы.ЛеваяКолонка.ТолькоПросмотр = Не ЕстьПолныеПрава;
	Элементы.Ответственный.ТолькоПросмотр = Не ЕстьПолныеПрава;
	
	ЭтоИсходящее = (Объект.Направление = Перечисления.НаправленияСообщенийМЭДО.Исходящее);
	Элементы.ГотовКОтправке.Видимость = ЭтоИсходящее;
	Элементы.Отправлен.Видимость = ЭтоИсходящее;
	Элементы.ОтправитьПоМЭДО.Видимость = ЭтоИсходящее;
	
	ПакетовНесколько = Объект.Пакеты.Количество() > 1;
	Элементы.Пакеты.Видимость = ПакетовНесколько;
	Элементы.ИдентификаторСообщения.Видимость = Не ПакетовНесколько;
	Элементы.ВерсияМЭДО.Видимость = Не ПакетовНесколько;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ФайлыПодходящиеДляМЭДО(Документ)
	
	СписокВыбора = Новый СписокЗначений();
	ТзОбычные = МЭДОПереопределяемый.ОбычныеФайлыДокумента(Документ, Неопределено);
	Для Каждого С Из ТзОбычные Цикл
		Если СтрНайти(НРег(С.Расширение), "pdf") > 0 Тогда
			СписокВыбора.Добавить(С.Файл, С.Наименование);
		КонецЕсли;
	КонецЦикла;
	
	Возврат СписокВыбора;
	
КонецФункции

&НаСервере
Процедура ПриСозданииНаСервереРедакцииКонфигурации()
	
	Элементы.Организация.Заголовок = МЭДОПереопределяемый.Организация();
	
КонецПроцедуры

&НаКлиенте
Процедура ОповеститьПредметМэдоОЗаписи()
	
	Отклик = Новый Структура();
	Отклик.Вставить("ПредметСообщения", Объект.Ссылка);
	Отклик.Вставить("Документ", Объект.Документ);
	Оповестить("Запись_ДанныеДокументаМЭДО", Отклик, ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти
