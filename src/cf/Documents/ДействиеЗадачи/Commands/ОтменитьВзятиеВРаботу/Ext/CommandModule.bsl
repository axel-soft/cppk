#Область ОбработчикиСобытий

&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	Если ПараметрКоманды.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	КлючевыеОперации = "ЗадачиОтменитьВзятиеВРаботу";
	Если ПараметрКоманды.Количество() > 1 Тогда
		КлючевыеОперации = "ЗадачиОтменитьВзятиеВРаботуНесколькоЗадач";
	КонецЕсли;
	ОценкаПроизводительностиКлиент.ЗамерВремени(КлючевыеОперации);
	
	Попытка
		
		ОбработкаКомандыНаСервере(ПараметрКоманды);
		
		РаботаСЗадачамиКлиент.ОповеститьОбОперации(
			ПараметрКоманды,
			НСтр("ru = 'Отменено взятие в работу'"));
		
	Исключение
		
		РаботаСЗадачамиКлиент.ОповеститьОбИсключении();
		
		ВызватьИсключение;
		
	КонецПопытки;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ОбработкаКомандыНаСервере(Знач ДействияЗадачи)
	
	Для Каждого ДействиеЗадачи Из ДействияЗадачи Цикл
		РаботаСЗадачами.ОтменитьВзятиеВРаботу(ДействиеЗадачи, Сотрудники.ОсновнойСотрудник());
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти