#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Параметры.ДействиеЗадачи) Тогда
		ДействиеЗадачи = Параметры.ДействиеЗадачи;
		Задача = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДействиеЗадачи, "Задача");
	ИначеЕсли ЗначениеЗаполнено(Параметры.Задача) Тогда
		Задача = Параметры.Задача;
	КонецЕсли;
	
	УстановитьТипыПредмета();
	ПостроитьДеревоВариантов();
	
	ЭтоМобильныйКлиент = ПараметрыСеанса.ЭтоМобильныйКлиент;
	Если ЭтоМобильныйКлиент Тогда
		МК_НастроитьЭлементыФормы();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ЭлементыДерева = ДеревоВариантов.ПолучитьЭлементы();
	Для Каждого ЭлементДерева Из ЭлементыДерева Цикл
		Развернуть = Ложь;
		Если СтруктураРазвернутости <> Неопределено Тогда
			СтруктураРазвернутости.Свойство(ЭлементДерева.КомандаСсылка, Развернуть);
		Иначе
			Развернуть = Истина;
		КонецЕсли;
		Если Развернуть = Истина Или Развернуть = Неопределено Тогда
			Элементы.ДеревоВариантов.Развернуть(ЭлементДерева.ПолучитьИдентификатор(), Ложь);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)

	УстановитьПредмет(ВыбранноеЗначение);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

//Код процедур и функций

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыДеревоВариантов

&НаКлиенте
Процедура ДеревоВариантовВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)

	ВыбратьПредметКлиент();
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоВариантовПередРазворачиванием(Элемент, Строка, Отказ)
	
	СохранитьПараметрыРазвернутости(Строка, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоВариантовПередСворачиванием(Элемент, Строка, Отказ)
	
	СохранитьПараметрыРазвернутости(Строка, Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоВариантовПриАктивизацииСтроки(Элемент)
	
	Элементы.ДеревоВариантовКонтекстноеМенюОткрытьКарточку.Доступность =
		(ТипЗнч(Элементы.ДеревоВариантов.ТекущиеДанные.КомандаСсылка) <> Тип("Строка"));
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВыбратьПредмет(Команда)
	
	ВыбратьПредметКлиент();

КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьКарточку(Команда)
	
	ПоказатьЗначение(, Элементы.ДеревоВариантов.ТекущиеДанные.КомандаСсылка);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ВыбратьПредметКлиент()
	
	ТекущиеДанные = Элементы.ДеревоВариантов.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Или ТекущиеДанные.ЭтоЗаголовок Тогда
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(ТекущиеДанные.КомандаСсылка) = Тип("Строка")
		И ТекущиеДанные.КомандаСсылка = "ФайлСДиска" Тогда
		
		УстановитьПредмет(ТекущиеДанные.КомандаСсылка);
		
	ИначеЕсли ТипЗнч(ТекущиеДанные.КомандаСсылка) = Тип("Строка") Тогда
		
		
		ПараметрыФормы = Новый Структура;
		ИмяФормыВыбора = ТекущиеДанные.КомандаСсылка + ".ФормаВыбора";
		
		Если ТекущиеДанные.КомандаСсылка = "Справочник.Файлы" Тогда
			
			ИмяФормыВыбора = "Справочник.Файлы.Форма.ФормаВыбораФайлаВПапках";
			
		ИначеЕсли ТекущиеДанные.КомандаСсылка = "Справочник.ПроектныеЗадачи" Тогда	
			
			ПараметрыФормы.Вставить("ВыбиратьТолькоПроектнуюЗадачу", Истина);
			ИмяФормыВыбора = "ОбщаяФорма.ВыборПроектаЗадачи";
			
		ИначеЕсли ТекущиеДанные.КомандаСсылка = "Справочник.УведомленияПрограммы" Тогда
			
			ПараметрыФормы.Вставить("РежимВыбора", Истина);
			
		ИначеЕсли ТекущиеДанные.КомандаСсылка = "Справочник.ПротоколыМероприятий" Тогда
			
			ПараметрыФормы.Вставить("РежимВыбора", Истина);
			
		ИначеЕсли ТекущиеДанные.КомандаСсылка = "Документ.Бронь" Тогда
			
			ПараметрыФормы.Вставить("РежимВыбора", Истина);
			
		КонецЕсли;
		
		ОткрытьФорму(ИмяФормыВыбора, ПараметрыФормы, ЭтотОбъект);
		
	Иначе
		
		УстановитьПредмет(ТекущиеДанные.КомандаСсылка);
		
	КонецЕсли;

КонецПроцедуры

&НаСервере
Функция МетаданныеПриложений()
	
	МетаданныеПриложений = Новый Массив;
	
	Типы = Метаданные.Документы.ДействиеЗадачи.ТабличныеЧасти.Приложения.Реквизиты.Приложение.Тип.Типы();
	Для каждого Тип Из Типы Цикл
		ПредметСсылка = Новый(Тип);
		МетаданныеПриложений.Добавить(ПредметСсылка.Метаданные());
	КонецЦикла;
	
	Возврат МетаданныеПриложений;
	
КонецФункции


&НаСервере
Процедура ПостроитьДеревоВариантов()
	
	Дерево = РеквизитФормыВЗначение("ДеревоВариантов");
	Дерево.Строки.Очистить();
	
	ПолучитьСвязанныеПредметы(Дерево);
	ПолучитьНедавниеПредметы(Дерево);
	ПолучитьДругиеПредметы(Дерево);
	
	ЗначениеВДанныеФормы(Дерево, ДеревоВариантов);

КонецПроцедуры

&НаСервере
Процедура ПолучитьСвязанныеПредметы(Дерево)
	
	Если Не ЗначениеЗаполнено(Задача) Тогда
		Возврат;
	КонецЕсли;
	
	ТаблицаПриложения = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Задача, "Приложения").Выгрузить();
	ПредметыСвязей = ТаблицаПриложения.ВыгрузитьКолонку("Приложение");
	
	Запрос = Новый Запрос( 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
		|	Объекты.СвязанныйОбъект
		|ИЗ
		|	(ВЫБРАТЬ
		|		РегистрСведенийСвязиОбъектов.Объект КАК Документ,
		|		ВЫБОР
		|			КОГДА РегистрСведенийСвязиОбъектов.СвязаннаяСтрока <> """"
		|				ТОГДА РегистрСведенийСвязиОбъектов.СвязаннаяСтрока
		|			ИНАЧЕ РегистрСведенийСвязиОбъектов.СвязанныйОбъект
		|		КОНЕЦ КАК СвязанныйОбъект
		|	ИЗ
		|		РегистрСведений.СвязиОбъектов КАК РегистрСведенийСвязиОбъектов
		|	ГДЕ
		|		ТИПЗНАЧЕНИЯ(РегистрСведенийСвязиОбъектов.СвязанныйОбъект) В (&ТипыПредметов)
		|		И РегистрСведенийСвязиОбъектов.Объект В (&ПредметыСвязей)) КАК Объекты
		|АВТОУПОРЯДОЧИВАНИЕ");
	
	ТипыПредметов = Новый Массив;
	Для Каждого Элемент Из ТипыПредмета Цикл
		
		ИмяТипа =
			СтрЗаменить(
				СтрЗаменить(
					Элемент.Значение,
					"Справочник.",
					"СправочникСсылка."),
				"Документ.",
				"ДокументСсылка.");
		
		ТипыПредметов.Добавить(Тип(ИмяТипа));
		
	КонецЦикла;
	
	Запрос.УстановитьПараметр("ПредметыСвязей", ПредметыСвязей);
	Запрос.УстановитьПараметр("ТипыПредметов", ТипыПредметов); 
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	СтрокаЗаголовка = Дерево.Строки.Добавить();
	СтрокаЗаголовка.КомандаСсылка = "Связанные";
	СтрокаЗаголовка.ЭтоЗаголовок = Истина;
	Количество = 0;
	
	Пока Выборка.Следующий() Цикл
		
		Если Не ПравоДоступа("Чтение", Выборка.СвязанныйОбъект.Метаданные()) Тогда
			Продолжить;
		КонецЕсли;
			
		СтрокаПредмета = СтрокаЗаголовка.Строки.Добавить();
		СтрокаПредмета.Наименование = ОбщегоНазначенияДокументооборотВызовСервера.ПредметСтрокой(Выборка.СвязанныйОбъект);
		СтрокаПредмета.КомандаСсылка = Выборка.СвязанныйОбъект;
		Количество = Количество + 1;
		
	КонецЦикла;
	
	СтрокаЗаголовка.Наименование = СтрШаблон(
		НСтр("ru = 'Связанные (%1)'"),
		Количество);
	
	Если Количество = 0 Тогда
		Дерево.Строки.Удалить(СтрокаЗаголовка);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПолучитьНедавниеПредметы(Дерево)
	
	Запрос = Новый Запрос( 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ ПЕРВЫЕ 10
		|	РегистрСведенийОбращенияКОбъектам.Объект КАК Объект,
		|	РегистрСведенийОбращенияКОбъектам.ДатаПоследнегоОбращения КАК ДатаПоследнегоОбращения
		|ИЗ
		|	РегистрСведений.ОбращенияКОбъектам КАК РегистрСведенийОбращенияКОбъектам
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Файлы КАК Файлы
		|		ПО РегистрСведенийОбращенияКОбъектам.Объект = Файлы.Ссылка
		|ГДЕ
		|	РегистрСведенийОбращенияКОбъектам.Пользователь = &ТекущийПользователь
		|	И ТИПЗНАЧЕНИЯ(РегистрСведенийОбращенияКОбъектам.Объект) В (&ТипыПредметов)
		|	И РегистрСведенийОбращенияКОбъектам.Объект.ПометкаУдаления = ЛОЖЬ
		|	И (Файлы.Ссылка ЕСТЬ NULL
		|			ИЛИ НЕ Файлы.Ссылка ЕСТЬ NULL
		|				И НЕ Файлы.ВладелецФайла ССЫЛКА Справочник.ДокументыПредприятия
		|				И НЕ Файлы.ВладелецФайла ССЫЛКА Справочник.Мероприятия)
		|
		|УПОРЯДОЧИТЬ ПО
		|	ДатаПоследнегоОбращения УБЫВ");
		
	ТипыПредметов = Новый Массив;
	Для Каждого Элемент Из ТипыПредмета Цикл
		
		ИмяТипа =
			СтрЗаменить(
				СтрЗаменить(
					Элемент.Значение,
					"Справочник.",
					"СправочникСсылка."),
				"Документ.",
				"ДокументСсылка.");
		
		ТипыПредметов.Добавить(Тип(ИмяТипа));
		
	КонецЦикла;
	
	Запрос.УстановитьПараметр("ТипыПредметов", ТипыПредметов);
	Запрос.УстановитьПараметр("ТекущийПользователь", Пользователи.ТекущийПользователь());
	
	Результат = Запрос.Выполнить();
	
	Если Результат.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	Выборка = Результат.Выбрать();
	
	СтрокаЗаголовка = Дерево.Строки.Добавить();
	СтрокаЗаголовка.Наименование = СтрШаблон(
		НСтр("ru = 'Недавние (%1)'"),
		Выборка.Количество());
	СтрокаЗаголовка.КомандаСсылка = "Недавние";
	СтрокаЗаголовка.ЭтоЗаголовок = Истина;
	
	Пока Выборка.Следующий() Цикл    
		
		ИспользоватьОбъект = Истина;
		
		Если ТипЗнч(Выборка.Объект) = Тип("СправочникСсылка.Файлы") Тогда
			ВладелецФайла = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Выборка.Объект, "ВладелецФайла");
			Если ДелопроизводствоКлиентСервер.ЭтоДокументПредприятия(ВладелецФайла)
				Или ДелопроизводствоКлиентСервер.ЭтоМероприятие(ВладелецФайла)Тогда
				ИспользоватьОбъект = Ложь;
			КонецЕсли;		
		КонецЕсли;	
		
		Если ИспользоватьОбъект Тогда
			СтрокаПредмета = СтрокаЗаголовка.Строки.Добавить();
			СтрокаПредмета.Наименование = ОбщегоНазначенияДокументооборотВызовСервера.ПредметСтрокой(Выборка.Объект);
			СтрокаПредмета.КомандаСсылка = Выборка.Объект;
		КонецЕсли;
		
	КонецЦикла;

КонецПроцедуры

&НаСервере
Процедура ПолучитьДругиеПредметы(Дерево)
	
	КоличествоТипов = ТипыПредмета.Количество();
	
	СтрокаЗаголовка = Дерево.Строки.Добавить();
	СтрокаЗаголовка.КомандаСсылка = "Другие";
	СтрокаЗаголовка.ЭтоЗаголовок = Истина;
	
	Для Каждого Строка Из ТипыПредмета Цикл
		
		СтрокаПредмета = СтрокаЗаголовка.Строки.Добавить();
		СтрокаПредмета.Наименование = Строка.Представление;
		СтрокаПредмета.КомандаСсылка = Строка.Значение;
		
	КонецЦикла;
	
	// Возможность добавить файл с диска
	СтрокаПредмета = СтрокаЗаголовка.Строки.Добавить();
	СтрокаПредмета.Наименование = НСтр("ru='Файл с диска'");
	СтрокаПредмета.КомандаСсылка = "ФайлСДиска";
	КоличествоТипов = КоличествоТипов + 1;
	
	СтрокаЗаголовка.Наименование = СтрШаблон(
		НСтр("ru = 'Другие (%1)'"),
		КоличествоТипов);
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьПараметрыРазвернутости(Строка, Развернуть)
	
	Если СтруктураРазвернутости = Неопределено Тогда
		СтруктураРазвернутости = Новый Структура;
	КонецЕсли;
	
	СтрокаДерева = ДеревоВариантов.НайтиПоИдентификатору(Строка);
	
	СтруктураРазвернутости.Вставить(СтрокаДерева.КомандаСсылка,Развернуть);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьПредмет(НовыйПредмет)
	
	Закрыть(НовыйПредмет);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьТипыПредмета()
	
	ТипыПредмета.Очистить();
	
	Для Каждого МетаданныеПриложения Из МетаданныеПриложений() Цикл
		
		Если Не ОбщегоНазначенияДокументооборот.МетаданныеДоступны(МетаданныеПриложения) Тогда
			Продолжить;
		КонецЕсли;
		
		ПредставлениеОбъекта = МетаданныеПриложения.ПредставлениеОбъекта;
		Если ПустаяСтрока(ПредставлениеОбъекта) Тогда
			ПредставлениеОбъекта = Строка(МетаданныеПриложения);
		КонецЕсли;
		
		ТипыПредмета.Добавить(МетаданныеПриложения.ПолноеИмя(), ПредставлениеОбъекта);
		
	КонецЦикла;
	
	ТипыПредмета.СортироватьПоПредставлению();
	
КонецПроцедуры

#Область СлужебныеПроцедурыИФункции_МобильныйКлиент

&НаСервере
Процедура МК_НастроитьЭлементыФормы()
	
	СворачиваниеЭлементовПоВажности = СворачиваниеЭлементовФормыПоВажности.НеИспользовать;
	ВертикальныйИнтервал = ИнтервалМеждуЭлементамиФормы.Половинный;
	
	МК.ОформитьАкцентнуюКнопку(Элементы.Выбрать);
	Элементы.Отмена.Видимость = Ложь;

КонецПроцедуры
	
#КонецОбласти

#КонецОбласти
