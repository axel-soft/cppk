
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Объект.Ссылка.Пустая() И (Параметры.ЭтоСозданиеИзОтчета Или Параметры.ЭтоСозданиеПоСписку) Тогда
		
		Объект.Дата = ТекущаяДатаСеанса();
		Объект.Организация = Параметры.Организация;
		Объект.СпособВыгрузки = Перечисления.СпособыВыгрузкиОбращений.ВФайл;
		Объект.Ответственный = Сотрудники.ОсновнойСотрудник();
		
		Если Параметры.ЭтоСозданиеПоСписку Тогда
			
			ДокументыОбращения = Параметры.ДокументыОбращения; // Массив Из СправочникСсылка.ДокументыПредприятия
			Если ТипЗнч(ДокументыОбращения) <> Тип("Массив") Или ДокументыОбращения.Количество() = 0 Тогда
				ОбщегоНазначения.СообщитьПользователю(
					НСтр("ru = 'В форму передан некорректный параметр ""ДокументыОбращения"".
						|Обратитесь к администратору.'"),
					, , , Отказ);
				Возврат;
			КонецЕсли;
			ПроблемныеОбъекты = Документы.ВыгрузкаВССТУ.ОшибкиВОбращениях(ДокументыОбращения);
			ВывестиПроблемныеОбъекты(ПроблемныеОбъекты, Отказ);
			Если Отказ Тогда
				Возврат;
			КонецЕсли;
			
			Если Не ЗначениеЗаполнено(Объект.Организация) Тогда
				Объект.Организация = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(
					ДокументыОбращения[0], "Организация");
				Если Не ЗначениеЗаполнено(Объект.Организация) Тогда
					Объект.Организация = Справочники.Организации.ОрганизацияПоУмолчанию();
				КонецЕсли;
			КонецЕсли;
			
			Для Каждого ДокументОбращение Из Параметры.ДокументыОбращения Цикл
				СтрокаТЧ = Объект.Обращения.Добавить();
				СтрокаТЧ.Обращение = ДокументОбращение;
			КонецЦикла
			
		ИначеЕсли Параметры.ЭтоСозданиеИзОтчета Тогда 
			
			ПараметрыОтчета = Отчеты.РегламентированныйОтчетПоРезультатамРассмотренияОбращений.НовыйПараметрыОтчета();
			ПараметрыОтчета.Организация = Параметры.Организация;
			ПараметрыОтчета.Период = Параметры.Период;
			ПараметрыОтчета.СостояниеВыгрузкиССТУ = РаботаСОбращениямиКлиентСервер.ВсеОбращения();
			ПараметрыОтчета.СостояниеГотовности = РаботаСОбращениямиКлиентСервер.ГотовыеКВыгрузке();
			
			УстановитьПривилегированныйРежим(Истина); // не на все обращения могут быть права, но выгрузка возможна.
			РезультатыЗапроса
				= Отчеты.РегламентированныйОтчетПоРезультатамРассмотренияОбращений.РезультатыЗапросаДляОтчета(
					ПараметрыОтчета);
			ВыборкаИтог = РезультатыЗапроса[6].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			Если ВыборкаИтог.Следующий() Тогда
				ВыборкаПоОбращениям = ВыборкаИтог.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
				Пока ВыборкаПоОбращениям.Следующий() Цикл
					СтрокаТЧ = Объект.Обращения.Добавить();
					СтрокаТЧ.Обращение = ВыборкаПоОбращениям.ДокументСсылка;
				КонецЦикла;
			КонецЕсли;
			УстановитьПривилегированныйРежим(Ложь);
			
		КонецЕсли;
		
		Модифицированность = Истина;
	КонецЕсли;
	ТекущийЭлемент = Элементы.Обращения;
	
	УправлениеДоступностью();
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	ОбщегоНазначенияДокументооборот.ПоказатьНадписьПометкиУдаления(ЭтотОбъект, ТекущийОбъект.ПометкаУдаления);
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	ДокументыИзТЧ = Объект.Обращения.Выгрузить(, "Обращение").ВыгрузитьКолонку("Обращение");
	ПроблемныеОбъекты = Документы.ВыгрузкаВССТУ.ОшибкиВОбращениях(ДокументыИзТЧ);
	ВывестиПроблемныеОбъекты(ПроблемныеОбъекты, Отказ);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	// Могло измениться "состояние выгрузки" и сняться флажок готовности к выгрузке в документе на закладке "Вопросы":
	Оповестить("Запись_ВыгрузкаВССТУ", Неопределено, ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОтветственныйОбработкаВыбора(Элемент, ВыбранноеЗначение, ДополнительныеДанные, СтандартнаяОбработка)
	
	СотрудникиКлиент.СотрудникОбработкаВыбора(Объект, "Ответственный", ВыбранноеЗначение, СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыОбращения

&НаКлиенте
Процедура ОбращенияВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ПоказатьЗначение(, Элементы.Обращения.ТекущиеДанные.Обращение);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбращенияОбращениеНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("РежимВыбора", Истина);
	ПараметрыОткрытия.Вставить("ПоказыватьКомандуСоздать", Ложь);
	ПараметрыОткрытия.Вставить("ЗакрыватьПриВыборе", Истина);
	ПараметрыОткрытия.Вставить("Заголовок", НСтр("ru = 'Выбор обращений граждан'"));
	ПараметрыОткрытия.Вставить(
		"КлючНазначенияИспользования", РаботаСОбращениямиКлиентСервер.КлючСписокОбращенийГраждан());
	ПараметрыОткрытия.Вставить("Организация", Объект.Организация);
	ПараметрыОткрытия.Вставить("ТолькоЗарегистрированные", Истина);
	ОткрытьФорму("Справочник.ДокументыПредприятия.Форма.ФормаВыбора", ПараметрыОткрытия, Элементы.ОбращенияОбращение);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПраваДоступа(Команда)
	
	ДокументооборотПраваДоступаКлиент.ОткрытьФормуПравДоступа(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Асинх Процедура СохранитьНаДиск(Команда)
	
	Если Не ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	ДокументыОбращения = Новый Массив();
	Для Каждого СтрокаТЧ Из Объект.Обращения Цикл
		ДокументыОбращения.Добавить(СтрокаТЧ.Обращение);
	КонецЦикла;
	
	Если РаботаСОбращениямиВызовСервера.ЕстьОшибкиВВыгрузкеВЦелом(ДокументыОбращения, Объект.Организация) Тогда
		Возврат;
	КонецЕсли;
	ПроблемныеОбъекты = РаботаСОбращениямиВызовСервера.ОшибкиВОбращениях(ДокументыОбращения);
	Если ПроблемныеОбъекты.Количество() > 0 Тогда
		РаботаСОбращениямиКлиент.ОткрытьПроблемныеОбъекты(ПроблемныеОбъекты);
		Возврат;
	КонецЕсли;
	
	Если Модифицированность Или Объект.Ссылка.Пустая() Тогда
		Если Ждать ВопросАсинх(
			НСтр("ru = 'Выгрузить в файл можно только записанную ""Выгрузку"", записать?'"),
			РежимДиалогаВопрос.ДаНет) = КодВозвратаДиалога.Нет Тогда
			Возврат;
		КонецЕсли;
		
		Если Не Записать() Тогда 
			Возврат;
		КонецЕсли;
		ПоказатьОповещениеПользователя(
			НСтр("ru = 'Документ ""Выгрузка в ССТУ"" записан'"), 
			ПолучитьНавигационнуюСсылку(Объект.Ссылка),
			Строка(Объект.Ссылка),
			БиблиотекаКартинок.Информация32);
	КонецЕсли;
	
	ПолноеИмяФайла = Ждать РаботаСОбращениямиКлиент.ФайлВыбранныйВДиалоге_Асинх(
		РежимДиалогаВыбораФайла.Сохранение,
		РаботаСОбращениямиКлиент.НачальноеИмяВыгрузки(Объект.Номер, Объект.Дата),
		РаботаСОбращениямиКлиент.ФильтрZip());
	Если Не ЗначениеЗаполнено(ПолноеИмяФайла) Тогда
		Возврат;
	КонецЕсли;
	
	Ответ = РаботаСОбращениямиВызовСервера.СоздатьДвоичныеДанныеВыгрузки(
		ДокументыОбращения,
		ПоместитьВоВременноеХранилище(Неопределено, УникальныйИдентификатор),
		Объект.Ссылка);
	Если Ответ.ПроблемныеОбъекты.Количество() > 0 Тогда
		РаботаСОбращениямиКлиент.ОткрытьПроблемныеОбъекты(Ответ.ПроблемныеОбъекты);
		Возврат;
	КонецЕсли;
	
	ДвоичныеДанныеФайла = ПолучитьИзВременногоХранилища(Ответ.АдресВХранилище);
	Если ТипЗнч(ДвоичныеДанныеФайла) = Тип("ДвоичныеДанные") Тогда
		Ждать ДвоичныеДанныеФайла.ЗаписатьАсинх(ПолноеИмяФайла);
		ПоказатьОповещениеПользователя(
			НСтр("ru = 'Файл выгрузки записан на диск'"),
			,
			ПолноеИмяФайла,
			БиблиотекаКартинок.Информация32);
	Иначе
		// Перестраховка, в случае ошибки должно быть конкретное сообщение, выше:
		ОбщегоНазначенияКлиент.СообщитьПользователю(
			НСтр("ru = 'Неожиданная ошибка, выгрузка не вернула ""ДвоичныеДанные""'") , Объект.Ссылка);
		Возврат;
	КонецЕсли;
	
	
	Если Не Объект.Выгружена Тогда
		Объект.Выгружена = Истина;
		Если Не Записать() Тогда 
			Возврат;
		КонецЕсли;
		ПоказатьОповещениеПользователя(
			НСтр("ru = 'Установлен признак ""Выгружена""'"),
			ПолучитьНавигационнуюСсылку(Объект.Ссылка),
			"" + Объект.Ссылка,
			БиблиотекаКартинок.Информация32);
	КонецЕсли;
	
	УправлениеДоступностью();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Процедура ВывестиПроблемныеОбъекты(ПроблемныеОбъекты, Отказ)

	Если ПроблемныеОбъекты.Количество() > 0 Тогда
		Для Каждого Проблема Из ПроблемныеОбъекты Цикл
			Фраза = Проблема.СообщениеОбОшибке + ?(
				ЗначениеЗаполнено(Проблема.СсылкаНаОбъект), " - " + Проблема.СсылкаНаОбъект, "");
			ОбщегоНазначения.СообщитьПользователю(Фраза, Проблема.СсылкаНаОбъект, , , Отказ);
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УправлениеДоступностью()
	
	Если Пользователи.ЭтоПолноправныйПользователь() Тогда
		Возврат;
	КонецЕсли;
	
	Элементы.Организация.ТолькоПросмотр = Истина;
	Элементы.Выгружена.ТолькоПросмотр = Истина; // Галка ставится автоматически, снять может только администратор.
	Элементы.Ответственный.ТолькоПросмотр = Истина;
	Если Объект.Выгружена Тогда
		ТолькоПросмотр = Истина; // Уже выгруженную редактировать может только адмнистратор.
	КонецЕсли;

КонецПроцедуры

#КонецОбласти
