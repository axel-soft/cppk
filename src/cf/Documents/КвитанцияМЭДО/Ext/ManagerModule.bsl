// @strict-types

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

// Обработка получения полей представления.
// 
// Параметры:
//  Поля - Массив из Строка -
//  СтандартнаяОбработка - Булево -
Процедура ОбработкаПолученияПолейПредставления(Поля, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Поля.Добавить("Дата");
	Поля.Добавить("ИдентификаторКвитанции");
	Поля.Добавить("ИдентификаторСообщения");
	Поля.Добавить("Направление");
	
КонецПроцедуры

// Обработка получения представления.
// 
// Параметры:
//  Данные - Структура - Данные:
//   * Дата - Дата -
//   * ИдентификаторКвитанции - Строка -
//   * ИдентификаторСообщения - Строка -
//   * Направление - ПеречислениеСсылка.НаправленияСообщенийМЭДО -
//  Представление - Строка - Представление
//  СтандартнаяОбработка - Булево - Стандартная обработка
Процедура ОбработкаПолученияПредставления(Данные, Представление, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Представление = СтрШаблон(
		НСтр("ru = '%1 квитанция МЭДО от %2 (id=%3 на сообщение id=%4)'"), 
		Перечисления.НаправленияСообщенийМЭДО.ЖенскийРод(Данные.Направление),
		Данные.Дата,
		Данные.ИдентификаторКвитанции,
		Данные.ИдентификаторСообщения);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Снять признак "Готова к отправке" - с исходящей квитанции - в случае неуспешной отправки.
// 
// Параметры:
//  КвитанцияСсылка - ДокументСсылка.КвитанцияМЭДО
//  ДанныеОтвета - см. МЭДОСтруктурыДанных.НовыйОтвет.
Процедура СнятьПризнакГотоваКОтправке(КвитанцияСсылка, ДанныеОтвета) Экспорт
	
	Квитанция = КвитанцияСсылка.ПолучитьОбъект();
	Если Не Квитанция.ГотоваКОтправке Тогда
		Возврат; // перестраховка, флаг должен стоять.
	КонецЕсли;
	Квитанция.ГотоваКОтправке = Ложь;
	Квитанция.Отправлена = Ложь;
	Попытка
		Квитанция.Записать();
		МЭДО.ЗаписьВЖурналСобытий(
			Перечисления.УровниСобытийМЭДО.Предупреждение,
			КвитанцияСсылка,
			МЭДО.Текст_НеуспешнаяОтправка(),
			МЭДО.Текст_СнятиеПризнакаГотовностиКОтправке(),
			ДанныеОтвета);
	Исключение
		МЭДО.ЗаписьВЖурналСобытий(
			Перечисления.УровниСобытийМЭДО.Ошибка,
			КвитанцияСсылка,
			НСтр("ru = 'Ошибка записи исходящего уведомления'"),
			ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()),
			ДанныеОтвета);
	КонецПопытки;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли