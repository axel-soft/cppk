#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	МассивПредметовОбработки = Параметры.МассивПредметовОбработки;
	ИспользованныеПредметы = Параметры.ИспользованныеПредметы;
	
	Для Каждого ИмяПредмета Из МассивПредметовОбработки Цикл
		
		НовСтр = ИменаПредметов.Добавить();
		НовСтр.Предмет = ИмяПредмета;
		Если ИспользованныеПредметы.Найти(ИмяПредмета) <> Неопределено Тогда
			НовСтр.Использован = Истина;
		КонецЕсли;	
		
	КонецЦикла;	 

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийШапкиФормы

&НаКлиенте
Процедура ИменаПредметовВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ВыполнитьВыбор();
	
КонецПроцедуры

&НаКлиенте
Процедура Выбрать(Команда)
	ВыполнитьВыбор();
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьВыбор()
	
	ТекущиеДанные = Элементы.ИменаПредметов.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Закрыть();
		Возврат;
	КонецЕсли;	
	
	// тут также предупреждение, если Использован Истина
	Если ТекущиеДанные.Использован Тогда
		
		ПараметрыОповещения = Новый Структура;
		ПараметрыОповещения.Вставить("Предмет", ТекущиеДанные.Предмет);
		
		ОписаниеОповещения = Новый ОписаниеОповещения(
			"ВыполнитьВыборПродолжение",
			ЭтотОбъект,
			ПараметрыОповещения);
		
		Кнопки = Новый СписокЗначений;
		Кнопки.Добавить(КодВозвратаДиалога.ОК, НСтр("ru = 'Продолжить'"));
		Кнопки.Добавить(КодВозвратаДиалога.Отмена);
		
		ТекстВопроса = НСтр("ru = 'Обработка этого предмета уже размещена в схеме.
		|При выборе, связанная с этим предметом настройка
		|обработки будет очищена.
		|
		|Продолжить?'");
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, Кнопки);
		
		Возврат;
		
	КонецЕсли;	
	
	Закрыть(ТекущиеДанные.Предмет);
		
КонецПроцедуры

&НаКлиенте
// Продолжение ВыполнитьВыбор
//
Процедура ВыполнитьВыборПродолжение(Результат, Параметры) Экспорт
	
	Предмет = Параметры.Предмет;
	
	Если Результат = КодВозвратаДиалога.Отмена Тогда
		Возврат;
	ИначеЕсли Результат = КодВозвратаДиалога.ОК Тогда
		
		Закрыть(Предмет);
		
	КонецЕсли;

КонецПроцедуры

#КонецОбласти
