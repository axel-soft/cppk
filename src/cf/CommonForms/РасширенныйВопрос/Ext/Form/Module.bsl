
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Заголовок = Параметры.Заголовок;
	ТекстВопроса = Параметры.ТекстВопроса;
	КлючНастройки = Параметры.КлючПерсональнойНастройки;
	ИмяНастройки = Параметры.ИмяПерсональнойНастройки;
	
	Если ЗначениеЗаполнено(Параметры.ВысотаТекста) Тогда
		Элементы.ТекстВопроса.Высота = Параметры.ВысотаТекста;
		Если Не ЗначениеЗаполнено(КлючСохраненияПоложенияОкна) Тогда
			КлючСохраненияПоложенияОкна = Строка(Параметры.ВысотаТекста);
		КонецЕсли;
	КонецЕсли;
	
	ОбработатьЗакрытиеФормы = Истина;
	НажатаКнопка = Ложь;
	
	Для Каждого ВариантОтвета Из Параметры.СписокДоступныхВариантов Цикл
		Для Каждого ЭлементФормы Из Элементы Цикл
			Если ТипЗнч(ЭлементФормы) = Тип("КнопкаФормы")
				И Команды[ЭлементФормы.ИмяКоманды].Заголовок = ВариантОтвета.Значение Тогда
				ЭлементФормы.Видимость = Истина;
				Если ЗначениеЗаполнено(ВариантОтвета.Представление) Тогда
					ЭлементФормы.Заголовок = ВариантОтвета.Представление;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	КнопкаПоУмолчанию = Элементы.Найти("ФормаКоманда" + Параметры.ВариантОтветаПоУмолчанию);
	Если КнопкаПоУмолчанию <> Неопределено Тогда
		КнопкаПоУмолчанию.КнопкаПоУмолчанию = Истина;
		КнопкаПоУмолчанию.АктивизироватьПоУмолчанию = Истина;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(КлючНастройки) И Не ЗначениеЗаполнено(ИмяНастройки) Тогда
		Элементы.БольшеНеПоказывать.Видимость = Ложь;
	КонецЕсли;
	
	Если Параметры.ЗадержкаВремени <> Неопределено Тогда
		СоответствиеЗадержкиВремени = Новый Соответствие;
		Для Каждого КлючИЗначение Из Параметры.ЗадержкаВремени Цикл
			ВариантОтвета = КлючИЗначение.Ключ;
			Задержка = Окр(КлючИЗначение.Значение);
			Кнопка = Элементы.Найти("ФормаКоманда" + ВариантОтвета);
			Если Кнопка = Неопределено Или Задержка <= 0 Тогда
				Продолжить;
			КонецЕсли;
			Стр = Новый Структура("ИмяКнопки, Заголовок, Задержка",
				Кнопка.Имя, Кнопка.Заголовок, Задержка);
			СоответствиеЗадержкиВремени[Кнопка.Имя] =
				Новый Структура("Заголовок, Задержка", Кнопка.Заголовок, Задержка);
		КонецЦикла;
		ДанныеДляОбработкиЗадержкиВремени = Новый ФиксированноеСоответствие(СоответствиеЗадержкиВремени);
	КонецЕсли;
	
	ЭтоHTML = СтрНайти(ТекстВопроса, "<BODY>") > 0;
	Если ЭтоHTML Тогда
		Элементы.ТекстВопроса.Вид = ВидПоляФормы.ПолеHTMLДокумента;
		ТекстВопроса = СтрЗаменить(ТекстВопроса, "<BODY>",
			"<HTML><HEAD>
			| <META content=""text/html; charset=utf-8"" http-equiv=Content-Type>
			| </HEAD>
			|<style type=""text/css"">
			|	body {
			|		overflow:    auto;
			|		margin-top:  6px; 		 
			|		margin-left: 0px; 
			|		margin-bottom: 0px;
			|		font-family: Arial, sans-serif;
			|		font-size:   10pt;}
			|	UL {
			|		padding: 10;
			|		margin: 10;}
			|</style>
			| <BODY>");
		ТекстВопроса = СтрЗаменить(ТекстВопроса, "</BODY>", "</BODY></HTML>");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	КлючСохраненияПоложенияОкна = Новый УникальныйИдентификатор;
	
	Если ДанныеДляОбработкиЗадержкиВремени <> Неопределено Тогда
		ЭтотОбъект.ПодключитьОбработчикОжидания("ОбновитьЗадержкуВремени", 1, Ложь);
		ОбновитьЗадержкуВремени();
	КонецЕсли;
	
	ОбновитьОтображениеДанных();
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если ОбщегоНазначенияДокументооборотКлиент.ПередЗакрытием(
		Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка, Модифицированность) Тогда
		Возврат;
	КонецЕсли;
	
	Если ОбработатьЗакрытиеФормы И НЕ НажатаКнопка Тогда
		ОбработатьЗакрытиеФормы = Ложь;
		СтандартнаяОбработка = Ложь;
		Закрыть(Ложь);
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	Если ОбщегоНазначенияДокументооборотКлиент.ПриЗакрытии(ЗавершениеРаботы) Тогда
		Возврат;
	КонецЕсли;
	
	ПриЗакрытииСервер();
	
КонецПроцедуры

&НаСервере
Процедура ПриЗакрытииСервер()
	
	ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(КлючНастройки, ИмяНастройки, НЕ БольшеНеПоказывать);
	ОбновитьПовторноИспользуемыеЗначения();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаНажатияКнопки(Команда)
	
	НажатаКнопка = Истина;
	Закрыть(Строка(КодВозвратаДиалога[СтрЗаменить(Команда.Имя, "Команда", "")]));
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьЗадержкуВремени()
	
	ЗадержкаАктуальна = Ложь;
	
	Для Каждого КлючИЗначение Из ДанныеДляОбработкиЗадержкиВремени Цикл
		ИмяКнопки = КлючИЗначение.Ключ;
		ПараметрыЗадержки = КлючИЗначение.Значение;
		Кнопка = Элементы[ИмяКнопки];
		Если ПараметрыЗадержки.Задержка > 0 Тогда
			Кнопка.Заголовок = ПараметрыЗадержки.Заголовок + " (" + ПараметрыЗадержки.Задержка + ")";
			Кнопка.Доступность = Ложь;
			ЗадержкаАктуальна = Истина;
		Иначе
			Кнопка.Заголовок = ПараметрыЗадержки.Заголовок;
			Кнопка.Доступность = Истина;
		КонецЕсли;
		ПараметрыЗадержки.Задержка = ПараметрыЗадержки.Задержка - 1;
	КонецЦикла;
	
	Если Не ЗадержкаАктуальна Тогда
		ЭтотОбъект.ОтключитьОбработчикОжидания("ОбновитьЗадержкуВремени");
	КонецЕсли;
	
КонецПроцедуры
