
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Для Каждого Состояние Из Параметры.Состояния Цикл
		Если ТипЗнч(Состояние.Значение) <> Тип("Строка") Тогда
			НовСтр = ТаблицаСостояний.Добавить();
			НовСтр.Состояние = Состояние.Значение;
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого Стр Из ТаблицаСостояний Цикл
		Если ТипЗнч(Параметры.ТекущееЗначение) = Тип("СписокЗначений") 
			И Параметры.ТекущееЗначение.НайтиПоЗначению(Стр.Состояние) <> Неопределено Тогда
			Стр.Пометка = Истина;
		ИначеЕсли Параметры.ТекущееЗначение = Стр.Состояние Тогда
			Стр.Пометка = Истина;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Готово(Команда)
	
	// вернем СписокЗначений
	СписокВыбораСостояний = Новый СписокЗначений;     
	
	Для Каждого Стр Из ТаблицаСостояний Цикл
		Если Стр.Пометка Тогда
			СписокВыбораСостояний.Добавить(Стр.Состояние);
		КонецЕсли;	
	КонецЦикла;	
	
	Если СписокВыбораСостояний.Количество() = 1 Тогда
		ОповеститьОВыборе(СписокВыбораСостояний[0].Значение);
	Иначе
		ОповеститьОВыборе(СписокВыбораСостояний); // если 0 - тоже здесь.
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаСостоянийПередУдалением(Элемент, Отказ)
	Отказ = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаСостоянийПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	Отказ = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ОтметитьВсе(Команда)
	
	Для Каждого Стр Из ТаблицаСостояний Цикл
		Стр.Пометка = Истина;
	КонецЦикла;	
	
КонецПроцедуры

&НаКлиенте
Процедура СнятьВсе(Команда)
	
	Для Каждого Стр Из ТаблицаСостояний Цикл
		Стр.Пометка = Ложь;
	КонецЦикла;	
	
КонецПроцедуры

#КонецОбласти