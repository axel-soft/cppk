
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Описание = Параметры.Описание;
	
	URLСхемы = ПоместитьВоВременноеХранилище(
		ПолучитьОбщийМакет("СтруктураУсловияСхемыОбработкиОбъекта"), УникальныйИдентификатор);
	Компоновщик.Инициализировать(Новый ИсточникДоступныхНастроекКомпоновкиДанных(URLСхемы));
	Компоновщик.ЗагрузитьНастройки(Параметры.НастройкиУсловия);
	
	ТипОбъекта = РаботаСПроцессамиПоОбработкамОбъектов.ТипОбъектаПоВладельцуСхемы(
		Параметры.ВладелецСхемы);
	
	// Для компоновщика скрываем поля ЛевоеЗначение, ВидСравнения, ПравоеЗначение.
	// Они будут заполняться программно.
	Элементы.КомпоновщикНастройкиОтборЛевоеЗначение.Видимость = Ложь;
	Элементы.КомпоновщикНастройкиОтборВидСравнения.Видимость = Ложь;
	Элементы.КомпоновщикНастройкиОтборПравоеЗначение.Видимость = Ложь;
	
	Если Параметры.ТолькоПросмотр Тогда
		Элементы.КоманднаяПанельКомпоновщика.Видимость = Ложь;
		Элементы.Готово.Видимость = Ложь;
		Элементы.Отмена.Видимость = Ложь;
		Элементы.Описание.ТолькоПросмотр = Истина;
		
		КлючСохраненияПоложенияОкна = "ТолькоПросмотр";
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ЗаполнитьПредставленияУсловийКомпоновщика();

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицы_КомпоновщикНастройкиОтбор

&НаКлиенте
Процедура КомпоновщикНастройкиОтборПередНачаломИзменения(Элемент, Отказ)
	
	ТекущийЭлементОтбора = Компоновщик.Настройки.Отбор.ПолучитьОбъектПоИдентификатору(
		Элементы.КомпоновщикНастройкиОтбор.ТекущаяСтрока);
	
	Если Параметры.ТолькоПросмотр
		И Элемент.ТекущийЭлемент.Имя = "КомпоновщикНастройкиОтборИспользование" Тогда
		
		Отказ = Истина;
		Возврат
	КонецЕсли;
	
	Если ТипЗнч(ТекущийЭлементОтбора) <> Тип("ЭлементОтбораКомпоновкиДанных")
		Или Элемент.ТекущийЭлемент.Имя = "КомпоновщикНастройкиОтборИспользование" Тогда
		
		Возврат;
	КонецЕсли;
	
	Отказ = Истина;
		
	ПолеУсловиеПоОбъекту = Новый ПолеКомпоновкиДанных(
		РаботаСПроцессамиПоОбработкамОбъектовКлиентСервер.ИмяПоляУсловиеПоОбъекту());
		
	УсловиеПоРезультатуВыполненияДействия = Новый ПолеКомпоновкиДанных(
		РаботаСПроцессамиПоОбработкамОбъектовКлиентСервер.
		ИмяПоляУсловиеПоРезультатуВыполненияДействия());
	
	Если ТекущийЭлементОтбора.ЛевоеЗначение = ПолеУсловиеПоОбъекту Тогда
		
		Если Параметры.ТолькоПросмотр Тогда
			Если ЗначениеЗаполнено(ТекущийЭлементОтбора.ПравоеЗначение) Тогда
				ПоказатьЗначение(, ТекущийЭлементОтбора.ПравоеЗначение);
			КонецЕсли;
			Возврат;
		КонецЕсли;
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("ТипОбъекта", ТипОбъекта);
		ПараметрыФормы.Вставить("ТекущаяСтрока", ТекущийЭлементОтбора.ПравоеЗначение);
		
		ОбработчикЗавершенияИзменения = Новый ОписаниеОповещения(
			"ЗавершитьИзменениеУсловияПоОбъекту",
			ЭтотОбъект,
			ТекущийЭлементОтбора);
		
		ОткрытьФорму("Справочник.АлгоритмыПроверки.ФормаВыбора",
			ПараметрыФормы,
			ЭтотОбъект,,,,
			ОбработчикЗавершенияИзменения,
			РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		
	ИначеЕсли ТекущийЭлементОтбора.ЛевоеЗначение = УсловиеПоРезультатуВыполненияДействия Тогда
	
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("ПредшествующиеДействия", Параметры.ПредшествующиеДействия);
		ПараметрыФормы.Вставить("ТекущийРезультатВыполненияДействия",
			ТекущийЭлементОтбора.ПравоеЗначение);
		
		ПараметрыФормы.Вставить("ТолькоПросмотр", Параметры.ТолькоПросмотр);
		
		ОбработчикЗавершенияИзменения = Новый ОписаниеОповещения(
			"ЗавершитьИзменениеРезультатаВыполненияДействияВУсловии",
			ЭтотОбъект,
			ТекущийЭлементОтбора);
		
		ОткрытьФорму("ОбщаяФорма.ВыборРезультатаВыполненияДействияСхемыОбработкиОбъекта",
			ПараметрыФормы,
			ЭтотОбъект,,,,
			ОбработчикЗавершенияИзменения,
			РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
				
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Готово(Команда)
	
	ОчиститьПредставленияУсловийКомпоновщика();
	
	РезультатНастройки = Новый Структура;
	РезультатНастройки.Вставить("Описание", Описание);
	РезультатНастройки.Вставить("НастройкиУсловия", НастройкиУсловия(Компоновщик));
	
	Закрыть(РезультатНастройки);
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	Закрыть(Неопределено);
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьРезультатВыполненияДействия(Команда)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ПредшествующиеДействия", Параметры.ПредшествующиеДействия);
	
	ОбработчикЗавершенияДобавления = Новый ОписаниеОповещения(
		"ЗавершитьДобавлениеРезультатаВыполненияДействияВУсловие", ЭтотОбъект);
	
	ОткрытьФорму("ОбщаяФорма.ВыборРезультатаВыполненияДействияСхемыОбработкиОбъекта",
		ПараметрыФормы,
		ЭтотОбъект,,,,
		ОбработчикЗавершенияДобавления);
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьУсловиеПоОбъекту(Команда)
		
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ТипОбъекта", ТипОбъекта);
	
	ОткрытьФорму("Справочник.АлгоритмыПроверки.ФормаВыбора",
		ПараметрыФормы,
		ЭтотОбъект,,,,
		Новый ОписаниеОповещения("ЗавершитьДобавлениеУсловияПоОбъекту", ЭтотОбъект));
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Функция НастройкиУсловия(Компоновщик)
	
	НастройкиУсловия = Компоновщик.ПолучитьНастройки();
	Возврат НастройкиУсловия;
	
КонецФункции

&НаКлиенте
Процедура ЗавершитьДобавлениеРезультатаВыполненияДействияВУсловие(
	РезультатДействия, ДопПараметры) Экспорт
	
	Если РезультатДействия = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	МенеджерМодуля = РаботаСПроцессамиПоОбработкамОбъектовКлиентСервер;
	ДобавитьУсловиеВКомпоновщик(
		МенеджерМодуля.ИмяПоляУсловиеПоРезультатуВыполненияДействия(),
		РезультатДействия);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗавершитьИзменениеРезультатаВыполненияДействияВУсловии(
	Результат, ТекущийЭлементОтбора) Экспорт
	
	Если Результат = Неопределено Или Параметры.ТолькоПросмотр Тогда
		Возврат;
	КонецЕсли;
	
	ТекущийЭлементОтбора.ПравоеЗначение = Результат;
	
	ЗаполнитьПредставленияУсловийКомпоновщика();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗавершитьДобавлениеУсловияПоОбъекту(УсловиеПоОбъекту, ДопПараметры) Экспорт
	
	Если УсловиеПоОбъекту = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	МенеджерМодуля = РаботаСПроцессамиПоОбработкамОбъектовКлиентСервер;
	ДобавитьУсловиеВКомпоновщик(
		МенеджерМодуля.ИмяПоляУсловиеПоОбъекту(),
		УсловиеПоОбъекту);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗавершитьИзменениеУсловияПоОбъекту(Результат, ТекущийЭлементОтбора) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТекущийЭлементОтбора.ПравоеЗначение = Результат;
	
	ЗаполнитьПредставленияУсловийКомпоновщика();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПредставленияУсловийКомпоновщика()
	
	Условия = Новый Массив;
	
	ПолучитьУсловияКомпоновщика(Компоновщик.Настройки.Отбор.Элементы, Условия);
			
	ПолеУсловиеПоОбъекту = Новый ПолеКомпоновкиДанных(
		РаботаСПроцессамиПоОбработкамОбъектовКлиентСервер.ИмяПоляУсловиеПоОбъекту());
	
	УсловиеПоРезультатуВыполненияДействий = Новый ПолеКомпоновкиДанных(
		РаботаСПроцессамиПоОбработкамОбъектовКлиентСервер.
		ИмяПоляУсловиеПоРезультатуВыполненияДействия());
	
	Для Каждого СтрокаУсловия Из Условия Цикл
		
		Если СтрокаУсловия.ЛевоеЗначение = ПолеУсловиеПоОбъекту Тогда
			
			СтрокаУсловия.Представление = Строка(СтрокаУсловия.ПравоеЗначение);
			
		ИначеЕсли СтрокаУсловия.ЛевоеЗначение = УсловиеПоРезультатуВыполненияДействий Тогда
			
			Если Не ЗначениеЗаполнено(СтрокаУсловия.ПравоеЗначение.ИмяДействия)
				Или Не ЗначениеЗаполнено(СтрокаУсловия.ПравоеЗначение.РезультатВыполнения) Тогда
			
				СтрокаУсловия.Представление = НСтр("ru = '<Некорректное условие>'");
			Иначе 
				СтрокаУсловия.Представление =
					Строка(Параметры.ПредшествующиеДействия[СтрокаУсловия.ПравоеЗначение.ИмяДействия])
					+ " - "
					+ Строка(СтрокаУсловия.ПравоеЗначение.РезультатВыполнения);
			КонецЕсли;
		
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьПредставленияУсловийКомпоновщика()
		
	Условия = Новый Массив;
	ПолучитьУсловияКомпоновщика(Компоновщик.Настройки.Отбор.Элементы, Условия);
	Для Каждого СтрокаУсловия Из Условия Цикл
		СтрокаУсловия.Представление = "";
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьУсловиеВКомпоновщик(ПутьКДанным, Условие)
	
	КоллекцияЭлементовОтбора = Компоновщик.Настройки.Отбор.Элементы;
	
	ТекущийЭлементОтбора = Компоновщик.Настройки.Отбор.ПолучитьОбъектПоИдентификатору(
		Элементы.КомпоновщикНастройкиОтбор.ТекущаяСтрока);
	
	Если ТипЗнч(ТекущийЭлементОтбора) = Тип("ГруппаЭлементовОтбораКомпоновкиДанных") Тогда
		КоллекцияЭлементовОтбора = ТекущийЭлементОтбора.Элементы;
	ИначеЕсли ТипЗнч(ТекущийЭлементОтбора) = Тип("ЭлементОтбораКомпоновкиДанных")
		И ТекущийЭлементОтбора.Родитель <> Неопределено Тогда
		
		КоллекцияЭлементовОтбора = ТекущийЭлементОтбора.Родитель.Элементы;
	КонецЕсли;
	
	НовыйЭлемент = ДобавитьУсловиеВЭлементыОтбораКомпоновщика(
		КоллекцияЭлементовОтбора, ПутьКДанным, Условие);
	
	Элементы.КомпоновщикНастройкиОтбор.ТекущаяСтрока = 
		Компоновщик.Настройки.Отбор.ПолучитьИдентификаторПоОбъекту(НовыйЭлемент);
	
	ЗаполнитьПредставленияУсловийКомпоновщика();
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ДобавитьУсловиеВЭлементыОтбораКомпоновщика(ЭлементыОтбора, ПутьКДанным, Условие)
	
	НовыйЭлемент = ЭлементыОтбора.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	НовыйЭлемент.ЛевоеЗначение = Новый ПолеКомпоновкиДанных(ПутьКДанным);
	НовыйЭлемент.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	НовыйЭлемент.ПравоеЗначение = Условие;
	
	Возврат НовыйЭлемент;
	
КонецФункции

&НаКлиенте
Процедура ПолучитьУсловияКомпоновщика(ЭлементыОтбораКомпоновщика, Условия)
	
	Для Каждого ЭлементОтбора Из ЭлементыОтбораКомпоновщика Цикл
		
		Если ТипЗнч(ЭлементОтбора) = Тип("ЭлементОтбораКомпоновкиДанных") Тогда
			Условия.Добавить(ЭлементОтбора);
		Иначе
			ПолучитьУсловияКомпоновщика(ЭлементОтбора.Элементы, Условия);
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти