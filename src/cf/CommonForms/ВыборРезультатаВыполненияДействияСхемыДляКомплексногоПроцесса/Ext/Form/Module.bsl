
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Параметры.ТекущийРезультатВыполненияДействия) Тогда
		ИмяДействия = Параметры.ТекущийРезультатВыполненияДействия.ИмяДействия;
		РезультатВыполнения = Параметры.ТекущийРезультатВыполненияДействия.РезультатВыполнения;
	КонецЕсли;
	
	ВариантыРезультатовВыполненияДействий = Новый Структура;
	
	Если ЗначениеЗаполнено(Параметры.ПредшествующиеДействия) Тогда
		
		Для Каждого КлючЗначение Из Параметры.ПредшествующиеДействия Цикл
			
			Элементы.ИмяДействия.СписокВыбора.Добавить(
				КлючЗначение.Ключ,
				Строка(КлючЗначение.Значение));
			
			РезультатыВыполненияДействия = 
				РаботаСКомплекснымиБизнесПроцессамиКлиентСервер.РезультатыВыполненияДляДействияКомплексногоПроцесса(
					КлючЗначение.Значение);
			
			ВариантыРезультатовВыполненияДействий.Вставить(
				КлючЗначение.Ключ, РезультатыВыполненияДействия);
			
		КонецЦикла;

	КонецЕсли;
	
	ЗаполнитьСписокВыбораРезультатовПоДействию(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ИмяДействияПриИзменении(Элемент)
	
	РезультатВыполнения = Неопределено;
	
	ЗаполнитьСписокВыбораРезультатовПоДействию(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Готово(Команда)
	
	Если Не ЗначениеЗаполнено(ИмяДействия) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(
			НСтр("ru = 'Не указано действие'"),, "ИмяДействия");
		
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(РезультатВыполнения) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(
			НСтр("ru = 'Не указан результат выполнения'"),, "РезультатВыполнения");
		
		Возврат;
	КонецЕсли;
	
	Результат = РаботаСКомплекснымиБизнесПроцессамиКлиентСервер.СтруктураУсловияРезультатВыполненияДействия(); 
	
	Результат.ИмяДействия = ИмяДействия;
	Результат.РезультатВыполнения = РезультатВыполнения;
	
	Закрыть(Результат);
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	Закрыть(Неопределено);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиентеНаСервереБезКонтекста
Процедура ЗаполнитьСписокВыбораРезультатовПоДействию(Форма)
	
	Форма.Элементы.РезультатВыполнения.СписокВыбора.Очистить();
	
	Если Не ЗначениеЗаполнено(Форма.ИмяДействия) Тогда
		Возврат;
	КонецЕсли;
	
	РезультатыВыполненияДействия = 
		Форма.ВариантыРезультатовВыполненияДействий[Форма.ИмяДействия];
	
	Для Каждого РезультатВыполненияДействия Из РезультатыВыполненияДействия Цикл
		Форма.Элементы.РезультатВыполнения.СписокВыбора.Добавить(
			РезультатВыполненияДействия.Значение,
			РезультатВыполненияДействия.Представление);
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти