&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьПризнакПроверки = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(ИмяФормы, "УстановитьПризнакПроверки");
	Если УстановитьПризнакПроверки = Неопределено Тогда
		УстановитьПризнакПроверки = Ложь;
	КонецЕсли;

	СписокДокументов.ЗагрузитьЗначения(Параметры.Документы);
	ВидСправочникаДокументов = Параметры.Документы[0].Метаданные().Имя;
	
	ПриСозданииНаСервереРедакцииКонфигурации();	
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьИЗакрыть(Команда)
	
	СохранитьИЗакрытьСервер();
	Оповестить("УстановкаРеквизитовДокументов", ВидСправочникаДокументов, ЭтаФорма);
	Закрыть();
	
КонецПроцедуры

&НаСервере
Процедура СохранитьИЗакрытьСервер()
	
	НачатьТранзакцию();
	
	Попытка
		МенеджерЗаписиРегистра = РегистрыСведений.ПроверкаЗагруженныхДанных.СоздатьМенеджерЗаписи();
		Для каждого Элемент Из СписокДокументов Цикл
			ДокументОбъект = Элемент.Значение.ПолучитьОбъект();
			ЗаблокироватьДанныеДляРедактирования(Элемент.Значение);
			ЗаполнитьРеквизитыДокумента(ДокументОбъект);
			ДокументОбъект.Записать();
			Если УстановитьПризнакПроверки Тогда
			    МенеджерЗаписиРегистра.Объект = Элемент.Значение;
				МенеджерЗаписиРегистра.Прочитать();
				Если МенеджерЗаписиРегистра.Выбран() Тогда
					МенеджерЗаписиРегистра.Проверен = Истина;
					МенеджерЗаписиРегистра.Проверил = ПользователиКлиентСервер.ТекущийПользователь();
					МенеджерЗаписиРегистра.ДатаПроверки = ТекущаяДатаСеанса();
					МенеджерЗаписиРегистра.Записать();
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
		ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(ИмяФормы, "УстановитьПризнакПроверки", УстановитьПризнакПроверки);
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		ВызватьИсключение ОписаниеОшибки();
	КонецПопытки;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьРеквизитыДокумента(ДокументОбъект)
	
	Если ВидСправочникаДокументов = "ДокументыПредприятия" Тогда
		ЗаполнитьРеквизитДокумента(ДокументОбъект, "ВидДокумента", ДокументыПредприятияВидДокумента);
		ЗаполнитьРеквизитДокумента(ДокументОбъект, "ГрифДоступа", ДокументыПредприятияГрифДоступа);
		ЗаполнитьРеквизитДокумента(ДокументОбъект, "Папка", ДокументыПредприятияПапка);
		ЗаполнитьРеквизитДокумента(ДокументОбъект, "НоменклатураДел", ДокументыПредприятияНоменклатураДел);
		ЗаполнитьРеквизитДокумента(ДокументОбъект, "Организация", ДокументыПредприятияОрганизация);
		ЗаполнитьРеквизитДокумента(ДокументОбъект, "Подразделение", ДокументыПредприятияПодразделение);
		ЗаполнитьРеквизитДокумента(ДокументОбъект, "ВопросДеятельности", ДокументыПредприятияВопросДеятельности);
					
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьРеквизитДокумента(ДокументОбъект, ИмяРеквизита, ЗначениеРеквизита)
	Если ЗначениеЗаполнено(ЗначениеРеквизита) Тогда
		ДокументОбъект[ИмяРеквизита] = ЗначениеРеквизита;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ВидДокументаПриИзменении(Элемент = Неопределено)
	
	Если Элемент = Элементы.ДокументыПредприятияВидДокумента Тогда
		УстановитьПараметрыФункциональныхОпцийФормы(Новый Структура("ВидДокумента", ДокументыПредприятияВидДокумента));
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ВидДокументаПриИзменении();
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервереРедакцииКонфигурации()	
	
	Элементы.ДокументыПредприятияОрганизация.Заголовок = РедакцииКонфигурацииКлиентСервер.Организация();
	
КонецПроцедуры
