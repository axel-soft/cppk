
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ДатаОтсчетаДляРасчетаСроков = ТекущаяДатаСеанса();
	Заголовок = Параметры.Заголовок;
	ОтложенноеНачалоДней = Параметры.Дни;
	ОтложенноеНачалоЧасов = Параметры.Часы;
	ОтложеннаяДата = Параметры.ОтложеннаяДата;
	
	Элементы.ОтложеннаяДата.Видимость = Параметры.РежимИзДействия;
	КлючСохраненияПоложенияОкна = Параметры.РежимИзДействия;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ОтложенныйСтартДнейПриИзменении(Элемент)
	
	ОтложеннаяДата = Неопределено;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтложенныйСтартЧасовПриИзменении(Элемент)
	
	ОтложеннаяДата = Неопределено;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтложеннаяДатаПриИзменении(Элемент)
	
	ОтложенноеНачалоДней = 0;
	ОтложенноеНачалоЧасов = 0;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтложеннаяДатаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("СрокИсполнения", ОтложеннаяДата);
	ПараметрыФормы.Вставить("ДатаОтсчета", ДатаОтсчетаДляРасчетаСроков);
	
	ОткрытьФорму("ОбщаяФорма.ВыборСрокаИсполнения",
		ПараметрыФормы, 
		Элементы.ОтложеннаяДата,,,,,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Готово(Команда)
	
	Если ЗначениеЗаполнено(ОтложеннаяДата) И ОтложеннаяДата <= ТекущаяДата() Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(
			НСтр("ru = 'Выбранная дата меньше текущей. 
				 |Укажите корректную дату отложенного выполнения.'"),, ОтложеннаяДата);
		Возврат;
	КонецЕсли;
	
	ДанныеВозврата = Новый Структура;
	ДанныеВозврата.Вставить("Дни", ОтложенноеНачалоДней);
	ДанныеВозврата.Вставить("Часы", ОтложенноеНачалоЧасов);
	ДанныеВозврата.Вставить("ОтложеннаяДата", ОтложеннаяДата);
	Закрыть(ДанныеВозврата);
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	Закрыть(Неопределено);
	
КонецПроцедуры

#КонецОбласти
