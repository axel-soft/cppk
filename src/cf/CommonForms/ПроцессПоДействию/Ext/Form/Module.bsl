#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Процесс = Параметры.Ключ;
	
	НавигационнаяСсылка = ПолучитьНавигационнуюСсылку(Процесс);
	
	РеквизитыПроцесса = 
		ОбщегоНазначенияДокументооборот.ЗначенияРеквизитовОбъектаВПривилегированномРежиме(Процесс,
		"Ссылка,
		|ДатаНачала,
		|ДатаЗавершения,
		|Состояние,
		|Стартован,
		|Завершен,
		|Предметы,
		|ПричинаПрерывания");
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, РеквизитыПроцесса);
	
	Действие = РегистрыСведений.ПроцессыДействий.ДействиеПоПроцессу(Процесс);
	
	Предмет = ОбщегоНазначенияДокументооборот.ЗначениеРеквизитаОбъектаВПривилегированномРежиме(
		Действие, "Предмет");
	
	Заголовок = Строка(Процесс);
	
	РаботаСПроцессамиПоОбработкамОбъектов.ОбновитьДлительностьПроцесса(ЭтотОбъект);
	РаботаСПроцессамиПоОбработкамОбъектов.ОбновитьОписаниеСостоянияПроцесса(ЭтотОбъект);
	
	ФорматДаты = "ДФ='dd.MM.yyyy HH:mm'";
	Элементы.ЗадачиДата.Формат = ФорматДаты;
	Элементы.ЗадачиДатаВыполнения.Формат = ФорматДаты;
	
	Задачи.Параметры.УстановитьЗначениеПараметра("БизнесПроцесс", Процесс);
	
	РаботаСПроцессамиПоОбработкамОбъектов.УстановитьУсловноеОформлениеСпискаЗадачПроцесса(ЭтотОбъект);
	
	Если МК_ПовтИсп.ЭтоМобильныйКлиент() Тогда
		НастроитьЭлементыФормыДляМобильногоУстройства();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ПараметрыОповещения = Новый Структура("МожноОткрыть, Процесс", Истина, Процесс);	
	Оповестить("ПроцессПоДействию_ПриОткрытии", ПараметрыОповещения, ЭтаФорма);
	
	Если Не ПараметрыОповещения.МожноОткрыть Тогда
		Отказ = Истина;
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ПроцессПоДействию_ПриОткрытии"
		И ТипЗнч(Параметр) = Тип("Структура")
		И Источник <> ЭтаФорма Тогда
	
		Если Процесс = Параметр.Процесс Тогда 
			Параметр.МожноОткрыть = Ложь;
		КонецЕсли;		
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ДекорацияОписаниеНажатие(Элемент)
	
	РаботаСПроцессамиПоОбработкамОбъектовКлиент.ОбработатьНажатиеНаОписаниеСостоянияПроцесса(
		ЭтотОбъект);
		
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

//Настраивает элементы формы для мобильного устройства
Процедура НастроитьЭлементыФормыДляМобильногоУстройства()
	
	//настраиваем форму
	СворачиваниеЭлементовПоВажности = СворачиваниеЭлементовФормыПоВажности.НеИспользовать;
	ВертикальныйИнтервал = ИнтервалМеждуЭлементамиФормы.Половинный;
	Элементы.Начато.РастягиватьПоГоризонтали = Истина;
	
	//настраиваем список подзадач
	Элементы.Задачи.Шапка = Ложь;
	
КонецПроцедуры

#КонецОбласти