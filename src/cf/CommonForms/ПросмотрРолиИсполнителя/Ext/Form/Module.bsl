#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Параметры.Заголовок) Тогда
		Заголовок = Параметры.Заголовок;
		АвтоЗаголовок = Ложь;
	КонецЕсли;

	Элементы.Исполнители.Видимость = ТолькоПросмотр;
	
	Роль = Параметры.РольИсполнителя;
	ОсновнойОбъектАдресации = Параметры.ОсновнойОбъектАдресации;
	ДополнительныйОбъектАдресации = Параметры.ДополнительныйОбъектАдресации;

	Если ТолькоПросмотр Тогда
		Элементы.Отмена.Видимость = Ложь;
		Элементы.Закрыть.Видимость = Истина;
		Элементы.Закрыть.КнопкаПоУмолчанию = Истина;
		
		ПолнаяРоль = Параметры.ПолнаяРоль;
		ЗаполнитьИсполнители(ПолнаяРоль);
	КонецЕсли;
	
	УстановитьТипыОбъектовАдресации();
	УстановитьСостояниеЭлементов();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ИсполнителиПередУдалением(Элемент, Отказ)
	
	Отказ = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ИсполнителиПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, ЭтоГруппа, Параметр)
	
	Отказ = Истина;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьТипыОбъектовАдресации()
	
	ТипыОсновногоОбъектаАдресации = Роль.ТипыОсновногоОбъектаАдресации.ТипЗначения;
	ТипыДополнительногоОбъектаАдресации = Роль.ТипыДополнительногоОбъектаАдресации.ТипЗначения;
	ИспользуетсяСОбъектамиАдресации = Роль.ИспользуетсяСОбъектамиАдресации;
	ИспользуетсяБезОбъектовАдресации = Роль.ИспользуетсяБезОбъектовАдресации;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьСостояниеЭлементов()

	ЗаданыТипыОсновногоОбъектаАдресации = ИспользуетсяСОбъектамиАдресации
		И ЗначениеЗаполнено(ТипыОсновногоОбъектаАдресации);
	ЗаданыТипыДополнительногоОбъектаАдресации = ИспользуетсяСОбъектамиАдресации 
		И ЗначениеЗаполнено(ТипыДополнительногоОбъектаАдресации);
		
	КлючСохраненияПоложенияОкна = 
		?(ЗаданыТипыОсновногоОбъектаАдресации Или ЗначениеЗаполнено(ОсновнойОбъектАдресации), "Основной", "")
		+ ?(ЗаданыТипыДополнительногоОбъектаАдресации Или ЗначениеЗаполнено(ДополнительныйОбъектАдресации), "Дополнительный", "");
		
	Элементы.ОсновнойОбъектАдресации.Видимость = ЗаданыТипыОсновногоОбъектаАдресации
		Или ЗначениеЗаполнено(ОсновнойОбъектАдресации);
	Элементы.ОсновнойОбъектАдресации.Заголовок = Роль.ТипыОсновногоОбъектаАдресации.Наименование;
	
	Элементы.ДополнительныйОбъектАдресации.Видимость = ЗаданыТипыДополнительногоОбъектаАдресации
		Или ЗначениеЗаполнено(ДополнительныйОбъектАдресации);
	Элементы.ДополнительныйОбъектАдресации.Заголовок = Роль.ТипыДополнительногоОбъектаАдресации.Наименование;
	
	Элементы.ОсновнойОбъектАдресации.АвтоОтметкаНезаполненного = ЗаданыТипыОсновногоОбъектаАдресации
		И НЕ ИспользуетсяБезОбъектовАдресации;
	Элементы.ДополнительныйОбъектАдресации.АвтоОтметкаНезаполненного = ЗаданыТипыДополнительногоОбъектаАдресации
		И НЕ ИспользуетсяБезОбъектовАдресации;
	Элементы.ОсновнойОбъектАдресации.ОграничениеТипа = ТипыОсновногоОбъектаАдресации;
	Элементы.ДополнительныйОбъектАдресации.ОграничениеТипа = ТипыДополнительногоОбъектаАдресации;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьИсполнители(ПолнаяРоль)
	
	Исполнители.Очистить();
	
	ВсеИсполнители = РегистрыСведений.ИсполнителиРолей.ИсполнителиРоли(ПолнаяРоль, Истина);
	
	Для Каждого ИсполнительРоли Из ВсеИсполнители Цикл
		НовСтр = Исполнители.Добавить();
		НовСтр.Исполнитель = ИсполнительРоли;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ИсполнителиВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ТекущиеДанные = Элементы.Исполнители.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
		
	ПоказатьЗначение(, ТекущиеДанные.Исполнитель);	
	
КонецПроцедуры
	

#КонецОбласти