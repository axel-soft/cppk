#Область ОписаниеПараметровФормы

// ИмяПредмета - имя текущего предмета в настройках процесса.
// АлгоритмПроверки - текущий алгоритм в настройках процесса.
// ПредметыПроцесса - Соответствие - все предметы процесса.
//  * Ключ - СправочникСсылка.ИменаПредметов
//  * Значение - Произвольный - ссылка на предмет процесса. Допустима пустая ссылка.
//                              т.е. тоже самое, что хранится в самом процессе/шаблоне. 

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ИмяПредмета = Параметры.ИмяПредмета;
	АлгоритмПроверки = Параметры.АлгоритмПроверки;
	
	Для Каждого ПредметПроцесса Из Параметры.ПредметыПроцесса Цикл
		
		СтрокаТаблицы = ПредметыПроцесса.Добавить();
		СтрокаТаблицы.ИмяПредмета = ПредметПроцесса.Ключ;
		СтрокаТаблицы.ТипОбъекта = Перечисления.ТипыОбъектов.ТипОбъектаПоСсылке(
			ПредметПроцесса.Значение);
		
		Если ЗначениеЗаполнено(ПредметПроцесса.Значение) Тогда
			ПредставлениеПредмета = СтрШаблон("%1 (%2)",
				ПредметПроцесса.Значение, ПредметПроцесса.Ключ); 
		Иначе
			ПредставлениеПредмета = Строка(ПредметПроцесса.Ключ);
		КонецЕсли;
		
		Элементы.ИмяПредмета.СписокВыбора.Добавить(ПредметПроцесса.Ключ, ПредставлениеПредмета);
		
	КонецЦикла;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ИмяПредметаПриИзменении(Элемент)
	
	АлгоритмПроверки = Неопределено;
	
КонецПроцедуры

&НаКлиенте
Процедура АлгоритмПроверкиНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыФормы = Новый Структура;
	
	ТипОбъекта = ТипОбъектаДляИмениПредмета(); 
	Если ТипОбъекта <> Неопределено Тогда	
		ПараметрыФормы.Вставить("ТипОбъекта", ТипОбъекта);
	КонецЕсли;
	
	ОткрытьФорму("Справочник.АлгоритмыПроверки.ФормаВыбора",
		ПараметрыФормы,
		Элементы.АлгоритмПроверки);
		
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Готово(Команда)
	
	Если Не ЗначениеЗаполнено(ИмяПредмета) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(
			НСтр("ru = 'Не указан предмет'"),, "ИмяПредмета");
		
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(АлгоритмПроверки) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(
			НСтр("ru = 'Не указано алгоритм проверки предмета'"),, "АлгоритмПроверки");
		
		Возврат;
	КонецЕсли;
	
	Результат = Новый Структура;
	Результат.Вставить("ИмяПредмета", ИмяПредмета);
	Результат.Вставить("АлгоритмПроверки", АлгоритмПроверки);
	Закрыть(Результат);
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	Закрыть(Неопределено);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Функция ТипОбъектаДляИмениПредмета()
	
	Если Не ЗначениеЗаполнено(ИмяПредмета) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Отбор = Новый Структура;
	Отбор.Вставить("ИмяПредмета", ИмяПредмета);
	
	НаденныеСтроки = ПредметыПроцесса.НайтиСтроки(Отбор);
	Если НаденныеСтроки.Количество() = 0 Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(НаденныеСтроки[0].ТипОбъекта) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Возврат НаденныеСтроки[0].ТипОбъекта;
	
КонецФункции

#КонецОбласти