
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Параметры.Свойство("Комментарий", Комментарий);
	
	Если Параметры.Свойство("Заголовок") И ЗначениеЗаполнено(Параметры.Заголовок) Тогда 
		Заголовок = Параметры.Заголовок;
		АвтоЗаголовок = Ложь;
	КонецЕсли;
	
	Если Параметры.Свойство("ИспользоватьШаблоны") И Параметры.ИспользоватьШаблоны Тогда
		
		Элементы.Комментарий.КнопкаВыбора = Истина; 
		Элементы.Комментарий.ОтображениеКнопкиВыбора = ОтображениеКнопкиВыбора.ОтображатьВПолеВвода; 
		
	КонецЕсли;
	Параметры.Свойство("ОбластьПримененияШаблона", ОбластьПримененияШаблона);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиЭлементовФормы

&НаКлиенте
Процедура КомментарийНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОписаниеОповещения = Новый ОписаниеОповещения("ЗавершениеВставкиШаблонаТекста", ЭтотОбъект);
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ОбластьПрименения", 
		ОбластьПримененияШаблона);
		
	ОткрытьФорму("Справочник.ШаблоныТекстов.Форма.ФормаВыбораШаблонаТекста",
		ПараметрыФормы,
		ЭтаФорма,,,,
		ОписаниеОповещения,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		
КонецПроцедуры

&НаКлиенте
Процедура ЗавершениеВставкиШаблонаТекста(ШаблонТекста, Параметры) Экспорт
	
	Если ШаблонТекста = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Комментарий = ШаблонТекста;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)
	Закрыть(Комментарий);
КонецПроцедуры

#КонецОбласти
