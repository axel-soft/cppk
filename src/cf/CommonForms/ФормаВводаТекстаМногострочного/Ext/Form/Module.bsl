
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Заголовок = Параметры.Описание;
	
	Если Параметры.Свойство("ОбластьПрименения") Тогда
		ОбластьПрименения = Параметры.ОбластьПрименения;
	КонецЕсли;	
	
	ПоказыватьПолеHTML = Истина;
	Если Параметры.Свойство("ПоказыватьПолеHTML") Тогда
		ПоказыватьПолеHTML = Параметры.ПоказыватьПолеHTML;
	КонецЕсли;	
	
	Если Не ЗначениеЗаполнено(ОбластьПрименения) Тогда
		Элементы.НовыйТекст.КнопкаВыбора = Ложь;
	КонецЕсли;	
	
	ТипЗамечания = Неопределено;
	Если Параметры.Свойство("ТипЗамечания") Тогда
		ТипЗамечания = Параметры.ТипЗамечания;
	КонецЕсли;	
	Если ТипЗамечания = Перечисления.ТипыЗамечаний.Комментарий Тогда
		Элементы.ДекорацияПривязка.Заголовок 
			= НСтр("ru = 'Комментарий не будет привязан к определенной части документа'");
	ИначеЕсли ТипЗамечания = Перечисления.ТипыЗамечаний.Замечание Тогда
		Элементы.ДекорацияПривязка.Заголовок 
			= НСтр("ru = 'Замечание не будет привязано к определенной части документа'");
	КонецЕсли;	
	
	Элементы.ГруппаПояснение.Видимость = Не ПоказыватьПолеHTML;
	
	КлючСохраненияПоложенияОкна = ?(ПоказыватьПолеHTML, "ПоказыватьПолеHTML", "НеПоказыватьПолеHTML");
	КлючНазначенияИспользования = ?(ПоказыватьПолеHTML, "ПоказыватьПолеHTML", "НеПоказыватьПолеHTML");
	
	ЭтоМобильныйКлиент = ПараметрыСеанса.ЭтоМобильныйКлиент;
	Если ЭтоМобильныйКлиент Тогда
		МК_НастроитьЭлементыФормы();
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура НовыйТекстНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)

	ОписаниеОповещения = Новый ОписаниеОповещения(
		"ПослеВыбораШаблонаТекста",
		ЭтаФорма);
	
	ПараметрыФормы = Новый Структура("ОбластьПрименения, Заголовок, ЗаголовокФормыСоздания");
	ПараметрыФормы.ОбластьПрименения = ОбластьПрименения;
	ПараметрыФормы.Заголовок = НСтр("ru = 'Выбор шаблона'");
	ПараметрыФормы.ЗаголовокФормыСоздания = НСтр("ru = 'Выбор шаблона'");
	
	ОткрытьФорму("Справочник.ШаблоныТекстов.ФормаВыбора", 
		ПараметрыФормы,,,,,
		ОписаниеОповещения);

КонецПроцедуры

&НаКлиенте
Процедура ПослеВыбораШаблонаТекста(Результат, ДополнительныеПараметры) Экспорт
	
	Если Не ЗначениеЗаполнено(Результат) Тогда
		Возврат;
	КонецЕсли;
	
	НовыйТекст = ШаблоныТекстовВызовСервера.ТекстШаблона(Результат);
	
КонецПроцедуры

&НаКлиенте
Процедура ОК(Команда)
	
	Закрыть(НовыйТекст);
	
КонецПроцедуры

&НаСервере
Процедура МК_НастроитьЭлементыФормы()
	
	Элементы.МК_НижнийБлок.Видимость = Истина;
	
	Элементы.Переместить(Элементы.ФормаОК, Элементы.МК_НижнийБлок);
	МК.ОформитьАкцентнуюКнопку(Элементы.ФормаОК);
	
	Элементы.Переместить(Элементы.ФормаОтмена, Элементы.МК_НижнийБлок);
	МК.ОформитьАкцентнуюКнопку(Элементы.ФормаОтмена);
	
	Элементы.ФормаОтмена.ЦветФона = ЦветаСтиля.ОтметкаОтрицательногоВыполненияЗадачи;
	Элементы.ФормаОтмена.ЦветРамки = ЦветаСтиля.ОтметкаОтрицательногоВыполненияЗадачи;
	
	Элементы.ГруппаПояснение.ЦветФона = ЦветаСтиля.МК_ЦветФонаГруппы;
	
	Элементы.НовыйТекст.Высота = 2;
	Элементы.НовыйТекст.РастягиватьПоВертикали = Истина;
	Элементы.ФормаОК.ПоложениеВКоманднойПанели = ПоложениеКнопкиВКоманднойПанели.ВКоманднойПанели;
	Элементы.ФормаОтмена.ПоложениеВКоманднойПанели = ПоложениеКнопкиВКоманднойПанели.ВКоманднойПанели;

КонецПроцедуры
