#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Инициализирует ЭтотУзел при первоначальном заполнении ИБ.
//
Процедура ИнициализироватьПредопределенныйУзел() Экспорт
	
	НачатьТранзакцию();
	Попытка
		
		СтарыйЭтотУзелСсылка = ЭтотУзел();
		
		ФиксированныйГУИД = "539c38a6-9e4a-4d51-ad18-5040b97d7348"; // Требуется для корректной выгрузки узлов в редакции Холдинг.
		СсылкаНовогоУзла = ПолучитьСсылку(Новый УникальныйИдентификатор(ФиксированныйГУИД));
		
		НовыйЭтотУзел = СоздатьУзел();
		НовыйЭтотУзел.УстановитьСсылкуНового(СсылкаНовогоУзла);
		НовыйЭтотУзел.ДополнительныеСвойства.Вставить("ОтключитьОтметкиВремени");
		НовыйЭтотУзел.ОбменДанными.Загрузка = Истина;
		НовыйЭтотУзел.ЭтотУзел = Истина;
		НовыйЭтотУзел.Код = "001";
		НовыйЭтотУзел.Наименование = НаименованиеЭтогоУзла();
		НовыйЭтотУзел.Записать();
		
		СтарыйЭтотУзел = СтарыйЭтотУзелСсылка.ПолучитьОбъект();
		СтарыйЭтотУзел.ДополнительныеСвойства.Вставить("ОтключитьОтметкиВремени");
		СтарыйЭтотУзел.ЭтотУзел = Ложь;
		СтарыйЭтотУзел.Записать();
		СтарыйЭтотУзел.Удалить();
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		ОтменитьТранзакцию();
		ВызватьИсключение;
	КонецПопытки;
	
КонецПроцедуры

// Возвращает наименование предопределенного узла.
//
// Возвращаемое значение:
//   Строка
//
Функция НаименованиеЭтогоУзла() Экспорт
	
	Возврат НСтр("ru='1С:Документооборот'");
	
КонецФункции

#КонецОбласти

#КонецЕсли