
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область НастройкиОтчетаПоУмолчанию

//Выполняет заполнение категорий и разделов в зависимости от варианта отчета
//Параметры:
//	КлючВариантаОтчета - Строка - Строковое название варианта отчета:
//	СписокКатегорий - СписокЗначений Из СправочникСсылка.КатегорииОтчетов - в список добавляются необходимые категории
//	СписокРазделов - СписокЗначений Из перечислениеСсылка.РазделыОтчетов - в список добавляются необходимые категории
Процедура ЗаполнитьСписокКатегорийИРазделовОтчета(КлючВариантаОтчета, СписокКатегорий, СписокРазделов) Экспорт
	
	СписокКатегорий.Добавить(Справочники.КатегорииОтчетов.ПоДокументам);
		
	СписокРазделов.Добавить(ОбщегоНазначения.ИдентификаторОбъектаМетаданных(
		Метаданные.Подсистемы.СтандартныеПодсистемы));
			
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Конструктор параметров для вызова формирования отчета
// 
// Возвращаемое значение:
//  Структура:
//	* Организация - СправочникСсылка.Организации - 
//	* Период - СтандартныйПериод -
//	* СостояниеВыгрузкиССТУ - Строка - Выгружались, создана выгрузка, но не выгружались, выгруженные или все.
//	* СостояниеГотовности - Строка - Готовые, не готовые или все
Функция НовыйПараметрыОтчета() Экспорт
	
	ПараметрыОтчета = Новый Структура();
	ПараметрыОтчета.Вставить("Организация", Справочники.Организации.ПустаяСсылка());
	ПараметрыОтчета.Вставить("Период", Новый СтандартныйПериод());
	ПараметрыОтчета.Вставить("СостояниеВыгрузкиССТУ", "");
	ПараметрыОтчета.Вставить("СостояниеГотовности", "");
	
	Возврат ПараметрыОтчета;
	
КонецФункции

// Выполняет запрос и возвращает результаты запроса для отчета.
// 
// Параметры:
//  ПараметрыОтчета - См. НовыйПараметрыОтчета
// 
// Возвращаемое значение:
//  Массив Из РезультатЗапроса
Функция РезультатыЗапросаДляОтчета(ПараметрыОтчета) Экспорт
	
	// Сначала без привилегированного режима:
	Запрос = Новый Запрос(
		"// Запрос 0
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
		|	ОбращенияВопросы.Ссылка.Основание КАК Документ
		|ПОМЕСТИТЬ ВТ_ВсеОбращения
		|ИЗ
		|	Документ.Корреспонденция.ВопросыОбращения КАК ОбращенияВопросы
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ДокументыПредприятия КАК ДокументыПредприятия
		|		ПО ДокументыПредприятия.Ссылка = ОбращенияВопросы.Ссылка.Основание
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ВидыДокументов КАК ВидыДокументов
		|		ПО ДокументыПредприятия.ВидДокумента = ВидыДокументов.Ссылка
		|ГДЕ
		|	ВидыДокументов.ЯвляетсяОбращениемОтГраждан
		|	И НЕ ОбращенияВопросы.Ссылка.ПометкаУдаления
		|	И ДокументыПредприятия.ДатаРегистрации > ДАТАВРЕМЯ(1, 1, 1)
		|	И (ДокументыПредприятия.ДатаРегистрации МЕЖДУ &ДатаНачала И &ДатаОкончания
		|			ИЛИ ОбращенияВопросы.ДатаОтвета МЕЖДУ &ДатаНачала И &ДатаОкончания)
		|	И &ОтборОрганизация
		|	И &ОтборГотовыеКВыгрузке");
	Запрос.УстановитьПараметр("ДатаНачала", ПараметрыОтчета.Период.ДатаНачала);
	Запрос.УстановитьПараметр("ДатаОкончания", КонецДня(ПараметрыОтчета.Период.ДатаОкончания));
	ОтборОрганизация = "ИСТИНА";
	ОтборГотовыеКВыгрузке = "ИСТИНА";
	Если ПолучитьФункциональнуюОпцию("ИспользоватьУчетПоОрганизациям") Тогда 
		ОтборОрганизация = "ДокументыПредприятия.Организация = &Организация";
		Запрос.УстановитьПараметр("Организация", ПараметрыОтчета.Организация);
	КонецЕсли;
	Если ПараметрыОтчета.СостояниеГотовности = РаботаСОбращениямиКлиентСервер.ГотовыеКВыгрузке() Тогда
		ОтборГотовыеКВыгрузке = "ОбращенияВопросы.Ссылка.ГотовоКВыгрузкеССТУ";
	ИначеЕсли ПараметрыОтчета.СостояниеГотовности = РаботаСОбращениямиКлиентСервер.НеГотовыеКВыгрузке() Тогда
		ОтборГотовыеКВыгрузке = "НЕ ОбращенияВопросы.Ссылка.ГотовоКВыгрузкеССТУ";
	КонецЕсли;
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ОтборОрганизация", ОтборОрганизация);
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ОтборГотовыеКВыгрузке", ОтборГотовыеКВыгрузке);
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц();
	Запрос.Выполнить();
	
	// При присоединении остального не обязательно на все связанные объекты должны быть права:
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос.Текст =
		"// Запрос 0
		|/////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВсеОбращения.Документ КАК ДокументОбращение,
		|	МАКСИМУМ(ВыгрузкаТЧ.Ссылка.Дата) КАК ДатаПоследнейВыгрузки
		|
		|ПОМЕСТИТЬ ВТ_ПоследниеДатыВыгрузок
		|ИЗ
		|	Документ.ВыгрузкаВССТУ.Обращения КАК ВыгрузкаТЧ
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
		|	ВТ_ВсеОбращения КАК ВсеОбращения
		|		ПО ВсеОбращения.Документ = ВыгрузкаТЧ.Обращение
		|ГДЕ
		|	НЕ ВыгрузкаТЧ.Ссылка.ПометкаУдаления
		|
		|СГРУППИРОВАТЬ ПО
		|	ВсеОбращения.Документ
		|;
		|
		|// Запрос 1
		|//////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВыгрузкаТЧ.Ссылка КАК ДокументВыгрузка,
		|	ВыгрузкаТЧ.Ссылка.Выгружена КАК Выгружена,
		|	ПоследниеДаты.ДокументОбращение КАК Документ
		|	
		|ПОМЕСТИТЬ ВТ_АктуальныеВыгрузки
		|ИЗ
		|	ВТ_ПоследниеДатыВыгрузок КАК ПоследниеДаты
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
		|	Документ.ВыгрузкаВССТУ.Обращения КАК ВыгрузкаТЧ
		|		ПО ВыгрузкаТЧ.Ссылка.Дата = ПоследниеДаты.ДатаПоследнейВыгрузки
		|			И ВыгрузкаТЧ.Обращение = ПоследниеДаты.ДокументОбращение
		|;
		|
		|// Запрос 2
		|////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВсеОбращения.Документ КАК Документ
		|
		|ПОМЕСТИТЬ ВТ_ОбращенияОтобранныеПоСостояниюВыгрузки
		|ИЗ
		|	ВТ_ВсеОбращения КАК ВсеОбращения
		|	ЛЕВОЕ СОЕДИНЕНИЕ
		|	ВТ_АктуальныеВыгрузки КАК АктуальныеВыгрузки
		|		ПО АктуальныеВыгрузки.Документ = ВсеОбращения.Документ
		|ГДЕ
		|	ИСТИНА
		|	И &ОтборНеВыгруженные
		|	И &ОтборСозданаВыгрузка
		|	И &ОтборВыгруженные
		|;
		|
		|// Запрос 3
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВЫБОР
		|		КОГДА Обращения.ДатаРегистрации <> ДАТАВРЕМЯ(1, 1, 1)
		|			ТОГДА Обращения.ДатаРегистрации
		|		ИНАЧЕ Обращения.ДатаСоздания
		|	КОНЕЦ КАК ДатаСортировки,
		|	Обращения.Ссылка КАК ДокументСсылка,
		|	ОбращенияВопросы.Ссылка.НомерКонтрагента КАК ДокументНомер,
		|	ВЫБОР
		|		КОГДА ОбращенияВопросы.Ссылка.НомерКонтрагента = """"
		|			ТОГДА ДАТАВРЕМЯ(1, 1, 1)
		|		ИНАЧЕ ОбращенияВопросы.Ссылка.ДатаКонтрагента
		|	КОНЕЦ КАК ДокументДата,
		|	Обращения.ДатаСоздания КАК ПоступлениеДата,
		|	Обращения.ДатаРегистрации КАК ПоступлениеДатаРегистрации,
		|	ОбращенияВопросы.ДатаОтвета КАК ОтветАвторуДата,
		|	ОбращенияВопросы.Ссылка.СрокИсполненияПервоначальный КАК СрокИсполненияПервоначальный,
		|	Обращения.СрокИсполнения КАК СрокИсполнения,
		|	ВЫБОР
		|		КОГДА Обращения.СрокИсполнения > ДАТАВРЕМЯ(1, 1, 1)
		|				И ОбращенияВопросы.Ссылка.СрокИсполненияПервоначальный > ДАТАВРЕМЯ(1, 1, 1)
		|				И Обращения.СрокИсполнения > ОбращенияВопросы.Ссылка.СрокИсполненияПервоначальный
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК СрокИсполненияПродлен,
		|	Обращения.Контрагент КАК Заявитель,
		|	Обращения.Контрагент.Наименование КАК ЗаявительНаименование,
		|	"""" КАК АдресЭлектроннойПочты,
		|	"""" КАК ПочтовыйАдрес,
		|	ЕСТЬNULL(ОбращенияВопросы.НомерСтроки, 1) КАК НомерСтроки,
		|	ОбращенияВопросы.Вопрос КАК Вопрос,
		|	ОбращенияВопросы.КодВопроса КАК ВопросКод,
		|	ОбращенияВопросы.РезультатРассмотрения КАК РезультатРассмотрения,
		|	ОбращенияВопросы.ОрганДляПередачи.Наименование КАК НаправленоГосУчреждение,
		|	ДАТАВРЕМЯ(1, 1, 1) КАК НаправленоДата,
		|	"""" КАК НаправленоНомер,
		|	ОрганыИУчреждения.Ссылка КАК НаправленоОрган,
		|	ОрганыИУчреждения.Представление КАК НаправленоОрганПредставление,
		|	ОрганыИУчреждения.ЭтоОрганИсполнительнойВласти КАК ЭтоОрганИсполнительнойВласти,
		|	ОрганыИУчреждения.ИдентификаторССТУ КАК НаправленоОрганССТУ,
		|	ПОДСТРОКА(ОбращенияВопросы.МнениеАвтораОРезультатах, 1, 200) КАК МнениеАвтораОРезультате,
		|	ПОДСТРОКА(ОбращенияВопросы.МнениеАвтораОМерах, 1, 200) КАК МнениеАвтораОМерах
		|ПОМЕСТИТЬ ОбращенияИВопросы
		|ИЗ
		|	Справочник.ДокументыПредприятия КАК Обращения
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ОбращенияОтобранныеПоСостояниюВыгрузки КАК ОбращенияОтобранные
		|		ПО Обращения.Ссылка = ОбращенияОтобранные.Документ
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.Корреспонденция.ВопросыОбращения КАК ОбращенияВопросы
		|		ПО Обращения.Ссылка = ОбращенияВопросы.Ссылка.Основание
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Контрагенты КАК ОрганыИУчреждения
		|		ПО (ОбращенияВопросы.ОрганДляПередачи = ОрганыИУчреждения.Ссылка)
		|;
		|
		|// Запрос 4
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СвязиОбъектов.СвязанныйОбъект КАК СвязанныйОбъект,
		|	СвязиОбъектов.ДополнительныйОбъектСвязи КАК ДополнительныйОбъектСвязи,
		|	ОбращенияИВопросы.ДокументСсылка КАК ДокументСсылка,
		|	СвязиОбъектов.ТипСвязи КАК ТипСвязи
		|ПОМЕСТИТЬ СвязанныеДокументы
		|ИЗ
		|	ОбращенияИВопросы КАК ОбращенияИВопросы
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СвязиОбъектов КАК СвязиОбъектов
		|		ПО ОбращенияИВопросы.ДокументСсылка = СвязиОбъектов.Объект
		|			И (СвязиОбъектов.ДополнительныйОбъектСвязи = ОбращенияИВопросы.Вопрос)
		|ГДЕ
		|	СвязиОбъектов.ТипСвязи В(&ТипыСвязи)
		|	И ТИПЗНАЧЕНИЯ(СвязиОбъектов.СвязанныйОбъект) = ТИП(Справочник.ДокументыПредприятия)
		|	И СвязиОбъектов.СвязанныйОбъект.ВидДокумента.ЯвляетсяИсходящейКорреспонденцией
		|	И СвязиОбъектов.СвязанныйОбъект.ДатаРегистрации <> ДАТАВРЕМЯ(1, 1, 1)
		|;
		|
		|// Запрос 5
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ОбращенияИВопросы.ДатаСортировки КАК ДатаСортировки,
		|	ОбращенияИВопросы.ДокументСсылка КАК ДокументСсылка,
		|	ОбращенияИВопросы.ДокументНомер КАК ДокументНомер,
		|	ОбращенияИВопросы.ДокументДата КАК ДокументДата,
		|	ОбращенияИВопросы.ПоступлениеДата КАК ПоступлениеДата,
		|	ОбращенияИВопросы.ПоступлениеДатаРегистрации КАК ПоступлениеДатаРегистрации,
		|	ОбращенияИВопросы.ОтветАвторуДата КАК ОтветАвторуДата,
		|	ОбращенияИВопросы.Заявитель КАК Заявитель,
		|	ОбращенияИВопросы.ЗаявительНаименование КАК ЗаявительНаименование,
		|	ОбращенияИВопросы.АдресЭлектроннойПочты КАК АдресЭлектроннойПочты,
		|	ОбращенияИВопросы.ПочтовыйАдрес КАК ПочтовыйАдрес,
		|	ОбращенияИВопросы.НомерСтроки КАК НомерСтроки,
		|	ОбращенияИВопросы.Вопрос КАК Вопрос,
		|	ОбращенияИВопросы.ВопросКод КАК ВопросКод,
		|	ОбращенияИВопросы.РезультатРассмотрения КАК РезультатРассмотрения,
		|	ОбращенияИВопросы.СрокИсполненияПродлен КАК СрокИсполненияПродлен,
		|	ОбращенияИВопросы.НаправленоГосУчреждение КАК НаправленоГосУчреждение,
		|	ЕСТЬNULL(СвязанныеДокументы.СвязанныйОбъект.ДатаРегистрации, ОбращенияИВопросы.НаправленоДата) КАК НаправленоДата,
		|	ЕСТЬNULL(СвязанныеДокументы.СвязанныйОбъект.РегистрационныйНомер, ОбращенияИВопросы.НаправленоНомер) КАК НаправленоНомер,
		|	ОбращенияИВопросы.НаправленоОрган КАК НаправленоОрган,
		|	ОбращенияИВопросы.НаправленоОрганПредставление КАК НаправленоОрганПредставление,
		|	ОбращенияИВопросы.ЭтоОрганИсполнительнойВласти КАК ЭтоОрганИсполнительнойВласти,
		|	ОбращенияИВопросы.НаправленоОрганССТУ КАК НаправленоОрганССТУ,
		|	ОбращенияИВопросы.МнениеАвтораОРезультате КАК МнениеАвтораОРезультате,
		|	ОбращенияИВопросы.МнениеАвтораОМерах КАК МнениеАвтораОМерах
		|ПОМЕСТИТЬ ОбращенияИВопросыИСвязанныеДокументы
		|ИЗ
		|	ОбращенияИВопросы КАК ОбращенияИВопросы
		|		ЛЕВОЕ СОЕДИНЕНИЕ СвязанныеДокументы КАК СвязанныеДокументы
		|		ПО ОбращенияИВопросы.ДокументСсылка = СвязанныеДокументы.ДокументСсылка
		|			И ОбращенияИВопросы.Вопрос = СвязанныеДокументы.ДополнительныйОбъектСвязи
		|			И (СвязанныеДокументы.ТипСвязи = &ПеренаправлениеПоВопросу)
		|;
		|
		|// Запрос 6 - Собственно, данные для отчета
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ОбращенияОбщие.ДокументСсылка КАК ДокументСсылка,
		|	ОбращенияОбщие.ДокументНомер КАК ДокументНомер,
		|	ОбращенияОбщие.ДокументДата КАК ДокументДата,
		|	ОбращенияОбщие.ПоступлениеДата КАК ПоступлениеДата,
		|	ОбращенияОбщие.ПоступлениеДатаРегистрации КАК ПоступлениеДатаРегистрации,
		|	0 КАК ДокументНаправленоДата,
		|	0 КАК ДокументРезультатДанОтветАвтору,
		|	0 КАК ДокументРезультатОставленоБезОтвета,
		|	ОбращенияОбщие.РезультатРассмотрения КАК РезультатРассмотрения,
		|	ОбращенияОбщие.СрокИсполненияПродлен КАК СрокИсполненияПродлен,
		|	ВЫБОР
		|		КОГДА ОбращенияОбщие.ОтветАвторуДата > ДАТАВРЕМЯ(1, 1, 1)
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ NULL
		|	КОНЕЦ КАК РезультатДанОтветАвтору,
		|	ОбращенияОбщие.ОтветАвторуДата КАК РезультатДанОтветАвторуДата,
		|	ОбращенияОбщие.Заявитель КАК Заявитель,
		|	ОбращенияОбщие.ЗаявительНаименование КАК ЗаявительНаименование,
		|	ОбращенияОбщие.АдресЭлектроннойПочты КАК АдресЭлектроннойПочты,
		|	ОбращенияОбщие.ПочтовыйАдрес КАК ПочтовыйАдрес,
		|	ОбращенияОбщие.НомерСтроки КАК НомерСтроки,
		|	ОбращенияОбщие.ВопросКод КАК ВопросКод,
		|	ОбращенияОбщие.Вопрос КАК Вопрос,
		|	ОбращенияОбщие.НаправленоДата КАК НаправленоДата,
		|	ОбращенияОбщие.НаправленоНомер КАК НаправленоНомер,
		|	ВЫБОР
		|		КОГДА ОбращенияОбщие.ЭтоОрганИсполнительнойВласти = ИСТИНА
		|			ТОГДА ОбращенияОбщие.НаправленоОрган
		|		ИНАЧЕ NULL
		|	КОНЕЦ КАК НаправленоГосУчреждение,
		|	ВЫБОР
		|		КОГДА ОбращенияОбщие.ЭтоОрганИсполнительнойВласти = ИСТИНА
		|			ТОГДА NULL
		|		ИНАЧЕ ОбращенияОбщие.НаправленоОрган
		|	КОНЕЦ КАК НаправленоИнойОгран,
		|	ОбращенияОбщие.НаправленоОрганПредставление КАК НаправленоОрганПредставление,
		|	ОбращенияОбщие.ЭтоОрганИсполнительнойВласти КАК ЭтоОрганИсполнительнойВласти,
		|	ОбращенияОбщие.НаправленоОрганССТУ КАК НаправленоОрганССТУ,
		|	ВЫБОР
		|		КОГДА ОбращенияОбщие.МнениеАвтораОРезультате = """"
		|			ТОГДА NULL
		|		ИНАЧЕ ОбращенияОбщие.МнениеАвтораОРезультате
		|	КОНЕЦ КАК МнениеАвтораОРезультате,
		|	ВЫБОР
		|		КОГДА ОбращенияОбщие.МнениеАвтораОМерах = """"
		|			ТОГДА NULL
		|		ИНАЧЕ ОбращенияОбщие.МнениеАвтораОМерах
		|	КОНЕЦ КАК МнениеАвтораОМерах
		|ИЗ
		|	ОбращенияИВопросыИСвязанныеДокументы КАК ОбращенияОбщие
		|
		|УПОРЯДОЧИТЬ ПО
		|	ОбращенияОбщие.ДатаСортировки,
		|	ОбращенияОбщие.ДокументНомер,
		|	ОбращенияОбщие.НомерСтроки
		|ИТОГИ
		|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ВЫБОР
		|			КОГДА ОбращенияОбщие.ДокументДата > ДАТАВРЕМЯ(1, 1, 1)
		|				ТОГДА ОбращенияОбщие.ДокументСсылка
		|			ИНАЧЕ NULL
		|		КОНЕЦ) КАК ДокументДата,
		|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ВЫБОР
		|			КОГДА ОбращенияОбщие.ПоступлениеДата > ДАТАВРЕМЯ(1, 1, 1)
		|				ТОГДА ОбращенияОбщие.ДокументСсылка
		|			ИНАЧЕ NULL
		|		КОНЕЦ) КАК ПоступлениеДата,
		|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ВЫБОР
		|			КОГДА ОбращенияОбщие.ПоступлениеДатаРегистрации > ДАТАВРЕМЯ(1, 1, 1)
		|				ТОГДА ОбращенияОбщие.ДокументСсылка
		|			ИНАЧЕ NULL
		|		КОНЕЦ) КАК ПоступлениеДатаРегистрации,
		|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ВЫБОР
		|			КОГДА ОбращенияОбщие.НаправленоДата > ДАТАВРЕМЯ(1, 1, 1)
		|				ТОГДА ОбращенияОбщие.ДокументСсылка
		|			ИНАЧЕ NULL
		|		КОНЕЦ) КАК ДокументНаправленоДата,
		|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ВЫБОР
		|			КОГДА ОбращенияОбщие.ОтветАвторуДата > ДАТАВРЕМЯ(1, 1, 1)
		|				ТОГДА ОбращенияОбщие.ДокументСсылка
		|			ИНАЧЕ NULL
		|		КОНЕЦ) КАК ДокументРезультатДанОтветАвтору,
		|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ВЫБОР
		|			КОГДА ОбращенияОбщие.РезультатРассмотрения = ЗНАЧЕНИЕ(перечисление.РезультатыРассмотренияОбращений.ОставленоБезОтвета)
		|				ТОГДА ОбращенияОбщие.ДокументСсылка
		|			ИНАЧЕ NULL
		|		КОНЕЦ) КАК ДокументРезультатОставленоБезОтвета,
		|	КОЛИЧЕСТВО(РезультатДанОтветАвтору),
		|	КОЛИЧЕСТВО(ВопросКод),
		|	КОЛИЧЕСТВО(ВЫБОР
		|			КОГДА НаправленоДата > ДАТАВРЕМЯ(1, 1, 1)
		|				ТОГДА НаправленоДата
		|			ИНАЧЕ NULL
		|		КОНЕЦ) КАК НаправленоДата,
		|	КОЛИЧЕСТВО(НаправленоГосУчреждение),
		|	КОЛИЧЕСТВО(НаправленоИнойОгран),
		|	КОЛИЧЕСТВО(МнениеАвтораОРезультате),
		|	КОЛИЧЕСТВО(МнениеАвтораОМерах)
		|ПО
		|	ОБЩИЕ,
		|	ДокументСсылка
		|;
		|
		|// Запрос 7
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ОбращенияОбщие.РезультатРассмотрения КАК РезультатРассмотрения,
		|	СУММА(1) КАК Количество
		|ИЗ
		|	ОбращенияИВопросыИСвязанныеДокументы КАК ОбращенияОбщие
		|ГДЕ
		|	ОбращенияОбщие.РезультатРассмотрения <> ЗНАЧЕНИЕ(Перечисление.РезультатыРассмотренияОбращений.Поддержано)
		|	И ОбращенияОбщие.РезультатРассмотрения <> ЗНАЧЕНИЕ(Перечисление.РезультатыРассмотренияОбращений.ПустаяСсылка)
		|
		|СГРУППИРОВАТЬ ПО
		|	ОбращенияОбщие.РезультатРассмотрения
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	Обращения.СрокИсполненияПродлен,
		|	СУММА(1)
		|ИЗ
		|	ОбращенияИВопросы КАК Обращения
		|ГДЕ
		|	Обращения.РезультатРассмотрения = ЗНАЧЕНИЕ(Перечисление.РезультатыРассмотренияОбращений.ПустаяСсылка)
		|
		|СГРУППИРОВАТЬ ПО
		|	Обращения.СрокИсполненияПродлен
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ЗНАЧЕНИЕ(Перечисление.РезультатыРассмотренияОбращений.Поддержано),
		|	СУММА(1)
		|ИЗ
		|	ОбращенияИВопросыИСвязанныеДокументы КАК ОбращенияОбщие
		|ГДЕ
		|	ОбращенияОбщие.РезультатРассмотрения В (
		|		ЗНАЧЕНИЕ(Перечисление.РезультатыРассмотренияОбращений.Поддержано),
		|		ЗНАЧЕНИЕ(Перечисление.РезультатыРассмотренияОбращений.ВТомЧислеМерыПриняты))
		|;
		|
		|// Запрос 8
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Файлы.ВладелецФайла КАК ВладелецФайла,
		|	Файлы.Наименование + ""."" + Файлы.ТекущаяВерсияРасширение КАК ПолноеИмя,
		|	Файлы.Ссылка КАК Ссылка,
		|	СвязанныеДокументы.ДополнительныйОбъектСвязи КАК Вопрос,
		|	СвязанныеДокументы.ДокументСсылка КАК Обращение
		|ИЗ
		|	СвязанныеДокументы КАК СвязанныеДокументы
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Файлы КАК Файлы
		|		ПО СвязанныеДокументы.СвязанныйОбъект = Файлы.ВладелецФайла
		|ГДЕ
		|	Файлы.ПометкаУдаления = ЛОЖЬ
		|
		|УПОРЯДОЧИТЬ ПО
		|	СвязанныеДокументы.ДокументСсылка,
		|	Файлы.ВладелецФайла,
		|	Файлы.Наименование + ""."" + Файлы.ТекущаяВерсияРасширение";
	
	ОтборНеВыгруженные = "ИСТИНА";
	ОтборСозданаВыгрузка = "ИСТИНА";
	ОтборВыгруженные = "ИСТИНА";
	Если ПараметрыОтчета.СостояниеВыгрузкиССТУ = РаботаСОбращениямиКлиентСервер.НеВыгружались() Тогда
		ОтборНеВыгруженные = "АктуальныеВыгрузки.ДокументВыгрузка IS NULL";
	ИначеЕсли ПараметрыОтчета.СостояниеВыгрузкиССТУ = РаботаСОбращениямиКлиентСервер.СозданаВыгрузка() Тогда
		ОтборСозданаВыгрузка = "НЕ АктуальныеВыгрузки.ДокументВыгрузка IS NULL И НЕ АктуальныеВыгрузки.Выгружена";
	ИначеЕсли ПараметрыОтчета.СостояниеВыгрузкиССТУ = РаботаСОбращениямиКлиентСервер.Выгруженные() Тогда
		ОтборВыгруженные = "НЕ АктуальныеВыгрузки.ДокументВыгрузка IS NULL И АктуальныеВыгрузки.Выгружена";
	КонецЕсли;
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ОтборНеВыгруженные", ОтборНеВыгруженные);
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ОтборСозданаВыгрузка", ОтборСозданаВыгрузка);
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ОтборВыгруженные", ОтборВыгруженные);
	
	ТипыСвязи = Новый Массив();
	ТипыСвязи.Добавить(Справочники.ТипыСвязей.ПереадресованДокументомПоВопросу);
	ТипыСвязи.Добавить(Справочники.ТипыСвязей.ОтправленОтвет);
	Запрос.УстановитьПараметр("ТипыСвязи", ТипыСвязи);
	Запрос.УстановитьПараметр("ПеренаправлениеПоВопросу", Справочники.ТипыСвязей.ПереадресованДокументомПоВопросу);
	
	Возврат Запрос.ВыполнитьПакет();
	
КонецФункции

// Заполнить список выбора у поля формы "Состояние выгрузки в ССТУ" на формах отчета.
// 
// Параметры:
//  ПолеФормы - РасширениеПоляФормыДляПоляВвода - Поле формы
Процедура ЗаполнитьСписокВыбораСостоянияВыгрузки(ПолеФормы) Экспорт

	#Если Не ВнешнееСоединение Тогда

	ПолеФормы.РежимВыбораИзСписка = Истина;
	ПолеФормы.РедактированиеТекста = Ложь;
	СписокВыбора = ПолеФормы.СписокВыбора;
	СписокВыбора.Добавить(РаботаСобращениямиКлиентСервер.ВсеОбращения(), НСтр("ru = 'Все обращения'"));
	СписокВыбора.Добавить(РаботаСобращениямиКлиентСервер.НеВыгружались(), НСтр("ru = 'Еще не выгружались'"));
	СписокВыбора.Добавить(
		РаботаСОбращениямиКлиентСервер.СозданаВыгрузка(), НСтр("ru = 'Создана выгрузка, но не выгружена в файл'"));
	СписокВыбора.Добавить(РаботаСОбращениямиКлиентСервер.Выгруженные(), НСтр("ru = 'Выгруженные'"));
	
	#КонецЕсли
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли