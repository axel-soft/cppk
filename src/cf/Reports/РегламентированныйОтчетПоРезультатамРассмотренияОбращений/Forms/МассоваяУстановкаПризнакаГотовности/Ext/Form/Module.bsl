
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ПриСозданииНаСервереРедакцииКонфигурации();
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, Параметры, "Организация, Период, СостояниеВыгрузкиССТУ");
	
	Элементы.Обращения.ИзменятьСоставСтрок = Ложь;
	
	Отчеты.РегламентированныйОтчетПоРезультатамРассмотренияОбращений.ЗаполнитьСписокВыбораСостоянияВыгрузки(
		Элементы.СостояниеВыгрузкиССТУ);
	
	Элементы.СостояниеГотовности.СписокВыбора.Добавить(
		РаботаСОбращениямиКлиентСервер.НеГотовыеКВыгрузке(), НСтр("ru = 'Установить'"));
	Элементы.СостояниеГотовности.СписокВыбора.Добавить(
		РаботаСОбращениямиКлиентСервер.ГотовыеКВыгрузке(), НСтр("ru = 'Снять'"));
	
	СостояниеГотовности = РаботаСОбращениямиКлиентСервер.НеГотовыеКВыгрузке();
	ОбновитьСписокОбращений_Сервер();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура СостояниеВыгрузкиССТУПриИзменении(Элемент)
	
	ОбновитьСписокОбращений_Сервер();
	
КонецПроцедуры

&НаКлиенте
Процедура СостояниеГотовностиПриИзменении(Элемент)
	
	ОбновитьСписокОбращений_Сервер();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыОбращения

&НаКлиенте
Процедура ОбращенияВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ЭлементСписка = Обращения.НайтиПоИдентификатору(ВыбраннаяСтрока);
	Если ЭлементСписка <> Неопределено Тогда
		ПоказатьЗначение( , ЭлементСписка.Значение);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Асинх Процедура УстановитьСнятьПризнак(Команда)
	
	Колво = 0;
	Для Каждого ЭлементСписка Из Обращения Цикл
		Если ЭлементСписка.Пометка Тогда
			Колво = Колво + 1;
		КонецЕсли;
	КонецЦикла;
	Если Колво = 0 Тогда
		ПоказатьПредупреждение( , НСтр("ru = 'Не отмечено ни одного документа-обращения'"));
		Возврат;
	КонецЕсли;
	
	СостояниеГотовностиВОсновнуюФорму = "";
	Фраза = "";
	ВсплывающаяФраза = "";
	Если СостояниеГотовности = РаботаСОбращениямиКлиентСервер.НеГотовыеКВыгрузке() Тогда
		Фраза = СтрШаблон(
			НСтр("ru = 'УСТАНОВИТЬ признак готовности к выгрузке в ССТУ у отмеченных документов-обращений - всего %1?'"),
			Формат(Колво, "ЧГ=0"));
		СостояниеГотовностиВОсновнуюФорму = РаботаСОбращениямиКлиентСервер.ГотовыеКВыгрузке();
		ВсплывающаяФраза = НСтр("ru = 'Установка признака готовности'");
	ИначеЕсли СостояниеГотовности = РаботаСОбращениямиКлиентСервер.ГотовыеКВыгрузке() Тогда
		Фраза = СтрШаблон(
			НСтр("ru = 'СНЯТЬ признак готовности к выгрузке в ССТУ у отмеченных документов-обращений - всего %1?'"),
			Формат(Колво, "ЧГ=0"));
		СостояниеГотовностиВОсновнуюФорму = РаботаСОбращениямиКлиентСервер.НеГотовыеКВыгрузке();
		ВсплывающаяФраза = НСтр("ru = 'Снятие признака готовности'");
	КонецЕсли;
	Если Ждать ВопросАсинх(Фраза, РежимДиалогаВопрос.ДаНет) = КодВозвратаДиалога.Нет Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьСнятьПризнак_Сервер();
	
	// Состояние готовности передаем с точностью до наоборот, т.к. было готовым стало неготовым или наоборот:
	Отклик = Новый Структура(
		"СостояниеВыгрузкиССТУ, СостояниеГотовности",  СостояниеВыгрузкиССТУ, СостояниеГотовностиВОсновнуюФорму);
	Оповестить("УстановкаСнятиеФлагаГотовности", Отклик, ЭтотОбъект);
	
	ПоказатьОповещениеПользователя(ВсплывающаяФраза, , , БиблиотекаКартинок.Информация32);
	
КонецПроцедуры

&НаКлиенте
Процедура ПеречитатьСписок(Команда)
	
	ОбновитьСписокОбращений_Сервер();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьСнятьПризнак_Сервер()
	
	ДокументыОбращения = Новый Массив();
	Для Каждого ЭлементСписка Из Обращения Цикл
		Если ЭлементСписка.Пометка Тогда
			ДокументыОбращения.Добавить(ЭлементСписка.Значение);
		КонецЕсли;
	КонецЦикла;
	
	Если СостояниеГотовности = РаботаСОбращениямиКлиентСервер.НеГотовыеКВыгрузке() Тогда
		Документы.ВыгрузкаВССТУ.УстановитьСнятьПризнакГотовности(ДокументыОбращения, Истина);
	ИначеЕсли СостояниеГотовности = РаботаСОбращениямиКлиентСервер.ГотовыеКВыгрузке() Тогда
		Документы.ВыгрузкаВССТУ.УстановитьСнятьПризнакГотовности(ДокументыОбращения, Ложь);
	КонецЕсли;
	
	ОбновитьСписокОбращений_Сервер();
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьСписокОбращений_Сервер()
	
	Обращения.Очистить();
	
	ПараметрыОтчета = Отчеты.РегламентированныйОтчетПоРезультатамРассмотренияОбращений.НовыйПараметрыОтчета();
	ЗаполнитьЗначенияСвойств(
		ПараметрыОтчета, ЭтотОбъект, "Организация, Период, СостояниеВыгрузкиССТУ, СостояниеГотовности");
	РезультатыЗапроса = Отчеты.РегламентированныйОтчетПоРезультатамРассмотренияОбращений.РезультатыЗапросаДляОтчета(
		ПараметрыОтчета);
	ВыборкаИтог = РезультатыЗапроса[6].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Если ВыборкаИтог.Следующий() Тогда
		ВыборкаПоОбращениям = ВыборкаИтог.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ВыборкаПоОбращениям.Следующий() Цикл
			Обращения.Добавить(ВыборкаПоОбращениям.ДокументСсылка, , Ложь, );
		КонецЦикла;
	КонецЕсли;
	
	УправлениеЭлементамиФормы(); // выше по стеку могло измениться "состояние"
	
КонецПроцедуры

&НаСервере
Процедура УправлениеЭлементамиФормы()
	
	Если СостояниеГотовности = РаботаСОбращениямиКлиентСервер.ГотовыеКВыгрузке() Тогда
		Элементы.Обращения.Заголовок = НСтр(
			"ru = 'Отметьте документы-обращения, у которых нужно снять признак готовности к выгрузке в ССТУ'");
		Команды.УстановитьСнятьПризнак.Заголовок = НСтр("ru = 'Снять признак готовности'");
		Команды.УстановитьСнятьПризнак.Подсказка = НСтр("ru = 'Снять признак у выбранных объектов'");
	ИначеЕсли СостояниеГотовности = РаботаСОбращениямиКлиентСервер.НеГотовыеКВыгрузке() Тогда
		Элементы.Обращения.Заголовок = НСтр(
			"ru = 'Отметьте обращения, у которых нужно установить признак готовности к выгрузке в ССТУ'");
		Команды.УстановитьСнятьПризнак.Заголовок = НСтр("ru = 'Установить признак готовности'");
		Команды.УстановитьСнятьПризнак.Подсказка = НСтр("ru = 'Установить признак у выбранных объектов'");
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервереРедакцииКонфигурации()
	
	Элементы.Организация.Заголовок = РедакцииКонфигурацииКлиентСервер.Организация();
	
КонецПроцедуры

#КонецОбласти
