Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	
	ТабДокумент = Новый ТабличныйДокумент;
	
	Настройки = КомпоновщикНастроек.ПолучитьНастройки();
	
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
   
    МакетКомпоновкиДанных = КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных,
	    Настройки, ДанныеРасшифровки, ,
	Тип("ГенераторМакетаКомпоновкиДанных"));

	ПроцессорКомпоновкиДанных = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновкиДанных.Инициализировать(МакетКомпоновкиДанных, , ДанныеРасшифровки, Истина);

	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;

	ПроцессорВывода.УстановитьДокумент(ТабДокумент);
	ПроцессорВывода.Вывести(ПроцессорКомпоновкиДанных);
	
	ОбъединитьЗаголовкиВШапке(ТабДокумент,"%ОБЪЕД1%");
	ОбъединитьЗаголовкиВШапке(ТабДокумент,"%ОБЪЕД2%");
	ДокументРезультат.Вывести(ТабДокумент);
	 
КонецПроцедуры

Процедура ОбъединитьЗаголовкиВШапке(ТабДокумент,Маркер) Экспорт
	ОбластьПерваяЯчейкаЗаголовка=ТабДокумент.НайтиТекст(Маркер);
	Если НЕ ОбластьПерваяЯчейкаЗаголовка = Неопределено Тогда
		Текст = ОбластьПерваяЯчейкаЗаголовка.Текст;
		Заголовок = СтрЗаменить(Текст,Маркер,"");
		ЛевоЗаголовка = ОбластьПерваяЯчейкаЗаголовка.Лево;
		СтрокаЗаголовка = ОбластьПерваяЯчейкаЗаголовка.Верх;
		СледующаяОбластьЛево = ЛевоЗаголовка+1;
		Пока ТабДокумент.Область(СтрокаЗаголовка,СледующаяОбластьЛево,СтрокаЗаголовка,СледующаяОбластьЛево).Текст = Текст Цикл
			СледующаяОбластьЛево = СледующаяОбластьЛево + 1;
        КонецЦикла;
		Область = ТабДокумент.Область(СтрокаЗаголовка,ЛевоЗаголовка,СтрокаЗаголовка,СледующаяОбластьЛево-1);
		Область.Объединить();
		Область.Текст = Заголовок;
	КонецЕсли;
КонецПроцедуры
