
&НаСервере
Процедура ПриЗагрузкеПользовательскихНастроекНаСервере(Настройки)
	
	Если ЗначениеЗаполнено(Документ) Тогда 
		ПараметрДокумент = Новый ПараметрКомпоновкиДанных("Документ"); 
		ЭлементНастроек = Отчет.КомпоновщикНастроек.Настройки.ПараметрыДанных.НайтиЗначениеПараметра(ПараметрДокумент);
		Если ЭлементНастроек <> Неопределено Тогда 
			ЭлементНастроек.Значение = Документ;
			ЭлементНастроек.Использование = Истина;
		КонецЕсли;
		
		ДействияОзнакомления = ДействияМассовогоОзнакомления(Документ);
		
		ПараметрДействия = Новый ПараметрКомпоновкиДанных("ДействияОзнакомления"); 
		ЭлементНастроек = Отчет.КомпоновщикНастроек.Настройки.ПараметрыДанных.НайтиЗначениеПараметра(ПараметрДействия);
		Если ЭлементНастроек <> Неопределено Тогда 
			ЭлементНастроек.Значение = ДействияОзнакомления;
			ЭлементНастроек.Использование = Истина;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеВариантаНаСервере(Настройки)
	
	Если ЗначениеЗаполнено(Документ) Тогда 
		ПараметрДокумент = Новый ПараметрКомпоновкиДанных("Документ"); 
		
		ЭлементНастроек = Отчет.КомпоновщикНастроек.Настройки.ПараметрыДанных.НайтиЗначениеПараметра(ПараметрДокумент);
		Если ЭлементНастроек <> Неопределено Тогда 
			ЭлементНастроек.Значение = Документ;
			ЭлементНастроек.Использование = Истина;
		КонецЕсли;  
		
		ДействияОзнакомления = ДействияМассовогоОзнакомления(Документ);
		
		ПараметрДействия = Новый ПараметрКомпоновкиДанных("ДействияОзнакомления"); 
		ЭлементНастроек = Отчет.КомпоновщикНастроек.Настройки.ПараметрыДанных.НайтиЗначениеПараметра(ПараметрДействия);
		Если ЭлементНастроек <> Неопределено Тогда 
			ЭлементНастроек.Значение = ДействияОзнакомления;
			ЭлементНастроек.Использование = Истина;
		КонецЕсли;
		
		ПараметрЗаголовок = Новый ПараметрКомпоновкиДанных("Заголовок");
		ЭлементНастроек = Отчет.КомпоновщикНастроек.Настройки.ПараметрыВывода.НайтиЗначениеПараметра(ПараметрЗаголовок);
		Если ЭлементНастроек <> Неопределено Тогда 
			
			ДатаНачала = Неопределено;
			Если ДействияОзнакомления.Количество() = 1 Тогда                           
				Действие = ДействияОзнакомления[0];
				ДатаНачала = РегистрыСведений.УчастникиОзнакомлений.ДатаНачала(Действие);
			КонецЕсли;	
			
			ЭтаФорма.Заголовок = ЭлементНастроек.Значение;
			ЭлементНастроек.Использование = Истина;
			НаименованиеДокумента = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Документ, "Наименование");
			Если СтрНайти(ЭлементНастроек.Значение, НаименованиеДокумента) = 0 Тогда 
				
				Если ДелопроизводствоКлиентСервер.ЭтоДокументПредприятия(Документ) Тогда
					ЭлементНастроек.Значение = СтрШаблон( НСтр("ru = 'Ознакомление с документом ""%1"" '"),
						ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Документ, "Наименование"));  
				Иначе	
					ЭлементНастроек.Значение = СтрШаблон( НСтр("ru = 'Ознакомление с мероприятием ""%1"" '"),
						ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Документ, "Наименование"));  
				КонецЕсли;	
					
				Если ЗначениеЗаполнено(ДатаНачала) Тогда        
					
					ИспользоватьДатуИВремяВСрокахЗадач = ПолучитьФункциональнуюОпцию("ИспользоватьДатуИВремяВСрокахЗадач");
					СтрокаФормата = ?(ИспользоватьДатуИВремяВСрокахЗадач, 
						"ДФ='дд.ММ.гггг ЧЧ:мм'", "ДЛФ=D");
					
					ЭлементНастроек.Значение = ЭлементНастроек.Значение 
					+ СтрШаблон( НСтр("ru = ' (начато: %1)'"), 
					Формат(ДатаНачала, СтрокаФормата));
					
				КонецЕсли;	
					
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	ПараметрДокумент = Новый ПараметрКомпоновкиДанных("ДействияОзнакомления");
	ЭлементНастроек = Отчет.КомпоновщикНастроек.Настройки.ПараметрыДанных.НайтиЗначениеПараметра(ПараметрДокумент);
	Если ЭлементНастроек = Неопределено Или ЭлементНастроек.Использование = Ложь Тогда 
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			НСтр("ru = 'Не включен параметр ""Действия ознакомления""'"),,
			"Отчет.КомпоновщикНастроек.ПользовательскиеНастройки",,Отказ);
	ИначеЕсли ЗначениеЗаполнено(ЭлементНастроек.Значение) 
		И ЭлементНастроек.Значение.Количество() = 0 Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			НСтр("ru = 'В данном документе нет действий в ""массовом режиме"".'"),,
			"Отчет.КомпоновщикНастроек.ПользовательскиеНастройки",,Отказ);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("Документ") И ЗначениеЗаполнено(Параметры.Документ) Тогда 
		Документ = Параметры.Документ;
	КонецЕсли;   
	
КонецПроцедуры  

&НаСервере
Функция ДействияМассовогоОзнакомления(Документ)
	
	ДействияМассовогоОзнакомления = Новый Массив;
	ВсеДействия = ДействияСервер.ТекущиеДействияПредмета(Документ,, Дата(1, 1, 1)).Выгрузить();
	МассивДействий = ВсеДействия.ВыгрузитьКолонку("Действие");        
	
	Для Каждого Действие Из МассивДействий Цикл               
		
		Если ТипЗнч(Действие) = Тип("СправочникСсылка.ДействияОзнакомления") Тогда
			ДействияМассовогоОзнакомления.Добавить(Действие);
		КонецЕсли;	
		
	КонецЦикла;
	
	Возврат ДействияМассовогоОзнакомления;
	
КонецФункции

