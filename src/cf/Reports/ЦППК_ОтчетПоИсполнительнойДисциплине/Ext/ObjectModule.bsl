//++ Демидова А.В. САНФ-027026 от 16.07.2024
Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
		
	ПользовательскийОтбор = КомпоновщикНастроек.ПользовательскиеНастройки.Элементы.Найти(
	КомпоновщикНастроек.Настройки.Отбор.ИдентификаторПользовательскойНастройки);
	
	ПолеОтбора = Новый ПолеКомпоновкиДанных("ГлавныйИсполнитель");    
	ЭлементОтбора = Неопределено;
	Для Каждого Отбор Из ПользовательскийОтбор.Элементы Цикл
		Если Отбор.ЛевоеЗначение = ПолеОтбора Тогда
			ЭлементОтбора = Отбор;
			Прервать;
		КонецЕсли;
	КонецЦикла;		
	
	Если ЭлементОтбора = Неопределено Тогда
		
		ЭлементОтбораПользовательский = ПользовательскийОтбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		
		ЭлементОтбораПользовательский.ИдентификаторПользовательскойНастройки = Новый УникальныйИдентификатор();
		ЭлементОтбораПользовательский.ЛевоеЗначение = ПолеОтбора;
		ЭлементОтбораПользовательский.ПравоеЗначение = Истина;
		ЭлементОтбораПользовательский.Использование = Истина;
		
	КонецЕсли;
	
	ПараметрИспользоватьСдвиг = КомпоновщикНастроек.Настройки.ПараметрыДанных.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("ДобавлятьСдвигКПериоду"));
	Если ПараметрИспользоватьСдвиг = Неопределено Тогда
		Возврат;
	КонецЕсли;  
	
	ПользПараметрИспользоватьСдвиг = КомпоновщикНастроек.ПользовательскиеНастройки.Элементы.Найти(ПараметрИспользоватьСдвиг.ИдентификаторПользовательскойНастройки);
	Если ПользПараметрИспользоватьСдвиг = Неопределено 
		Или Не ПользПараметрИспользоватьСдвиг.Использование 
		Или Не ПользПараметрИспользоватьСдвиг.Значение Тогда
		
		Возврат;
		
	КонецЕсли;  
	
	ПараметрСдвиг = КомпоновщикНастроек.Настройки.ПараметрыДанных.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("Сдвиг"));
	Если ПараметрСдвиг = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПользПараметрСдвиг = КомпоновщикНастроек.ПользовательскиеНастройки.Элементы.Найти(ПараметрСдвиг.ИдентификаторПользовательскойНастройки);
	Если ПользПараметрСдвиг = Неопределено Тогда
		Возврат;
	КонецЕсли; 
	
	Сдвиг  =  ПользПараметрСдвиг.Значение;
	СдвигВСекундах = Сдвиг*60*60*24;
	
	ПараметрПериод = КомпоновщикНастроек.Настройки.ПараметрыДанных.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("Период"));
	Если ПараметрПериод = Неопределено Тогда
		Возврат;
	КонецЕсли; 
	
	ПользПараметрПараметрПериод = КомпоновщикНастроек.ПользовательскиеНастройки.Элементы.Найти(ПараметрПериод.ИдентификаторПользовательскойНастройки);
	Если ПользПараметрПараметрПериод = Неопределено 
		Или Не ПользПараметрПараметрПериод.Использование Тогда
		
		Возврат;
	КонецЕсли; 
		
	ПользПараметрПараметрПериод.Значение.ДатаОкончания  = ПользПараметрПараметрПериод.Значение.ДатаОкончания + СдвигВСекундах;  
	
КонецПроцедуры
