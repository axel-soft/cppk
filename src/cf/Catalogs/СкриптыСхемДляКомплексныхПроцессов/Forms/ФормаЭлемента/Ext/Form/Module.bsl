#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
		
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
	
	ЗаполнитьКонтекстВыполнения();
	
	Элементы.ВставитьОбъектРеквизит.Доступность = Ложь;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормы_КонтекстВыполнения

&НаКлиенте
Процедура КонтекстВыполненияВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если ТолькоПросмотр Тогда
		Возврат;
	КонецЕсли;
	
	Если Элементы.КонтекстВыполнения.ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Объект.Выражение = Объект.Выражение + " "
		+ Элементы.КонтекстВыполнения.ТекущиеДанные.ПолныйПуть;
	
	Модифицированность = Истина;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВставитьОбъектРеквизит(Команда)
		
	Если Элементы.КонтекстВыполнения.ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Объект.Выражение = Объект.Выражение + " "
		+ Элементы.КонтекстВыполнения.ТекущиеДанные.ПолныйПуть;
	
	Модифицированность = Истина;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьКонтекстВыполнения()
	
	Дерево = РеквизитФормыВЗначение("КонтекстВыполнения");
	
	Дерево.Строки.Очистить();
	
	// Комплексный процесс
	
	СтрокаКонтекст = Дерево.Строки.Добавить();
	СтрокаКонтекст.Представление = НСтр("ru = 'Процесс (объект)'");
	СтрокаКонтекст.Тип = НСтр("ru = 'БизнесПроцессОбъект.КомплексныйПроцесс'");
	СтрокаКонтекст.ПолныйПуть = "Параметры.ПроцессОбъект";
		
	МетаданныеПроцеса = Метаданные.БизнесПроцессы.КомплексныйПроцесс;
	Для Каждого СтандартныйРеквизит Из МетаданныеПроцеса.СтандартныеРеквизиты Цикл
		СтрокаРеквизитПроцесса = СтрокаКонтекст.Строки.Добавить();
		
		Если ЗначениеЗаполнено(СтандартныйРеквизит.Синоним) Тогда
			СтрокаРеквизитПроцесса.Представление = СтандартныйРеквизит.Синоним;
		Иначе
			СтрокаРеквизитПроцесса.Представление =
				ОбщегоНазначенияДокументооборотКлиентСервер.ПредставлениеПоНаименованию(
				СтандартныйРеквизит.Имя);
		КонецЕсли;
		
		СтрокаРеквизитПроцесса.Тип = ПредставлениеТиповКонтекста(СтандартныйРеквизит.Тип.Типы());
		СтрокаРеквизитПроцесса.ПолныйПуть = СтрокаКонтекст.ПолныйПуть
			+ "." + СтандартныйРеквизит.Имя;
	КонецЦикла;
	Для Каждого Реквизит Из МетаданныеПроцеса.Реквизиты Цикл
		СтрокаРеквизитПроцесса = СтрокаКонтекст.Строки.Добавить();
		
		Если ЗначениеЗаполнено(Реквизит.Синоним) Тогда
			СтрокаРеквизитПроцесса.Представление = Реквизит.Синоним;
		Иначе
			СтрокаРеквизитПроцесса.Представление =
				ОбщегоНазначенияДокументооборотКлиентСервер.ПредставлениеПоНаименованию(
				Реквизит.Имя);
		КонецЕсли;
		
		СтрокаРеквизитПроцесса.Тип = ПредставлениеТиповКонтекста(Реквизит.Тип.Типы());
		СтрокаРеквизитПроцесса.ПолныйПуть = СтрокаКонтекст.ПолныйПуть
			+ "." + Реквизит.Имя;
	КонецЦикла;
	Для Каждого ТабличнаяЧасть Из МетаданныеПроцеса.ТабличныеЧасти Цикл
		СтрокаТЧПроцесса = СтрокаКонтекст.Строки.Добавить();
		
		Если ЗначениеЗаполнено(ТабличнаяЧасть.Синоним) Тогда
			СтрокаТЧПроцесса.Представление = ТабличнаяЧасть.Синоним;
		Иначе
			СтрокаТЧПроцесса.Представление =
				ОбщегоНазначенияДокументооборотКлиентСервер.ПредставлениеПоНаименованию(
				ТабличнаяЧасть.Имя);
		КонецЕсли;
		
		СтрокаТЧПроцесса.Тип = "ТабличнаяЧасть";
		СтрокаТЧПроцесса.ПолныйПуть = СтрокаКонтекст.ПолныйПуть
			+ "." + ТабличнаяЧасть.Имя + "[0]";
		
		Для Каждого СтандартныйРеквизитТЧ Из ТабличнаяЧасть.СтандартныеРеквизиты Цикл
			СтрокаРеквизитТЧПроцесса = СтрокаТЧПроцесса.Строки.Добавить();
			
			Если ЗначениеЗаполнено(СтандартныйРеквизитТЧ.Синоним) Тогда
				СтрокаРеквизитТЧПроцесса.Представление = СтандартныйРеквизитТЧ.Синоним;
			Иначе
				СтрокаРеквизитТЧПроцесса.Представление =
					ОбщегоНазначенияДокументооборотКлиентСервер.ПредставлениеПоНаименованию(
					СтандартныйРеквизитТЧ.Имя);
			КонецЕсли;
			
			СтрокаРеквизитТЧПроцесса.Тип = ПредставлениеТиповКонтекста(СтандартныйРеквизитТЧ.Тип.Типы());
			СтрокаРеквизитТЧПроцесса.ПолныйПуть = СтрокаТЧПроцесса.ПолныйПуть
				+ "." + СтандартныйРеквизитТЧ.Имя; 
		КонецЦикла;
		
		Для Каждого РеквизитТЧ Из ТабличнаяЧасть.Реквизиты Цикл
			СтрокаРеквизитТЧПроцесса = СтрокаТЧПроцесса.Строки.Добавить();
			
			Если ЗначениеЗаполнено(РеквизитТЧ.Синоним) Тогда
				СтрокаРеквизитТЧПроцесса.Представление = РеквизитТЧ.Синоним;
			Иначе
				СтрокаРеквизитТЧПроцесса.Представление =
					ОбщегоНазначенияДокументооборотКлиентСервер.ПредставлениеПоНаименованию(
					РеквизитТЧ.Имя);
			КонецЕсли;
			
			СтрокаРеквизитТЧПроцесса.Тип = ПредставлениеТиповКонтекста(РеквизитТЧ.Тип.Типы());
			СтрокаРеквизитТЧПроцесса.ПолныйПуть = СтрокаТЧПроцесса.ПолныйПуть
				+ "." + РеквизитТЧ.Имя;
		КонецЦикла;
		 
	КонецЦикла;
	
	ЗначениеВРеквизитФормы(Дерево, "КонтекстВыполнения");
	
КонецПроцедуры

&НаСервере
Функция ПредставлениеТиповКонтекста(Типы)
	
	ПредставленияТипов = Новый Массив;
	
	Для Каждого Тип Из Типы Цикл
			
		Если БизнесПроцессы.ТипВсеСсылкиТочекМаршрутаБизнесПроцессов().СодержитТип(Тип) Тогда
			ПредставленияТип = НСтр("ru = 'ТочкаМаршрутаБизнесПроцессаСсылка.'")
				+ Метаданные.НайтиПоТипу(Тип).Имя;
		ИначеЕсли Тип = Тип("УникальныйИдентификатор") Тогда
			ПредставленияТип = НСтр("ru = 'УникальныйИдентификатор'");
		Иначе
			ПредставленияТип = ОбщегоНазначения.СтроковоеПредставлениеТипа(Тип);
		КонецЕсли;
		ПредставленияТипов.Добавить(ПредставленияТип);		
	КонецЦикла;
	
	Возврат СтрСоединить(ПредставленияТипов, ", ");
	
КонецФункции

#КонецОбласти