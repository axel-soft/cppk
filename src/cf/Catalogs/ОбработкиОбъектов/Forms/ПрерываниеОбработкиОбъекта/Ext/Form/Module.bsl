
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ОбъектОбработки = Параметры.Документ;
	ПричинаПрерывания = Параметры.ПричинаПрерывания;
	Если Не ЗначениеЗаполнено(ОбъектОбработки) Тогда
		Возврат;
	КонецЕсли;
	
	МассивТекстСообщения = Новый Массив;
	
	Если ТипЗнч(ОбъектОбработки) = Тип("СправочникСсылка.ДокументыПредприятия") Тогда
		
		МассивТекстСообщения.Добавить(НСтр("ru = 'Будет выполнено прерывание обработки документа '"));
		
	ИначеЕсли ТипЗнч(ОбъектОбработки) = Тип("СправочникСсылка.Мероприятия") Тогда
		
		МассивТекстСообщения.Добавить(НСтр("ru = 'Будет выполнено прерывание обработки мероприятия '"));
		
	Иначе
		
		ВызватьИсключение СтрШаблон(
			НСтр("ru = 'Неизвестный тип объекта обработки %1 (%2).'"),
			ОбъектОбработки,
			ТипЗнч(ОбъектОбработки));
			
	КонецЕсли;
	
	МассивТекстСообщения.Добавить("""");
	МассивТекстСообщения.Добавить(Строка(ОбъектОбработки));
	МассивТекстСообщения.Добавить("""");
	МассивТекстСообщения.Добавить(".");
	МассивТекстСообщения.Добавить(Символы.ПС);
	МассивТекстСообщения.Добавить(Символы.ПС);
	МассивТекстСообщения.Добавить(НСтр("ru = 'Информация о выполнении будет помещена в историю.'"));
	МассивТекстСообщения.Добавить(Символы.ПС);
	МассивТекстСообщения.Добавить(Символы.ПС);
	МассивТекстСообщения.Добавить(НСтр("ru = 'Внимание! Все внесенные изменения будут необратимыми.'"));
	ТекстСообщения = СтрСоединить(МассивТекстСообщения);
	
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПрерватьОбработку(Команда)
	
	Результат = Новый Структура;
	
	Результат.Вставить(
		"ПричинаПрерывания", 
		СтрЗаменить(ПричинаПрерывания, Символы.ПС, " "));
		
	Результат.Вставить(
		"ВладелецФормы",
		ВладелецФормы);
	
	Закрыть(Результат);
	
КонецПроцедуры

#КонецОбласти