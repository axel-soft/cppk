#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	РаботаСПроцессамиПоОбработкамОбъектовСобытия.ПередЗаписьюОбработкиОбъекта(ЭтотОбъект);
	
	ОбщегоНазначенияДокументооборот.УстановитьДополнительноеСвойствоПредыдущиеЗначенияРеквизитов(ЭтотОбъект);
	
	Если ОбщегоНазначенияДокументооборот.ИзменилосьЗначениеРеквизитов(ЭтотОбъект, "Состояние") Тогда
		ДатаИзмененияСостояния = ТекущаяДатаСеанса();
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ОбработкиОбъектов.ЗаписатьОбработку(ЭтотОбъект, Отказ);
	
КонецПроцедуры

Процедура ПередУдалением(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	РаботаСПроцессамиПоОбработкамОбъектовСобытия.ПередУдалениемОбработкиОбъекта(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область ПрограммныйИнтерфейс

// Выполняет процедуры записи объекта.
//
// Параметры:
//	Отказ - Булево - Признак отказа от записи.
//
Процедура ЗаписатьОбъект() Экспорт
	
	СостояниеВсейОбработки = Состояние;
	Если ДополнительныеСвойства.Свойство("ИзмениласьПометкаУдаления") Тогда
		ДействияОбработки = РегистрыСведений.ДействияОбработкиОбъектов.ДействияОбработки(Ссылка);
		ДействияСервер.ПометитьДействияНаУдаление(ДействияОбработки, ПометкаУдаления);
		
		Если ПометкаУдаления Тогда
			СостояниеВсейОбработки = Перечисления.СостоянияОбработкиОбъектов.ПустаяСсылка();
		КонецЕсли;
	КонецЕсли;
	
	Если ОбщегоНазначенияДокументооборот.ИзменилосьЗначениеРеквизитов(ЭтотОбъект, "ПомещенаВИсторию")
		И ПомещенаВИсторию Тогда
		
		СостояниеВсейОбработки = Перечисления.СостоянияОбработкиОбъектов.ПустаяСсылка();
	КонецЕсли;
	
	Если ДелопроизводствоКлиентСервер.ЭтоДокументПредприятия(Владелец) Тогда
		ПрежнееСостояниеОбработки = Неопределено;
		Делопроизводство.ПрочитатьДанныеДокумента(Владелец, "СостояниеОбработки", ПрежнееСостояниеОбработки);
		
		Если ПрежнееСостояниеОбработки <> СостояниеВсейОбработки Тогда
			Делопроизводство.ЗаписатьДанныеДокумента(Владелец, "СостояниеОбработки", СостояниеВсейОбработки);
			Делопроизводство.ЗаписатьДанныеДокумента(Владелец, "ДатаИзмененияСостоянияОбработки", ТекущаяДатаСеанса());
		КонецЕсли;
	КонецЕсли;
	
	РаботаСПроцессамиПоОбработкамОбъектовСобытия.ПриЗаписиОбработкиОбъекта(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли