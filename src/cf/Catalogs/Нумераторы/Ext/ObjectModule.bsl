#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	Если ЭтоНовый() Тогда 
		Если ПолучитьФункциональнуюОпцию("ИспользоватьУчетПоОрганизациям") Тогда 
			НезависимаяНумерацияПоОрганизациям = Истина;
		КонецЕсли;
	КонецЕсли;	
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Если Ссылка = Справочники.Нумераторы.Штрихкоды Тогда
		ПроверяемыеРеквизиты.Очистить();
	КонецЕсли;
	
	Если (Найти(ФорматНомера, "НомерСвязДок") > 0) Или НезависимаяНумерацияПоСвязанномуДокументу Тогда 
		ПроверяемыеРеквизиты.Добавить("ТипСвязи");
	КонецЕсли;	
	
	// Проверка настроек типа связи
	Если ЗначениеЗаполнено(ТипСвязи) Тогда 
		
		УстановитьПривилегированныйРежим(Истина);
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	НастройкаСвязей.СсылкаНа
		|ИЗ
		|	РегистрСведений.НастройкаСвязей КАК НастройкаСвязей
		|ГДЕ
		|	НастройкаСвязей.ТипСвязи = &ТипСвязи";
		
		Запрос.УстановитьПараметр("ТипСвязи", ТипСвязи);
		Выборка = Запрос.Выполнить().Выбрать();
		
		РазрешенныеТипы = Новый Массив;
		РазрешенныеТипы.Добавить(Тип("СправочникСсылка.ВидыДокументов"));
		
		НедопустимыеТипы = "";
		
		Пока Выборка.Следующий() Цикл
			
			ТипСвязанногоДокумента = ТипЗнч(Выборка.СсылкаНа);
			Если РазрешенныеТипы.Найти(ТипСвязанногоДокумента) = Неопределено Тогда
				
				Если ТипСвязанногоДокумента = Тип("Строка") Тогда
					ПредставлениеНедопустимогоТипа = НСтр("ru = 'Внешняя ссылка'");
				Иначе
					ПредставлениеНедопустимогоТипа = Строка(ТипСвязанногоДокумента);
				КонецЕсли;
				
				НедопустимыеТипы = НедопустимыеТипы + ?(НедопустимыеТипы = "", "", ", ") + ПредставлениеНедопустимогоТипа;
				
			КонецЕсли
			
		КонецЦикла;
		
		Если НедопустимыеТипы <> "" Тогда
			Если Найти(НедопустимыеТипы, ",") = 0 Тогда 
				ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Настройки типа связи содержат недопустимый тип связанного документа: %1'"),
					НедопустимыеТипы);
			Иначе
				ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Настройки типа связи содержат недопустимые типы связанного документа: %1'"),
					НедопустимыеТипы);
			КонецЕсли;	
			
			ОбщегоНазначения.СообщитьПользователю(
				ТекстСообщения,
				ЭтотОбъект,
				,,
				Отказ);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли