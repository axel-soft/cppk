
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ПриСозданииНаСервереРедакцииКонфигурации();
	
	ПротоколированиеРаботыСотрудников.ЗаписатьОткрытие(Объект.Ссылка);
	ИспользоватьСвязиДокументов = ПолучитьФункциональнуюОпцию("ИспользоватьСвязиОбъектов");
	
	ЗаполнитьСлужебныеПоля();
	
	ЗаполнитьНезависимуюНумерацию();
	
	ПрочитатьНастройкиНумерации();
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьГрифыДоступа") Тогда
		Элементы.НастройкиНумерацииГрифДоступа.Видимость = Истина; 
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Объект.ТипСвязи) Тогда  
		Если Объект.НезависимаяНумерацияПоСвязанномуДокументу Тогда 
			Для Каждого Строка Из НезависимаяНумерация Цикл
				Если Строка.Значение = "НезависимаяНумерацияПоСвязанномуДокументу" Тогда 
					Строка.Представление = Строка.Представление + " (" + Строка(Объект.ТипСвязи) + ")";
					Прервать;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
		Если ФорматСодержитНомерСвязанного(Объект.ФорматНомера) Тогда 
		
			Для Каждого Строка Из СлужебныеПоля Цикл
				Если Строка.Значение = "[НомерСвязДок]" Тогда 
					Строка.Представление = Строка.Представление + " (" + Строка(Объект.ТипСвязи) + ")";
					Прервать;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	
	СформироватьПримерНомера();
	
	Элементы.ИспользоватьПропущенныеНомера.Доступность = 
		Объект.Назначение <> ПредопределенноеЗначение("Перечисление.НазначенияНумераторов.ВременныйНомер");
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	СформироватьЗаголовокДействуетДля();
	
	СформироватьЗаголовокНезависимаяНумерация();
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если ОбщегоНазначенияДокументооборотКлиент.ПередЗакрытием(
		Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка, Модифицированность) Тогда
		Возврат;
	КонецЕсли;
	
	Если Модифицированность Тогда
		Оповещение = Новый ОписаниеОповещения("ВопросПередЗакрытиемЗавершение", ЭтотОбъект);
		ТекстВопроса = НСтр("ru = 'Данные были изменены. Сохранить изменения?'");
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНетОтмена);
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	Для Каждого Строка Из НезависимаяНумерация Цикл
		Объект[Строка.Значение] = Строка.Пометка;
	КонецЦикла;
	
	ТекстОшибки = "";
	
	Если Объект.Ссылка <> ПредопределенноеЗначение("Справочник.Нумераторы.Штрихкоды")
		И Не ПараметрыЗаписи.Свойство("ПоказаноПодтверждениеЗаписиФорматаБезНомера") Тогда
		Если Не НайденТегНомер(Объект.ФорматНомера, ТекстОшибки, Отказ) Тогда 
			Если Отказ Тогда 
				ОбщегоНазначенияКлиент.СообщитьПользователю(
				ТекстОшибки,,
				"Объект.ФорматНомера",,
				Отказ);
				Возврат;
				
			ИначеЕсли (Найти(Объект.ФорматНомера, "НомерСвязДок") > 0) Тогда 
				ОписаниеОповещения = Новый ОписаниеОповещения(
					"ПередЗаписьюПродолжениеПослеПодтвержденияЗаписиФорматаБезНомера",
					ЭтотОбъект,
					ПараметрыЗаписи);
				ТекстВопроса = НСтр("ru = 'Формат номера не содержит служебное поле ""Номер""
					|Продолжить?'");
				
				ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет,,КодВозвратаДиалога.Нет);
				Отказ = Истина;
				Возврат;
				
			Иначе 
				ОбщегоНазначенияКлиент.СообщитьПользователю(
					НСтр("ru = 'Формат номера не содержит служебное поле ""Номер""'"),,
					"Объект.ФорматНомера",,
					Отказ);
				Возврат;
				
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	ПараметрыНумерации = ИзменениеПараметровНумерации();
	
	Если ПараметрыНумерации.ИзмененыПараметрыНумерации Тогда 
		ТекстВопроса = НСтр("ru = 'Для данного нумератора имеются сформированные номера.
			|Вы действительно хотите изменить параметры нумерации?'");
			
		Если Не ПараметрыЗаписи.Свойство("ПоказаноПодтверждениеЗаписи") Тогда
			ОписаниеОповещения = Новый ОписаниеОповещения(
				"ПередЗаписьюПродолжениеПослеПодтвержденияЗаписи",
				ЭтотОбъект,
				ПараметрыЗаписи);
			ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет,,КодВозвратаДиалога.Нет);
			Отказ = Истина;
			Возврат;
		КонецЕсли;
		
		Если ПараметрыНумерации.ВключенаНумерацияПоОрганизациям 
			И Не ПараметрыЗаписи.Свойство("ПоказанВыборОрганизаций") Тогда 
			ОписаниеОповещения = Новый ОписаниеОповещения(
				"ПередЗаписьюПродолжениеПослеВыбораОрганизации",
				ЭтотОбъект,
				ПараметрыЗаписи);
			ВыбранноеЗначение = "";
			ПоказатьВводЗначения(
				ОписаниеОповещения,
				ВыбранноеЗначение, 
				РедакцииКонфигурацииКлиентСервер.ОшибкаНеВыбранаОрганизацияНумерации(), 
				Тип("СправочникСсылка.Организации"));
			Отказ = Истина;
			Возврат;
		КонецЕсли;
		
		Если ПараметрыНумерации.ВключенаНумерацияПоВидуДокумента
			И Не ПараметрыЗаписи.Свойство("ПоказанВыборВидаДокумента") Тогда 
			ОписаниеОповещения = Новый ОписаниеОповещения(
				"ПередЗаписьюПродолжениеПослеВыбораВидаДокумента",
				ЭтотОбъект,
				ПараметрыЗаписи);
				
			Массив = Новый Массив;
			Массив.Добавить(Тип("СправочникСсылка.ВидыДокументов"));
			ОписаниеТипов = Новый ОписаниеТипов(Массив);
			
			ВыбранноеЗначение = "";
			ПоказатьВводЗначения(
				ОписаниеОповещения,
				ВыбранноеЗначение, 
				НСтр("ru = 'Укажите вид документа, к которому отнести существующие номера'"), 
				ОписаниеТипов); 
			Отказ = Истина;
			Возврат;
		КонецЕсли;
		
		Если ПараметрыНумерации.ВключенаНумерацияПоПодразделению
			И Не ПараметрыЗаписи.Свойство("УказаноПодразделение") Тогда 
			ОписаниеОповещения = Новый ОписаниеОповещения(
				"ПередЗаписьюПродолжениеПослеВыбораПодразделения",
				ЭтотОбъект,
				ПараметрыЗаписи);
			ВыбранноеЗначение = "";
			ПоказатьВводЗначения(
				ОписаниеОповещения,
				ВыбранноеЗначение, 
				НСтр("ru = 'Укажите подразделение, к которому отнести существующие номера'"), 
				Тип("СправочникСсылка.СтруктураПредприятия"));  
			Отказ = Истина;
			Возврат;
		КонецЕсли;
		
		Если ПараметрыНумерации.ВключенаНумерацияПоПроекту 
			И Не ПараметрыЗаписи.Свойство("УказанПроект") Тогда 
			ОписаниеОповещения = Новый ОписаниеОповещения(
				"ПередЗаписьюПродолжениеПослеВыбораПроекта",
				ЭтотОбъект,
				ПараметрыЗаписи);
			ВыбранноеЗначение = "";
			ПоказатьВводЗначения(
				ОписаниеОповещения,
				ВыбранноеЗначение, 
				НСтр("ru = 'Укажите проект, к которому отнести существующие номера'"), 
				Тип("СправочникСсылка.Проекты"));
			Отказ = Истина;
			Возврат;
		КонецЕсли;
		
		Если ПараметрыНумерации.ВключенаНумерацияПоВопросуДеятельности
			И Не ПараметрыЗаписи.Свойство("УказанВопросДеятельности") Тогда 
			ОписаниеОповещения = Новый ОписаниеОповещения(
				"ПередЗаписьюПродолжениеПослеВыбораВопросаДеятельности",
				ЭтотОбъект,
				ПараметрыЗаписи);
			ВыбранноеЗначение = "";
			ПоказатьВводЗначения(
				ОписаниеОповещения,
				ВыбранноеЗначение, 
				НСтр("ru = 'Укажите вопрос деятельности, к которому отнести существующие номера'"), 
				Тип("СправочникСсылка.ВопросыДеятельности"));  
			Отказ = Истина;
			Возврат;					
		КонецЕсли;
		
		Если ПараметрыНумерации.ПереключенНаНепериодическийНумератор Тогда 
			Если Не ПараметрыЗаписи.Свойство("ПоказаноПредупреждениеОПереключенииНумератора") Тогда
				ОписаниеОповещения = Новый ОписаниеОповещения(
					"ПередЗаписьюПродолжениеПослеПредупрежденияОПереключенииНумератора",
					ЭтотОбъект,
					ПараметрыЗаписи);
				ТекстСообщения = НСтр("ru = 'Изменена периодичность нумератора на Непериодический.
					|Будет удалена информация о номерах по периодам и установлен последний номер в качестве текущего'");
				ПоказатьПредупреждение(ОписаниеОповещения, ТекстСообщения);
				Отказ = Истина;
				Возврат;
			КонецЕсли;
		КонецЕсли;	
		
	КонецЕсли;	
	
	Если Найти(Объект.ФорматНомера, "ИндексНомДел") > 0 
		И Не ПараметрыЗаписи.Свойство("ПоказанВопросОВключенииУчетаПоНоменклатуреДел") Тогда 
		ВидыДокументов = ВидыДокументовБезУчетаПоНоменклатуреДел();
		Если ВидыДокументов.Количество() > 0 Тогда 
			
			ПараметрыОбработчика = Новый Структура;
			ПараметрыОбработчика.Вставить("ВидыДокументов", ВидыДокументов);
			ПараметрыОбработчика.Вставить("ПараметрыЗаписи", ПараметрыЗаписи);
			
			ОписаниеОповещения = Новый ОписаниеОповещения(
				"ПередЗаписьюПродолжениеПослеВопросаОВключенииУчетаПоНоменклатуреДел",
				ЭтотОбъект,
				ПараметрыОбработчика);
			
			ТекстВопроса = НСтр("ru = 'Текущий нумератор использует индекс номенклатуры дел.
				|Найдены виды документов, для которых не включен учет по номенклатуре дел.
				|Включить учет по номенклатуре дел для найденных видов документов?'");
				
			ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Да);
			Отказ = Истина;
			Возврат;
			
		КонецЕсли;
	КонецЕсли;
	
	Если ПараметрыНумерации.ВключенаНумерацияПоГрифДоступа 
		И Не ПараметрыЗаписи.Свойство("УказанГриф") Тогда 
		ОписаниеОповещения = Новый ОписаниеОповещения(
			"ПередЗаписьюПродолжениеПослеВыбораГрифа",
			ЭтотОбъект,
			ПараметрыЗаписи);
		ВыбранноеЗначение = "";
		ПоказатьВводЗначения(
			ОписаниеОповещения,
			ВыбранноеЗначение, 
			НСтр("ru = 'Укажите гриф доступа, к которому отнести существующие номера'"), 
			Тип("СправочникСсылка.ГрифыДоступа"));
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписьюПродолжениеПослеПодтвержденияЗаписиФорматаБезНомера(Результат, ПараметрыЗаписи) Экспорт
	
	Если Результат <> КодВозвратаДиалога.Да Тогда 
		Возврат;
	КонецЕсли;
	
	ПараметрыЗаписи.Вставить("ПоказаноПодтверждениеЗаписиФорматаБезНомера", Истина);
	Записать(ПараметрыЗаписи);
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписьюПродолжениеПослеПодтвержденияЗаписи(Результат, ПараметрыЗаписи) Экспорт
	
	Если Результат <> КодВозвратаДиалога.Да Тогда 
		Возврат;
	КонецЕсли;
	ПараметрыЗаписи.Вставить("ПоказаноПодтверждениеЗаписи", Истина);
	Записать(ПараметрыЗаписи);
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписьюПродолжениеПослеВыбораОрганизации(ВыбранноеЗначение, ПараметрыЗаписи) Экспорт
	
	Если ВыбранноеЗначение = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыЗаписи.Вставить("ПоказанВыборОрганизаций", Истина);
	
	Если ТипЗнч(ВыбранноеЗначение) = Тип("СправочникСсылка.Организации") Тогда 
		Организация = ВыбранноеЗначение;
	Иначе 
		Организация = ВыбранноеЗначение.Значение;
	КонецЕсли;
	ЗаполнитьИзмерениеВРегистреНумерация("Организация", Организация);
	Записать(ПараметрыЗаписи);
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписьюПродолжениеПослеВыбораВидаДокумента(ВыбранноеЗначение, ПараметрыЗаписи) Экспорт
	
	Если ВыбранноеЗначение = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	ПараметрыЗаписи.Вставить("ПоказанВыборВидаДокумента", Истина);
	ЗаполнитьИзмерениеВРегистреНумерация("ВидДокумента", ВыбранноеЗначение);
	Записать(ПараметрыЗаписи);
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписьюПродолжениеПослеВыбораПодразделения(ВыбранноеЗначение, ПараметрыЗаписи) Экспорт
	
	Если ВыбранноеЗначение = Неопределено Тогда
		Возврат;
	КонецЕсли;
	ЗаполнитьИзмерениеВРегистреНумерация("Подразделение", ВыбранноеЗначение);
	ПараметрыЗаписи.Вставить("УказаноПодразделение", Истина);
	Записать(ПараметрыЗаписи);
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписьюПродолжениеПослеВыбораПроекта(ВыбранноеЗначение, ПараметрыЗаписи) Экспорт
	
	Если ВыбранноеЗначение = Неопределено Тогда
		Возврат;
	КонецЕсли;
	ЗаполнитьИзмерениеВРегистреНумерация("Проект", ВыбранноеЗначение);
	ПараметрыЗаписи.Вставить("УказанПроект", Истина);
	Записать(ПараметрыЗаписи);
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписьюПродолжениеПослеВыбораГрифа(ВыбранноеЗначение, ПараметрыЗаписи) Экспорт
	
	Если ВыбранноеЗначение = Неопределено Тогда
		Возврат;
	КонецЕсли;
	ЗаполнитьИзмерениеВРегистреНумерация("ГрифДоступа", ВыбранноеЗначение);
	ПараметрыЗаписи.Вставить("УказанГриф", Истина);
	Записать(ПараметрыЗаписи);
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписьюПродолжениеПослеВыбораВопросаДеятельности(ВыбранноеЗначение, ПараметрыЗаписи) Экспорт
	
	Если ВыбранноеЗначение = Неопределено Тогда
		Возврат;
	КонецЕсли;
	ЗаполнитьИзмерениеВРегистреНумерация("ВопросДеятельности", ВыбранноеЗначение);
	ПараметрыЗаписи.Вставить("УказанВопросДеятельности", Истина);
	Записать(ПараметрыЗаписи);
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписьюПродолжениеПослеПредупрежденияОПереключенииНумератора(ПараметрыЗаписи) Экспорт
	
	ПереключитьНаНепериодическийНумератор();
	ПараметрыЗаписи.Вставить("ПоказаноПредупреждениеОПереключенииНумератора", Истина);
	Записать(ПараметрыЗаписи);
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписьюПродолжениеПослеВопросаОВключенииУчетаПоНоменклатуреДел(Результат, Параметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда 
		УстановитьУчетПоНоменклатуреДел(Параметры.ВидыДокументов);
		ТекстСообщения = НСтр("ru = 'Включен учет по номенклатуре дел для видов документов'");
		Состояние(ТекстСообщения);
	КонецЕсли;
	Параметры.ПараметрыЗаписи.Вставить("ПоказанВопросОВключенииУчетаПоНоменклатуреДел", Истина);
	Записать(Параметры.ПараметрыЗаписи);
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	ПараметрыЗаписи.Вставить("ЭтоНовыйОбъект", НЕ ЗначениеЗаполнено(ТекущийОбъект.Ссылка));
	
КонецПроцедуры

&НаСервере
Процедура ПриЗаписиНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	ЗаписатьНастройкиНумерации(ТекущийОбъект.Ссылка);
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	ПротоколированиеРаботыСотрудников.ЗаписатьСоздание(Объект.Ссылка, ПараметрыЗаписи.ЭтоНовыйОбъект);
	ПротоколированиеРаботыСотрудников.ЗаписатьИзменение(Объект.Ссылка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	Если ПараметрыЗаписи.Свойство("Закрыть") Тогда
		ПоказатьОповещениеПользователя(
			НСтр("ru = 'Изменение:'"),
			ПолучитьНавигационнуюСсылку(Объект.Ссылка),
			Строка(Объект.Ссылка),
			БиблиотекаКартинок.Информация32);
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ФорматНомераПриИзменении(Элемент)
	
	УстановитьДоступностьТипаСвязи(Истина);
	
	СформироватьПримерНомера();
	
КонецПроцедуры

&НаКлиенте
Процедура СлужебныеПоляВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ПеренестиСлужебноеПоле();
	
КонецПроцедуры

&НаКлиенте
Процедура СлужебныеПоляПриАктивизацииСтроки(Элемент)
	
	Если ИспользоватьСвязиДокументов Тогда 
		ПодключитьОбработчикОжидания("ОбновитьДоступностьКомандыИзмененияТипаСвязиСлужебныеПоля",
			0.1, Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НезависимаяНумерацияПриАктивизацииСтроки(Элемент)
	
	Если ИспользоватьСвязиДокументов Тогда 
		ПодключитьОбработчикОжидания("ОбновитьДоступностьКомандыИзмененияТипаСвязиНезависимаяНумерация",
			0.1, Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НезависимаяНумерацияПометкаПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.НезависимаяНумерация.ТекущаяСтрока;
	Если ТекущаяСтрока = Неопределено Тогда 
		Возврат;
	КонецЕсли;	
	
	Строка = НезависимаяНумерация.НайтиПоИдентификатору(ТекущаяСтрока);
	Если Строка.Значение = "НезависимаяНумерацияПоСвязанномуДокументу" Тогда 
		УстановитьДоступностьТипаСвязи();
	Иначе 
		СформироватьЗаголовокНезависимаяНумерация();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НастройкиНумерацииВидДокументаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ТекущиеДанные = Элементы.НастройкиНумерации.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ТекущиеДанные.ТипДокумента) Или ТекущиеДанные.ВидДокумента = Неопределено Тогда 
		ТекущиеДанные.ТипДокумента = ПредопределенноеЗначение("Перечисление.ТипыОбъектов.ДокументыПредприятия");
		ТекущиеДанные.ВидДокумента = ПредопределенноеЗначение("Справочник.ВидыДокументов.ПустаяСсылка");
	КонецЕсли;		
	
КонецПроцедуры

&НаКлиенте
Процедура НастройкиНумерацииВидДокументаНачалоВыбораПродолжение(ВыбранныйЭлемент, Параметры) Экспорт
	
	Если ВыбранныйЭлемент = Неопределено Тогда 
		Возврат;
	КонецЕсли;	
	ТекущиеДанные = Элементы.НастройкиНумерации.ТекущиеДанные;
	ОписаниеТипов = Новый ОписаниеТипов(ВыбранныйЭлемент.Значение);
	ТекущиеДанные.ВидДокумента = ОписаниеТипов.ПривестиЗначение(ТекущиеДанные.ВидДокумента);
	
	Если ВыбранныйЭлемент.Значение = "СправочникСсылка.ВидыДокументов" Тогда 
		ТекущиеДанные.ТипДокумента = ПредопределенноеЗначение("Перечисление.ТипыОбъектов.ДокументыПредприятия");
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура НастройкиНумерацииПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	Если НоваяСтрока Тогда
		СформироватьЗаголовокДействуетДля();
		Элемент.ТекущиеДанные.ТипДокумента = ПредопределенноеЗначение("Перечисление.ТипыОбъектов.ДокументыПредприятия");
	КонецЕсли;
	
КонецПроцедуры
 
&НаКлиенте
Процедура НастройкиНумерацииПослеУдаления(Элемент)
	
	СформироватьЗаголовокДействуетДля();
	
КонецПроцедуры

&НаКлиенте
Процедура НастройкиНумерацииТипДокументаОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	ТекущиеДанные = Элементы.НастройкиНумерации.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные.ТипДокумента = ВыбранноеЗначение;
	
	Если ВыбранноеЗначение = ПредопределенноеЗначение("Перечисление.ТипыОбъектов.ДокументыПредприятия") Тогда 
		ИмяТипа = "СправочникСсылка.ВидыДокументов";
	КонецЕсли;
	
	ОписаниеТипов = Новый ОписаниеТипов(ИмяТипа);
	ТекущиеДанные.ВидДокумента = ОписаниеТипов.ПривестиЗначение(ТекущиеДанные.ВидДокумента);
	
КонецПроцедуры

&НаКлиенте
Процедура НазначениеПриИзменении(Элемент)

	ВременныйНомерНазначение = ПредопределенноеЗначение("Перечисление.НазначенияНумераторов.ВременныйНомер");

	Если Объект.Назначение = ВременныйНомерНазначение Тогда
		Объект.ИспользоватьПропущенныеНомера = Ложь;
	КонецЕсли;

	Элементы.ИспользоватьПропущенныеНомера.Доступность =
		Объект.Назначение <> ВременныйНомерНазначение;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ДобавитьСлужебноеПоле(Команда)
	
	ПеренестиСлужебноеПоле();
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаЗаписатьИЗакрыть(Команда)
	
	ЗаписатьИЗакрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьТипСвязи(Команда)
	
	Если ЗначениеЗаполнено(Объект.ТипСвязи) Тогда 
		СтрокаНумерации = НезависимаяНумерация.НайтиПоЗначению(
			"НезависимаяНумерацияПоСвязанномуДокументу");
		НезависимаяНумерацияПоСвязанномуДокументу = СтрокаНумерации.Пометка;
		ФорматСодержитНомерСвязанного = ФорматСодержитНомерСвязанного(Объект.ФорматНомера);
		
		СтруктураДанных = Новый Структура;
		СтруктураДанных.Вставить("НезависимаяНумерацияПоСвязанномуДокументу", 
			НезависимаяНумерацияПоСвязанномуДокументу);
		СтруктураДанных.Вставить("ФорматСодержитНомерСвязанного", ФорматСодержитНомерСвязанного);
		ОписаниеОповещения = Новый ОписаниеОповещения("ОбработатьИзменениеТипаСвязи", ЭтаФорма,
			СтруктураДанных);
		
		Если ФорматСодержитНомерСвязанного Или НезависимаяНумерацияПоСвязанномуДокументу Тогда
			ПараметрыОткрытияФормы = Новый Структура("Заголовок",
				НСтр("ru = 'Укажите тип связи для настройки нумерации по связанному документу'")); 
			ОткрытьФорму("Справочник.ТипыСвязей.ФормаВыбора", ПараметрыОткрытияФормы, ЭтаФорма,,,,
			 	ОписаниеОповещения, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиентеНаСервереБезКонтекста
Функция ФорматСодержитНомерСвязанного(ФорматНомера)
	
	Возврат (Найти(ФорматНомера, НСтр("ru = 'НомерСвязДок'")) > 0);
	
КонецФункции

&НаКлиенте
Процедура УстановитьДоступностьТипаСвязи(СлужебноеПоле = Ложь)
	
	Если Не ИспользоватьСвязиДокументов Тогда 
		Возврат;
	КонецЕсли;
	
	СтрокаНумерации = НезависимаяНумерация.НайтиПоЗначению(
		"НезависимаяНумерацияПоСвязанномуДокументу");
	НезависимаяНумерацияПоСвязанномуДокументу = СтрокаНумерации.Пометка;
	ФорматСодержитНомерСвязанного = ФорматСодержитНомерСвязанного(Объект.ФорматНомера);
	
	СтруктураДанных = Новый Структура;
	СтруктураДанных.Вставить("СлужебноеПоле", СлужебноеПоле);
	СтруктураДанных.Вставить("ФорматСодержитНомерСвязанного", ФорматСодержитНомерСвязанного);
	ОписаниеОповещения = Новый ОписаниеОповещения("ОбработатьВводТипаСвязи", ЭтаФорма,
		СтруктураДанных);
	
	Если ФорматСодержитНомерСвязанного Или НезависимаяНумерацияПоСвязанномуДокументу Тогда
		Если ЗначениеЗаполнено(Объект.ТипСвязи) Тогда
			ВыполнитьОбработкуОповещения(ОписаниеОповещения, Объект.ТипСвязи);
		Иначе 
			ПараметрыОткрытияФормы = Новый Структура("Заголовок, ПоказатьТолькоСвязиСДокументами",
				НСтр("ru = 'Укажите тип связи для настройки нумерации по связанному документу'"), Истина); 
			ОткрытьФорму("Справочник.ТипыСвязей.ФормаВыбора", ПараметрыОткрытияФормы, ЭтаФорма,,,,
			 	ОписаниеОповещения, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		КонецЕсли;
	Иначе
		Если ЗначениеЗаполнено(Объект.ТипСвязи) Тогда
			Объект.ТипСвязи = Неопределено;
		КонецЕсли;
		
		СтрокаНумерации.Представление = НСтр("ru = 'Связанному документу'");
		СтрокаСлужебныеПоля = СлужебныеПоля.НайтиПоЗначению("[НомерСвязДок]");
		СтрокаСлужебныеПоля.Представление = НСтр("ru = 'Номер связанного документа'");
		Элементы.НезависимаяНумерацияКонтекстноеМенюИзменитьТипСвязи.Доступность = Ложь;
		Элементы.СлужебныеПоляКонтекстноеМенюИзменитьТипСвязи.Доступность = Ложь;
		СформироватьЗаголовокНезависимаяНумерация();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьВводТипаСвязи(Результат, Параметры) Экспорт 
	
	Если Параметры.СлужебноеПоле Тогда 
		Если Результат = Неопределено Тогда
			Объект.ФорматНомера = СтрЗаменить(Объект.ФорматНомера, НСтр("ru = '[НомерСвязДок]'"), "");
			
		ИначеЕсли Результат <> Неопределено Тогда 
			Объект.ТипСвязи = Результат;
			Для Каждого Строка Из СлужебныеПоля Цикл
				Если Строка.Значение = "[НомерСвязДок]" Тогда 
					Если Параметры.ФорматСодержитНомерСвязанного Тогда 
						Строка.Представление = НСтр("ru = 'Номер связанного документа'") 
							+ " (" + Строка(Объект.ТипСвязи) + ")";
						Элементы.СлужебныеПоляКонтекстноеМенюИзменитьТипСвязи.Доступность = Истина;
					Иначе 
						Строка.Представление = НСтр("ru = 'Номер связанного документа'");
						Элементы.СлужебныеПоляКонтекстноеМенюИзменитьТипСвязи.Доступность = Ложь;
					КонецЕсли;
					
					Прервать;
				КонецЕсли;
			КонецЦикла;
			
		КонецЕсли;
		
	Иначе 
		Для Каждого Строка Из НезависимаяНумерация Цикл
			Если Строка.Значение = "НезависимаяНумерацияПоСвязанномуДокументу" Тогда  
				Если Результат = Неопределено Тогда 
					Строка.Пометка = Ложь;
				ИначеЕсли Строка.Пометка Тогда 
					Объект.ТипСвязи = Результат;
					Строка.Представление = НСтр("ru = 'Связанному документу'") + " (" + Строка(Объект.ТипСвязи) + ")";
					Элементы.НезависимаяНумерацияКонтекстноеМенюИзменитьТипСвязи.Доступность = Истина;
				Иначе 
					Строка.Представление = НСтр("ru = 'Связанному документу'");
					Элементы.НезависимаяНумерацияКонтекстноеМенюИзменитьТипСвязи.Доступность = Ложь;
				КонецЕсли;
				
				Прервать;
			КонецЕсли;
			
		КонецЦикла;
		
		СформироватьЗаголовокНезависимаяНумерация();
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура СформироватьПримерНомера()
	
	ОписаниеОшибки = "";
	Если Не Нумерация.СформироватьПримерНомера(Объект.ФорматНомера, Пример, ОписаниеОшибки) Тогда 
		Пример = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Ошибка в формате номера: %1'"), ОписаниеОшибки);
	КонецЕсли;
	
	Если Объект.Пример <> Пример Тогда 
		Объект.Пример = Пример;
	КонецЕсли;
	
	Пример = НСтр( "ru = 'Пример:'") + " " + Пример;
	
КонецПроцедуры

&НаКлиенте
Процедура ПеренестиСлужебноеПоле()
	
	Если Не ТолькоПросмотр Тогда 
		ТекущаяСтрока = Элементы.СлужебныеПоля.ТекущаяСтрока;
		ЗначениеПоля  = СлужебныеПоля.Получить(ТекущаяСтрока).Значение;
		Объект.ФорматНомера  = Объект.ФорматНомера + ЗначениеПоля;
		УстановитьДоступностьТипаСвязи(Истина);
		
		СформироватьПримерНомера();
		Модифицированность = Истина;
	КонецЕсли;
	
КонецПроцедуры	

&НаСервере
Функция ИзменениеПараметровНумерации()
	
	НаборЗаписей = РегистрыСведений.Нумерация.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Нумератор.Установить(Объект.Ссылка);
	НаборЗаписей.Прочитать();
	
	ИзмененыПараметрыНумерации = ЗначениеЗаполнено(Объект.Ссылка) 
		И (Объект.ФорматНомера <> Объект.Ссылка.ФорматНомера 
	    Или Объект.Периодичность <> Объект.Ссылка.Периодичность
	    Или Объект.НезависимаяНумерацияПоОрганизациям <> Объект.Ссылка.НезависимаяНумерацияПоОрганизациям
		Или Объект.НезависимаяНумерацияПоВидуДокумента <> Объект.Ссылка.НезависимаяНумерацияПоВидуДокумента
		Или Объект.НезависимаяНумерацияПоПодразделению <> Объект.Ссылка.НезависимаяНумерацияПоПодразделению)
		И НаборЗаписей.Количество() > 0;
		
	ПереключенНаНепериодическийНумератор = ЗначениеЗаполнено(Объект.Ссылка) 
		И Объект.Периодичность <> Объект.Ссылка.Периодичность
		И Объект.Периодичность = Перечисления.ПериодичностьНумераторов.Непериодический
		И НаборЗаписей.Количество() > 0;	
		
	// Включена нумерация по организациям
	НаборЗаписей = РегистрыСведений.Нумерация.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Нумератор.Установить(Объект.Ссылка);
	НаборЗаписей.Отбор.Организация.Установить(Справочники.Организации.ПустаяСсылка());
	НаборЗаписей.Прочитать();
	
	ВключенаНумерацияПоОрганизациям = ЗначениеЗаполнено(Объект.Ссылка) 
		И Объект.НезависимаяНумерацияПоОрганизациям 
		И (Не Объект.Ссылка.НезависимаяНумерацияПоОрганизациям)
		И НаборЗаписей.Количество() > 0;	
		
	// Включена нумерация по подразделению
	НаборЗаписей = РегистрыСведений.Нумерация.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Нумератор.Установить(Объект.Ссылка);
	НаборЗаписей.Отбор.Подразделение.Установить(Справочники.СтруктураПредприятия.ПустаяСсылка());
	НаборЗаписей.Прочитать();	
		
	ВключенаНумерацияПоПодразделению = ЗначениеЗаполнено(Объект.Ссылка) 
		И Объект.НезависимаяНумерацияПоПодразделению
		И (Не Объект.Ссылка.НезависимаяНумерацияПоПодразделению)
		И НаборЗаписей.Количество() > 0;
		
	// Включена нумерация по проекту
	НаборЗаписей = РегистрыСведений.Нумерация.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Нумератор.Установить(Объект.Ссылка);
	НаборЗаписей.Отбор.Проект.Установить(Справочники.Проекты.ПустаяСсылка());
	НаборЗаписей.Прочитать();	
		
	ВключенаНумерацияПоПроекту = ЗначениеЗаполнено(Объект.Ссылка) 
		И Объект.НезависимаяНумерацияПоПроекту
		И (Не Объект.Ссылка.НезависимаяНумерацияПоПроекту)
		И НаборЗаписей.Количество() > 0;	
		
	// Включена нумерация по вопросу деятельности	
	НаборЗаписей = РегистрыСведений.Нумерация.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Нумератор.Установить(Объект.Ссылка);
	НаборЗаписей.Отбор.ВопросДеятельности.Установить(Справочники.ВопросыДеятельности.ПустаяСсылка());
	НаборЗаписей.Прочитать();	
		
	ВключенаНумерацияПоВопросуДеятельности = ЗначениеЗаполнено(Объект.Ссылка) 
		И Объект.НезависимаяНумерацияПоВопросуДеятельности
		И (Не Объект.Ссылка.НезависимаяНумерацияПоВопросуДеятельности)
		И НаборЗаписей.Количество() > 0;	
		
	// Включена нумерация по виду документа
	НаборЗаписей = РегистрыСведений.Нумерация.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Нумератор.Установить(Объект.Ссылка);
	НаборЗаписей.Отбор.ВидДокумента.Установить(Неопределено);
	НаборЗаписей.Прочитать();
	
	ВключенаНумерацияПоВидуДокумента = ЗначениеЗаполнено(Объект.Ссылка) 
		И Объект.НезависимаяНумерацияПоВидуДокумента
		И (Не Объект.Ссылка.НезависимаяНумерацияПоВидуДокумента)
		И НаборЗаписей.Количество() > 0;
	
	// Включена нумерация по Грифу
	НаборЗаписей = РегистрыСведений.Нумерация.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Нумератор.Установить(Объект.Ссылка);
	НаборЗаписей.Отбор.ГрифДоступа.Установить(Справочники.ГрифыДоступа.ПустаяСсылка());
	НаборЗаписей.Прочитать();	
		
	ВключенаНумерацияПоГрифДоступа = ЗначениеЗаполнено(Объект.Ссылка) 
		И Объект.НезависимаяНумерацияПоГрифуДоступа
		И НаборЗаписей.Количество() > 0;
			
	Результат = Новый Структура;
	Результат.Вставить("ИзмененыПараметрыНумерации", 			ИзмененыПараметрыНумерации);
	Результат.Вставить("ПереключенНаНепериодическийНумератор", 	ПереключенНаНепериодическийНумератор);
	Результат.Вставить("ВключенаНумерацияПоОрганизациям", 		ВключенаНумерацияПоОрганизациям);
	Результат.Вставить("ВключенаНумерацияПоПодразделению", 		ВключенаНумерацияПоПодразделению);
	Результат.Вставить("ВключенаНумерацияПоПроекту", 			ВключенаНумерацияПоПроекту);
	Результат.Вставить("ВключенаНумерацияПоВопросуДеятельности",ВключенаНумерацияПоВопросуДеятельности);
	Результат.Вставить("ВключенаНумерацияПоВидуДокумента", 		ВключенаНумерацияПоВидуДокумента);
	Результат.Вставить("ВключенаНумерацияПоГрифДоступа", 		ВключенаНумерацияПоГрифДоступа);
	
	Возврат Результат;
	
КонецФункции	

&НаСервере
Функция ВидыДокументовБезУчетаПоНоменклатуреДел()
	
	ВидыДокументов = Новый Массив;
	
	Если Объект.Ссылка.Пустая() Тогда 
		Возврат ВидыДокументов;
	КонецЕсли;	
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	НастройкиНумерации.ВидДокумента
	|ИЗ
	|	РегистрСведений.НастройкиНумерации КАК НастройкиНумерации
	|ГДЕ
	|	НастройкиНумерации.Нумератор = &Нумератор
	|	И НЕ НастройкиНумерации.ВидДокумента.ВестиУчетПоНоменклатуреДел";
	
	Запрос.УстановитьПараметр("Нумератор", Объект.Ссылка);
	
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("ВидДокумента");
	
КонецФункции	

&НаСервере
Процедура УстановитьУчетПоНоменклатуреДел(ВидыДокументов)
	
	УстановитьПривилегированныйРежим(Истина);
	
	НачатьТранзакцию();
	Попытка
		Для Каждого ВидДокумента Из ВидыДокументов Цикл
			ЗаблокироватьДанныеДляРедактирования(ВидДокумента);
			ВидДокументаОбъект = ВидДокумента.ПолучитьОбъект();
			ВидДокументаОбъект.ВестиУчетПоНоменклатуреДел = Истина;
			ВидДокументаОбъект.Записать();
		КонецЦикла;	
		
		ЗафиксироватьТранзакцию();	
	Исключение
		ОтменитьТранзакцию();
		ВызватьИсключение;
	КонецПопытки;
	
КонецПроцедуры	

&НаСервере
Процедура ПереключитьНаНепериодическийНумератор()
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Нумерация.ПериодНумерации,
	|	Нумерация.Нумератор,
	|	Нумерация.Организация,
	|	Нумерация.СвязанныйДокумент,
	|	Нумерация.Подразделение,
	|	Нумерация.Проект,
	|	Нумерация.ВопросДеятельности,
	|	Нумерация.ГрифДоступа,
	|	ВЫБОР
	|		КОГДА (Нумерация.ПериодНумерации, Нумерация.Нумератор, Нумерация.Организация, Нумерация.СвязанныйДокумент,
	|			Нумерация.Подразделение, Нумерация.Проект, Нумерация.ВопросДеятельности, Нумерация.ГрифДоступа) В
	|			(ВЫБРАТЬ
	|				МАКСИМУМ(Нумерация.ПериодНумерации) КАК ПериодНумерации,
	|				Нумерация.Нумератор,
	|				Нумерация.Организация,
	|				Нумерация.СвязанныйДокумент,
	|				Нумерация.Подразделение,
	|				Нумерация.Проект,
	|				Нумерация.ВопросДеятельности,
	|				Нумерация.ГрифДоступа
	|			ИЗ
	|				РегистрСведений.Нумерация КАК Нумерация
	|			ГДЕ
	|				Нумерация.Нумератор = &Нумератор
	|			СГРУППИРОВАТЬ ПО
	|				Нумерация.Нумератор,
	|				Нумерация.Организация,
	|				Нумерация.СвязанныйДокумент,
	|				Нумерация.Подразделение,
	|				Нумерация.Проект,
	|				Нумерация.ВопросДеятельности,
	|				Нумерация.ГрифДоступа)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК МаксЗапись
	|ИЗ
	|	РегистрСведений.Нумерация КАК Нумерация
	|ГДЕ
	|	Нумерация.Нумератор = &Нумератор
	|СГРУППИРОВАТЬ ПО
	|	Нумерация.ПериодНумерации,
	|	Нумерация.Нумератор,
	|	Нумерация.Организация,
	|	Нумерация.СвязанныйДокумент,
	|	Нумерация.Подразделение,
	|	Нумерация.Проект,
	|	Нумерация.ВопросДеятельности,
	|	Нумерация.ГрифДоступа";
	
	Запрос.УстановитьПараметр("Нумератор", Объект.Ссылка);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		МенеджерЗаписи = РегистрыСведений.Нумерация.СоздатьМенеджерЗаписи();
		ЗаполнитьЗначенияСвойств(МенеджерЗаписи, Выборка);
		МенеджерЗаписи.Прочитать();
		
		Если Выборка.МаксЗапись Тогда
			МенеджерЗаписи.ПериодНумерации = '00010101';
			МенеджерЗаписи.Записать();
		Иначе
			МенеджерЗаписи.Удалить();
		КонецЕсли;
	КонецЦикла;	
	
КонецПроцедуры	

&НаКлиенте
Процедура ВопросПередЗакрытиемЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		ЗаписатьИЗакрыть();
	Иначе
		Модифицированность = Ложь;
		Закрыть();
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьИЗакрыть()
	
	ПараметрыЗаписи = Новый Структура();
	ПараметрыЗаписи.Вставить("Закрыть", Истина);
	Записать(ПараметрыЗаписи);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция НайденТегНомер(ФорматНомера, ТекстОшибки, Отказ)
	
	СтруктураФорматаНомера = Неопределено;
	ОписаниеОшибки = "";
	
	Если Не Нумерация.РазобратьФорматНомера(ФорматНомера, ОписаниеОшибки, СтруктураФорматаНомера) Тогда
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Ошибка в формате номера: %1'"), ОписаниеОшибки);
		Отказ = Истина;
		Возврат Ложь;
	КонецЕсли;
	
	НайденТегНомер = Ложь;
	Для Каждого Строка Из СтруктураФорматаНомера Цикл
		Если Строка.Ключ = "СлужебноеПоле" И Строка.Значение = "Номер" Тогда 
			НайденТегНомер = Истина;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Возврат НайденТегНомер;
	
КонецФункции

&НаСервере
Процедура ЗаполнитьНезависимуюНумерацию()
	
	НезависимаяНумерация.Очистить();
	НезависимаяНумерация.Добавить("НезависимаяНумерацияПоВидуДокумента",
		НСтр("ru = 'Виду документа'"));
		
	Если ПолучитьФункциональнуюОпцию("ИспользоватьВопросыДеятельности") Тогда
		НезависимаяНумерация.Добавить("НезависимаяНумерацияПоВопросуДеятельности",
			НСтр("ru = 'Вопросу деятельности'"));
	КонецЕсли;
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьГрифыДоступа") Тогда
		НезависимаяНумерация.Добавить("НезависимаяНумерацияПоГрифуДоступа", НСтр("ru = 'Грифу доступа'"));
	КонецЕсли;
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьУчетПоОрганизациям") Тогда
		НезависимаяНумерация.Добавить("НезависимаяНумерацияПоОрганизациям", РедакцииКонфигурацииКлиентСервер.Организации());
	КонецЕсли;
	
	НезависимаяНумерация.Добавить("НезависимаяНумерацияПоПодразделению", НСтр("ru = 'Подразделению'"));
	
	Если ПолучитьФункциональнуюОпцию("ВестиУчетПоПроектам") Тогда
		НезависимаяНумерация.Добавить("НезависимаяНумерацияПоПроекту", НСтр("ru = 'Проекту'"));
	КонецЕсли;
	
	Если ИспользоватьСвязиДокументов Тогда 
		НезависимаяНумерация.Добавить("НезависимаяНумерацияПоСвязанномуДокументу",
			НСтр("ru = 'Связанному документу'"));
	КонецЕсли;
	
	Если РаботаСТематикамиДокументов.ТематикиИспользуются() Тогда
		НезависимаяНумерация.Добавить("НезависимаяНумерацияПоТематике", НСтр("ru = 'Тематике'"));
	КонецЕсли;
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьКОД") Тогда
		НезависимаяНумерация.Добавить("НезависимаяНумерацияПоУзлуКОД", НСтр("ru = 'Узлу обмена'"));
	КонецЕсли;

	Для Каждого Строка Из НезависимаяНумерация Цикл
		Если Объект[Строка.Значение] = Истина Тогда 
			Строка.Пометка = Истина;
		КонецЕсли;
	КонецЦикла;	
	
КонецПроцедуры	

&НаСервере
Процедура ЗаполнитьСлужебныеПоля()
	
	СлужебныеПоля.Очистить();
	
	СлужебныеПоля.Добавить(НСтр("ru = '[Номер]'"),		НСтр("ru = 'Номер'"));
	СлужебныеПоля.Добавить(НСтр("ru = '[День]'"),		НСтр("ru = 'День месяца'"));
	СлужебныеПоля.Добавить(НСтр("ru = '[Месяц]'"),		НСтр("ru = 'Номер месяца'"));
	СлужебныеПоля.Добавить(НСтр("ru = '[Квартал]'"),	НСтр("ru = 'Номер квартала'"));
	СлужебныеПоля.Добавить(НСтр("ru = '[Год2]'"),		НСтр("ru = 'Год (2 знака)'"));
	СлужебныеПоля.Добавить(НСтр("ru = '[Год4]'"),		НСтр("ru = 'Год (4 знака)'"));
	СлужебныеПоля.Добавить(НСтр("ru = '[ИндексВидаДок]'"), НСтр("ru = 'Индекс вида документа'"));
	
	Если РаботаСТематикамиДокументов.ТематикиИспользуются() Тогда
		СлужебныеПоля.Добавить(НСтр("ru = '[ИндексТематики]'"), НСтр("ru = 'Индекс тематики'"));
	КонецЕсли;
	
	СлужебныеПоля.Добавить(НСтр("ru = '[ИндексПодр]'"), 	НСтр("ru = 'Индекс подразделения'"));
	СлужебныеПоля.Добавить(НСтр("ru = '[ИндексКонтр]'"), 	НСтр("ru = 'Индекс контрагента'"));
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьУчетПоОрганизациям") Тогда 
		СлужебныеПоля.Добавить(НСтр("ru = '[ИндексОрг]'"), РедакцииКонфигурацииКлиентСервер.ИндексОрганизации());
	КонецЕсли;	
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьВопросыДеятельности") Тогда 
		СлужебныеПоля.Добавить(НСтр("ru = '[ИндексВопрДеят]'"), НСтр("ru = 'Индекс вопроса деятельности'"));
	КонецЕсли;
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьНоменклатуруДел") Тогда
		СлужебныеПоля.Добавить(НСтр("ru = '[ИндексНомДел]'"), НСтр("ru = 'Индекс номенклатуры дел'"));
	КонецЕсли;
	
	Если ПолучитьФункциональнуюОпцию("ВестиУчетПоПроектам") Тогда 
		СлужебныеПоля.Добавить(НСтр("ru = '[ИндексПроекта]'"), НСтр("ru = 'Индекс проекта'"));
	КонецЕсли;

	Если ИспользоватьСвязиДокументов Тогда 
		СлужебныеПоля.Добавить(НСтр("ru = '[НомерСвязДок]'"), НСтр("ru = 'Номер связанного документа'"));
	КонецЕсли;
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьГрифыДоступа") Тогда 
		СлужебныеПоля.Добавить(НСтр("ru = '[ИндексГрифаДоступа]'"), НСтр("ru = 'Индекс грифа доступа'"));
	КонецЕсли;
	
	СлужебныеПоля.Добавить(НСтр("ru = '[ИндексОтв]'"), НСтр("ru = 'Индекс ответственного'"));
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьКОД") Тогда
		СлужебныеПоля.Добавить(НСтр("ru = '[КодУзлаОбмена]'"), НСтр("ru = 'Узел обмена'"));
	КонецЕсли;
	
КонецПроцедуры	

&НаКлиенте
Процедура СформироватьЗаголовокДействуетДля()
	
	КоличествоНастроекНумерации = НастройкиНумерации.Количество();
	
КонецПроцедуры

&НаКлиенте
Процедура СформироватьЗаголовокНезависимаяНумерация()
	
	КоличествоПометокНезависимойНумерации = 0;
	
	Для Каждого Строка Из НезависимаяНумерация Цикл
		Если Строка.Пометка Тогда 
			КоличествоПометокНезависимойНумерации = КоличествоПометокНезависимойНумерации + 1
		КонецЕсли;
	КонецЦикла;	
	
КонецПроцедуры

&НаСервере
Процедура ПрочитатьНастройкиНумерации()
	
	Если Объект.Ссылка.Пустая() Тогда 
		Возврат;
	КонецЕсли;	
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	НастройкиНумерации.ТипДокумента КАК ТипДокумента,
	|	НастройкиНумерации.ВидДокумента КАК ВидДокумента,
	|	НастройкиНумерации.Организация КАК Организация,
	|	НастройкиНумерации.Подразделение КАК Подразделение,
	|	НастройкиНумерации.Контрагент КАК Контрагент,
	|	НастройкиНумерации.Проект КАК Проект,
	|	НастройкиНумерации.ВопросДеятельности КАК ВопросДеятельности,
	|	НастройкиНумерации.Тематика КАК Тематика,
	|	НастройкиНумерации.ГрифДоступа,
	|	НастройкиНумерации.УзелКОД
	|ИЗ
	|	РегистрСведений.НастройкиНумерации КАК НастройкиНумерации
	|ГДЕ
	|	НастройкиНумерации.Нумератор = &Нумератор";
	Запрос.УстановитьПараметр("Нумератор", Объект.Ссылка);
	
	ЗначениеВРеквизитФормы(Запрос.Выполнить().Выгрузить(), "НастройкиНумерации");
	
КонецПроцедуры	

&НаСервере
Процедура ЗаписатьНастройкиНумерации(Ссылка)
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	НастройкиНумерации.ТипДокумента КАК ТипДокумента,
	|	НастройкиНумерации.ВидДокумента КАК ВидДокумента,
	|	НастройкиНумерации.Организация КАК Организация,
	|	НастройкиНумерации.Подразделение КАК Подразделение,
	|	НастройкиНумерации.ВопросДеятельности КАК ВопросДеятельности,
	|	НастройкиНумерации.Контрагент КАК Контрагент,
	|	НастройкиНумерации.Проект КАК Проект,
	|	НастройкиНумерации.Назначение КАК Назначение,
	|	НастройкиНумерации.Тематика КАК Тематика,
	|	НастройкиНумерации.ГрифДоступа,
	|	НастройкиНумерации.УзелКОД
	|ИЗ
	|	РегистрСведений.НастройкиНумерации КАК НастройкиНумерации
	|ГДЕ
	|	НастройкиНумерации.Нумератор = &Нумератор";
	Запрос.УстановитьПараметр("Нумератор", Ссылка);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		МенеджерЗаписи = РегистрыСведений.НастройкиНумерации.СоздатьМенеджерЗаписи();
		ЗаполнитьЗначенияСвойств(МенеджерЗаписи, Выборка);
		МенеджерЗаписи.Удалить();
	КонецЦикла;	
	
	Для Каждого Строка Из НастройкиНумерации Цикл
		МенеджерЗаписи = РегистрыСведений.НастройкиНумерации.СоздатьМенеджерЗаписи();
		ЗаполнитьЗначенияСвойств(МенеджерЗаписи, Строка);
		МенеджерЗаписи.Нумератор = Ссылка;
		МенеджерЗаписи.СпособНумерации = Перечисления.СпособыНумерации.Автоматически;
		МенеджерЗаписи.Назначение = Объект.Назначение;
		МенеджерЗаписи.Записать();
	КонецЦикла;	
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьИзмерениеВРегистреНумерация(ИмяИзмерения, ЗначениеИзмерения)
	
	УстановитьПривилегированныйРежим(Истина);
	
	НаборЗаписей = РегистрыСведений.Нумерация.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Нумератор.Установить(Объект.Ссылка);
	НаборЗаписей.Прочитать();
	
	Для Каждого Запись Из НаборЗаписей Цикл
		Если Не ЗначениеЗаполнено(Запись[ИмяИзмерения]) Тогда 
			Запись[ИмяИзмерения] = ЗначениеИзмерения;
		КонецЕсли;	
	КонецЦикла;	
	НаборЗаписей.Записать();	
	
КонецПроцедуры	

&НаКлиенте
Процедура ОбработатьИзменениеТипаСвязи(Результат, Параметры) Экспорт 
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Объект.ТипСвязи = Результат;
	
	Если Параметры.ФорматСодержитНомерСвязанного Тогда 
		Для Каждого Строка Из СлужебныеПоля Цикл
			Если Строка.Значение = "[НомерСвязДок]" Тогда 
					Строка.Представление = НСтр("ru = 'Номер связанного документа'") 
						+ " (" + Строка(Объект.ТипСвязи) + ")";
				Прервать;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
		
	Если Параметры.НезависимаяНумерацияПоСвязанномуДокументу Тогда 
		Для Каждого Строка Из НезависимаяНумерация Цикл
			Если Строка.Значение = "НезависимаяНумерацияПоСвязанномуДокументу" Тогда  
				Строка.Представление = НСтр("ru = 'Связанному документу'") + " (" + Строка(Объект.ТипСвязи) + ")";
				Прервать;
			КонецЕсли;
			
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьДоступностьКомандыИзмененияТипаСвязиНезависимаяНумерация()
	
	ТекущиеДанные = Элементы.НезависимаяНумерация.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда 
		Элементы.НезависимаяНумерацияКонтекстноеМенюИзменитьТипСвязи.Доступность = Ложь;
	ИначеЕсли ТекущиеДанные.Значение = "НезависимаяНумерацияПоСвязанномуДокументу" 
		И ТекущиеДанные.Пометка Тогда 
		Элементы.НезависимаяНумерацияКонтекстноеМенюИзменитьТипСвязи.Доступность = Истина;
	Иначе 
		Элементы.НезависимаяНумерацияКонтекстноеМенюИзменитьТипСвязи.Доступность = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьДоступностьКомандыИзмененияТипаСвязиСлужебныеПоля()
	
	ТекущиеДанные = Элементы.СлужебныеПоля.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда 
		Элементы.СлужебныеПоляКонтекстноеМенюИзменитьТипСвязи.Доступность = Ложь;
	ИначеЕсли ТекущиеДанные.Значение = "[НомерСвязДок]" 
		И ФорматСодержитНомерСвязанного(Объект.ФорматНомера) Тогда 
		Элементы.СлужебныеПоляКонтекстноеМенюИзменитьТипСвязи.Доступность = Истина;
	Иначе 
		Элементы.СлужебныеПоляКонтекстноеМенюИзменитьТипСвязи.Доступность = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервереРедакцииКонфигурации()
	
	Элементы.НастройкиНумерацииОрганизация.Заголовок = РедакцииКонфигурацииКлиентСервер.Организация();
		
КонецПроцедуры

#КонецОбласти
