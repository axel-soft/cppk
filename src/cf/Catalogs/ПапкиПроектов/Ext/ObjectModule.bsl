#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

// Обновление адресной книги
Перем ЭтоНовый;
Перем ОбновитьДанныеПапкиПроектовВАдреснойКниге;
Перем ПроектыДляОбновленияАдреснойКниги;

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	Если ЭтоНовый() Тогда 
		ДатаСоздания = ТекущаяДатаСеанса();
		Ответственный = Сотрудники.ОсновнойСотрудник();
	КонецЕсли;	
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ЭтоНовый = ЭтоНовый();
	
	Если Не ЭтоНовый Тогда
	ПредыдущееЗначениеПометкаУдаления = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Ссылка, "ПометкаУдаления");
		Если ПометкаУдаления <> ПредыдущееЗначениеПометкаУдаления Тогда
			Если Не ДокументооборотПраваДоступа.ПраваПользователяПоОбъекту(Ссылка).Удаление Тогда
				ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'У вас нет права ""Пометка на удаление"" папки ""%1"".'"),
					Строка(Ссылка));
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;

	// Обновление адресной книги.
	ДополнительныеСвойства.Вставить(
		"ПараметрыОбновленияАдреснойКниги",
		Справочники.ПапкиПроектов.ЗначенияПараметровОбновленияАдреснойКнигиПоОбъекту(ЭтотОбъект));
		
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
		
	// Настройки прав по умолчанию.
	Если ЭтоНовый И Не ЗначениеЗаполнено(Родитель) Тогда
		НастройкаПрав = РегистрыСведений.НастройкиПравОбъектов.СоздатьМенеджерЗаписи();
		НастройкаПрав.Объект = Ссылка;
		НастройкаПрав.Пользователь = Справочники.РабочиеГруппы.ВсеПользователи;
		НастройкаПрав.Право = "ЧтениеПапокИПроектов";
		НастройкаПрав.ПравоЗапрещено = Ложь;
		НастройкаПрав.НаследованиеРазрешено = Истина;
		НастройкаПрав.Записать();
	КонецЕсли;
	
	// Обновление адресной книги.
	Справочники.ПапкиПроектов.ОбновитьАдреснуюКнигу(
		ЭтотОбъект, ДополнительныеСвойства.ПараметрыОбновленияАдреснойКниги);
	
КонецПроцедуры

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли