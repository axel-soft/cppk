#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает отслеживаемые реквизиты для дополнительного свойства ПредыдущиеЗначенияРеквизитов.
//
// Возвращаемое значение:
//  Строка.
//
Функция ОтслеживаемыеРеквизиты() Экспорт
	
	ОтслеживаемыеРеквизиты =
		"ПометкаУдаления,
		|Проект, Создал,
		|Доступ, Приложения";
	
	Возврат ОтслеживаемыеРеквизиты;
	
КонецФункции

#Область ДляВызоваИзДругихПодсистем

#Область ВерсионированиеОбъектов

// СтандартныеПодсистемы.ВерсионированиеОбъектов
// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
// Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт
	
	// Стандартные настройки.
	
КонецПроцедуры
// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

#КонецОбласти

#Область ПраваДоступа

// Возвращает строку, содержащую перечисление полей доступа через запятую.
// Это перечисление используется в дальнейшем для передачи в метод
// ОбщегоНазначения.ЗначенияРеквизитовОбъекта().
// 
// Возвращаемое значение:
//  Строка.
// 
Функция ПолучитьПоляДоступа() Экспорт
	
	Возврат "Ссылка, Создал, Доступ";
	
КонецФункции

// Проверяет наличие процедуры ЗаполнитьДескрипторыОбъекта.
// 
// Возвращаемое значение:
//  Булево - Есть метод заполнить дескрипторы объекта.
// 
Функция ЕстьМетодЗаполнитьДескрипторыОбъекта() Экспорт
	
	Возврат Истина;
	
КонецФункции

// Заполняет переданную таблицу дескрипторов объекта.
// 
// Параметры:
//  ОбъектДоступа - Структура.
//  ТаблицаДескрипторов - ТаблицаЗначений.
//  ПротоколРасчетаПрав - Неопределено.
// 
Процедура ЗаполнитьДескрипторыОбъекта(ОбъектДоступа, ТаблицаДескрипторов, ПротоколРасчетаПрав = Неопределено) Экспорт
	
	КонтейнерыДескрипторов = Новый ТаблицаЗначений;
	КонтейнерыДескрипторов.Колонки.Добавить("КонтейнерСотрудников");
	КонтейнерыДескрипторов.Колонки.Добавить("Изменение");
	
	// Создавший
	Если ЗначениеЗаполнено(ОбъектДоступа.Создал) Тогда
		СтрокаКонтейнера = КонтейнерыДескрипторов.Добавить();
		СтрокаКонтейнера.КонтейнерСотрудников = ОбъектДоступа.Создал;
		СтрокаКонтейнера.Изменение = Истина;
	КонецЕсли;
	
	// Доступ.
	ТаблицаДоступ = ОбъектДоступа.Доступ.Выгрузить();
	
	Для Каждого СтрокаДоступа Из ТаблицаДоступ Цикл
		СтрокаКонтейнера = КонтейнерыДескрипторов.Добавить();
		СтрокаКонтейнера.КонтейнерСотрудников = СтрокаДоступа.Участник;
		СтрокаКонтейнера.Изменение = СтрокаДоступа.Изменение;
	КонецЦикла;
	
	ДокументооборотПраваДоступа.ДобавитьИндивидуальныеДескрипторы(
		ОбъектДоступа,
		ТаблицаДескрипторов,
		КонтейнерыДескрипторов);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецОбласти

#КонецЕсли