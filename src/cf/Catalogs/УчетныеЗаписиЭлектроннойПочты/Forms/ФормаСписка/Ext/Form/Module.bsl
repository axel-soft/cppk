
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("Пользователь") И ЗначениеЗаполнено(Параметры.Пользователь) Тогда 
		
		Сотрудник = Сотрудники.ОсновнойСотрудникПользователя(
			Параметры.Пользователь);
		
		ОтветственныеЗаОбработку = Новый Массив;
		ОтветственныеЗаОбработку.Добавить(Параметры.Пользователь);
		ОтветственныеЗаОбработку.Добавить(Сотрудник);
		
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(Список.Отбор,
			"ОтветственныеЗаОбработкуПисем.Сотрудник",
			ОтветственныеЗаОбработку,
			ВидСравненияКомпоновкиДанных.ВСписке);
		
		КонецЕсли;  
		
	Элементы.УзелОбмена.Видимость = ПолучитьФункциональнуюОпцию("ИспользоватьКОД");
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура СписокПриПолученииДанныхНаСервере(ИмяЭлемента, Настройки, Строки)
	
	Для Каждого КлючИЗначение Из Строки Цикл
		 
		ДанныеСтроки = КлючИЗначение.Значение.Данные;
		ДанныеСтроки.УзелОбменаСтрока = 
			КОДПовтИсп.УзелПредставлениеПоИдентификатору(Строка(ДанныеСтроки.УзелОбработки));
		
	КонецЦикла;	

КонецПроцедуры


&НаКлиенте
Процедура СписокПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Если ЗначениеЗаполнено(Сотрудник) И Не Копирование Тогда 
		Отказ = Истина; 
		ПараметрыОткрытия = Новый Структура;
		ПараметрыОткрытия.Вставить("Сотрудник", Сотрудник);
		ОткрытьФорму("Справочник.УчетныеЗаписиЭлектроннойПочты.ФормаОбъекта", ПараметрыОткрытия);
	КонецЕсли;
	
КонецПроцедуры
