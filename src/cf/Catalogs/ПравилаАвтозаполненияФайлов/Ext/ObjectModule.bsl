#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	//Проверка на заполненность поля ВидДокумента
	Если НЕ ЗначениеЗаполнено(ВладелецФайла) Тогда
		ТекстСообщения = НСтр("ru = 'Поле ""Владелец файла"" не заполнено'");
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, Ссылка, "Объект.ВладелецФайла", ,Отказ);
	КонецЕсли;	
	
	
	// Во избежание случайных замен, когда строка в 1 символ может встретиться где угодно и документ будет бесконечно
	// разрастаться:
	Для Каждого СтрокаТЧ Из ДанныеДляАвтозаполнения Цикл
		Если ЗначениеЗаполнено(СтрокаТЧ.ЗаменяемаяСтрока) И СтрДлина(СокрЛП(СтрокаТЧ.ЗаменяемаяСтрока)) < 3 Тогда
			ТекстОшибки = СтрШаблон(
				НСтр("ru = 'Строка %1 - длина строки для автозамены должна быть не меньше 3 символов!'"),
				СтрокаТЧ.НомерСтроки
			);
			Поле = СтрШаблон("ДанныеДляАвтозаполнения[%1].ЗаменяемаяСтрока", Формат(СтрокаТЧ.НомерСтроки - 1, "ЧН=0; ЧГ=0;"));
			ОбщегоНазначения.СообщитьПользователю(ТекстОшибки, ЭтотОбъект, Поле, , Отказ);
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли