#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает структуру полей ПравилаАвтозаполненияШаблоновФайлов
//
// Возвращаемое значение:
//   Структура
//
Функция ПолучитьСтруктуруПравилаАвтозаполнения() Экспорт
	
	ПараметрыПравила = Новый Структура;
	ПараметрыПравила.Вставить("Наименование");
	ПараметрыПравила.Вставить("ВладелецФайла");
	ПараметрыПравила.Вставить("ШаблонФайла");
	ПараметрыПравила.Вставить("Ответственный");
	ПараметрыПравила.Вставить("Комментарий");
	
	ДанныеДляАвтозаполнения = Новый ТаблицаЗначений;
	ДанныеДляАвтозаполнения.Колонки.Добавить("ТермДляЗамены");
	ДанныеДляАвтозаполнения.Колонки.Добавить("ЗаменяемаяСтрока");
	ДанныеДляАвтозаполнения.Колонки.Добавить("ЗначениеЗамены");
	ДанныеДляАвтозаполнения.Колонки.Добавить("ВыражениеОбработкиРезультатаЗамены");
	ДанныеДляАвтозаполнения.Колонки.Добавить("ФорматЗначенияЗамены");
	ДанныеДляАвтозаполнения.Колонки.Добавить("ТипЗначенияЗамены");
	ДанныеДляАвтозаполнения.Колонки.Добавить("Падеж");
	ПараметрыПравила.Вставить("ДанныеДляАвтозаполнения", ДанныеДляАвтозаполнения);
	
	СкриптыШтамповЭП = Новый ТаблицаЗначений;
	СкриптыШтамповЭП.Колонки.Добавить("ЗаменяемаяСтрока");
	СкриптыШтамповЭП.Колонки.Добавить("ТекстСкрипта");
	ПараметрыПравила.Вставить("СкриптыШтамповЭП", СкриптыШтамповЭП);
	
	Возврат ПараметрыПравила;
	
КонецФункции

// Создает и записывает в БД правило автозаполнения
//
// Параметры:
//  СтруктураПравила - Структура - см ПолучитьСтруктуруПравилаАвтозаполнения.
// 
// Возвращаемое значение:
//  СправочникСсылка.ПравилаАвтозаполненияФайлов - ссылка на записанное правило.
//
Функция СоздатьПравилоАвтозаполнения(СтруктураПравила) Экспорт
	
	НовоеПравило = СоздатьЭлемент();
	ЗаполнитьЗначенияСвойств(НовоеПравило, СтруктураПравила);
	Для Каждого Строка Из СтруктураПравила.ДанныеДляАвтозаполнения Цикл
		НоваяСтрока = НовоеПравило.ДанныеДляАвтозаполнения.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
	КонецЦикла;
	
	НовоеПравило.ДанныеДляАвтозаполнения.Сортировать("НомерКолонкиТабличнойЧасти,
		|ТермДляЗамены, ЗначениеЗамены, ВыражениеОбработкиРезультатаЗамены");
	
	Для Каждого Строка Из СтруктураПравила.СкриптыШтамповЭП Цикл
		НоваяСтрока = НовоеПравило.СкриптыШтамповЭП.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
	КонецЦикла;
	НовоеПравило.Записать();
	
	Возврат НовоеПравило.Ссылка;
	
КонецФункции

#КонецОбласти

#КонецЕсли