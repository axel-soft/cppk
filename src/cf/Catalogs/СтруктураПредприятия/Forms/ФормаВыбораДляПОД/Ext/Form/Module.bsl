
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Подразделение = Параметры.Подразделение;
	ВключатьПодчиненные = Параметры.ВключатьПодчиненные;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПодразделениеАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Если СтрДлина(Текст) < 2 Тогда
		Возврат;
	КонецЕсли;
	
	ДанныеВыбора = ДанныеВыбораЭлементГруппировки(Текст);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Готово(Команда)
	
	СтруктураВозврата = Новый Структура("Подразделение, ВключатьПодчиненные",
		Подразделение, ВключатьПодчиненные);
	
	Закрыть(СтруктураВозврата);
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	Закрыть();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Функция ДанныеВыбораЭлементГруппировки(Текст)
	
	ДанныеВыбора = Новый СписокЗначений();
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	СправочникВыбора.Ссылка
		|ИЗ
		|	Справочник.СтруктураПредприятия КАК СправочникВыбора
		|ГДЕ
		|	НЕ СправочникВыбора.ПометкаУдаления
		|	И СправочникВыбора.Наименование ПОДОБНО &Текст");
		
	Запрос.Параметры.Вставить("Текст", Текст + "%");
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ДанныеВыбора.Добавить(Выборка.Ссылка,
			СокрЛП(Выборка.Ссылка) + " (" + СокрЛП(ТипЗнч(Выборка.Ссылка)) + ")");
	КонецЦикла;
	
	Возврат ДанныеВыбора;
	
КонецФункции

#КонецОбласти
