
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	СсылкаНаНастройку = Параметры.СсылкаНаНастройку;
	
	РеквизитыНастройки = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(СсылкаНаНастройку, 
		"ВидБизнесСобытия, ВидОбъекта, ВидИнтерактивногоСобытия");
	
	Заголовок = СтрШаблон(НСтр("ru = 'Режим автозапуска (%1)'"), СсылкаНаНастройку);
	
	ТипОбъекта = ОбщегоНазначенияДокументооборотКлиентСервер.ТипОбъектаПоТипуВидаОбъекта(
		ТипЗнч(РеквизитыНастройки.ВидОбъекта));
	
	ЗаполнитьВидИнтерактивногоСобытияСписокВыбора();
	ЗаполнитьВидБизнесСобытияСписокВыбора();
	
	ВидБизнесСобытия = РеквизитыНастройки.ВидБизнесСобытия;
	ВидИнтерактивногоСобытия = РеквизитыНастройки.ВидИнтерактивногоСобытия;
	ВидБизнесСобытияПриОткрытии = ВидБизнесСобытия;	
		
	Если ЗначениеЗаполнено(РеквизитыНастройки.ВидБизнесСобытия) 
		И Не ЗначениеЗаполнено(РеквизитыНастройки.ВидИнтерактивногоСобытия) Тогда	
			
		РежимЗапуска = "АвтоматическийЗапуск";
		Элементы.ГруппаВидыИнтерСобытий.Видимость = Ложь;
		Элементы.ГруппаВидыАвтоЗапуска.Видимость = Истина;

	ИначеЕсли (Не ЗначениеЗаполнено(РеквизитыНастройки.ВидБизнесСобытия) 
		И ЗначениеЗаполнено(РеквизитыНастройки.ВидИнтерактивногоСобытия))
		Или (Не ЗначениеЗаполнено(РеквизитыНастройки.ВидБизнесСобытия) 
		И Не ЗначениеЗаполнено(РеквизитыНастройки.ВидИнтерактивногоСобытия)) Тогда
			
		// для пустой настройки - тоже тут покажем. 	
			
		РежимЗапуска = "РучнойЗапуск";
		Элементы.ГруппаВидыИнтерСобытий.Видимость = Истина;
		Элементы.ГруппаВидыАвтоЗапуска.Видимость = Ложь;
		
		Если Не ЗначениеЗаполнено(ВидИнтерактивногоСобытия) Тогда
			ВидИнтерактивногоСобытия = ПредопределенноеЗначение(
				"Перечисление.ВидыИнтерактивныхДействий.ЗакрытиеКарточкиТолькоЧтоСозданногоДокумента");
		КонецЕсли;		
		
	КонецЕсли;		 	
	
	Если Параметры.Свойство("ТолькоПросмотр") Тогда
		ТолькоПросмотр = Параметры.ТолькоПросмотр;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Удалить(Команда)
	
	ВидИнтерактивногоСобытия = Неопределено;
	ВидБизнесСобытия = Неопределено;
	
	ОКНаСервере();
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура РежимЗапускаПриИзменении(Элемент)
	
	Если РежимЗапуска = "АвтоматическийЗапуск" Тогда
		
		Элементы.ГруппаВидыИнтерСобытий.Видимость = Ложь;
		Элементы.ГруппаВидыАвтоЗапуска.Видимость = Истина;
		
		Если Не ЗначениеЗаполнено(ВидБизнесСобытия) Тогда
			
			Если ТипОбъекта = ПредопределенноеЗначение("Перечисление.ТипыОбъектов.ДокументыПредприятия") Тогда
				
				ВидБизнесСобытия = ПредопределенноеЗначение(
					"Справочник.ВидыБизнесСобытий.РегистрацияДокумента");
					
			ИначеЕсли ТипОбъекта = ПредопределенноеЗначение("Перечисление.ТипыОбъектов.Мероприятия") Тогда
				
				ВидБизнесСобытия = ПредопределенноеЗначение(
					"Справочник.ВидыБизнесСобытий.СозданиеМероприятия");
				
			Иначе
				
				ВызватьИсключение СтрШаблон(
					НСтр("ru = 'Неизвестный тип объекта %1'"),
					ТипОбъекта);
				
			КонецЕсли;
			
		КонецЕсли;
			
		ВидИнтерактивногоСобытия = Неопределено;	

	ИначеЕсли РежимЗапуска = "РучнойЗапуск" Тогда
		
		Элементы.ГруппаВидыИнтерСобытий.Видимость = Истина;
		Элементы.ГруппаВидыАвтоЗапуска.Видимость = Ложь;
		
		ВидБизнесСобытия = Неопределено;
		Если Не ЗначениеЗаполнено(ВидИнтерактивногоСобытия) Тогда
			ВидИнтерактивногоСобытия = ПредопределенноеЗначение(
				"Перечисление.ВидыИнтерактивныхДействий.ЗакрытиеКарточкиТолькоЧтоСозданногоДокумента");
		КонецЕсли;		
		
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура РежимЗапускаОчистка(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
КонецПроцедуры

&НаКлиенте
Процедура ОК(Команда)
	
	ОКНаСервере();
	Закрыть();
	
КонецПроцедуры

&НаСервере
Процедура ОКНаСервере()
	
	НачатьТранзакцию();
	Попытка
	
		НастройкаОбъект = СсылкаНаНастройку.ПолучитьОбъект();
		
		НастройкаОбъект.ВидБизнесСобытия = ВидБизнесСобытия;
		НастройкаОбъект.ВидИнтерактивногоСобытия = ВидИнтерактивногоСобытия;
		
		НастройкаОбъект.Записать();	
		
		// тут обновим РС ПодпискиНаБизнесСобытия
		Если ЗначениеЗаполнено(ВидБизнесСобытия) Или ЗначениеЗаполнено(ВидБизнесСобытияПриОткрытии) Тогда
			
			ВидБизнесСобытияДляПроверки = ВидБизнесСобытия;
			Если Не ЗначениеЗаполнено(ВидБизнесСобытияДляПроверки) Тогда
				ВидБизнесСобытияДляПроверки = ВидБизнесСобытияПриОткрытии;
			КонецЕсли;	
			
			ДействияСервер.ОбновитьПодпискуНаБизнесСобытия(ВидБизнесСобытияДляПроверки);
			 
		КонецЕсли;	
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		
		ОтменитьТранзакцию();
		ВызватьИсключение;
			
	КонецПопытки;		
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьВидИнтерактивногоСобытияСписокВыбора()
	
	Если ТипОбъекта = Перечисления.ТипыОбъектов.ДокументыПредприятия Тогда
		
		Элементы.ВидИнтерактивногоСобытия.СписокВыбора.Добавить(
			Перечисления.ВидыИнтерактивныхДействий.ЗакрытиеКарточкиТолькоЧтоСозданногоДокумента,
			НСтр("ru = 'Закрытие карточки только что созданного документа'"));
		Элементы.ВидИнтерактивногоСобытия.СписокВыбора.Добавить(
			Перечисления.ВидыИнтерактивныхДействий.ИнтерактивнаяРегистрацияДокумента,
			НСтр("ru = 'Ручная регистрация документа'"));
		
	ИначеЕсли ТипОбъекта = Перечисления.ТипыОбъектов.Мероприятия Тогда
		
		Элементы.ВидИнтерактивногоСобытия.СписокВыбора.Добавить(
			Перечисления.ВидыИнтерактивныхДействий.ЗакрытиеКарточкиТолькоЧтоСозданногоДокумента,
			НСтр("ru = 'Закрытие карточки только что созданного мероприятия'"));
		
	Иначе
		
		ВызватьИсключение СтрШаблон(
			НСтр("ru = 'Неизвестный тип объекта %1'"),
			ТипОбъекта);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьВидБизнесСобытияСписокВыбора()
	
	Если ТипОбъекта = Перечисления.ТипыОбъектов.ДокументыПредприятия Тогда
		
		Элементы.ВидБизнесСобытия.СписокВыбора.Добавить(
			Справочники.ВидыБизнесСобытий.СозданиеДокумента,
			НСтр("ru = 'Создание нового документа'"));
		Элементы.ВидБизнесСобытия.СписокВыбора.Добавить(
			Справочники.ВидыБизнесСобытий.ИзменениеДокумента,
			НСтр("ru = 'Изменение документа'"));
		Элементы.ВидБизнесСобытия.СписокВыбора.Добавить(
			Справочники.ВидыБизнесСобытий.РегистрацияДокумента,
			НСтр("ru = 'Регистрация документа'"));
		Элементы.ВидБизнесСобытия.СписокВыбора.Добавить(
			Справочники.ВидыБизнесСобытий.ПеререгистрацияДокумента,
			НСтр("ru = 'Перерегистрация документа'"));
		
	ИначеЕсли ТипОбъекта = Перечисления.ТипыОбъектов.Мероприятия Тогда
		
		Элементы.ВидБизнесСобытия.СписокВыбора.Добавить(
			Справочники.ВидыБизнесСобытий.СозданиеМероприятия,
			НСтр("ru = 'Создание нового мероприятия'"));
		Элементы.ВидБизнесСобытия.СписокВыбора.Добавить(
			Справочники.ВидыБизнесСобытий.ИзменениеМероприятия,
			НСтр("ru = 'Изменение мероприятия'"));
			
	Иначе
		
		ВызватьИсключение СтрШаблон(
			НСтр("ru = 'Неизвестный тип объекта %1'"),
			ТипОбъекта);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти