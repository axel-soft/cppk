#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	СообщениеВопрос = Параметры.СообщениеВопрос;
	СообщениеЗаголовок = Параметры.СообщениеЗаголовок;
	Заголовок = Параметры.Заголовок;
	
	Если Параметры.Свойство("ТекстКнопкиЗакончитьИЗакрыть") Тогда
		Команды.Да.Заголовок = Параметры.ТекстКнопкиЗакончитьИЗакрыть;
	КонецЕсли;	
	Если Параметры.Свойство("ТекстКнопкиЗакрыть") Тогда
		Команды.КомандаЗакрыть.Заголовок = Параметры.ТекстКнопкиЗакрыть;
	КонецЕсли;	
	
	Если ЗначениеЗаполнено(Параметры.ВладелецФайла) Тогда
		
		Если ТипЗнч(Параметры.ВладелецФайла) = Тип("СписокЗначений") Тогда
			ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(
				СписокФайлов.Отбор, "ВладелецФайла", Параметры.ВладелецФайла, ВидСравненияКомпоновкиДанных.ВСписке);
		Иначе
			ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(
				СписокФайлов.Отбор, "ВладелецФайла", Параметры.ВладелецФайла);
		КонецЕсли;
			
		ВладелецФайла = Параметры.ВладелецФайла;
			
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Параметры.ФайлыДляПроверки) Тогда
		
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(
			СписокФайлов.Отбор,
			"Файл",
			Параметры.ФайлыДляПроверки,
			ВидСравненияКомпоновкиДанных.ВСписке);
			
		ФайлыДляПроверки = Параметры.ФайлыДляПроверки;
			
	КонецЕсли;
		
	Если ЗначениеЗаполнено(Параметры.Редактирует) Тогда 
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(
			СписокФайлов.Отбор, "Редактирует", Параметры.Редактирует);
		Редактирует = Параметры.Редактирует;	
	КонецЕсли;
	
	Идентификатор = ФайловыеФункции.ПолучитьСоставнойИдентификаторПользователя();
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(
		СписокФайлов, "Идентификатор", Идентификатор);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ЗаконченоРедактирование" Тогда
		Элементы.Список.Обновить(); 
	КонецЕсли;
	
	Если ИмяСобытия = "Запись_Файл" Тогда
		Элементы.Список.Обновить();
	КонецЕсли;
	
	Если ИмяСобытия = "ПомощникОтправить_ПроверкаВозможностиЗакрытияФормы"
		И ТипЗнч(Параметр) = Тип("Структура") Тогда
		
		Параметр.МожноЗакрытьФормуПомощникаОтправки = Ложь;
		НадоЗакрытьФормуПомощника = Истина;
			
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	Если НадоЗакрытьФормуПомощника Тогда
		Оповестить("ЗакрытьФормуПомощникаОтправкиБезусловно");
	КонецЕсли;	
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если Элементы.Список.ТекущиеДанные = Неопределено Тогда  
		Возврат;
	КонецЕсли;
	
	ДанныеФайла = РаботаСФайламиВызовСервера.ДанныеФайлаДляОткрытия(Элементы.Список.ТекущиеДанные.Файл, Неопределено, УникальныйИдентификатор);	
	КомандыРаботыСФайламиКлиент.Открыть(ДанныеФайла);	
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	Отказ = Истина;
КонецПроцедуры

&НаКлиенте
Процедура СписокПередУдалением(Элемент, Отказ)
	Отказ = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ЗакончитьРедактирование(Команда)
	
	Если Элементы.Список.ТекущиеДанные = Неопределено Тогда  
		Возврат;
	КонецЕсли;
	
	ПараметрыОбновленияФайла = РаботаСФайламиКлиент.ПараметрыОбновленияФайла(Неопределено, 
		Элементы.Список.ТекущиеДанные.Файл, УникальныйИдентификатор);
	ПараметрыОбновленияФайла.ХранитьВерсии = Элементы.Список.ТекущиеДанные.ХранитьВерсии;
	ПараметрыОбновленияФайла.РедактируетТекущийПользователь = Истина;
	ПараметрыОбновленияФайла.Редактирует = Элементы.Список.ТекущиеДанные.Редактирует;
	РаботаСФайламиКлиент.ЗакончитьРедактированиеСОповещением(ПараметрыОбновленияФайла);
		
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФайл(Команда)
	
	Если Элементы.Список.ТекущиеДанные = Неопределено Тогда  
		Возврат;
	КонецЕсли;
	
	ДанныеФайла = РаботаСФайламиВызовСервера.ДанныеФайлаДляОткрытия(Элементы.Список.ТекущиеДанные.Файл, Неопределено, УникальныйИдентификатор);
	КомандыРаботыСФайламиКлиент.Открыть(ДанныеФайла);
	
КонецПроцедуры

&НаКлиенте
Процедура Освободить(Команда)
	
	Если Элементы.Список.ТекущиеДанные = Неопределено Тогда  
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	ПараметрыОсвобожденияФайла = РаботаСФайламиКлиент.ПараметрыОсвобожденияФайла(Неопределено, 
		Элементы.Список.ТекущиеДанные.Файл);
	ПараметрыОсвобожденияФайла.ХранитьВерсии = ТекущиеДанные.ХранитьВерсии;	
	ПараметрыОсвобожденияФайла.РедактируетТекущийПользователь = Истина;	
	ПараметрыОсвобожденияФайла.Редактирует = ТекущиеДанные.Редактирует;	
	РаботаСФайламиКлиент.ОсвободитьФайлСОповещением(ПараметрыОсвобожденияФайла);
		
КонецПроцедуры

&НаКлиенте
Процедура СохранитьИзменения(Команда)
	
	Если Элементы.Список.ТекущиеДанные = Неопределено Тогда  
		Возврат;
	КонецЕсли;
	
	РаботаСФайламиКлиент.СохранитьИзмененияФайлаСОповещением(
		Неопределено,
		Элементы.Список.ТекущиеДанные.Файл,
		УникальныйИдентификатор);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьКаталогФайла(Команда)
	
	Если Элементы.Список.ТекущиеДанные = Неопределено Тогда  
		Возврат;
	КонецЕсли;
	
	ДанныеФайла = РаботаСФайламиВызовСервера.ДанныеФайлаДляОткрытия(
		Элементы.Список.ТекущиеДанные.Файл, Неопределено, УникальныйИдентификатор);
	
	КомандыРаботыСФайламиКлиент.ОткрытьКаталогФайла(ДанныеФайла);
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьКак(Команда)
	
	Если Элементы.Список.ТекущиеДанные = Неопределено Тогда  
		Возврат;
	КонецЕсли;
	
	ДанныеФайла = РаботаСФайламиВызовСервера.ДанныеФайлаДляСохранения(
		Элементы.Список.ТекущиеДанные.Файл, Неопределено, УникальныйИдентификатор);
	
	КомандыРаботыСФайламиКлиент.СохранитьКак(ДанныеФайла, УникальныйИдентификатор);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьИзФайлаНаДиске(Команда)
	
	Если Элементы.Список.ТекущиеДанные = Неопределено Тогда  
		Возврат;
	КонецЕсли;
	
	ДанныеФайла = РаботаСФайламиВызовСервера.ДанныеФайлаИРабочийКаталог(Элементы.Список.ТекущиеДанные.Файл);
	
	РаботаСФайламиКлиент.ОбновитьИзФайлаНаДискеСОповещением(
		Неопределено,
		ДанныеФайла,
		УникальныйИдентификатор);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьКарточку(Команда)
	
	Если Элементы.Список.ТекущиеДанные = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	ПоказатьЗначение(, Элементы.Список.ТекущиеДанные.Файл);
	
КонецПроцедуры

&НаКлиенте
Процедура Да(Команда)
	
	ЗанятыеФайлы = ПолучитьСписокЗанятыхФайлов(ВладелецФайла, Редактирует, СотрудникиТекущегоПользователя, ФайлыДляПроверки);
	
	Если ЗанятыеФайлы.Количество() <> 0 Тогда
		
		Обработчик = Новый ОписаниеОповещения("ЗакрытьФормуПослеОсвобождения", ЭтотОбъект);
		
		РаботаСФайламиКлиент.ЗакончитьРедактированиеПоСсылкам(
			Обработчик,
			ЗанятыеФайлы,
			УникальныйИдентификатор,
			Истина, // СоздатьНовуюВерсию
			"", // КомментарийКВерсии
			Истина, // ПоказыватьОповещение
			Истина); //ОсвобождатьБезВопросаФайлыКоторыхНетВРабочемКаталоге
			
	Иначе
			
		Закрыть(КодВозвратаДиалога.Да);
			
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура Нет(Команда)
	
	Закрыть(КодВозвратаДиалога.Нет);
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаЗакрыть(Команда)
	
	Закрыть(КодВозвратаДиалога.Да);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Функция ПолучитьСписокЗанятыхФайлов(ВладелецФайла, Редактирует, СотрудникиТекущегоПользователя, ФайлыДляПроверки)
	
	Запрос = Новый Запрос;
	
	Если Не ЗначениеЗаполнено(Редактирует) Тогда
		Редактирует = Пользователи.ТекущийПользователь();
	КонецЕсли;	
	
	Если ФайлыДляПроверки.Количество() > 0 Тогда
		
		Запрос.Текст = 
			"ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	Файлы.Файл КАК Ссылка
			|ИЗ
			|	РегистрСведений.ЗанятыеФайлы КАК Файлы
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ФайлыВРабочемКаталогеКомпьютера КАК ФайлыВРабочемКаталогеКомпьютера
			|		ПО Файлы.ТекущаяВерсия = ФайлыВРабочемКаталогеКомпьютера.Версия
			|			И (ФайлыВРабочемКаталогеКомпьютера.Идентификатор = &Идентификатор)
			|			И (ФайлыВРабочемКаталогеКомпьютера.НаЧтение = ЛОЖЬ)
			|ГДЕ
			|	Файлы.Редактирует = &Редактирует
			|	И Файлы.Файл В(&ФайлыДляПроверки)";
		
		Запрос.УстановитьПараметр("ФайлыДляПроверки", ФайлыДляПроверки);
		
	ИначеЕсли ТипЗнч(ВладелецФайла) = Тип("СписокЗначений") Тогда
		
		Запрос.Текст = 
			"ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	Файлы.Файл КАК Ссылка
			|ИЗ
			|	РегистрСведений.ЗанятыеФайлы КАК Файлы
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ФайлыВРабочемКаталогеКомпьютера КАК ФайлыВРабочемКаталогеКомпьютера
			|		ПО Файлы.ТекущаяВерсия = ФайлыВРабочемКаталогеКомпьютера.Версия
			|			И (ФайлыВРабочемКаталогеКомпьютера.Идентификатор = &Идентификатор)
			|			И (ФайлыВРабочемКаталогеКомпьютера.НаЧтение = ЛОЖЬ)
			|ГДЕ
			|	Файлы.Редактирует = &Редактирует
			|	И Файлы.ВладелецФайла В(&ВладелецФайла)";
		
		Запрос.УстановитьПараметр("ВладелецФайла", ВладелецФайла);
		
	Иначе
		
		Запрос.Текст = 
			"ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	Файлы.Файл КАК Ссылка
			|ИЗ
			|	РегистрСведений.ЗанятыеФайлы КАК Файлы
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ФайлыВРабочемКаталогеКомпьютера КАК ФайлыВРабочемКаталогеКомпьютера
			|		ПО Файлы.ТекущаяВерсия = ФайлыВРабочемКаталогеКомпьютера.Версия
			|			И (ФайлыВРабочемКаталогеКомпьютера.Идентификатор = &Идентификатор)
			|			И (ФайлыВРабочемКаталогеКомпьютера.НаЧтение = ЛОЖЬ)
			|ГДЕ
			|	Файлы.Редактирует = &Редактирует
			|	И Файлы.ВладелецФайла = &ВладелецФайла";
		
		Запрос.УстановитьПараметр("ВладелецФайла", ВладелецФайла);
		
	КонецЕсли;	
		
	Запрос.УстановитьПараметр("Редактирует", Редактирует);
	
	Если ЗначениеЗаполнено(Редактирует) Тогда 
		
		Запрос.УстановитьПараметр("Редактирует", Редактирует);
		
	КонецЕсли;
	
	Идентификатор = ФайловыеФункции.ПолучитьСоставнойИдентификаторПользователя();
	Запрос.УстановитьПараметр("Идентификатор", Идентификатор);
	
	МассивФайлов = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
	
	Возврат МассивФайлов;
	
КонецФункции

&НаКлиенте
Процедура ЗакрытьФормуПослеОсвобождения(Результат = Неопределено, ПараметрыВыполнения = Неопределено) Экспорт
	
	КоличествоЗанятыхФайлов = РаботаСФайламиВызовСервера.ПолучитьКоличествоЗанятыхФайлов(,,Истина);
	
	СтандартныеПодсистемыКлиент.УстановитьПараметрКлиента(
		"КоличествоЗанятыхФайлов", КоличествоЗанятыхФайлов);
	
	Закрыть(КодВозвратаДиалога.Да);
	
КонецПроцедуры

#КонецОбласти