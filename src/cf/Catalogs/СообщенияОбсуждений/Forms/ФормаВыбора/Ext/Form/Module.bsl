#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("Тема") Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(
			Список.Отбор, "ВладелецСообщения", Параметры.Тема);
		Элементы.Тема.Видимость = Ложь;
	КонецЕсли;
	
	// Отображение удаленных
	ПереключитьОтображатьУдаленные();
	
	ЭтоМобильныйКлиент = ПараметрыСеанса.ЭтоМобильныйКлиент;
	Если ЭтоМобильныйКлиент Тогда
		МК_НастроитьЭлементыФормы();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Настройки)
	
	// Отображение удаленных
	Если ОтображатьУдаленные Тогда
		ПереключитьОтображатьУдаленные();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОтображатьУдаленные(Команда)
	
	ОтображатьУдаленные = Не ОтображатьУдаленные;
	ПереключитьОтображатьУдаленные();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПереключитьОтображатьУдаленные()
	
	Элементы.ФормаОтображатьУдаленные.Пометка = ОтображатьУдаленные;
	Список.Параметры.УстановитьЗначениеПараметра("ОтображатьУдаленные", ОтображатьУдаленные);
	
КонецПроцедуры

#Область СлужебныеПроцедурыИФункции_МобильныйКлиент

&НаСервере
Процедура МК_НастроитьЭлементыФормы()
	
	// Общее
	СворачиваниеЭлементовПоВажности = СворачиваниеЭлементовФормыПоВажности.НеИспользовать;
	ВертикальныйИнтервал = ИнтервалМеждуЭлементамиФормы.Половинный;
	
	Элементы.ФормаОбновить.Видимость = Ложь;
	Элементы.ФормаСтрокаПоиска.Видимость = Ложь;
	Элементы.ФормаУправлениеПоиском.Видимость = Ложь;
	
	// Список
	Элементы.ГруппаСообщениеЗаголовокИТело.Видимость = Ложь;
	Элементы.МК_СписокГруппаКолонок.Видимость = Истина;
	
	Элементы.Переместить(Элементы.ТекстСообщенияСписок, Элементы.МК_СписокПерваяКолонка);
	
	Элементы.ТекстСообщенияСписок.Высота = 3;
	Элементы.ТекстСообщенияСписок.Шрифт = ШрифтыСтиля.МелкийШрифтТекста;
	Элементы.ТекстСообщенияСписок.РастягиватьПоВертикали = Ложь;
	Элементы.ТекстСообщенияСписок.ЦветТекста = WebЦвета.Черный;

КонецПроцедуры
	
#КонецОбласти

#КонецОбласти