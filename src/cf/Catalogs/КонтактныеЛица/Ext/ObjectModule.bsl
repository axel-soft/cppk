#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ПредыдущиеРеквизиты = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
		Ссылка, "ПометкаУдаления, Наименование, НеДействует");
	ДополнительныеСвойства.Вставить("ПредыдущаяПометкаУдаления", ПредыдущиеРеквизиты.ПометкаУдаления);
	ДополнительныеСвойства.Вставить("ПредыдущееНаименование", ПредыдущиеРеквизиты.Наименование);
	ДополнительныеСвойства.Вставить("ПредыдущееНеДействует", ПредыдущиеРеквизиты.НеДействует);
	
	// Обновление адресной книги.
	ДополнительныеСвойства.Вставить(
		"ПараметрыОбновленияАдреснойКниги",
		Справочники.КонтактныеЛица.ЗначенияПараметровОбновленияАдреснойКнигиПоОбъекту(ЭтотОбъект));
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ПредыдущаяПометкаУдаления =
		ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДополнительныеСвойства, "ПредыдущаяПометкаУдаления", Ложь);
	
	Если ПометкаУдаления <> ПредыдущаяПометкаУдаления Тогда
		ПротоколированиеРаботыСотрудников.ЗаписатьПометкуУдаления(Ссылка, ПометкаУдаления);
	КонецЕсли;
	
	ОсновноеКонтактноеЛицо = РегистрыСведений.ОсновныеКонтактныеЛицаКонтрагентов.ОсновноеКонтактноеЛицоКонтрагента(
		Владелец);
	
	Если (ПометкаУдаления = Истина Или НеДействует = Истина) И Ссылка = ОсновноеКонтактноеЛицо Тогда
		РегистрыСведений.ОсновныеКонтактныеЛицаКонтрагентов.СнятьОтметкуОсновногоКонтактногоЛицаКонтрагента(
			Владелец);
	КонецЕсли;
	
	// При переименовании может измениться кэш "ДанныеДокументовПредприятия":
	ПредыдущееНаименование = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(
		ДополнительныеСвойства, "ПредыдущееНаименование");
	ПредыдущееНеДействует = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(
		ДополнительныеСвойства, "ПредыдущееНеДействует");
	Если (ПредыдущееНаименование <> Неопределено И ПредыдущееНаименование <> Наименование)
		Или (ПредыдущееНеДействует <> Неопределено И ПредыдущееНеДействует <> НеДействует) Тогда
		Делопроизводство.ЗаписатьДанныеДокументовПредприятия_ПереименованиеАдресата(Ссылка);
	КонецЕсли;
	
	// Обновление адресной книги.
	Справочники.КонтактныеЛица.ОбновитьАдреснуюКнигу(
		ЭтотОбъект, ДополнительныеСвойства.ПараметрыОбновленияАдреснойКниги);	
	
КонецПроцедуры

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли