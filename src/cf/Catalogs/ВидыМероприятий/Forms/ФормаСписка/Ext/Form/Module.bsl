#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Отображение удаленных
	ПереключитьОтображатьУдаленные();
	
	МассивКолонок = Новый Массив;
	МассивКолонок.Добавить("ОбработкаЗадана");
	Список.УстановитьОграниченияИспользованияВПорядке(МассивКолонок);
	
	МультиязычностьСервер.ПриСозданииНаСервере(ЭтотОбъект);
		
КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Настройки)
	
	// Отображение удаленных
	ПереключитьОтображатьУдаленные();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ТекущиеДанные = Элементы.Список.ДанныеСтроки(ВыбраннаяСтрока);
	Если ТекущиеДанные = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	СтандартнаяОбработка = Ложь;
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("Ключ", ТекущиеДанные.Ссылка);
	
	Если Поле = Элементы.ОбработкаЗадана Тогда
		ПараметрыОткрытия.Вставить("АктивироватьОбработку", Истина);
	КонецЕсли;
	
	ОткрытьФорму("Справочник.ВидыМероприятий.ФормаОбъекта",
		ПараметрыОткрытия, Элементы.Список);
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура СписокПриПолученииДанныхНаСервере(ИмяЭлемента, Настройки, Строки)
	
	МассивВидов = Новый Массив;
	Для Каждого КлючИЗначение Из Строки Цикл 
		ДанныеСтроки = КлючИЗначение.Значение.Данные;
		МассивВидов.Добавить(ДанныеСтроки.Ссылка);
	КонецЦикла;
	
	Если МассивВидов.Количество() = 0 Тогда 
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос( 
		"ВЫБРАТЬ
		|	НастройкиОбработкиВидовОбъектов.ВидОбъекта КАК ВидОбъекта,
		|	КОЛИЧЕСТВО(ВидыДействийНастройки.ВидДействия) КАК КоличествоДействий
		|ИЗ
		|	Справочник.НастройкиОбработкиВидовОбъектов КАК НастройкиОбработкиВидовОбъектов
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.НастройкиОбработкиВидовОбъектов.ВидыДействий КАК ВидыДействийНастройки
		|		ПО НастройкиОбработкиВидовОбъектов.Ссылка = ВидыДействийНастройки.Ссылка
		|ГДЕ
		|	НастройкиОбработкиВидовОбъектов.ВидОбъекта В(&МассивВидов)
		|	И НастройкиОбработкиВидовОбъектов.ПометкаУдаления = ЛОЖЬ
		|	И НастройкиОбработкиВидовОбъектов.ДействуетС <= &ДатаОбработки
		|	И (НастройкиОбработкиВидовОбъектов.ДействуетПо = ДАТАВРЕМЯ(1, 1, 1)
		|			ИЛИ КОНЕЦПЕРИОДА(НастройкиОбработкиВидовОбъектов.ДействуетПо, ДЕНЬ) >= &ДатаОбработкиПо)
		|
		|СГРУППИРОВАТЬ ПО
		|	НастройкиОбработкиВидовОбъектов.ВидОбъекта");
	
	Запрос.Параметры.Вставить("МассивВидов", МассивВидов);
	Запрос.Параметры.Вставить("ДатаОбработки", ТекущаяДатаСеанса());
	Запрос.Параметры.Вставить("ДатаОбработкиПо", КонецДня(ТекущаяДатаСеанса()));
	КоличествоДействийВидов = Запрос.Выполнить().Выгрузить();
	
	Для Каждого КлючИЗначение Из Строки Цикл 
		
		ДанныеСтроки = КлючИЗначение.Значение.Данные;
		
		ПараметрыОтбора = Новый Структура("ВидОбъекта", ДанныеСтроки.Ссылка);
		НайденныеСтроки = КоличествоДействийВидов.НайтиСтроки(ПараметрыОтбора);
		
		Если НайденныеСтроки.Количество() > 0 И НайденныеСтроки[0].КоличествоДействий > 0 Тогда 
			ДанныеСтроки.ОбработкаЗадана = Истина;
		Иначе
			ДанныеСтроки.ОбработкаЗадана = Ложь;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОтображатьУдаленные(Команда)
	
	ОтображатьУдаленные = Не ОтображатьУдаленные;
	ПереключитьОтображатьУдаленные();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПереключитьОтображатьУдаленные()
	
	Элементы.ФормаОтображатьУдаленные.Пометка = ОтображатьУдаленные;
	Список.Параметры.УстановитьЗначениеПараметра("ОтображатьУдаленные", ОтображатьУдаленные);
	
КонецПроцедуры

#КонецОбласти

