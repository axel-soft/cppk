#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Ссылка) Тогда
		ПредыдущийТип = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Ссылка, "Тип");
		ДополнительныеСвойства.Вставить("ПредыдущийТип", ПредыдущийТип);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	// При смене типа, нужно очистить функцию
	Если ДополнительныеСвойства.Свойство("ПредыдущийТип")
		И ДополнительныеСвойства.ПредыдущийТип <> Тип Тогда
		ОчиститьНаименованияИОписанияЗадач();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ОчиститьНаименованияИОписанияЗадач() Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	НаименованияЗадачУчастниковДействий.ВидДействия,
	|	НаименованияЗадачУчастниковДействий.Функция,
	|	НаименованияЗадачУчастниковДействий.Этап
	|ИЗ
	|	РегистрСведений.НаименованияЗадачУчастниковДействий КАК НаименованияЗадачУчастниковДействий
	|ГДЕ
	|	НаименованияЗадачУчастниковДействий.ВидДействия = &ВидДействия");
	
	Запрос.УстановитьПараметр("ВидДействия", Ссылка);
	Выборка =  Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		НаборЗаписей = РегистрыСведений.НаименованияЗадачУчастниковДействий.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.ВидДействия.Установить(Выборка.ВидДействия);
		НаборЗаписей.Отбор.Функция.Установить(Выборка.Функция);
		НаборЗаписей.Отбор.Этап.Установить(Выборка.Этап);
		НаборЗаписей.Записать();
		
	КонецЦикла;
		
КонецПроцедуры

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли