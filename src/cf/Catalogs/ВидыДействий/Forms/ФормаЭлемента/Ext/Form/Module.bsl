#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Объект.Ссылка.Пустая() Тогда
		ОбновитьДоступностьВидимостьЭлементов();
	КонецЕсли;
	НаименованияЗадач.Параметры.УстановитьЗначениеПараметра("ВидДействия", Объект.Ссылка);
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	ОбновитьДоступностьВидимостьЭлементов();
	
КонецПроцедуры


&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	НаименованияЗадач.Параметры.УстановитьЗначениеПараметра("ВидДействия", Объект.Ссылка);
	Элементы.НаименованияЗадач.Обновить();
	ОбновитьДоступностьВидимостьЭлементов();
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	Оповестить("ИзмененВидДействия", Объект.Ссылка, ЭтаФорма);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ТипПриИзменении(Элемент)
	
	ОбновитьДоступностьВидимостьЭлементов();
	
	ИзменениеПредметаРазрешеноПоУмолчанию =
		Объект.Тип = ПредопределенноеЗначение("Перечисление.ТипыДействий.Исполнение")
		Или Объект.Тип = ПредопределенноеЗначение("Перечисление.ТипыДействий.Подписание")
		Или Объект.Тип = ПредопределенноеЗначение("Перечисление.ТипыДействий.Регистрация")
		Или Объект.Тип = ПредопределенноеЗначение("Перечисление.ТипыДействий.Согласование")
		Или Объект.Тип = ПредопределенноеЗначение("Перечисление.ТипыДействий.Утверждение");
	
	Объект.РазрешитьУчастникамИзменятьПредмет = ИзменениеПредметаРазрешеноПоУмолчанию;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовСпискаНаименованияЗадач

&НаКлиенте
Процедура НаименованияЗадачПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, ЭтоГруппа, Параметр)
	
	Отказ = Истина;
	Если Объект.Ссылка.Пустая() Тогда

		Если Не Записать() Тогда 
			Возврат;
		КонецЕсли;
		
		ПоказатьОповещениеПользователя(
			НСтр("ru = 'Создание:'"), 
			ПолучитьНавигационнуюСсылку(Объект.Ссылка),
			Строка(Объект.Ссылка),
			БиблиотекаКартинок.Информация32);
	
	ИначеЕсли Модифицированность Тогда

		Если Не Записать() Тогда 
			Возврат;
		КонецЕсли;
		
	КонецЕсли;
	
	Если Копирование Тогда
		ТекущиеДанные = Элемент.ТекущиеДанные;
		Если ТекущиеДанные = Неопределено Тогда 
			Возврат;
		КонецЕсли;
		
		ТекущаяСтрока = Элемент.ТекущаяСтрока;
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("ЗначениеКопирования", ТекущаяСтрока);
	Иначе
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("ВидДействия", Объект.Ссылка);
	КонецЕсли;
	
	Открытьформу("РегистрСведений.НаименованияЗадачУчастниковДействий.Форма.ФормаЗаписи",
		ПараметрыФормы, Элементы.НаименованияЗадач);

КонецПроцедуры

&НаКлиенте
Процедура НаименованияЗадачОбработкаЗаписиНового(Элемент, НовыйОбъект, Источник, СтандартнаяОбработка)
	
	ОбновитьДоступностьВидимостьЭлементов();

КонецПроцедуры

&НаКлиенте
Процедура НаименованияЗадачПослеУдаления(Элемент)
	
	ОбновитьДоступностьВидимостьЭлементов();

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ОбновитьДоступностьВидимостьЭлементов()
	
	ПараметрыВыбораЭтапа = Новый Структура("ТипДействия", Объект.Тип);
	ДанныеВыбора = Перечисления.ЭтапыОбработкиПредметов.ПолучитьДанныеВыбора(ПараметрыВыбораЭтапа);
	
	Элементы.ЭтапОбработкиПредмета.Доступность = ДанныеВыбора.Количество() > 0;
	Элементы.НаименованияЗадачЭтап.Видимость = ДействияКлиентСервер.ЭтоДействиеСЭтапами(Объект.Тип);
	
	
	КоличествоСтрок = 0;
	Если Не Объект.Ссылка.Пустая() Тогда
		Запрос = Новый Запрос(
			"ВЫБРАТЬ
			|	КОЛИЧЕСТВО (НаименованияЗадачУчастниковДействий.ВидДействия) КАК КоличествоСтрок
			|ИЗ
			|	РегистрСведений.НаименованияЗадачУчастниковДействий КАК НаименованияЗадачУчастниковДействий
			|Где
			|	НаименованияЗадачУчастниковДействий.ВидДействия = &ВидДействия"
		);
		Запрос.УстановитьПараметр("ВидДействия", Объект.Ссылка);
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			КоличествоСтрок = Выборка.КоличествоСтрок;
		КонецЕсли;
	КонецЕсли;
	НаименованияЗадачЗаголовок = НСтр("ru = 'Наименования задач'");
	Если КоличествоСтрок <> 0 Тогда
		НаименованияЗадачЗаголовок = СтрШаблон("%1 (%2)", НаименованияЗадачЗаголовок, КоличествоСтрок);
	КонецЕсли;
	Элементы.НаименованияЗадачГруппа.Заголовок = НаименованияЗадачЗаголовок;
	
КонецПроцедуры

#КонецОбласти