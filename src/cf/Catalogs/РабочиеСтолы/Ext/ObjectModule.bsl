#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПриЧтенииПредставленийНаСервере() Экспорт
	МультиязычностьСервер.ПриЧтенииПредставленийНаСервере(ЭтотОбъект);
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Ошибки = Неопределено;
	
	ВсеУстановленныеВиджеты = Новый Соответствие;
	ДанныеВсехВиджетов = РаботаСРабочимСтоломПовтИсп.ДанныеВсехВиджетов();
	
	Для Каждого СтрокаСхемы Из Схема Цикл
		
		Если Не ЗначениеЗаполнено(СтрокаСхемы.Виджет) Тогда
			Продолжить;
		КонецЕсли;
		
		ВсеУстановленныеВиджеты[СтрокаСхемы.Виджет] = СтрокаСхемы.Позиция;
		
		ДанныеВиджета = ДанныеВсехВиджетов[СтрокаСхемы.Виджет];
		Если ДанныеВиджета = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		Для Каждого ЭлементКомлпексного Из ДанныеВиджета.ЭлементыКомплексногоВиджета Цикл
				
			Если ЭлементКомлпексного.Тип <> Перечисления.ТипыЭлементовРабочегоСтола.Виджет Тогда
				Продолжить;
			КонецЕсли;
			
			ВсеУстановленныеВиджеты[ЭлементКомлпексного.Виджет] = СтрокаСхемы.Позиция;
				
		КонецЦикла;
		
	КонецЦикла;
	
	Для Каждого СтрокаСхемы Из Схема Цикл
		
		Если Не ЗначениеЗаполнено(СтрокаСхемы.Виджет) Тогда
			Продолжить;
		КонецЕсли;
		
		ПараметрыОтбора = Новый Структура("Позиция", СтрокаСхемы.Позиция);
		НайденныеСтроки = Схема.НайтиСтроки(ПараметрыОтбора);
		Если НайденныеСтроки.Количество() > 1 Тогда
			ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(
				Ошибки,
				"Схема[%1].Позиция",
				СтрШаблон(
					НСтр("ru = 'Позиция указана несколько раз ""%1"".'"),
					СтрокаСхемы.Позиция),
				"Схема",
				Схема.Индекс(СтрокаСхемы),
				СтрШаблон(
					НСтр("ru = 'Позиция указана несколько раз ""%1"" в строке %2.'"),
					СтрокаСхемы.Позиция,
					"%1"));
		КонецЕсли;
		
		ОсновнаяПозицияВиджета = ВсеУстановленныеВиджеты[СтрокаСхемы.Виджет];
		Если ОсновнаяПозицияВиджета <> СтрокаСхемы.Позиция Тогда
			ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(
				Ошибки,
				"Схема[%1].Виджет",
				СтрШаблон(
					НСтр("ru = 'Виджет указан несколько раз ""%1"".'"),
					СтрокаСхемы.Виджет),
				"Схема",
				Схема.Индекс(СтрокаСхемы),
				СтрШаблон(
					НСтр("ru = 'Виджет указан несколько раз ""%1"" в строке %2.'"),
					СтрокаСхемы.Виджет,
					"%1"));
		КонецЕсли;
		
	КонецЦикла;
	
	Если Не НаименованиеУникально() Тогда
		ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(
				Ошибки,
				"Объект.Наименование",
				НСтр("ru = 'Данное наименование уже указано для другого рабочего стола.'"));
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.СообщитьОшибкиПользователю(Ошибки, Отказ);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Проверяет, что наименование уникально.
// 
// Возвращаемое значение:
//  Булево - Наименование уникально.
//
Функция НаименованиеУникально()
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	РабочиеСтолы.Ссылка
		|ИЗ
		|	Справочник.РабочиеСтолы КАК РабочиеСтолы
		|ГДЕ
		|	РабочиеСтолы.Наименование = &Наименование
		|	И РабочиеСтолы.Ссылка <> &Ссылка";
	
	Запрос.УстановитьПараметр("Наименование", Наименование);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	НаименованиеУникально = РезультатЗапроса.Пустой();
	
	Возврат НаименованиеУникально;
	
КонецФункции

#КонецОбласти

#КонецЕсли