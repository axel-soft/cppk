// @strict-types

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Обновляет предопределенные элементы.
//
Процедура ЗаполнитьПредопределенные() Экспорт
	
	Объект = КабинетСотрудника.ПолучитьОбъект();
	Объект.ТипСервиса = Перечисления.ТипыСервисовВнешнегоПодписания.КабинетСотрудника;
	Объект.Наименование = НСтр("ru = '1С:Кабинет сотрудника'");
	ОбновлениеИнформационнойБазы.ЗаписатьОбъект(Объект);
	
КонецПроцедуры

// Настройки сервиса.
// 
// Параметры:
//  Сервис - СправочникСсылка.СервисыВнешнегоПодписания
// 
// Возвращаемое значение:
//  см. СтруктураНастроек
//
Функция НастройкиСервиса(Сервис) Экспорт
	
	Настройки = СтруктураНастроек();
	ИменаРеквизитов = ОбщегоНазначенияКлиентСервер.КлючиСтруктурыВСтроку(Настройки);
	ИменаРеквизитов = СтрЗаменить(ИменаРеквизитов, "Сервис,", "");
	Реквизиты = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Сервис, ИменаРеквизитов);
	ЗаполнитьЗначенияСвойств(Настройки, Реквизиты);
	Настройки.Сервис = Сервис;
	
	Возврат Настройки;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Структура настроек.
// 
// Возвращаемое значение:
//  Структура - Структура настроек:
// * Сервис                                     - СправочникСсылка.СервисыВнешнегоПодписания
// * АдресПриложения                            - Строка
// * АдресПриложенияПоИмени                     - Строка
// * АдресПриложенияПоИмениДоступен             - Булево
// * ВерсияПриложения                           - Строка
//
Функция СтруктураНастроек()
	
	Результат = Новый Структура;
	Результат.Вставить("Сервис",							ПустаяСсылка());
	Результат.Вставить("АдресПриложения",					"");
	Результат.Вставить("АдресПриложенияПоИмени",			"");
	Результат.Вставить("АдресПриложенияПоИмениДоступен",	Ложь);
	Результат.Вставить("ВерсияПриложения",					"");
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#КонецЕсли
