// @strict-types

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ОбновитьНаСервере();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВвестиКлючиПриложения(Команда)
	
	ОповещениеОЗакрытии = Новый ОписаниеОповещения("ПослеВводаКлючейСервиса", ЭтотОбъект);
	
	ПараметрыФорма = Новый Структура;
	ПараметрыФорма.Вставить("Сервис", Объект.Ссылка);
	ОткрытьФорму("Справочник.СервисыВнешнегоПодписания.Форма.ФормаВводаКлючейСервиса",
		ПараметрыФорма, ЭтотОбъект,,,, ОповещениеОЗакрытии, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ОбновитьНаСервере()
	
	ЕстьКлючиСервиса = РаботаСВнешнимПодписанием.ЕстьКлючиСервиса(Объект.Ссылка);
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы,
		"ГруппаКлючиПриложенияУказаны", "Видимость", ЕстьКлючиСервиса);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы,
		"ГруппаКлючиПриложенияНеУказаны", "Видимость", Не ЕстьКлючиСервиса);
	
КонецПроцедуры

// После ввода ключей сервиса.
// 
// Параметры:
//  
//  Результат - Неопределено, Структура - Неопределено или структура с данными ключей сервиса:
//    * ИдентификаторКлиента - Строка
//    * СекретКлиента        - Строка
//    
//  ДополнительныеПараметры - Неопределено
//
&НаКлиенте
Процедура ПослеВводаКлючейСервиса(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СохранитьКлючиНаСервере(Результат);
	
КонецПроцедуры

&НаСервере
Процедура СохранитьКлючиНаСервере(Результат)
	
	РаботаСВнешнимПодписанием.СохранитьКлючиСервисаВБезопасномХранилище(
		Объект.Ссылка,
		Результат.ИдентификаторКлиента,
		Результат.СекретКлиента);
		
	ОбновитьНаСервере();
	
КонецПроцедуры

#КонецОбласти
