
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Имена = ОбщегоНазначенияДокументооборот.ИменаРеквизитовФормы(ЭтотОбъект);
	Для Каждого Имя Из Имена Цикл
		Параметры.Свойство(Имя, ЭтотОбъект[Имя]);
	КонецЦикла;
	КлючНазначенияИспользования = СтрСоединить(Имена, ",");

	Если Параметры.Свойство("Заголовок") Тогда
		Заголовок = Параметры.Заголовок; //@skip-check unknown-form-parameter-access
		АвтоЗаголовок = Ложь;
	КонецЕсли;
	
	НастроитьЭлементы();

КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если Не ЗавершениеРаботы И Модифицированность Тогда
		
		Отказ = Истина;
		ОписаниеОповещения = Новый ОписаниеОповещения("ПередЗакрытиемЗавершение", ЭтотОбъект);
		ТекстВопроса = НСтр("ru = 'Найстройки были изменены. Сохранить изменения?'");
		
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНетОтмена);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура КонтейнерПриИзменении(Элемент)
	
	НастроитьЭлементы();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыИерархия

&НаКлиенте
Процедура ИерархияПриИзменении(Элемент)
	
	Модифицированность = Истина;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)
	
	СохранитьИзменения();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтметитьВсе(Команда)
	
	Для Каждого Элемент Из Иерархия Цикл
		Элемент.Пометка = Истина;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура СнятьОтметки(Команда)
	
	Для Каждого Элемент Из Иерархия Цикл
		Элемент.Пометка = Ложь;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура СохранитьИзменения()
	
	Модифицированность = Ложь;
	
	Значения = Новый Структура(КлючНазначенияИспользования);
	ЗаполнитьЗначенияСвойств(Значения, ЭтотОбъект);
	ОповеститьОВыборе(Значения);
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытиемЗавершение(Результат, Параметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		СохранитьИзменения();
	ИначеЕсли Результат = КодВозвратаДиалога.Нет Тогда
		Модифицированность = Ложь;
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура НастроитьЭлементы()
	
	ИерархияДоступна =
		Не ЗначениеЗаполнено(Контейнер) Или ТипЗнч(Контейнер) = Тип("СправочникСсылка.ВидыПодразделений");
	
	Если Не ИерархияДоступна Тогда
		Иерархия.ЗаполнитьПометки(Ложь);
	КонецЕсли;
	
	Элементы.Иерархия.Доступность = ИерархияДоступна;
	
КонецПроцедуры

#КонецОбласти
