#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	МассивСсылок = Новый Массив;
	Для Каждого НайденныйЭлемент Из Параметры.НайденныеОбъекты Цикл
		МассивСсылок.Добавить(НайденныйЭлемент.Значение.Ссылка);
	КонецЦикла;
	
	Если Параметры.ИмяОбъектаМетаданных = "Контрагенты" Тогда
		СписокКонтрагентов.ТекстЗапроса =
			"ВЫБРАТЬ
			|	Т.Ссылка,
			|	Т.Код,
			|	Т.Наименование,
			|	Т.ИНН,
			|	Т.КПП,
			|	Т.РегистрационныйНомер,
			|	Т.НаименованиеПолное
			|ИЗ
			|	Справочник.Контрагенты КАК Т
			|ГДЕ
			|	Т.Ссылка В (&МассивСсылок)";
		СписокКонтрагентов.ОсновнаяТаблица = "Справочник.Контрагенты";
		Элементы.НадписьСообщение.Заголовок = НСтр("ru = 'В базе уже записаны похожие контрагенты:'");
	ИначеЕсли Параметры.ИмяОбъектаМетаданных = "Организации" Тогда
		СписокКонтрагентов.ТекстЗапроса =
			"ВЫБРАТЬ
			|	Т.Ссылка,
			|	Т.Код,
			|	Т.Наименование,
			|	Т.ИНН,
			|	Т.КПП,
			|	Т.ОГРН КАК РегистрационныйНомер,
			|	Т.НаименованиеПолное
			|ИЗ
			|	Справочник.Организации КАК Т
			|ГДЕ
			|	Т.Ссылка В (&МассивСсылок)";
		СписокКонтрагентов.ОсновнаяТаблица = "Справочник.Организации";
		Элементы.НадписьСообщение.Заголовок = СтрШаблон(
			НСтр("ru = 'В базе уже записаны похожие %1:'"),
			РедакцииКонфигурацииКлиентСервер.Организации());
	Иначе
		ВызватьИсключение СтрШаблон(НСтр("ru = 'Передан неожиданный параметр - %1'"), Параметры.ИмяОбъектаМетаданных);
	КонецЕсли;
	СписокКонтрагентов.Параметры.УстановитьЗначениеПараметра("МассивСсылок", МассивСсылок);
	
	Для Каждого НайденныйЭлемент Из Параметры.НайденныеОбъекты Цикл
		УстановитьОформлениеСписка(НайденныйЭлемент.Значение, СписокКонтрагентов.УсловноеОформление);
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписокКонтрагентов

&НаКлиенте
Процедура СписокКонтрагентовВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ПоказатьЗначение(, Элементы.СписокКонтрагентов.ТекущиеДанные.Ссылка); 
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ДаЗаписать(Команда)
	
	Закрыть(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура НетОтменитьЗапись(Команда)
	
	Закрыть(Ложь);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьОформлениеСписка(НайденныйЭлемент, Знач УсловноеОформление)

	ЭлементУсловногоОформления = УсловноеОформление.Элементы.Добавить();
	
	ЭлементОтбораДанных = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементОтбораДанных.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Ссылка");
	ЭлементОтбораДанных.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ЭлементОтбораДанных.ПравоеЗначение = НайденныйЭлемент.Ссылка;
	ЭлементОтбораДанных.Использование = Истина;
	
	ЭлементЦветаОформления = ЭлементУсловногоОформления.Оформление.Элементы.Найти("TextColor");
	ЭлементЦветаОформления.Значение =  Метаданные.ЭлементыСтиля.ПоясняющийОшибкуТекст.Значение; 
	ЭлементЦветаОформления.Использование = Истина;
	
	Если НайденныйЭлемент.Свойство("Наименование_Флаг") И НайденныйЭлемент.Наименование_Флаг Тогда
		НовыйЭлемент = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
		НовыйЭлемент.Поле = Новый ПолеКомпоновкиДанных("Наименование");		
	КонецЕсли;
	
	Если НайденныйЭлемент.Свойство("ИНН_Флаг") И НайденныйЭлемент.ИНН_Флаг Тогда
		НовыйЭлемент = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
		НовыйЭлемент.Поле = Новый ПолеКомпоновкиДанных("ИНН");
	КонецЕсли;
	
	Если НайденныйЭлемент.Свойство("КПП_Флаг") И НайденныйЭлемент.КПП_Флаг Тогда
		НовыйЭлемент = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
		НовыйЭлемент.Поле = Новый ПолеКомпоновкиДанных("КПП");
	КонецЕсли;
	
	Если НайденныйЭлемент.Свойство("РегистрационныйНомер_Флаг") И НайденныйЭлемент.РегистрационныйНомер_Флаг Тогда
		НовыйЭлемент = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
		НовыйЭлемент.Поле = Новый ПолеКомпоновкиДанных("РегистрационныйНомер");
	КонецЕсли;
	
	Если НайденныйЭлемент.Свойство("ПолноеНаименование_Флаг") И НайденныйЭлемент.ПолноеНаименование_Флаг Тогда
		НовыйЭлемент = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
		НовыйЭлемент.Поле = Новый ПолеКомпоновкиДанных("ПолноеНаименование");
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
