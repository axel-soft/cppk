#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	Алгоритмы.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.Алгоритмы КАК Алгоритмы
		|ГДЕ
		|	Алгоритмы.КлючДляВызова = &КлючДляВызова
		|	И НЕ Алгоритмы.Ссылка = &Ссылка");
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("КлючДляВызова", КлючДляВызова);
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		ТекстСообщения = СтрШаблон(
			НСтр("ru = 'Этот ключ уже используется для алгоритма ""%2""'"),
			КлючДляВызова,
			Выборка.Ссылка);
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,, "Объект.КлючДляВызова",, Отказ);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ЭтоГруппа Тогда
		ТекущаяДатаСеанса = ТекущаяДатаСеанса();
		Если ЭтоНовый() Или Не ЗначениеЗаполнено(ДатаСоздания) Тогда
			ДатаСоздания = ТекущаяДатаСеанса;
		КонецЕсли;
		ДатаИзменения = ТекущаяДатаСеанса;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
	
#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли