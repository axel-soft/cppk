#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ПоказатьУдаленные();
	
	ОбщегоНазначенияДокументооборот.УстановитьУсловноеОформлениеСписка(Список.УсловноеОформление, "Наименование");
	
	МультиязычностьСервер.ПриСозданииНаСервере(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Настройки)
	
	ПоказатьУдаленные();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	РаботаСРеестрамиКлиент.НастроитьРеестр(ВыбраннаяСтрока);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПередНачаломИзменения(Элемент, Отказ)
	
	Отказ = Истина;
	
	РаботаСРеестрамиКлиент.НастроитьРеестр(Элемент.ТекущаяСтрока);
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура СписокПриПолученииДанныхНаСервере(ИмяЭлемента, Настройки, Строки)
	
	ВсеРеестры = Новый Массив;
	Для Каждого КлючИЗначение Из Строки Цикл
		
		ДанныеСтроки = КлючИЗначение.Значение.Данные;
		
		ВсеРеестры.Добавить(ДанныеСтроки.Ссылка);
		
	КонецЦикла;
	
	ВидыЭлементовРеестров = ОбщегоНазначения.ЗначениеРеквизитаОбъектов(ВсеРеестры, "ВидыЭлементовРеестра");
	Для Каждого КлючИЗначение Из Строки Цикл
		
		ДанныеСтроки = КлючИЗначение.Значение.Данные;
		
		Если ДанныеСтроки.Свойство("ВидыЭлементов") Тогда
			
			МассивВидыЭлементов = Новый Массив;
			ВидыЭлементовРеестра = ВидыЭлементовРеестров[ДанныеСтроки.Ссылка].Выгрузить();
			Для Каждого СтрокаВидЭлемента Из ВидыЭлементовРеестра Цикл
				
				МассивВидыЭлементов.Добавить(Строка(СтрокаВидЭлемента.ВидЭлементовРеестра));
				
			КонецЦикла; 
			
			ДанныеСтроки.ВидыЭлементов = СтрСоединить(МассивВидыЭлементов, ", ");
									
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПоказыватьУдаленные(Команда)
	
	ПоказыватьУдаленные = Не ПоказыватьУдаленные;
	
	ПоказатьУдаленные();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПоказатьУдаленные()
	
	Если ПоказыватьУдаленные Тогда
		ОбщегоНазначенияКлиентСервер.УдалитьЭлементыГруппыОтбораДинамическогоСписка(Список, "ПометкаУдаления");
	Иначе	
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список, "ПометкаУдаления", Ложь);
	КонецЕсли;	
	
	Элементы.ФормаПоказыватьУдаленные.Пометка = ПоказыватьУдаленные;
	
КонецПроцедуры

#КонецОбласти