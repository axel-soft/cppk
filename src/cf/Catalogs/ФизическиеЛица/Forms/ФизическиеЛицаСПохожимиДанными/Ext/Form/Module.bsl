
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Для Каждого ФизЛицо Из Параметры.ПохожиеФизЛица Цикл
		СтрокаТаблицы = ПохожиеФизЛица.Добавить();
		СтрокаТаблицы.ФизЛицо = ФизЛицо;
	КонецЦикла;
	
	НастройкаПоказаФотографии();
	
	ЗаполнитьЗаголовок();
	
	ЗаполнитьКлючСохраненияПоложенияОкна();
	
	НастроитьОтображениеСпискаПохожиеФизЛица();
	
	ЗаполнитьДанныеФизЛицаПриОткрытии();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормы_ПохожиеФизЛица

&НаКлиенте
Процедура ПохожиеФизЛицаПриАктивизацииСтроки(Элемент)
	
	ТекущиеДанные = Элементы.ПохожиеФизЛица.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ФизическоеЛицо = ТекущиеДанные.ФизЛицо Тогда
		Возврат;
	КонецЕсли;
	
	ФизическоеЛицо = ТекущиеДанные.ФизЛицо;
	
	ЗаполнитьДанныеФизЛица();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ДаЭтоТотКтоМнеНужен(Команда)
	
	Закрыть(ФизическоеЛицо);
	
КонецПроцедуры

&НаКлиенте
Процедура НетЭтоДругойЧеловек(Команда)
	
	Закрыть(Неопределено);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьЗаголовок()
	
	Если ПохожиеФизЛица.Количество() = 1 Тогда
		Заголовок = НСтр("ru = 'Найден человек с похожим именем'");
	Иначе
		Заголовок = НСтр("ru = 'Найдены люди с похожими именами'");
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура НастроитьОтображениеСпискаПохожиеФизЛица()
	
	Если ПохожиеФизЛица.Количество() = 1 Тогда
		Элементы.ПохожиеФизЛица.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьКлючСохраненияПоложенияОкна()
	
	Если ПохожиеФизЛица.Количество() = 1 Тогда
		КлючСохраненияПоложенияОкна = "ОдинЧеловек";
	Иначе
		КлючСохраненияПоложенияОкна = "НесколькоЧеловек";
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДанныеФизЛицаПриОткрытии()
	
	Если Элементы.ПохожиеФизЛица.Видимость = Истина Тогда
		Возврат;
	КонецЕсли;
	
	ФизическоеЛицо = ПохожиеФизЛица[0].ФизЛицо;
	ЗаполнитьДанныеФизЛица();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДанныеФизЛица()
	
	РеквизитыФизЛица = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
		ФизическоеЛицо, "ДатаРождения, Пол, Комментарий");
	ЗаполнитьЗначенияСвойств(ЭтаФорма, РеквизитыФизЛица);
	
	ПоказатьФотоФизлица(ФизическоеЛицо, УникальныйИдентификатор, Фотография);
	
КонецПроцедуры

&НаСервере
Процедура НастройкаПоказаФотографии()
	
	ОтображатьФотографииПерсональнаяНастройка =
		ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
		"НастройкиПрограммы",
		"ОтображатьФотографииПерсональнаяНастройка",
		Истина);
	ОтображатьФотографииОбщаяНастройка = ПолучитьФункциональнуюОпцию("ОтображатьФотографииОбщаяНастройка");
	ПолучатьФотографии = Истина;
	
	Если Не ОтображатьФотографииОбщаяНастройка 
		Или Не ОтображатьФотографииПерсональнаяНастройка
		Или ПолучитьСкоростьКлиентскогоСоединения() = СкоростьКлиентскогоСоединения.Низкая Тогда
		ПолучатьФотографии = Ложь;
		Элементы.ГруппаСтраницыФотографии.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПоказатьФотоФизлица(Контакт, УникальныйИдентификатор, Фотография)
	
	Если Не ПолучатьФотографии Тогда
		Возврат;
	КонецЕсли;
	
	// фото пользователя
	Если ЭтоАдресВременногоХранилища(Фотография) Тогда
		УдалитьИзВременногоХранилища(Фотография);
	КонецЕсли;	
	
	Фотография = "";
	Если ЗначениеЗаполнено(Контакт) 
		И ТипЗнч(Контакт) <> Тип("СправочникСсылка.РолиИсполнителей") Тогда
		
		ЕстьКартинка = Ложь;
		Фотография = РаботаСФотографиями.ПолучитьАдресФото(Контакт, УникальныйИдентификатор, ЕстьКартинка);
		
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Фотография) Тогда
		Фотография = "";
		ЭтаФорма.Элементы.ГруппаСтраницыФотографии.ТекущаяСтраница = ЭтаФорма.Элементы.СтраницаКартинкаПоУмолчанию;
	Иначе	
		ЭтаФорма.Элементы.ГруппаСтраницыФотографии.ТекущаяСтраница = ЭтаФорма.Элементы.СтраницаФотография;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти