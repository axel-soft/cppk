#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПриЧтенииПредставленийНаСервере() Экспорт
	МультиязычностьСервер.ПриЧтенииПредставленийНаСервере(ЭтотОбъект);
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Строка") Тогда
		ЗаполнитьНаОснованииПоставляемыхДанных(ДанныеЗаполнения);
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Наименование) И ЗначениеЗаполнено(Заголовок) Тогда
		Наименование = Заголовок;
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Ошибки = Неопределено;
	
	Если Не НаименованиеУникально() Тогда
		ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(
				Ошибки,
				"Объект.Наименование",
				НСтр("ru = 'Данное наименование уже указано для другого блока навигации.'"));
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.СообщитьОшибкиПользователю(Ошибки, Отказ);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Заполняет объект на основании поставляемых данных.
Процедура ЗаполнитьНаОснованииПоставляемыхДанных(БлокНавигации)
	
	ДанныеБлокаНавигации = Справочники.БлокиНавигации.ДанныеБлокаНавигации(БлокНавигации);
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, ДанныеБлокаНавигации, "Заголовок, Подсказка, Условие");
	
	Команды.Очистить();
	Для Каждого КомандаБлока Из ДанныеБлокаНавигации.КомандыБлока Цикл
		НоваяСтрока = Команды.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, КомандаБлока, "ОсновнаяКоманда, ВспомогательнаяКоманда");
		НоваяСтрока.СкрытьВЕще = Ложь;
	КонецЦикла;
	
	Команды.Очистить();
	Для Каждого КомандаЕщеБлока Из ДанныеБлокаНавигации.КомандыЕщеБлока Цикл
		НоваяСтрока = Команды.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, КомандаЕщеБлока, "ОсновнаяКоманда, ВспомогательнаяКоманда");
		НоваяСтрока.СкрытьВЕще = Истина;
	КонецЦикла;
	
КонецПроцедуры

// Проверяет, что наименование уникально.
// 
// Возвращаемое значение:
//  Булево - Наименование уникально.
//
Функция НаименованиеУникально()
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	БлокиНавигации.Ссылка
		|ИЗ
		|	Справочник.БлокиНавигации КАК БлокиНавигации
		|ГДЕ
		|	БлокиНавигации.Наименование = &Наименование
		|	И БлокиНавигации.Ссылка <> &Ссылка";
	
	Запрос.УстановитьПараметр("Наименование", Наименование);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	НаименованиеУникально = РезультатЗапроса.Пустой();
	
	Возврат НаименованиеУникально;
	
КонецФункции

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли