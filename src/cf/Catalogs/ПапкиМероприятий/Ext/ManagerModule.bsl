#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает структуру полей папки мероприятий
//
// Возвращаемое значение:
//   Структура
//     Наименование
//     Родитель
//     Описание
//     Ответственный
//     ДатаСоздания
//
Функция ПолучитьСтруктуруПапки() Экспорт
	
	СтруктураПапкиМероприятий = Новый Структура;
	СтруктураПапкиМероприятий.Вставить("Наименование");
	СтруктураПапкиМероприятий.Вставить("Родитель");
	СтруктураПапкиМероприятий.Вставить("Описание");
	СтруктураПапкиМероприятий.Вставить("Ответственный");
	СтруктураПапкиМероприятий.Вставить("ДатаСоздания");
	
	Возврат СтруктураПапкиМероприятий;
	
КонецФункции

// Создает и записывает в БД папку мероприятий
//
// Параметры:
//   СтруктураПапкиМероприятий - Структура - структура полей папки мероприятия.
//
// Возвращаемое значение:
//   СправочникСсылка.ПапкиМероприятий
//
Функция СоздатьПапку(СтруктураПапкиМероприятий) Экспорт
	
	НоваяПапкаМероприятий = СоздатьЭлемент();
	ЗаполнитьЗначенияСвойств(НоваяПапкаМероприятий, СтруктураПапкиМероприятий);
	НоваяПапкаМероприятий.Записать();
	
	Возврат НоваяПапкаМероприятий.Ссылка;
	
КонецФункции

#Область ПраваДоступа

Функция ПолучитьПоляДоступа() Экспорт
	
	Возврат "Ссылка";
	
КонецФункции

// Заполняет переданный дескриптор доступа
Процедура ЗаполнитьОсновнойДескриптор(ОбъектДоступа, ДескрипторДоступа) Экспорт
	
	ДокументооборотПраваДоступа.ЗаполнитьНастройкиДескриптора(ДескрипторДоступа, ОбъектДоступа);
	
КонецПроцедуры

// Возвращает признак того, что менеджер содержит метод ЗапросДляРасчетаПрав()
// 
Функция ЕстьМетодЗапросДляРасчетаПрав() Экспорт
	
	Возврат Истина;
	
КонецФункции

// Возвращает запрос для расчета прав доступа по дескрипторам объекта
// 
// Параметры:
//  
//  Дескрипторы - Массив - массив дескрипторов, чьи права нужно рассчитать
//  ИдОбъекта - Ссылка - идентификатор объекта метаданных, назначенный переданным дескрипторам
//  МенеджерОбъектаДоступа - СправочникМенеджер, ДокументМенеджер - менеджер объекта доступа
// 
// Возвращаемое значение - Запрос - запрос, который выберет права доступа для переданного массива дескрипторов
// 
Функция ЗапросДляРасчетаПрав(Дескрипторы, ИдОбъекта, МенеджерОбъектаДоступа) Экспорт
	
	Запрос = Справочники.ДескрипторыДоступаОбъектов.ЗапросДляСтандартногоРасчетаПрав(
		Дескрипторы, ИдОбъекта, МенеджерОбъектаДоступа, Истина, Ложь);
	Запрос.Текст = ДокументооборотПраваДоступаПовтИсп.ТекстЗапросаДляРасчетаПравПапок();
	
	Возврат Запрос;
	
КонецФункции

// Заполняет протокол расчета прав дескрипторов
// 
// Параметры:
//  
//  ПротоколРасчетаПрав - Массив - протокол для заполнения
//  ЗапросПоПравам - Запрос - запрос, который использовался для расчета прав дескрипторов
//  Дескрипторы - Массив - массив дескрипторов, чьи права были рассчитаны
//  
Процедура ЗаполнитьПротоколРасчетаПрав(ПротоколРасчетаПрав, ЗапросПоПравам) Экспорт
	
	Справочники.ДескрипторыДоступаОбъектов.ЗаполнитьПротоколРасчетаПравСтандартно(
		ПротоколРасчетаПрав, ЗапросПоПравам);
	
КонецПроцедуры

// Проверяет наличие метода.
// 
Функция ЕстьМетодПолучитьПравилаОбработкиНастроекПравПапки() Экспорт
	
	Возврат Истина;
	
КонецФункции

// Возвращает таблицу значений с правилами обработки настроек прав папки,
// которые следует применять для расчета прав объекта
// 
Функция ПолучитьПравилаОбработкиНастроекПравПапки() Экспорт
	
	ТаблицаПравил = ДокументооборотПраваДоступа.ТаблицаПравилОбработкиНастроекПапки();
	
	Стр = ТаблицаПравил.Добавить();
	Стр.Настройка = "ЧтениеПапокИМероприятий";
	Стр.Чтение = Истина;
	
	Стр = ТаблицаПравил.Добавить();
	Стр.Настройка = "ИзменениеПапокМероприятий";
	Стр.Добавление = Истина;
	Стр.Изменение = Истина;
	Стр.Удаление = Истина;
	
	Стр = ТаблицаПравил.Добавить();
	Стр.Настройка = "УправлениеПравами";
	Стр.УправлениеПравами = Истина;
	
	Возврат ТаблицаПравил;
	
КонецФункции

#КонецОбласти

#Область ОбновлениеАдреснойКниги

// Конструктор параметров обновления адресной книги.
//
// Возвращаемое значение:
//	Структура:
//		* ОбновитьДанныеОбъекта - Булево - Признак обновления данных по объекту.
//		* МероприятияДляОбновления - Массив Из СправочникОбъект.Мероприятия - Список мероприятий для обновления.
//
Функция ПараметрыОбновленияАдреснойКниги() Экспорт
	
	ПараметрыОбновленияАдреснойКниги = Новый Структура;
	ПараметрыОбновленияАдреснойКниги.Вставить("ОбновитьДанныеОбъекта", Ложь);
	ПараметрыОбновленияАдреснойКниги.Вставить("МероприятияДляОбновления", Новый Массив);
	
	Возврат ПараметрыОбновленияАдреснойКниги;
	
КонецФункции

// Устанавливает значения параметров обновления адресной книги по данным объекта.
//
// Параметры:
//	Объект - СправочникОбъект.ПапкиМероприятий - Объект, для которго необходимо определить параметры обновления.
//
// Возвращаемое значение:
//	Структура: см. ПараметрыОбновленияАдреснойКниги.
//
Функция ЗначенияПараметровОбновленияАдреснойКнигиПоОбъекту(Объект) Экспорт
	
	ПараметрыОбновленияАдреснойКниги = ПараметрыОбновленияАдреснойКниги();
		
	Если Не РаботаСАдреснойКнигой.ТребуетсяОбновлениеАдреснойКниги(Объект) Тогда
		Возврат ПараметрыОбновленияАдреснойКниги; 
	КонецЕсли;

	Если Объект.ЭтоНовый() Тогда
		ПараметрыОбновленияАдреснойКниги.ОбновитьДанныеОбъекта = Истина;
	Иначе
		ПредыдущиеЗначенияРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
			Объект.Ссылка, "Наименование, ПометкаУдаления, Родитель");
		
		Если ПредыдущиеЗначенияРеквизитов.Родитель <> Объект.Родитель
			Или ПредыдущиеЗначенияРеквизитов.ПометкаУдаления <> Объект.ПометкаУдаления
			Или ПредыдущиеЗначенияРеквизитов.Наименование <> Объект.Наименование Тогда
			
			ПараметрыОбновленияАдреснойКниги.ОбновитьДанныеОбъекта = Истина;
		КонецЕсли;
		
		Если ПредыдущиеЗначенияРеквизитов.ПометкаУдаления <> Объект.ПометкаУдаления Тогда 
			Запрос = Новый Запрос(
				"ВЫБРАТЬ РАЗРЕШЕННЫЕ
				|	Мероприятия.Ссылка,
				|	Мероприятия.ПометкаУдаления
				|ИЗ
				|	Справочник.Мероприятия КАК Мероприятия
				|ГДЕ
				|	Мероприятия.Папка = &Ссылка");
			Запрос.УстановитьПараметр("Ссылка", Объект.Ссылка);			
			Выборка = Запрос.Выполнить().Выбрать();
			
			Пока Выборка.Следующий() Цикл
				Если Объект.ПометкаУдаления <> Выборка.ПометкаУдаления Тогда 
					Мероприятие = Выборка.Ссылка.ПолучитьОбъект();
					РаботаСАдреснойКнигой.ОтключитьОбновлениеАдреснойКниги(Мероприятие);
					Мероприятие.Заблокировать();
					
					ПараметрыОбновленияАдреснойКниги.МероприятияДляОбновления.Добавить(Мероприятие);
					
					Мероприятие.УстановитьПометкуУдаления(Объект.ПометкаУдаления);
				КонецЕсли;
			КонецЦикла;
			
		КонецЕсли;
	КонецЕсли;
	
	Возврат ПараметрыОбновленияАдреснойКниги;

КонецФункции

// Обновляет адресную книгу, согласно установленным параметрам.
//
// Параметры:
//	Объект - СправочникОбъект.ПапкиМероприятий - Объект, по данным которого необходимо обновить адресной книги.
//	ПараметрыОбновления - Структура Из КлючИЗначение - см. ПараметрыОбновленияАдреснойКниги.
//
Процедура ОбновитьАдреснуюКнигу(Объект, ПараметрыОбновления) Экспорт
	
	Если ПараметрыОбновления.ОбновитьДанныеОбъекта Тогда
		Справочники.АдреснаяКнига.ОбновитьДанныеОбъекта(
			Объект.Ссылка, Объект.Родитель, Справочники.АдреснаяКнига.ПоМероприятиям, Объект.Ссылка);
	КонецЕсли;
	
	Для Каждого МероприятиеОбъект Из ПараметрыОбновления.МероприятияДляОбновления Цикл
		
		Справочники.АдреснаяКнига.ОбновитьДанныеОбъекта(
			МероприятиеОбъект.Ссылка,
			МероприятиеОбъект.Папка,
			Справочники.АдреснаяКнига.ПоМероприятиям,
			МероприятиеОбъект.Ссылка);
			
		РегистрыСведений.ОбъектыПоискаВАдреснойКниге.ОбновитьДоступностьВПоиске(МероприятиеОбъект);
		
		УчастникиМероприятия = 
			УправлениеМероприятиями.ПолучитьУчастниковМероприятия(МероприятиеОбъект.Ссылка, Истина);
		УчастникиМероприятия =	УчастникиМероприятия.ВыгрузитьКолонку("Исполнитель");
			
		Справочники.АдреснаяКнига.ОбновитьСписокПодчиненныхОбъектов(
			МероприятиеОбъект.Ссылка,
			МероприятиеОбъект.Папка,
			УчастникиМероприятия,
			Справочники.АдреснаяКнига.ПоМероприятиям,
			МероприятиеОбъект.Ссылка);		
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли
