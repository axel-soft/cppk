#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

Перем ОбновитьДанныеГруппыЛичныхАдресатовВАдреснойКниге;

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	РеквизитыГруппыПоСсылке =  ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Ссылка, "Родитель, ПометкаУдаления, Наименование");
	
	ПометкаИБ = РеквизитыГруппыПоСсылке.ПометкаУдаления;
	
	Если ПометкаУдаления <> ПометкаИБ И Не Ссылка.Пустая() Тогда
		
		// Отбираем адресатов и пытаемся поставить им пометку удаления
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	ЛичныеАдресаты.Ссылка
			|ИЗ
			|	Справочник.ЛичныеАдресаты КАК ЛичныеАдресаты
			|ГДЕ
			|	ЛичныеАдресаты.Группа = &Ссылка";
		
		Запрос.УстановитьПараметр("Ссылка", Ссылка);
		
		Результат = Запрос.Выполнить();
		Выборка = Результат.Выбрать();
		Пока Выборка.Следующий() Цикл
			
			АдресатОбъект = Выборка.Ссылка.ПолучитьОбъект();
			АдресатОбъект.Заблокировать();
			АдресатОбъект.УстановитьПометкуУдаления(ПометкаУдаления);
			
		КонецЦикла;
		
	КонецЕсли;
	
	// Обновление адресной книги
	ДополнительныеСвойства.Вставить(
		"ПараметрыОбновленияАдреснойКниги",
		Справочники.ГруппыЛичныхАдресатов.ЗначенияПараметровОбновленияАдреснойКнигиПоОбъекту(ЭтотОбъект));
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	// Обновление адресной книги.
	Справочники.ГруппыЛичныхАдресатов.ОбновитьАдреснуюКнигу(
		ЭтотОбъект, ДополнительныеСвойства.ПараметрыОбновленияАдреснойКниги);

	
КонецПроцедуры

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли