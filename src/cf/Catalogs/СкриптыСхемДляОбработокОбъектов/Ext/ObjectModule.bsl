#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает пустую структуру данных для заполнения.
//
// Возвращаемое значение:
//  Структура
//   * Наименование - Строка
//   * Группа - СправочникСсылка.СкриптыСхемДляОбработокОбъектов
//   * Выражение - Строка
//   * Комментарий - Строка
//
Функция СтруктураДанныхДляЗаполнения() Экспорт
	
	СтруктураДляЗаполнения = Новый Структура;
	СтруктураДляЗаполнения.Вставить("Наименование", ""); 
	СтруктураДляЗаполнения.Вставить("Группа", Справочники.СкриптыСхемДляОбработокОбъектов.ПустаяСсылка());
	СтруктураДляЗаполнения.Вставить("Выражение", "");
	СтруктураДляЗаполнения.Вставить("Комментарий", "");
	
	Возврат СтруктураДляЗаполнения;
	
КонецФункции

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ПриКопировании(ОбъектКопирования)
	
	Если ЭтоГруппа Тогда
		Возврат;
	КонецЕсли;
	
	ДатаСоздания = Дата("00000000");
	ДатаИзменения = Дата("00000000");
	
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда
		
		Если ДанныеЗаполнения.Свойство("Наименование") Тогда
			Наименование = ДанныеЗаполнения.Наименование;			
		КонецЕсли;
		
		Если ДанныеЗаполнения.Свойство("Группа") Тогда
			Родитель = ДанныеЗаполнения.Группа;			
		КонецЕсли;
		
		Если ДанныеЗаполнения.Свойство("Выражение") Тогда
			Выражение = ДанныеЗаполнения.Выражение;			
		КонецЕсли;
		
		Если ДанныеЗаполнения.Свойство("Комментарий") Тогда
			Комментарий = ДанныеЗаполнения.Комментарий;			
		КонецЕсли;
		
	КонецЕсли; 
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	
	Если ОбщегоНазначенияДокументооборот.ЭтоЗагрузкаИзУзлаРИБ(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	Если МиграцияПриложенийПереопределяемый.ЭтоЗагрузка(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ЭтоГруппа Тогда
		ТекущаяДатаСеанса = ТекущаяДатаСеанса();
		Если ЭтоНовый() Или Не ЗначениеЗаполнено(ДатаСоздания) Тогда
			ДатаСоздания = ТекущаяДатаСеанса;
		КонецЕсли;
		ДатаИзменения = ТекущаяДатаСеанса;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
	
#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли