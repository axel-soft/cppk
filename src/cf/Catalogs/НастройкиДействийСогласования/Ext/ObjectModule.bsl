#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Заполняет участников по умолчанию.
//
Процедура ЗаполнитьУчастниковПоУмолчанию() Экспорт
	
	УчастникиПоУмолчанию = НастройкиДействий.УчастникиПоУмолчанию(ВидДействия);
	
	Участники.Очистить();
	
	Для Каждого УчастникПоУмолчанию Из УчастникиПоУмолчанию Цикл
		
		СтрокаУчастника  = Участники.Добавить();
		СтрокаУчастника.ФункцияУчастника = Перечисления.ФункцииУчастниковСогласования.Согласующий;
		СтрокаУчастника.Участник = УчастникПоУмолчанию;
		
	КонецЦикла;
	
	МоментРазыменованияУчастников =
		Перечисления.МоментыРазыменованияУчастниковДействий.ПередВыполнениемДействия;
	ПорядокВыполненияУчастниками = Перечисления.ПорядокВыполненияЭтапаДействия.Параллельно;
	
КонецПроцедуры

// Возвращает участников с функций "Обрабатывающий результат".
// 
// Возвращаемое значение:
//  Массив - Участники.
//
Функция УчастникОбрабатывающийРезультат() Экспорт
	
	ПараметрыОтбора = Новый Структура;
	ПараметрыОтбора.Вставить("ФункцияУчастника", 
		ПредопределенноеЗначение("Перечисление.ФункцииУчастниковСогласования.ОбрабатывающийРезультат"));
	Возврат Участники.НайтиСтроки(ПараметрыОтбора);
	
КонецФункции

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	НастройкиДействий.ОбработкаЗаполнения(
		ЭтотОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка);
	
	ВидОбъекта = Неопределено;
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") И
		ДанныеЗаполнения.Свойство("ПравилоОбработки") Тогда
		ВидОбъекта = Справочники.ПравилаОбработки.ВидОбъектаКорневогоПравила(
			ДанныеЗаполнения.ПравилоОбработки);
		Если ТипЗнч(ВидОбъекта) = Тип("СправочникСсылка.ВидыДокументов") Тогда
			ФормироватьВизы = Истина;
		ИначеЕсли ТипЗнч(ВидОбъекта) = Тип("СправочникСсылка.ВидыМероприятий") Тогда
			СогласованиеВРежимеЗамечаний = Ложь;
			ПодписыватьУЭП = Ложь;
			ФормироватьВизы = Ложь;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	НастройкиДействий.ПередЗаписью(Ссылка, ДополнительныеСвойства, Отказ);
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	НастройкиДействий.ПриЗаписи(Ссылка, ДополнительныеСвойства, ПометкаУдаления, Отказ);
	
КонецПроцедуры

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли