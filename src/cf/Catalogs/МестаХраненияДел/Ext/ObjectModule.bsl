#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьСхемыПомещений") Тогда
		Если Не Ссылка.Пустая() Тогда 
			ПредыдущееПомещение = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Ссылка, "ТерриторияПомещение");
			
			Если ТерриторияПомещение <> ПредыдущееПомещение Тогда 
				УбратьСведенияОПомещении(ПредыдущееПомещение, Ссылка);
			КонецЕсли;
		КонецЕсли;
		
		ТерриторияСодержитМестаХранения(ТерриторияПомещение);
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Если Не ЗначениеЗаполнено(Организация) Тогда 
		Организация = Справочники.Организации.ОрганизацияПоУмолчанию();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

Процедура УбратьСведенияОПомещении(Территория, МестоХранения)  
	
	Если Не ЗначениеЗаполнено(Территория) Тогда 
		Возврат;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	МестаХраненияДел.Ссылка
		|ИЗ
		|	Справочник.МестаХраненияДел КАК МестаХраненияДел
		|ГДЕ
		|	МестаХраненияДел.ТерриторияПомещение В ИЕРАРХИИ(&Территория)
		|	И МестаХраненияДел.Ссылка <> &МестоХранения";
	Запрос.УстановитьПараметр("Территория", Территория);
	Запрос.УстановитьПараметр("МестоХранения", МестоХранения);
	
	Выборка = Запрос.Выполнить();
	
	Запись = РегистрыСведений.СведенияОПомещениях.СоздатьМенеджерЗаписи();
	Запись.Объект = Территория;
	Запись.Прочитать();
	
	СодержитМестаХранения = Не Выборка.Пустой();
	
	Если СодержитМестаХранения <> Запись.СодержитМестаХранения Тогда 
		Запись.Объект = Территория;
		Запись.СодержитМестаХранения = Не Выборка.Пустой();
		Запись.Записать(Истина);
		
		РодительскаяТерритория = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Территория, "Родитель");
		Если ЗначениеЗаполнено(РодительскаяТерритория) Тогда 
			УбратьСведенияОПомещении(РодительскаяТерритория, МестоХранения);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ТерриторияСодержитМестаХранения(Территория)  
	
	Если Не ЗначениеЗаполнено(Территория) Тогда 
		Возврат;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запись = РегистрыСведений.СведенияОПомещениях.СоздатьМенеджерЗаписи();
	Запись.Объект = Территория;
	Запись.Прочитать();
	
	Если Не Запись.СодержитМестаХранения Тогда 
		Запись.Объект = Территория;
		Запись.СодержитМестаХранения = Истина;
		Запись.Записать(Истина);
		
		РодительскаяТерритория = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Территория, "Родитель");
		Если ЗначениеЗаполнено(РодительскаяТерритория) Тогда 
			ТерриторияСодержитМестаХранения(РодительскаяТерритория);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли