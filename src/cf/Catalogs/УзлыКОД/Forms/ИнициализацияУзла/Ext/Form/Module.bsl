
#Область ОписаниеПеременных

&НаКлиенте
Перем ОтветЗакрытьФорму;

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Не КОДСервер.НеобходимаИнициализацияУзла() Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	ЭтоПолноправныйПользователь = Пользователи.ЭтоПолноправныйПользователь();
	
	НастроитьВидимостьЭлементов(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ПроверитьДоступностьНастройкаУзла();
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если ЗавершениеРаботы Тогда
		Возврат;
	КонецЕсли;
	
	Если ОтветЗакрытьФорму <> Истина И Не НастройкаЗавершена Тогда
		Отказ = Истина;
		ЗадатьВопросОПрекращенииРаботы();	
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ВариантИспользованияПриИзменении(Элемент)
	
	НастроитьВидимостьЭлементов(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ВариантИнициализацииПериферийногоУзлаПриИзменении(Элемент)
	
	НастроитьВидимостьЭлементов(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ФайлИнициализацииНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	КОДКлиент.ВыбратьФайлНастроек(
		Новый ОписаниеОповещения("ПослеВыбораФайлаНастроек", ЭтотОбъект), ФайлИнициализации);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ИнициализироватьУзел(Команда)
	
	НастройкаЗавершена = Ложь;
	
	Если ВариантИспользования = 1
			И ВариантИнициализацииПериферийногоУзла = 1 Тогда
			
		Если Не ЗначениеЗаполнено(ФайлИнициализации) Тогда
			ПредупреждениеАсинх(Нстр(
				"ru = 'Нужно указать путь к файлу с настройками.'",
				ОбщегоНазначенияКлиент.КодОсновногоЯзыка()));
			Возврат;
		КонецЕсли;
	КонецЕсли;

	ПрименитьНастройки();

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Асинх Процедура ПроверитьДоступностьНастройкаУзла()
	
	Если Не ЭтоПолноправныйПользователь Тогда
		Ждать ПредупреждениеАсинх(
			Нстр("ru = 'Первоначальная настройка узла должна производиться от имени полноправного пользователя.
						|Работа программы будет завершена.'",
				ОбщегоНазначенияКлиент.КодОсновногоЯзыка()));
				
		ОтветЗакрытьФорму = Истина;
		Закрыть(НастройкаЗавершена);
	КонецЕсли;
			
КонецПроцедуры

&НаКлиенте
Процедура ЗадатьВопросОПрекращенииРаботы()
		
	Оповещение = Новый ОписаниеОповещения(
		"ЗадатьВопросОПрекращенииРаботыЗавершение", ЭтотОбъект);
	
	ТекстВопроса =
		НСтр("ru = 'Не выбран вариант использования информационной базы, поэтому работа программы будет завершена.
					|Вы желаете отказаться от выбора варианта и завершить работу программы?'",
			ОбщегоНазначенияКлиент.КодОсновногоЯзыка());
		
	Кнопки = Новый СписокЗначений;
	Кнопки.Добавить(
		КодВозвратаДиалога.Да,
		Нстр("ru = 'Прервать настройку и прекратить работу'", ОбщегоНазначенияКлиент.КодОсновногоЯзыка()));
	Кнопки.Добавить(
		КодВозвратаДиалога.Отмена,
		Нстр("ru = 'Отмена'", ОбщегоНазначенияКлиент.КодОсновногоЯзыка()));
	
	ПараметрыВопроса = СтандартныеПодсистемыКлиент.ПараметрыВопросаПользователю();
	ПараметрыВопроса.Заголовок = НСтр("ru = 'Внимание'", ОбщегоНазначенияКлиент.КодОсновногоЯзыка());
	ПараметрыВопроса.БлокироватьВесьИнтерфейс = Истина;
	ПараметрыВопроса.КнопкаПоУмолчанию = КодВозвратаДиалога.Да;
	ПараметрыВопроса.ПредлагатьБольшеНеЗадаватьЭтотВопрос = Ложь;
	ПараметрыВопроса.Картинка = БиблиотекаКартинок.ДиалогВопрос;
	
	СтандартныеПодсистемыКлиент.ПоказатьВопросПользователю(Оповещение, ТекстВопроса, Кнопки, ПараметрыВопроса);
		
КонецПроцедуры

&НаКлиенте
Процедура ЗадатьВопросОПрекращенииРаботыЗавершение(Ответ, ДополнительныеПараметры) Экспорт
		
	Если ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Ответ, "Значение") = КодВозвратаДиалога.Да Тогда
		ОтветЗакрытьФорму = Истина;
		Закрыть(НастройкаЗавершена);
	Иначе
		ОтветЗакрытьФорму = Неопределено;
	КонецЕсли;
		
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура НастроитьВидимостьЭлементов(Форма)
	
	ЭлементыФормы = Форма.Элементы;
	ЭлементыФормы.ГруппаПериферийныйУзел.Видимость = Форма.ВариантИспользования = 1;
	ЭлементыФормы.ГруппаНастройкиИнициализацииУзла.Видимость = Форма.ВариантИнициализацииПериферийногоУзла = 1;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеВыбораФайлаНастроек(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> Неопределено Тогда
		ФайлИнициализации = Результат;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПрименитьНастройки()
	
	ОписаниеОповещенияОЗавершении =
		Новый ОписаниеОповещения("ОтметитьОкончаниеНастройкиУзла", ЭтотОбъект);
	
	Если ВариантИспользования = 1 Тогда 
		Если ВариантИнициализацииПериферийногоУзла = 0 Тогда
			ПараметрыИнициализации = Новый Структура(
				"Форма, ОбработчикЗавершения", ЭтотОбъект, ОписаниеОповещенияОЗавершении);
			
			КОДКлиент.ПодготовитьУзелКОтложеннойНастройке(ПараметрыИнициализации);
		Иначе
			ПараметрыЗагрузки = КОДКлиент.ПараметрыЗагрузкиФайлаНастроекУзла();
			ПараметрыЗагрузки.ИмяФайла = ФайлИнициализации;
			ПараметрыЗагрузки.Форма = ЭтотОбъект;
			ПараметрыЗагрузки.ВключитьРегламентныеЗаданияОбмена = ВключитьРегламентныеЗаданияОбмена;
			ПараметрыЗагрузки.ОбработчикЗавершения = ОписаниеОповещенияОЗавершении;
			
			КОДКлиент.ЗагрузитьНастройкиУзлаИзФайла(ПараметрыЗагрузки);
		КонецЕсли;
	Иначе
		ВыполнитьОбработкуОповещения(
			ОписаниеОповещенияОЗавершении, Новый Структура("Статус", "Выполнено"));
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура ОтметитьОкончаниеНастройкиУзла(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат) <> Тип("Структура") Тогда
		НастройкаЗавершена = Ложь;
	
		ТекстОшибки =
			Нстр("ru = 'Во время применения настроек произошла ошибка:
						|Неизвестная ошибка.'",
				ОбщегоНазначенияКлиент.КодОсновногоЯзыка());
	
		ВызватьИсключение ТекстОшибки;
	КонецЕсли;
	
	Если Результат.Статус = "Выполнено" Тогда
		НастройкаЗавершена = Истина;
		ЗафиксироватьРезультатНастройкиВИБ();
		Закрыть(НастройкаЗавершена);
	Иначе
		НастройкаЗавершена = Ложь;
	
		ТекстОшибки = СтрШаблон(
			Нстр("ru = 'Во время применения настроек произошла ошибка:
						|%1'", ОбщегоНазначенияКлиент.КодОсновногоЯзыка()),
			Результат.ПодробноеПредставлениеОшибки);
	
		ВызватьИсключение ТекстОшибки;
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ЗафиксироватьРезультатНастройкиВИБ()
	
	КОДСервер.ЗавершитьИнициализациюУзла();
	
КонецПроцедуры

#КонецОбласти
