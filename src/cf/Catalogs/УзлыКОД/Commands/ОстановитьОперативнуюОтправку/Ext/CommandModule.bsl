
// @strict-types

#Область ОбработчикиСобытий

// Обработчик команды.
//
// Параметры:
//	ПараметрКоманды - СправочникСсылка.УзлыКОД - Узел обмена данными.
//	ПараметрыВыполненияКоманды -Структура Из КлючИЗначение - Параметры выполнения команды.
//
&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	ПараметрыОстановки = ПараметрыОстановкиОбменаДанными();
	ПараметрыОстановки.Узел = ПараметрКоманды;
	ПараметрыОстановки.Направление = ПредопределенноеЗначение("Перечисление.НаправленияПотоковКОД.Отправка");
	
	Если ОтправкаДанныхОстановлена(ПараметрКоманды) Тогда
		ПараметрыОстановки.Остановлен = Ложь;
		
		ОписаниеОповещения = Новый ОписаниеОповещения(
			"ОстановитьОперативнуюОтправкуЗавершение", ЭтотОбъект, ПараметрыОстановки);
		
		ОбщегоНазначенияДокументооборотКлиент.ПоказатьВопросДаНет(
			ОписаниеОповещения,
			СтрШаблон(НСтр("ru = 'Возобновить отправку оперативных данных в узел ""%1""?'"), ПараметрКоманды));
	Иначе
		ПараметрыОстановки.Остановлен = Истина;
		ПараметрыОстановки.ГраницаОстановки = ГраницаОстановки();
		
		ОписаниеОповещения = Новый ОписаниеОповещения(
			"ОстановитьОперативнуюОтправкуЗавершение", ЭтотОбъект, ПараметрыОстановки);
		
		ОбщегоНазначенияДокументооборотКлиент.ПоказатьВопросДаНет(
			ОписаниеОповещения,
			СтрШаблон(НСтр("ru = 'Остановить отправку оперативных данных в узел ""%1""?'"), ПараметрКоманды));
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Конструктор параметров остановки обмена данными.
//
// Возвращаемое значение:
//	см. КОДСервер.ПараметрыОстановкиОбмена.
//
&НаСервере
Функция ПараметрыОстановкиОбменаДанными()
	
	Возврат КОДСервер.ПараметрыОстановкиОбмена();
	
КонецФункции

// Возвращает границу остановки обмена данными по направлению.
//
// Возвращаемое значение:
//	Число - Граница остановки.
//
&НаСервере
Функция ГраницаОстановки()
	
	Возврат ОтметкиВремени.Текущая();
	
КонецФункции

// Выполняет установку/снятие признака остановки обмена данными по направлению.
//
// Параметры:
//	Результат - КодВозвратаДиалога - Результат выбора пользователя.
//	ПараметрыОповещения - см. ПараметрыОстановкиОбменаДанными.
//
&НаКлиенте
Процедура ОстановитьОперативнуюОтправкуЗавершение(Результат, ПараметрыОповещения) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		ПриостановитьОтправкуДанных(ПараметрыОповещения);
		Оповестить("КОД_ИзменениеСтатусаОстановлен", ПараметрыОповещения.Узел);
	КонецЕсли;
	
КонецПроцедуры

// Определяет остановлена ли отправка данных в указанный узел.
//
// Параметры:
//	ПараметрыОповещения - см. ПараметрыОстановкиОбменаДанными.
//
&НаСервере
Процедура ПриостановитьОтправкуДанных(ПараметрыОповещения)
	
	КОДСервер.ОстановитьПолучениеДанных(ПараметрыОповещения);
	
КонецПроцедуры

// Определяет остановлена ли отправка данных в указанный узел.
//
// Параметры:
//	Узел - СправочникСсылка.УзлыКОД - Узел обмена данными.
//
// Возвращаемое значение:
//	Булево - Истина, если отправка данных приостановлена.
//
&НаСервере
Функция ОтправкаДанныхОстановлена(Узел)
	
	Возврат РегистрыСведений.СостоянияУзловКОД.ОбменСУзломОстановлен(
		Узел, Перечисления.НаправленияПотоковКОД.Отправка);
	
КонецФункции

#КонецОбласти
