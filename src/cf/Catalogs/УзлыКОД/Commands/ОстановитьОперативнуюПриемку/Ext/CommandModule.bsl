
// @strict-types

#Область ОбработчикиСобытий

// Обработчик команды.
//
// Параметры:
//	ПараметрКоманды - СправочникСсылка.УзлыКОД - Узел обмена данными.
//	ПараметрыВыполненияКоманды - Структура Из КлючИЗначение - Параметры выполнения команды.
//
&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	ПараметрыОстановки = ПараметрыОстановкиОбменаДанными();
	ПараметрыОстановки.Узел = ПараметрКоманды;
	ПараметрыОстановки.Направление = ПредопределенноеЗначение("Перечисление.НаправленияПотоковКОД.Получение");
	
	Если ПолучениеДанныхОстановлено(ПараметрКоманды) Тогда
		ПараметрыОстановки.Остановлен = Ложь;
		
		ОписаниеОповещения = Новый ОписаниеОповещения(
			"ОстановитьОперативноеПолучениеЗавершение", ЭтотОбъект, ПараметрыОстановки);
		
		ОбщегоНазначенияДокументооборотКлиент.ПоказатьВопросДаНет(
			ОписаниеОповещения,
			СтрШаблон(НСтр("ru = 'Возобновить получение оперативных данных из узла ""%1""?'"), ПараметрКоманды));
	Иначе
		ПараметрыОстановки.Остановлен = Истина;
		
		ОписаниеОповещения = Новый ОписаниеОповещения(
			"ОстановитьОперативноеПолучениеЗавершение", ЭтотОбъект, ПараметрыОстановки);
		
		ОбщегоНазначенияДокументооборотКлиент.ПоказатьВопросДаНет(
			ОписаниеОповещения,
			СтрШаблон(НСтр("ru = 'Остановить получение оперативных данных из узла ""%1""?'"), ПараметрКоманды));
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Конструктор параметров остановки обмена данными.
//
// Возвращаемое значение:
//	см. КОДСервер.ПараметрыОстановкиОбмена.
//
&НаСервере
Функция ПараметрыОстановкиОбменаДанными()
	
	Возврат КОДСервер.ПараметрыОстановкиОбмена();
	
КонецФункции

// Выполняет установку/снятие признака остановки обмена данными по направлению.
//
// Параметры:
//	Результат - КодВозвратаДиалога - Результат выбора пользователя.
//	ПараметрыОповещения - см. ПараметрыОстановкиОбменаДанными.
//
&НаКлиенте
Процедура ОстановитьОперативноеПолучениеЗавершение(Результат, ПараметрыОповещения) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		ОстановитьПолучениеДанных(ПараметрыОповещения);
		Оповестить("КОД_ИзменениеСтатусаОстановлен", ПараметрыОповещения.Узел);
	КонецЕсли;
	
КонецПроцедуры

// Выполняет остановку получения данных из указанного узла.
//
// Параметры:
//	ПараметрыОповещения - см. ПараметрыОстановкиОбменаДанными.
//
&НаСервере
Процедура ОстановитьПолучениеДанных(ПараметрыОповещения)
	
	КОДСервер.ОстановитьПолучениеДанных(ПараметрыОповещения);
	
КонецПроцедуры

// Определяет остановлено ли получение данных из указанного узла.
//
// Параметры:
//	Узел - СправочникСсылка.УзлыКОД - Узел обмена данными.
//
// Возвращаемое значение:
//	Булево - Истина, если получение данных приостановлено.
//
&НаСервере
Функция ПолучениеДанныхОстановлено(Узел)
	
	Возврат РегистрыСведений.СостоянияУзловКОД.ОбменСУзломОстановлен(
		Узел, Перечисления.НаправленияПотоковКОД.Получение);
	
КонецФункции

#КонецОбласти
