#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ТипОбъекта = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(
		Параметры.Автоподстановка,
		"ТипОбъекта");
	
	Если ТипОбъекта = Перечисления.ТипыОбъектов.ДокументыПредприятия Тогда
		
		Элементы.ПроцессСсылка.ВыбиратьТип = Ложь;
		ДокументСсылка = Справочники.ДокументыПредприятия.ПустаяСсылка();
		
	ИначеЕсли ТипОбъекта = Перечисления.ТипыОбъектов.Мероприятия Тогда
		
		Элементы.ПроцессСсылка.ВыбиратьТип = Ложь;
		ДокументСсылка = Справочники.Мероприятия.ПустаяСсылка();
		
	ИначеЕсли ТипОбъекта = Перечисления.ТипыОбъектов.ЗадачиПроцессов Тогда
		
		Элементы.ПроцессСсылка.ВыбиратьТип = Ложь;
		ДокументСсылка = Задачи.ЗадачаИсполнителя.ПустаяСсылка();
	
	ИначеЕсли ТипОбъекта = Перечисления.ТипыОбъектов.НаименованияОписанияЗадач Тогда
		
		Элементы.ПроцессСсылка.ВыбиратьТип = Истина;
		ВключаяСтроки = Истина;
			
	ИначеЕсли ТипОбъекта = Перечисления.ТипыОбъектов.Задача Тогда
		
		Элементы.ПроцессСсылка.ВыбиратьТип = Ложь;
		ДокументСсылка = Документы.Задача.ПустаяСсылка();
		ВключаяСтроки = Истина;
		
	ИначеЕсли ТипОбъекта = Перечисления.ТипыОбъектов.ПустаяСсылка() Тогда
		
		Элементы.ПроцессСсылка.ВыбиратьТип = Истина;
		
	Иначе
		
		ВызватьИсключение СтрШаблон(НСтр("ru = 'Неизвестный тип объекта %1.'"), ТипОбъекта);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Проверить(Команда)
	
	Если Не ЗначениеЗаполнено(ДокументСсылка) Тогда
		Сообщить(НСтр("ru = 'Не заполнен документ'"));
		Возврат;
	КонецЕсли;
	
	ПроверитьНаСервере();
	
КонецПроцедуры


#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыРезультатТаблица

&НаКлиенте
Процедура РезультатТаблицаВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ПоказатьЗначение(, Элемент.ТекущиеДанные.Элемент);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПроверитьНаСервере()
	
	РезультатТаблица.Очистить();
	ДокументОбъект = ДокументСсылка.ПолучитьОбъект();
	РезультатВыполнения = ШаблоныДокументов.ПолучитьЗначениеАвтоподстановки(
		Параметры.Автоподстановка, ДокументОбъект,, ВключаяСтроки);
	
	Если ТипЗнч(РезультатВыполнения) = Тип("ФорматированныйДокумент") Тогда
		ФорматированныйДокумент = РезультатВыполнения; // ФорматированныйДокумент
		РезультатВыполнения = ФорматированныйДокумент.ПолучитьТекст();
	КонецЕсли;
	
	РезультатВМассиве = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(РезультатВыполнения);
	Если ТипЗнч(РезультатВыполнения) = Тип("Массив") И РезультатВыполнения.Количество() > 0 Тогда
		РезультатВМассиве = РезультатВыполнения;
	КонецЕсли;
	
	Для Каждого Эл Из РезультатВМассиве Цикл
		Стр = РезультатТаблица.Добавить();
		Стр.Элемент = Эл;
		Стр.ТипЭлемента = ТипЗнч(Эл);
		Если Стр.ТипЭлемента = Тип("Массив") И Эл.Количество() = 0 Тогда
			Стр.Элемент = НСтр("ru = '<Пустой массив>'");
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти