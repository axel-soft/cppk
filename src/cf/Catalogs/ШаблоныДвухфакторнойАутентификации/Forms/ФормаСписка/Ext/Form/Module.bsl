
#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПрочитатьШаблоныИзБазыДанных(Команда)
	ПрочитатьШаблоныИзБазыДанныхНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьВсеНастройкиДвухфакторнойАутентификации(Команда)
	Оповещение = Новый ОписаниеОповещения("ОчиститьВсеНастройкиДвухфакторнойАутентификацииПродолжение",
		ЭтотОбъект);
		
	ПоказатьВопрос(Оповещение,
		НСтр("ru='Все шаблоны будут помечены на удаление, а у пользователей информационной базы будут очищены оставшиеся настройки двухфакторной аутентификации. Продолжить?'"),
		РежимДиалогаВопрос.ДаНет);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ОчиститьВсеНастройкиДвухфакторнойАутентификацииПродолжение(Результат, Параметры) Экспорт
	Если Результат = КодВозвратаДиалога.Да Тогда
		ОчиститьВсеНастройкиДвухфакторнойАутентификацииНаСервере();
	КонецЕсли;
КонецПроцедуры

// Считывает шаблоны 2ФА из ИБ, и на их основании создает шаблоны 2ФА в справочнике
&НаСервере
Процедура ПрочитатьШаблоныИзБазыДанныхНаСервере()
	
	Шаблоны = ШаблоныНастроекВторогоФактораАутентификации.ПолучитьШаблоны();
	
	Для Каждого Шаблон Из Шаблоны Цикл
		НайденныйШаблон = Справочники.ШаблоныДвухфакторнойАутентификации.НайтиПоНаименованию(Шаблон.Имя);
		Если НайденныйШаблон.Пустая() Тогда
			НовыйШаблон = Справочники.ШаблоныДвухфакторнойАутентификации.СоздатьЭлемент();
			НовыйШаблон.Наименование = Шаблон.Имя;
			НовыйШаблон.МетодHTTP = Шаблон.МетодHTTPЗапросаНаАутентификацию;
			НовыйШаблон.АдресРесурса = Шаблон.HTTPЗапросНаАутентификацию.АдресРесурса;
			
			// Создадим параметр &secret
			НовыйПараметр = НовыйШаблон.Параметры.Добавить();
			НовыйПараметр.ИмяПараметра = "secret_code";
			НовыйПараметр.ЗначениеПараметра = "&secret";

			НовыйШаблон.Записать();
		Иначе
			Сообщение = СтрШаблон(НСтр("ru='Шаблон с наименованием ""%1"" уже существует'"), Шаблон.Имя);
			ОбщегоНазначения.СообщитьПользователю(Сообщение);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

// Помечает на удаление все шаблоны 2ФА, после чего очищает все оставшиеся настройки у пользователей ИБ
&НаСервере
Процедура ОчиститьВсеНастройкиДвухфакторнойАутентификацииНаСервере()

	Справочники.ШаблоныДвухфакторнойАутентификации.ОчиститьВсеНастройкиДвухфакторнойАутентификации();
	
КонецПроцедуры

#КонецОбласти