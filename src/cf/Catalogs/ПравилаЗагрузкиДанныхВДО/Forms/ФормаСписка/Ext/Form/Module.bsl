#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Параметры.Свойство("ПредставлениеИС", ПредставлениеИС);
	Параметры.Свойство("ИнтегрированнаяСистема", ИнтегрированнаяСистема);
	Параметры.Свойство("ИмяМакета", ИмяМакета);
	
	Если ЗначениеЗаполнено(ПредставлениеИС) Тогда
		АвтоЗаголовок = Ложь;
		Заголовок = СтрШаблон(НСтр("ru = 'Правила загрузки данных в 1С:Документооборот из %1'"), ПредставлениеИС);
		
		Элементы.ПредставлениеОбъектаИС.Заголовок = СтрШаблон(НСтр("ru = 'В %1'"), ПредставлениеИС);
	КонецЕсли;
	
	Элементы.ИнтегрированнаяСистема.ТолькоПросмотр = Истина;
	Если ЗначениеЗаполнено(ИнтегрированнаяСистема) Тогда
		УзелИнтегрированнойСистемы = ИнтегрированнаяСистема;
	ИначеЕсли ЗначениеЗаполнено(ИмяМакета) Тогда
		УзелИнтегрированнойСистемы = ИмяМакета;
	Иначе
		УзелИнтегрированнойСистемы = Неопределено;
		Элементы.ИнтегрированнаяСистема.ТолькоПросмотр = Ложь;
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
		Список,
		"УзелИнтегрированнойСистемы",
		УзелИнтегрированнойСистемы,
		ВидСравненияКомпоновкиДанных.Равно);
	
	ОбновитьСписокИС();
	
	Элементы.ГруппаКоманднаяПанель.Доступность = (ДанныеИнтегрированныхСистем.Количество() > 0);
	Элементы.Список.Доступность = (ДанныеИнтегрированныхСистем.Количество() > 0);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Элементы.ПоказатьУдаленные.Пометка = ПоказыватьУдаленные;
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
		Список,
		"ПометкаУдаления",
		Ложь,
		ВидСравненияКомпоновкиДанных.Равно,,
		Не ПоказыватьУдаленные,
		РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный);
	
	Если ПредставлениеИС = "" И ДанныеИнтегрированныхСистем.Количество() > 0 Тогда
		ЗаполнитьИнтегрированнуюСистемуИзСпискаВыбора(ДанныеИнтегрированныхСистем[0].ИнтегрированнаяСистема);
		ЗаполнитьДанныеИнтегрированнойСистемы();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ЗаписьИнтегрированнойСистемы" Тогда
		ОбновитьСписокИС();
		Элементы.ГруппаКоманднаяПанель.Доступность = (ДанныеИнтегрированныхСистем.Количество() > 0);
		Элементы.Список.Доступность = (ДанныеИнтегрированныхСистем.Количество() > 0);
	КонецЕсли;
	
	Если ИмяСобытия = "Запись_ДополнительныеРеквизитыИСведения"
			Или ИмяСобытия = "Запись_НаборыДополнительныхРеквизитовИСведений" Тогда
		ОбновитьПовторноИспользуемыеЗначения();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ИнтегрированнаяСистемаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ИнтегрированнаяСистемаОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ЗаполнитьИнтегрированнуюСистемуИзСпискаВыбора(ВыбранноеЗначение);
	ЗаполнитьДанныеИнтегрированнойСистемы();
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Отказ = Истина;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ПредставлениеИС", ПредставлениеИС);
	ПараметрыФормы.Вставить("ИнтегрированнаяСистема", ИнтегрированнаяСистема);
	ПараметрыФормы.Вставить("ИмяМакета", ИмяМакета);
	ПараметрыФормы.Вставить("ИзМакета", ЗначениеЗаполнено(ИмяМакета));
	
	Если Копирование Тогда
		ПараметрыФормы.Вставить("ЗначениеКопирования", Элементы.Список.ТекущаяСтрока);
	КонецЕсли;
	
	ОткрытьФорму("Справочник.ПравилаЗагрузкиДанныхВДО.Форма.ФормаЭлемента",
		ПараметрыФормы,
		Элементы.Список);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПоказатьУдаленные(Команда)
	
	ПоказыватьУдаленные = Не ПоказыватьУдаленные;
	Элементы.ПоказатьУдаленные.Пометка = ПоказыватьУдаленные;
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
		Список,
		"ПометкаУдаления",
		Ложь,
		ВидСравненияКомпоновкиДанных.Равно,,
		Не ПоказыватьУдаленные,
		РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ЗаполнитьДанныеИнтегрированнойСистемы()
	
	ПредставлениеИС = "";
	
	Если ЗначениеЗаполнено(ИнтегрированнаяСистема) Тогда
		СтрокиДанныеИС = ДанныеИнтегрированныхСистем.НайтиСтроки(
			Новый Структура("ИнтегрированнаяСистема", ИнтегрированнаяСистема));
		
	ИначеЕсли ЗначениеЗаполнено(ИмяМакета) Тогда
		СтрокиДанныеИС = ДанныеИнтегрированныхСистем.НайтиСтроки(
			Новый Структура("ИнтегрированнаяСистема", ИмяМакета));
		
	Иначе
		Возврат;
		
	КонецЕсли;
	
	Если СтрокиДанныеИС.Количество() > 0 Тогда
		ПредставлениеИС = СтрокиДанныеИС[0].Наименование;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьИнтегрированнуюСистемуИзСпискаВыбора(ЗначениеСпискаВыбора)
	
	ИнтегрированнаяСистема = ПредопределенноеЗначение("ПланОбмена.ИнтегрированныеСистемы.ПустаяСсылка");
	ИмяМакета = "";
	Если ТипЗнч(ЗначениеСпискаВыбора) = Тип("ПланОбменаСсылка.ИнтегрированныеСистемы") Тогда
		ИнтегрированнаяСистема = ЗначениеСпискаВыбора;
	ИначеЕсли ТипЗнч(ЗначениеСпискаВыбора) = Тип("Строка") Тогда
		ИмяМакета = ЗначениеСпискаВыбора;
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
		Список,
		"УзелИнтегрированнойСистемы",
		ЗначениеСпискаВыбора,
		ВидСравненияКомпоновкиДанных.Равно);
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьСписокИС()
	
	РаботаСИнтегрированнымиСистемамиВызовСервера.ОбновитьСписокИС(
		Элементы.ИнтегрированнаяСистема.СписокВыбора,
		ДанныеИнтегрированныхСистем);
	
КонецПроцедуры

#КонецОбласти