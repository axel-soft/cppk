#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ОбъектыИС = РаботаСИнтегрированнымиСистемамиПовтИсп.СтруктураМетаданныхИнтегрированнойСистемы(
		?(ЗначениеЗаполнено(Параметры.ИнтегрированнаяСистема), Параметры.ИнтегрированнаяСистема, Параметры.ИмяМакета),
		Истина);
	
	ЗначениеВРеквизитФормы(ОбъектыИС, "ДеревоМетаданных");
	
	Если ЗначениеЗаполнено(Параметры.ТекущаяСтрока) Тогда
		Для Каждого СтрокаГруппа Из ДеревоМетаданных.ПолучитьЭлементы() Цикл
			Для Каждого Строка Из СтрокаГруппа.ПолучитьЭлементы() Цикл
				Если Строка.Имя = Параметры.ТекущаяСтрока Тогда
					Элементы.ДеревоМетаданных.ТекущаяСтрока = Строка.ПолучитьИдентификатор();
					Прервать;
				КонецЕсли;
			КонецЦикла;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыДеревоМетаданных

&НаКлиенте
Процедура ДеревоМетаданныхВыборЗначения(Элемент, Значение, СтандартнаяОбработка)
	
	ОбработатьВыборЭлементаДерева();

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Выбрать(Команда)
	
	ОбработатьВыборЭлементаДерева();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ОбработатьВыборЭлементаДерева()
	
	ТекущиеДанные = Элементы.ДеревоМетаданных.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Или ТекущиеДанные.Имя = "" Тогда
		Возврат;
	КонецЕсли;
	
	Результат = Новый Структура;
	Результат.Вставить("Имя", ТекущиеДанные.Имя);
	Результат.Вставить("Синоним", ТекущиеДанные.Синоним);
	
	Закрыть(Результат);
	
КонецПроцедуры

#КонецОбласти