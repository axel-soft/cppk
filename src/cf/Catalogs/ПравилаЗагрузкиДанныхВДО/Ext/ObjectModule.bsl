#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ)
	
	ПроверитьВозможностьЗаписиПравила();
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ОбновитьПредставление();
	
	Для Каждого ПравилоЗаполнения Из ПравилаЗаполненияРеквизитовДО Цикл
		
		Если Не ЗначениеЗаполнено(ПравилоЗаполнения.ID) Тогда
			ПравилоЗаполнения.ID = Строка(Новый УникальныйИдентификатор);
		КонецЕсли;
		
		Если ПравилоЗаполнения.Вариант = Перечисления.ВариантыПравилЗаполненияРеквизитов.РеквизитТаблицы Тогда
			БылиИзменения = Ложь;
			ПравилаЗаполненияСтрокТаблицы = ПравилоЗаполнения.ПравилаЗаполненияСтрокТаблицы.Получить();
			Если ТипЗнч(ПравилаЗаполненияСтрокТаблицы) = Тип("ТаблицаЗначений") Тогда
				БылиИзменения = ОбновитьИдентификаторыПравилаЗаполненияСтрокТаблицы(ПравилаЗаполненияСтрокТаблицы);
			КонецЕсли;
			Если БылиИзменения Тогда
				ПравилоЗаполнения.ПравилаЗаполненияСтрокТаблицы = Новый ХранилищеЗначения(ПравилаЗаполненияСтрокТаблицы);
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Обновляет представление правила согласно представлениям объектов и ключевых реквизитов.
//
Процедура ОбновитьПредставление()
	
	МассивПредставлениеОбъектаДО = Новый Массив;
	МассивПредставлениеОбъектаДО.Добавить(ПредставлениеОбъектаДО);
	
	Если СокрЛП(Комментарий) <> "" Тогда
		МассивПредставлениеОбъектаДО.Добавить(СокрЛП(Комментарий));
	КонецЕсли;
	
	// Соберем наименование.
	Наименование = СтрШаблон("%1 - %2", СтрСоединить(МассивПредставлениеОбъектаДО, ", "), ПредставлениеОбъектаИС);
	
КонецПроцедуры

Функция ОбновитьИдентификаторыПравилаЗаполненияСтрокТаблицы(ПравилаЗаполненияСтрокТаблицы)
	
	БылиИзменения = Ложь;
	
	Для Каждого Строка Из ПравилаЗаполненияСтрокТаблицы Цикл
		Для Каждого Колонка Из ПравилаЗаполненияСтрокТаблицы.Колонки Цикл
			Если Прав(Колонка.Имя, 3) <> "_ID" Или ЗначениеЗаполнено(Строка[Колонка.Имя]) Тогда
				Продолжить;
			КонецЕсли;
			
			ИмяРеквизита = Лев(Колонка.Имя, СтрДлина(Колонка.Имя) - 3);
			Если ЗначениеЗаполнено(Строка[СтрШаблон("%1_Вариант", ИмяРеквизита)]) Тогда
				Строка[СтрШаблон("%1_ID", ИмяРеквизита)] = Строка(Новый УникальныйИдентификатор);
				БылиИзменения = Истина;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	Возврат БылиИзменения;
	
КонецФункции

Процедура ПроверитьВозможностьЗаписиПравила()
	
	Если Пользователи.ЭтоПолноправныйПользователь() Тогда
		Возврат;
	КонецЕсли;
	
	СписокВыраженийДляЗаполненияРеквизитовДО = Новый Соответствие;
	СписокВыраженийДляЗаполненияРеквизитовТаблицыДО = Новый Соответствие;
	
	Для Каждого Строка Из ПравилаЗаполненияРеквизитовДО Цикл
		Ключ = Новый Структура("ИмяРеквизитаОбъектаДО, ЭтоТаблица, Таблица");
		ЗаполнитьЗначенияСвойств(Ключ, Строка);
		Если ЗначениеЗаполнено(Строка.ВычисляемоеВыражение) Тогда
			СписокВыраженийДляЗаполненияРеквизитовДО.Вставить(Ключ, Строка.ВычисляемоеВыражение);
		КонецЕсли;
		ПравилаЗаполненияСтрокТаблицы = Строка.ПравилаЗаполненияСтрокТаблицы.Получить();
		Если ТипЗнч(ПравилаЗаполненияСтрокТаблицы) = Тип("ТаблицаЗначений") Тогда
			МассивВыражений = Новый Массив;
			Для Каждого СтрокаТаблицы Из ПравилаЗаполненияСтрокТаблицы Цикл
				Для Каждого Колонка Из ПравилаЗаполненияСтрокТаблицы.Колонки Цикл
					Если Прав(Колонка.Имя, 21) <> "_ВычисляемоеВыражение" Тогда
						Продолжить;
					КонецЕсли;
					Если ЗначениеЗаполнено(СтрокаТаблицы[Колонка.Имя])
							И МассивВыражений.Найти(СтрокаТаблицы[Колонка.Имя]) = Неопределено Тогда
						МассивВыражений.Добавить(СтрокаТаблицы[Колонка.Имя]);
					КонецЕсли;
				КонецЦикла;
			КонецЦикла;
			Если МассивВыражений.Количество() > 0 Тогда
				СписокВыраженийДляЗаполненияРеквизитовТаблицыДО.Вставить(Ключ, МассивВыражений);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Если СписокВыраженийДляЗаполненияРеквизитовДО.Количество() = 0
			И СписокВыраженийДляЗаполненияРеквизитовТаблицыДО.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Если ЭтоНовый() Тогда
		ВызватьИсключение НСтр("ru = 'В правилах загрузки данных в 1С:Документооборот запрещено изменять выражения на встроенном языке.
			|Обратитесь к администратору.'");
	КонецЕсли;
	
	СтарыеПравилаЗаполненияРеквизитовДО =
		ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Ссылка, "ПравилаЗаполненияРеквизитовДО").Выгрузить();
	
	Для Каждого ТекущееВыражение Из СписокВыраженийДляЗаполненияРеквизитовДО Цикл
		Строки = СтарыеПравилаЗаполненияРеквизитовДО.НайтиСтроки(ТекущееВыражение.Ключ);
		Если Строки.Количество() = 1 И Строки[0].ВычисляемоеВыражение = ТекущееВыражение.Значение Тогда
			Продолжить;
		КонецЕсли;
		ВызватьИсключение НСтр("ru = 'В правилах загрузки данных в 1С:Документооборот запрещено изменять выражения на встроенном языке.
			|Обратитесь к администратору.'");
	КонецЦикла;
	
	Для Каждого ТекущиеВыражения Из СписокВыраженийДляЗаполненияРеквизитовТаблицыДО Цикл
		Строки = СтарыеПравилаЗаполненияРеквизитовДО.НайтиСтроки(ТекущиеВыражения.Ключ);
		Если Строки.Количество() <> 1 Тогда
			ВызватьИсключение НСтр("ru = 'В правилах загрузки данных в 1С:Документооборот запрещено изменять выражения на встроенном языке.
				|Обратитесь к администратору.'");
		КонецЕсли;
		СтарыеПравилаЗаполненияСтрокТаблицы = Строки[0].ПравилаЗаполненияСтрокТаблицы.Получить();
		МассивСтарыхВыражений = Новый Массив;
		Для Каждого СтрокаТаблицы Из СтарыеПравилаЗаполненияСтрокТаблицы Цикл
			Для Каждого Колонка Из СтарыеПравилаЗаполненияСтрокТаблицы.Колонки Цикл
				Если Прав(Колонка.Имя, 21) <> "_ВычисляемоеВыражение" Тогда
					Продолжить;
				КонецЕсли;
				Если ЗначениеЗаполнено(СтрокаТаблицы[Колонка.Имя])
						И МассивСтарыхВыражений.Найти(СтрокаТаблицы[Колонка.Имя]) = Неопределено Тогда
					МассивСтарыхВыражений.Добавить(СтрокаТаблицы[Колонка.Имя]);
				КонецЕсли;
			КонецЦикла;
		КонецЦикла;
		Для Каждого ТекущееВыражение Из ТекущиеВыражения.Значение Цикл
			Если МассивСтарыхВыражений.Найти(ТекущееВыражение) = Неопределено Тогда
				ВызватьИсключение НСтр("ru = 'В правилах загрузки данных в 1С:Документооборот запрещено изменять выражения на встроенном языке.
					|Обратитесь к администратору.'");
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли