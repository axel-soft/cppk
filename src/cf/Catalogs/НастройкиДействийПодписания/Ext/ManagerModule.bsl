#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Заполняет дерево обработки участниками действия.
// 
// Параметры:
// 	ЭлементыДействия - ДанныеФормыЭлементДерева - коллекцию элементов дерева
// 	ЗначенияЗаполнения - Структура:
// 	 ВидДействия - СправочникСсылка.ВидыДействий - вид действия.
//	 Настройка - СправочникСсылка.НастройкиДействийПодписания - ссылка на настройку действия. 
//	 НастройкаВключена - Булево - Истина, если включена.
//
Процедура ДополнитьДеревоПредставлениемНастройки(ЭлементыДействия, ЗначенияЗаполнения) Экспорт
	
	Настройка = ЗначенияЗаполнения.Настройка;
	ИспользоватьДатуИВремяВСрокахЗадач = 
		ПолучитьФункциональнуюОпцию("ИспользоватьДатуИВремяВСрокахЗадач");
	
	Реквизиты = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Настройка,
		"ВидДействия, ВидДействия.Наименование, Автор, 
		|Этапы, Участники");
		
	ИдентификаторПодписать = ДействияСервер.ПредопределенныйИдентификаторУчастника(
			"ЭтапПодписать");
	Этапы = Реквизиты.Этапы.Выгрузить();
	Если Этапы.Количество() = 0 Тогда
		ЭтапУчастников = Этапы.Добавить();
		ЭтапУчастников.НаименованиеЭтапа = НСтр("ru = 'Согласовать'");
		ЭтапУчастников.Идентификатор = ИдентификаторПодписать;		
	КонецЕсли;
	
	Этапы.Сортировать("НомерСтроки");
	
	Участники = Реквизиты.Участники.Выгрузить();
	Участники.Сортировать("НомерСтроки");
	
	ЭлементДействия = ЭлементыДействия.Добавить();
	ЭлементДействия.ЭтоДействие = Истина;
	ЗаполнитьЗначенияСвойств(ЭлементДействия, ЗначенияЗаполнения);
	ЭлементДействия.Представление = Реквизиты.ВидДействияНаименование;
	
	ЕдинственныйЭтап = Этапы.Количество() = 1;
		
	ПодчиненныеЭлементы = ЭлементДействия.ПолучитьЭлементы();
	Для Каждого Этап Из Этапы Цикл
		
		Если ЕдинственныйЭтап Тогда
			ПодчиненныеЭлементыДействия = ПодчиненныеЭлементы;
			
			Если Этап.СрокОбщий = Истина Тогда
				ЭлементДействия.СрокПредставление = СрокиИсполненияПроцессовКлиентСервер.
					ПредставлениеСрокаИсполнения(
						Этап.Срок,
						Этап.СрокДни,
						Этап.СрокЧасы,
						Этап.СрокМинуты,
						ИспользоватьДатуИВремяВСрокахЗадач,
						Этап.ВариантУстановкиСрока);
			КонецЕсли;
		Иначе
			ПодчиненныйЭлемент = ПодчиненныеЭлементы.Добавить();
			ЗаполнитьЗначенияСвойств(ПодчиненныйЭлемент, Этап);
			ЗаполнитьЗначенияСвойств(ПодчиненныйЭлемент, ЗначенияЗаполнения);
			ПодчиненныйЭлемент.ЭтоЭтап = Истина;
			ПодчиненныйЭлемент.Представление = Этап.НаименованиеЭтапа;
			ПодчиненныеЭлементыДействия = ПодчиненныйЭлемент.ПолучитьЭлементы();
			
			Если Этап.СрокОбщий Тогда
				ПодчиненныйЭлемент.СрокПредставление = СрокиИсполненияПроцессовКлиентСервер.
					ПредставлениеСрокаИсполнения(
						Этап.Срок,
						Этап.СрокДни,
						Этап.СрокЧасы,
						Этап.СрокМинуты,
						ИспользоватьДатуИВремяВСрокахЗадач,
						Этап.ВариантУстановкиСрока);
			КонецЕсли;
		КонецЕсли;
		
		Для Каждого Участник Из Участники Цикл
			Если (ЗначениеЗаполнено(Участник.ИдентификаторЭтапа) 
					И Участник.ИдентификаторЭтапа <> Этап.Идентификатор)
				Или (Не ЗначениеЗаполнено(Участник.ИдентификаторЭтапа) 
					И Участник.ФункцияУчастника <> ПредопределенноеЗначение("Перечисление.ФункцииУчастниковПодписания.Подписывающий")) Тогда
				Продолжить;
			КонецЕсли;
			
			// На первом уровне выводим только тех, кто никому не подчинен.						
			Если ЗначениеЗаполнено(Участник.ВышестоящийУчастник) Тогда
				Продолжить;
			КонецЕсли;
			
			ПодчиненныйЭлементДействия = ПодчиненныеЭлементыДействия.Добавить();
			ЗаполнитьЗначенияСвойств(ПодчиненныйЭлементДействия, Участник);
			ЗаполнитьЗначенияСвойств(ПодчиненныйЭлементДействия, ЗначенияЗаполнения);
			ПодчиненныйЭлементДействия.ЭтоУчастник = Истина;
			ПодчиненныйЭлементДействия.Представление = Участник.Участник;
			ПодчиненныйЭлементДействия.УсловиеПредставление = Участник.Условие;
			
			ПодчиненныйЭлементДействия.СрокПредставление = СрокиИсполненияПроцессовКлиентСервер.
				ПредставлениеСрокаИсполнения(
					Участник.Срок,
					Участник.СрокДни,
					Участник.СрокЧасы,
					Участник.СрокМинуты,
					ИспользоватьДатуИВремяВСрокахЗадач,
					Участник.ВариантУстановкиСрока);

			// Второй уровень.					
			ПодчиненныеЭлементыДействия2 = ПодчиненныйЭлементДействия.ПолучитьЭлементы();		

			Для Каждого УчастникПодчиненный Из Участники Цикл

				Если УчастникПодчиненный.ВышестоящийУчастник <> Участник.Идентификатор Тогда
					Продолжить;
				КонецЕсли;						
				
				ПодчиненныйЭлементДействия = ПодчиненныеЭлементыДействия2.Добавить();
				ЗаполнитьЗначенияСвойств(ПодчиненныйЭлементДействия, УчастникПодчиненный);
				ЗаполнитьЗначенияСвойств(ПодчиненныйЭлементДействия, ЗначенияЗаполнения);
				ПодчиненныйЭлементДействия.ЭтоУчастник = Истина;
				ПодчиненныйЭлементДействия.Представление = УчастникПодчиненный.Участник;
				ПодчиненныйЭлементДействия.УсловиеПредставление = УчастникПодчиненный.Условие;
				
				ПодчиненныйЭлементДействия.СрокПредставление = СрокиИсполненияПроцессовКлиентСервер.
					ПредставлениеСрокаИсполнения(
						УчастникПодчиненный.Срок,
						УчастникПодчиненный.СрокДни,
						УчастникПодчиненный.СрокЧасы,
						УчастникПодчиненный.СрокМинуты,
						ИспользоватьДатуИВремяВСрокахЗадач,
						УчастникПодчиненный.ВариантУстановкиСрока);
						
			КонецЦикла;
			
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ОбработкаПолученияПолейПредставления(Поля, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Поля.Добавить("Ссылка");
	Поля.Добавить("ВидДействия");
	
КонецПроцедуры

Процедура ОбработкаПолученияПредставления(Данные, Представление, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Представление = НастройкиДействий.ПредставлениеНастройки(Данные.Ссылка, Данные.ВидДействия);
	
КонецПроцедуры

// Возвращает всех участников настройки действия
// 
// Параметры:
// 	НастройкаДействия - СправочникСсылка.НастройкиДействийПодписания - ссылка на действие
// 	
// Возвращаемое значение:
// 	ТаблицаЗначений - таблица участников действия.
// 
Функция ВсеУчастники(НастройкаДействия) Экспорт
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	Участники.Участник КАК Участник,
		|	ЗНАЧЕНИЕ(Перечисление.ФункцииУчастниковПодписания.Подписывающий) КАК Функция
		|ИЗ
		|	Справочник.НастройкиДействийПодписания.Участники КАК Участники
		|ГДЕ
		|	Участники.Ссылка = &НастройкаДействия
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	НастройкиДействийПодписания.Автор,
		|	ЗНАЧЕНИЕ(Перечисление.ФункцииУчастниковПодписания.ОбрабатывающийРезультат)
		|ИЗ
		|	Справочник.НастройкиДействийПодписания КАК НастройкиДействийПодписания
		|ГДЕ
		|	НастройкиДействийПодписания.Ссылка = &НастройкаДействия");
	
	Запрос.УстановитьПараметр("НастройкаДействия", НастройкаДействия);
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

// Возвращает идентификатор объекта метаданных.
//
// Возвращаемое значение:
//  СправочникСсылка.ИдентификаторыОбъектовМетаданных - Возвращает идентификатор объекта метаданных.
//
Функция ИдентификаторОбъектаМетаданных() Экспорт
	
	ИдентификаторОбъектаМетаданных =
		ОбщегоНазначения.ИдентификаторОбъектаМетаданных(
			Метаданные.Справочники.НастройкиДействийПодписания);
	
	Возврат ИдентификаторОбъектаМетаданных;
	
КонецФункции

// Возвращает разрешения по действию в виде соответствия
// 
// Параметры:
// 	НастройкаДействия - СправочникСсылка.НастройкиДействийПодписания - ссылка на действие
// 	
// Возвращаемое значение:
// 	Соответствие - разрешения по действию.
// 
Функция РазрешенияИзмененияУчастников(НастройкаДействия) Экспорт
	
	РеквизитыНастройки = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(НастройкаДействия,
		 "ВозможностьИзменятьУчастников, Этапы, Участники");
	УчастникиНастройки = РеквизитыНастройки.Участники.Выгрузить();
	
	// Для режима Авто смотрим на Участников. Если участники заданы, то запрещено менять
	Если РеквизитыНастройки.ВозможностьИзменятьУчастников = Перечисления.ВариантыДоступностиИзмененияДействий.Авто Тогда
		Если УчастникиНастройки.Количество() > 0 Тогда
			ВозможностьИзменятьУчастников = Перечисления.ВариантыДоступностиИзмененияДействий.Запрещено;
		Иначе
			ВозможностьИзменятьУчастников = Перечисления.ВариантыДоступностиИзмененияДействий.Разрешено;
		КонецЕсли;
	Иначе
		ВозможностьИзменятьУчастников = РеквизитыНастройки.ВозможностьИзменятьУчастников;
	КонецЕсли;
	
	Разрешения = Новый Соответствие();
	Разрешения.Вставить(
		ДействияСервер.ПредопределенныйИдентификаторУчастника("Пустой"),
		ВозможностьИзменятьУчастников);
		
	Выборка = РеквизитыНастройки.Этапы.Выбрать();
	Пока Выборка.Следующий() Цикл
		
		// Для режима Авто смотрим на Участников. Если участники в этапе заданы, то запрещено менять
		Если Выборка.ВозможностьИзменятьУчастников = Перечисления.ВариантыДоступностиИзмененияДействий.Авто Тогда
			УчастникиЭтапа = УчастникиНастройки.НайтиСтроки(
				Новый Структура("ИдентификаторЭтапа", Выборка.Идентификатор));
			Если УчастникиЭтапа.Количество() > 0 Тогда
				ВозможностьИзменятьУчастников = Перечисления.ВариантыДоступностиИзмененияДействий.Запрещено;
			Иначе
				ВозможностьИзменятьУчастников = Перечисления.ВариантыДоступностиИзмененияДействий.Разрешено;
			КонецЕсли;
		Иначе
			ВозможностьИзменятьУчастников = Выборка.ВозможностьИзменятьУчастников;
		КонецЕсли;
		
		Разрешения.Вставить(
			Выборка.Идентификатор,
			ВозможностьИзменятьУчастников);
	КонецЦикла;
	
	Возврат Разрешения;
	
Конецфункции

// СтандартныеПодсистемы.ВерсионированиеОбъектов
// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
// Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт
	
КонецПроцедуры
// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

#КонецОбласти

#Область ОбработчикиСобытий

#КонецОбласти

#КонецЕсли
