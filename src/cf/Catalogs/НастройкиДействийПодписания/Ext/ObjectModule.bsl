#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Заполняет участников по умолчанию.
//
Процедура ЗаполнитьУчастниковПоУмолчанию() Экспорт
	
	УчастникиПоУмолчанию = НастройкиДействий.УчастникиПоУмолчанию(ВидДействия);
	
	Участники.Очистить();
	
	Для Каждого УчастникПоУмолчанию Из УчастникиПоУмолчанию Цикл
		
		СтрокаУчастника  = Участники.Добавить();
		СтрокаУчастника.ФункцияУчастника = Перечисления.ФункцииУчастниковПодписания.Подписывающий;
		СтрокаУчастника.Участник = УчастникПоУмолчанию;
		
	КонецЦикла;
	
	МоментРазыменованияУчастников =
		Перечисления.МоментыРазыменованияУчастниковДействий.ПередВыполнениемДействия;
	Если СпособПодписания = Перечисления.СпособыПодписанияПредметаДействия.НаБумаге Тогда
		ПорядокВыполненияУчастниками = Перечисления.ПорядокВыполненияЭтапаДействия.Последовательно;
	Иначе
		ПорядокВыполненияУчастниками = Перечисления.ПорядокВыполненияЭтапаДействия.Параллельно;
	КонецЕсли;
	
КонецПроцедуры

// Возвращает участников с функций "Обрабатывающий результат".
// 
// Возвращаемое значение:
//  Массив - Участники.
//
Функция УчастникОбрабатывающийРезультат() Экспорт
	
	ПараметрыОтбора = Новый Структура;
	ПараметрыОтбора.Вставить("ФункцияУчастника", Перечисления.ФункцииУчастниковПодписания.ОбрабатывающийРезультат);
	Возврат Участники.НайтиСтроки(ПараметрыОтбора);
	
КонецФункции

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	НастройкиДействий.ОбработкаЗаполнения(
		ЭтотОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка);
	// Заполним способ подписания из настройки формы документа
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") 
		И ДанныеЗаполнения.Свойство("ПравилоОбработки") Тогда
		ВидОбъекта = Справочники.ПравилаОбработки.ВидОбъектаКорневогоПравила(
			ДанныеЗаполнения.ПравилоОбработки);
		
		Если ЗначениеЗаполнено(ВидОбъекта) 
			И ТипЗнч(ВидОбъекта) = Тип("СправочникСсылка.ВидыДокументов") Тогда
			ФормаДокумента = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ВидОбъекта, "ФормаДокумента");
			
			Если ФормаДокумента = Перечисления.ВариантыФормДокументов.Бумажная Тогда
				СпособПодписания = Перечисления.СпособыПодписанияПредметаДействия.НаБумаге;
							
			ИначеЕсли ФормаДокумента = Перечисления.ВариантыФормДокументов.Электронная Тогда
				СпособПодписания = Перечисления.СпособыПодписанияПредметаДействия.ПЭП;
				
			КонецЕсли;
		КонецЕсли;	
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	НастройкиДействий.ПередЗаписью(Ссылка, ДополнительныеСвойства, Отказ);
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	НастройкиДействий.ПриЗаписи(Ссылка, ДополнительныеСвойства, ПометкаУдаления, Отказ);
	
КонецПроцедуры

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли