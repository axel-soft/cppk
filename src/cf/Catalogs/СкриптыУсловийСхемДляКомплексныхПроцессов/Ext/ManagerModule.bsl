#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс 

// Проверяет скрипт условия и возвращает результат проверки.
// 
// Параметры:
//  СкриптУсловия - СправочникСсылка.СкриптыУсловийСхемДляКомплексныхПроцессов
//  Процесс - БизнесПроцессОбъект.КомплексныйПроцесс
// 
// Возвращаемое значение:
//  Булево
// 
Функция ПроверитьСкриптУсловия(СкриптУсловия, Процесс) Экспорт
	
	Выражение = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СкриптУсловия, "Выражение");	
	Возврат ВычислитьВыражениеСкриптаУсловия(Выражение, Процесс);
	
КонецФункции

// Вычисляет выражение скрипта условия и возвращает результат.
// 
// Параметры:
//  Выражение - Строка - выражение скрипта условия на встроенном языке.
//  Процесс - БизнесПроцессОбъект.КомплексныйПроцесс
// 
// Возвращаемое значение:
//  Булево
//
Функция ВычислитьВыражениеСкриптаУсловия(Выражение, Процесс) Экспорт
	
	Параметры = Новый Структура;
	Параметры.Вставить("Результат", Ложь);
	Параметры.Вставить("ПроцессОбъект", Процесс);
	
	ОбщегоНазначения.ВыполнитьВБезопасномРежиме(Выражение, Параметры);
	
	Возврат Параметры.Результат;
	
КонецФункции

// Возвращает скрипт условия схемы по хешу выражения.
// 
// Параметры:
//  ХешВыражения - Строка
// 
// Возвращаемое значение:
//  СправочникСсылка.СкриптыУсловийСхемДляКомплексныхПроцессов
//
Функция СкриптСхемыПоХешуВыражения(ХешВыражения) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	СкриптыУсловийСхемДляКомплексныхПроцессов.Ссылка
		|ИЗ
		|	Справочник.СкриптыУсловийСхемДляКомплексныхПроцессов КАК СкриптыУсловийСхемДляКомплексныхПроцессов
		|ГДЕ
		|	СкриптыУсловийСхемДляКомплексныхПроцессов.ХешВыражения = &ХешВыражения";
	Запрос.УстановитьПараметр("ХешВыражения", ХешВыражения);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Ссылка;
	Иначе
		Возврат Справочники.СкриптыУсловийСхемДляКомплексныхПроцессов.ПустаяСсылка();
	КонецЕсли;
	
КонецФункции

// СтандартныеПодсистемы.ВерсионированиеОбъектов
// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
// Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт
	
КонецПроцедуры
// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

#КонецОбласти

#КонецЕсли