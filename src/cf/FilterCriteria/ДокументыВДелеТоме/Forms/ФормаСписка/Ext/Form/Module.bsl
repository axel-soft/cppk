
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Список.Параметры.УстановитьЗначениеПараметра("ЗначениеОтбора", Параметры.ЗначениеОтбора);
	Делопроизводство.СписокДокументовУсловноеОформлениеПомеченныхНаУдаление(Список);
	
	// Информация о недоступных по правам документах.
	Запрос = Новый Запрос(
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	КОЛИЧЕСТВО(1) КАК КолЗаписей
		|ИЗ
		|	КритерийОтбора.ДокументыВДелеТоме(&ЗначениеОтбора) КАК КритерийОтбораДокументыВДеле");
	Запрос.УстановитьПараметр("ЗначениеОтбора", Параметры.ЗначениеОтбора);
	Выборка = Запрос.Выполнить().Выбрать();
	Выборка.Следующий();
	КолДоступных = Выборка.КолЗаписей;
	
	УстановитьПривилегированныйРежим(Истина);
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "РАЗРЕШЕННЫЕ", "");
	Выборка = Запрос.Выполнить().Выбрать();
	Выборка.Следующий();
	КолВсего = Выборка.КолЗаписей;
	УстановитьПривилегированныйРежим(Ложь);
	
	Элементы.ГруппаНедоступныеДокументы.Видимость = КолДоступных <> КолВсего;
	Элементы.НедоступныеДокументыНадпись.Заголовок = СтрШаблон(
		НСтр("ru = 'Есть документы, недоступные по правам (%1 из %2)'"), КолВсего - КолДоступных, КолВсего);
	
КонецПроцедуры

&НаКлиенте
Процедура ПереносДокументовДела(Команда)
	
	ПараметрыФормы = Новый Структура("ПеренестиИзДела", Параметры.ЗначениеОтбора);
	Открытьформу("Справочник.ДелаХраненияДокументов.Форма.ФормаПереносаДокументовДела", ПараметрыФормы, ЭтаФорма);
	
КонецПроцедуры