
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	ПоказыватьИнформацию = Истина;
	ПоказыватьОшибки = Истина;
	ПоказыватьПредупреждения = Истина;
	СобытияКлиента = Истина;
	СобытияСервера = Истина;
	ПоказыватьСообщенияОПроблемахОтПользователей = Истина;

	Если Не РольДоступна("ПолныеПрава") Тогда
		Пользователь = Пользователи.ТекущийПользователь();
		Элементы.Пользователь.Доступность = Ложь;
	КонецЕсли;

	ЗаполнитьСписокПриложенийИУстановитьПараметры();

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПользовательНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	РаботаСПользователямиКлиент.ВыбратьПользователя(Элемент, Пользователь);
	
КонецПроцедуры

&НаКлиенте
Процедура ПользовательПриИзменении(Элемент)

	ЗаполнитьСписокПриложенийИУстановитьПараметры();

КонецПроцедуры

&НаКлиенте
Процедура ПоказыватьПриИзменении(Элемент)

	УстановитьПараметры();

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьПараметры()

	Список.Параметры.УстановитьЗначениеПараметра("ПоказыватьИнформацию", ПоказыватьИнформацию);
	Список.Параметры.УстановитьЗначениеПараметра("ПоказыватьОшибки", ПоказыватьОшибки);
	Список.Параметры.УстановитьЗначениеПараметра("ПоказыватьПредупреждения", ПоказыватьПредупреждения);
	Список.Параметры.УстановитьЗначениеПараметра("СобытияКлиента", СобытияКлиента);
	Список.Параметры.УстановитьЗначениеПараметра("СобытияСервера", СобытияСервера);
	Список.Параметры.УстановитьЗначениеПараметра("ДатаС", ДатаС);
	Список.Параметры.УстановитьЗначениеПараметра("ДатаПо", ДатаПо);
	Список.Параметры.УстановитьЗначениеПараметра("МобильныйКлиент", МобильноеПриложение);
	Список.Параметры.УстановитьЗначениеПараметра("Пользователь", Пользователь);
	Список.Параметры.УстановитьЗначениеПараметра(
		"ПоказыватьСообщенияОПроблемахОтПользователей", ПоказыватьСообщенияОПроблемахОтПользователей);

КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСписокПриложений()

	
	АктивныеПриложения = Справочники.ПользователиМобильногоПриложения.АктивныеПриложенияПользователя();
	
	Элементы.МобильноеПриложение.СписокВыбора.Очистить();
	Элементы.МобильноеПриложение.КнопкаВыпадающегоСписка = Ложь;

	Для Каждого АктивноеПриложение Из АктивныеПриложения Цикл

		Элементы.МобильноеПриложение.СписокВыбора.Добавить(АктивноеПриложение, Строка(АктивноеПриложение));
		Элементы.МобильноеПриложение.КнопкаВыпадающегоСписка = Истина;

	КонецЦикла;

КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСписокПриложенийИУстановитьПараметры()

	ЗаполнитьСписокПриложений();
	УстановитьПараметры();

КонецПроцедуры;

#КонецОбласти
