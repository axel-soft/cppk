#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает часто используемые папки текущего пользователя.
// 
// Возвращаемое значение:
//  ТаблицаЗначений - Часто используемые папки пользователя.
//   * Папка - СправочникСсылка.ПапкиПисем - Часто используемая папка пользователя.
//   * Представление - Строка - Представление папки.
//   * Порядок - Число - Порядок папки.
//   * ВариантОтображенияКоличестваПисем - ПеречислениеСсылка.ВариантыОтображенияКоличестваПисемВПапке - ВариантОтображенияКоличестваПисем.
//
Функция ПолучитьПапкиДляТекущегоПользователя() Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Возврат ПолучитьПапкиПользователя(ПользователиКлиентСервер.ТекущийПользователь());
	
КонецФункции

// Возвращает часто используемые папки пользователя.
//
// Параметры:
//  Пользователь - СправочникСсылка.Пользователи - Пользователь.
// 
// Возвращаемое значение:
//  ТаблицаЗначений - Часто используемые папки пользователя.
//   * Папка - СправочникСсылка.ПапкиПисем - Часто используемая папка пользователя.
//   * Представление - Строка - Представление папки.
//   * Порядок - Число - Порядок папки.
//   * ВариантОтображенияКоличестваПисем - ПеречислениеСсылка.ВариантыОтображенияКоличестваПисемВПапке - ВариантОтображенияКоличестваПисем.
//
Функция ПолучитьПапкиПользователя(Пользователь) Экспорт
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	ПапкиПисемЧастоИспользуемые.Папка КАК Папка,
		|	ПРЕДСТАВЛЕНИЕ(ПапкиПисемЧастоИспользуемые.Папка) КАК Представление,
		|	ПапкиПисемЧастоИспользуемые.Порядок КАК Порядок,
		|	ПапкиПисем.ВариантОтображенияКоличестваПисем КАК ВариантОтображенияКоличестваПисем
		|ИЗ
		|	РегистрСведений.ПапкиПисемЧастоИспользуемые КАК ПапкиПисемЧастоИспользуемые
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ПапкиПисем КАК ПапкиПисем
		|		ПО ПапкиПисемЧастоИспользуемые.Папка = ПапкиПисем.Ссылка
		|ГДЕ
		|	ПапкиПисемЧастоИспользуемые.Пользователь = &Пользователь
		|
		|УПОРЯДОЧИТЬ ПО
		|	Порядок");
		
	Запрос.УстановитьПараметр("Пользователь", Пользователь);
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

// Записывает массив папок для текущего пользователя, затирая предыдущие.
//
// Параметры:
//  МассивПапок - Массив - Новые часто используемые папки пользователя.
//
Процедура ЗаписатьПапкиДляТекущегоПользователя(МассивПапок) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	НаборЗаписей = РегистрыСведений.ПапкиПисемЧастоИспользуемые.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Пользователь.Установить(ПользователиКлиентСервер.ТекущийПользователь());
	НаборЗаписей.Записать();
	
	НомерПоПорядку = 0;
	Для Каждого Папка Из МассивПапок Цикл
		
		МенеджерЗаписи = РегистрыСведений.ПапкиПисемЧастоИспользуемые.СоздатьМенеджерЗаписи();
		МенеджерЗаписи.Папка = Папка;
		МенеджерЗаписи.Пользователь = ПользователиКлиентСервер.ТекущийПользователь();
		МенеджерЗаписи.Порядок = НомерПоПорядку;
		МенеджерЗаписи.Записать();
		
		НомерПоПорядку = НомерПоПорядку + 1;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли