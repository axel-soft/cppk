#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает значение настройки блока навигации для указанного пользователя.
//
// Параметры:
//  Пользователь  - СправочникСсылка.Пользователи         - Пользователь.
//  БлокНавигации - Справочники.БлокиНавигации - Блок навигации.
// 
// Возвращаемое значение:
//  Произвольный - Значение настройки.
//
Функция ЗначениеНастройки(Пользователь, БлокНавигации) Экспорт
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	НастройкиБлоковНавигации.ЗначениеНастройки КАК ЗначениеНастройки
		|ИЗ
		|	РегистрСведений.НастройкиБлоковНавигации КАК НастройкиБлоковНавигации
		|ГДЕ
		|	НастройкиБлоковНавигации.Пользователь = &Пользователь
		|	И НастройкиБлоковНавигации.БлокНавигации = &БлокНавигации");
	
	Запрос.УстановитьПараметр("Пользователь", Пользователь);
	Запрос.УстановитьПараметр("БлокНавигации", БлокНавигации);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Возврат ?(Выборка.Следующий(), Выборка.ЗначениеНастройки.Получить(), Неопределено);
	
КонецФункции

// Записывает настройки блока навигации.
Процедура ЗаписатьЗначениеНастройки(Пользователь, БлокНавигации, ЗначениеНастройки) Экспорт
	
	НаборЗаписей = СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Пользователь.Установить(Пользователь);
	НаборЗаписей.Отбор.БлокНавигации.Установить(БлокНавигации);
	
	Если ЗначениеНастройки <> Неопределено Тогда
		СтрокаНабора = НаборЗаписей.Добавить();
		СтрокаНабора.Пользователь = Пользователь;
		СтрокаНабора.БлокНавигации = БлокНавигации;
		СтрокаНабора.ЗначениеНастройки = Новый ХранилищеЗначения(ЗначениеНастройки);
		СтрокаНабора.ПредставлениеНастройки = Строка(ЗначениеНастройки);
	КонецЕсли;
	
	НаборЗаписей.Записать();
	
КонецПроцедуры

// Возвращает структуру настройки блока навигации.
Функция СтруктураНастройкиБлокаНавигации() Экспорт
	
	СтруктураНастройкиБлокаНавигации = Новый Структура;
	СтруктураНастройкиБлокаНавигации.Вставить("Команды", Новый Соответствие);
	
	Возврат СтруктураНастройкиБлокаНавигации;
	
КонецФункции

// Возвращает структуру настройки команды блока навигации.
Функция СтруктураНастройкиКомандыБлокаНавигации() Экспорт
	
	СтруктураНастройкиБлокаНавигации = Новый Структура("Скрыть, СкрытьВЕще", Ложь, Ложь);
	
	Возврат СтруктураНастройкиБлокаНавигации;
	
КонецФункции

#КонецОбласти

#КонецЕсли