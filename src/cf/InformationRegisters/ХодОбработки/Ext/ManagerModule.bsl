#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Структура события.
// 
// Возвращаемое значение:
//  Структура - Структура события:
// * Обработка - СправочникСсылка.ОбработкиОбъектов
// * Описание - Строка
// * ТипСобытия - ПеречислениеСсылка.ТипСобытияХодаОбработки 
// * Действие - ОпределяемыйТип.Действия
// * Участник - СправочникСсылка.Сотрудники,СправочникСсылка.СтруктураПредприятия,СправочникСсылка.РабочиеГруппы,Строка,СправочникСсылка.ПолныеРоли,СправочникСсылка.Пользователи
// * Функция - ОпределяемыйТип.ФункцииУчастниковДействий
// * Результат - ОпределяемыйТип.РезультатыВыполненияДействий
// * ИдентификаторПункта - УникальныйИдентификатор
// * ФактическийИсполнитель - 
// * Поручение - ДокументСсылка.Задача
// * ИдентификаторУчастника - УникальныйИдентификатор
// * Сотрудник - СправочникСсылка.Сотрудники,СправочникСсылка.Пользователи
Функция СтруктураСобытия() Экспорт
	
	Возврат Новый Структура("Обработка, 
	|Описание, 
	|ТипСобытия, 
	|Действие, 
	|Участник, 
	|Функция,
	|Результат,
	|ИдентификаторПункта,
	|ФактическийИсполнитель,
	|Поручение,
	|ИдентификаторУчастника,
	|Сотрудник,
	|ОписаниеПункта");
	
КонецФункции	

// Добавляет описание события в ход обработки
// 
// Параметры:
//  Событие - см. СтруктураСобытия
//
Процедура ЗаписатьСобытие(Событие) Экспорт
	
	Если Не ЗначениеЗаполнено(Событие.Обработка) Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	МенеджерЗаписи = СоздатьМенеджерЗаписи();
	
	ЗаполнитьЗначенияСвойств(МенеджерЗаписи, Событие);
	
	МенеджерЗаписи.Дата = ТекущаяДатаСеанса();
	МенеджерЗаписи.МоментВремени = ТекущаяУниверсальнаяДатаВМиллисекундах();
	
	Если Не ЗначениеЗаполнено(МенеджерЗаписи.Сотрудник) Тогда
		ОлицетворяемыйСотрудник = Сотрудники.ОсновнойСотрудникПользователя(
			ПользователиДокументооборот.ОлицетворяемыйПользователь());
		МенеджерЗаписи.Сотрудник = ОлицетворяемыйСотрудник;
	КонецЕсли;

	Если ЗначениеЗаполнено(Событие.ОписаниеПункта) Тогда
		МенеджерЗаписи.Описание = МенеджерЗаписи.Описание + " " 
			+ НСтр("ru = 'Пункт: '") + Событие.ОписаниеПункта;
	КонецЕсли;
	
	МенеджерЗаписи.Записать();
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли