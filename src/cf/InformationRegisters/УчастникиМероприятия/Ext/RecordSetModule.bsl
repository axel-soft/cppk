#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

// Обновление адресной книги
Перем МероприятияДляОбновленияВАдреснойКниге;

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ПередЗаписьюОбновлениеАдреснойКниги();
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ДополнительныеСвойства.Свойство("ПропуститьОбновлениеДанныхМероприятия")
		Или Не ДополнительныеСвойства.ПропуститьОбновлениеДанныхМероприятия Тогда
		РегистрыСведений.ДанныеМероприятий.ОбновитьДанныеМероприятия(Отбор.Мероприятие.Значение);
	КонецЕсли;
	
	Если Количество() > 0 И Отбор.Найти("Мероприятие") <> Неопределено Тогда
		
		Мероприятие = Отбор.Мероприятие.Значение;
		
		// Рабочая группа мероприятия.
		Если Не ДополнительныеСвойства.Свойство("ПропуститьРасширениеРабочейГруппыМероприятия")
			Или Не ДополнительныеСвойства.ПропуститьРасширениеРабочейГруппыМероприятия Тогда
			ТипКонтейнеровПользователей = Метаданные.ОпределяемыеТипы.КонтейнерыСотрудников.Тип;
			ТаблицаНабора = РаботаСРабочимиГруппами.ПолучитьРабочуюГруппуДокумента(Мероприятие);
			КолСтрокДоДобавления = ТаблицаНабора.Количество();
			Для Каждого Запись Из ЭтотОбъект Цикл
				Если ТипКонтейнеровПользователей.СодержитТип(ТипЗнч(Запись.Исполнитель)) Тогда
					РаботаСРабочимиГруппами.ДобавитьУчастникаВТаблицуНабора(
						ТаблицаНабора, Запись.Исполнитель, Ложь);
				КонецЕсли;
			КонецЦикла;
			Если ТаблицаНабора.Количество() > КолСтрокДоДобавления Тогда
				РаботаСРабочимиГруппами.ПерезаписатьРабочуюГруппуОбъекта(
					Мероприятие, ТаблицаНабора, Истина);
			КонецЕсли;
		КонецЕсли;
		
		// Рабочая группа документа-предмета.
		Предмет = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Мероприятие, "Предмет");
		Если РаботаСРабочимиГруппами.ПоОбъектуВедетсяАвтоматическоеЗаполнениеРабочейГруппы(Предмет) Тогда
			ТипКонтейнеровПользователей = Метаданные.ОпределяемыеТипы.КонтейнерыСотрудников.Тип;
			ТаблицаНабора = РаботаСРабочимиГруппами.ПолучитьРабочуюГруппуДокумента(Предмет);
			КолСтрокДоДобавления = ТаблицаНабора.Количество();
			Для Каждого Запись Из ЭтотОбъект Цикл
				Если ТипКонтейнеровПользователей.СодержитТип(ТипЗнч(Запись.Исполнитель)) Тогда
					РаботаСРабочимиГруппами.ДобавитьУчастникаВТаблицуНабора(
						ТаблицаНабора, Запись.Исполнитель, Ложь);
				КонецЕсли;
			КонецЦикла;
			Если ТаблицаНабора.Количество() > КолСтрокДоДобавления Тогда
				РаботаСРабочимиГруппами.ПерезаписатьРабочуюГруппуОбъекта(Предмет, ТаблицаНабора, Истина);
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
	ПриЗаписиОбновлениеАдреснойКниги();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ПередЗаписьюОбновлениеАдреснойКниги()
	
	Если ДополнительныеСвойства.Свойство("ПропуститьОбновлениеАдреснойКниги")
		И ДополнительныеСвойства.ПропуститьОбновлениеАдреснойКниги Тогда
		Возврат;
	КонецЕсли;
	
	МероприятияДляОбновленияВАдреснойКниге = Новый Массив;
	Если Отбор.Количество() > 0 Тогда
		
		Запрос = Новый Запрос;
		
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	УчастникиМероприятия.Мероприятие
			|ИЗ
			|	РегистрСведений.УчастникиМероприятия КАК УчастникиМероприятия
			|ГДЕ
			|	%Условие%
			|
			|СГРУППИРОВАТЬ ПО
			|	УчастникиМероприятия.Мероприятие";
		
		Условие = "";
		Разделитель = "";
		
		Для Каждого ЭлементОтбора Из Отбор Цикл
			
			Если НЕ ЭлементОтбора.Использование Тогда
				Продолжить;
			КонецЕсли;
			
			СтрокаУсловия = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				"УчастникиМероприятия.%1 = &%2",
				ЭлементОтбора.Имя,
				ЭлементОтбора.Имя);
			
			Условие = Условие
				+ Разделитель
				+ СтрокаУсловия;
				
			Запрос.УстановитьПараметр(ЭлементОтбора.Имя, ЭлементОтбора.Значение);
			
			Разделитель = Символы.ПС + "	И ";
			
		КонецЦикла;
		
		Если ЗначениеЗаполнено(Условие) Тогда
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "%Условие%", Условие);
			
			МероприятияДляОбновленияВАдреснойКниге = 
				Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Мероприятие");
		КонецЕсли;	
				
	КонецЕсли;
	
	Для Каждого Запись Из ЭтотОбъект Цикл
		Если МероприятияДляОбновленияВАдреснойКниге.Найти(Запись.Мероприятие) = Неопределено Тогда
			МероприятияДляОбновленияВАдреснойКниге.Добавить(Запись.Мероприятие);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Процедура ПриЗаписиОбновлениеАдреснойКниги()
	
	Если ДополнительныеСвойства.Свойство("ПропуститьОбновлениеАдреснойКниги")
		И ДополнительныеСвойства.ПропуститьОбновлениеАдреснойКниги Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого Мероприятие Из МероприятияДляОбновленияВАдреснойКниге Цикл
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	УчастникиМероприятия.Исполнитель
			|ИЗ
			|	РегистрСведений.УчастникиМероприятия КАК УчастникиМероприятия
			|ГДЕ
			|	УчастникиМероприятия.Мероприятие = &Мероприятие
			|	И (УчастникиМероприятия.Исполнитель ССЫЛКА Справочник.Сотрудники
			|			ИЛИ УчастникиМероприятия.Исполнитель ССЫЛКА Справочник.ПолныеРоли)
			|
			|СГРУППИРОВАТЬ ПО
			|	УчастникиМероприятия.Исполнитель";
		Запрос.УстановитьПараметр("Мероприятие", Мероприятие);
		
		УчастникиМероприятия = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Исполнитель");
		
		Справочники.АдреснаяКнига.ОбновитьСписокПодчиненныхОбъектов(
			Мероприятие,
			Неопределено,
			УчастникиМероприятия,
			Справочники.АдреснаяКнига.ПоМероприятиям,
			Мероприятие);
			
		РеквизитыМероприятия = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
			Мероприятие, "Ссылка, Наименование, ПометкаУдаления");
			
		РегистрыСведений.ОбъектыПоискаВАдреснойКниге.ОбновитьСловаПоискаПоМероприятию(РеквизитыМероприятия);
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли