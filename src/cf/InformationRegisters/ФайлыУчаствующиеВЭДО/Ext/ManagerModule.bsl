
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

// Добавляет файл в регистр.
//
// Параметры:                    
//  ВерсияФайла - СправочникССылка.ВерсииФайлов
//  ДокументЭДО - ДокументССылка.ЭлектронныйДокументВходящийЭДО, ДокументССылка.ЭлектронныйДокументИсходящийЭДО
//  Файл - СправочникССылка.Файлы
//
Процедура ДобавитьОтправленныйФайл(ВерсияФайла, ДокументЭДО, Файл) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	НаборЗаписей = СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.ВерсияФайла.Установить(ВерсияФайла);
	
	Запись = НаборЗаписей.Добавить();
	Запись.ВерсияФайла = ВерсияФайла;
	Запись.ДокументЭДО = ДокументЭДО;
	Запись.Файл = Файл;
	Запись.ВидФайла = Перечисления.ВидыФайловЭДО.ОтправленныйФайл;
	
	НаборЗаписей.Записать();
	
КонецПроцедуры

// Есть ли запись в РС по файлу
//
//
// Параметры:
//  Файл - СправочникССылка.Файлы
//
// Возвращаемое значение:
//   Булево
//
Функция ФайлОтправленПоЭДО(Файл) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ФайлыОтправленныеПоЭДО.Файл КАК Файл
		|ИЗ
		|	РегистрСведений.ФайлыУчаствующиеВЭДО КАК ФайлыОтправленныеПоЭДО
		|ГДЕ
		|	ФайлыОтправленныеПоЭДО.Файл = &Файл
		|	И ФайлыОтправленныеПоЭДО.ВидФайла = ЗНАЧЕНИЕ(Перечисление.ВидыФайловЭДО.ОтправленныйФайл)";
		
	Запрос.УстановитьПараметр("Файл", Файл);
	
	Возврат Не Запрос.Выполнить().Пустой();
	
КонецФункции

// Вернет запись  - файл - в РС по документу ЭДО
//
//
// Параметры:
//  ДокументЭДО - ДокументССылка.ЭлектронныйДокументВходящийЭДО, ДокументССылка.ЭлектронныйДокументИсходящийЭДО -
//
// Возвращаемое значение:
//   СправочникССылка.Файлы, Неопределено -
//
Функция ОтправленныйФайлПоДокументуЭДО(ДокументЭДО) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ФайлыОтправленныеПоЭДО.Файл КАК Файл
		|ИЗ
		|	РегистрСведений.ФайлыУчаствующиеВЭДО КАК ФайлыОтправленныеПоЭДО
		|ГДЕ
		|	ФайлыОтправленныеПоЭДО.ДокументЭДО = &ДокументЭДО
		|	И ФайлыОтправленныеПоЭДО.ВидФайла = ЗНАЧЕНИЕ(Перечисление.ВидыФайловЭДО.ОтправленныйФайл)";
		
	Запрос.УстановитьПараметр("ДокументЭДО", ДокументЭДО);
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Файл;
	КонецЕсли;	
	
	Возврат Неопределено;
	
КонецФункции

#КонецОбласти

#КонецЕсли