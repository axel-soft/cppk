
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.ЗначенияЗаполнения.Свойство("Исполнитель") Тогда
		Исполнитель = Параметры.ЗначенияЗаполнения.Исполнитель;
		Элементы.Исполнитель.Доступность = Ложь;
	КонецЕсли;
	Если Параметры.ЗначенияЗаполнения.Свойство("РольИсполнителя") Тогда
		РольИсполнителя = Параметры.ЗначенияЗаполнения.РольИсполнителя;
		Элементы.РольИсполнителя.Доступность = Ложь;
	КонецЕсли;
	
	Если Параметры.ИсполнительНедоступенДляИзменения Тогда
		Элементы.Исполнитель.Доступность = Ложь;
	КонецЕсли;
	
	УстановитьСостояниеЭлементов();
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	Исполнитель = ТекущийОбъект.Исполнитель;
	
	РеквизитыРоли = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ТекущийОбъект.РольИсполнителя,
		"Владелец, ОсновнойОбъектАдресации, ДополнительныйОбъектАдресации");
		
	РольИсполнителя = РеквизитыРоли.Владелец;
	ОсновнойОбъектАдресации = РеквизитыРоли.ОсновнойОбъектАдресации;
	ДополнительныйОбъектАдресации = РеквизитыРоли.ДополнительныйОбъектАдресации;
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Если Не ЗначениеЗаполнено(ОсновнойОбъектАдресации) Тогда
		ОсновнойОбъектАдресации = Неопределено;
	КонецЕсли;
	Если Не ЗначениеЗаполнено(ДополнительныйОбъектАдресации) Тогда
		ДополнительныйОбъектАдресации = Неопределено;
	КонецЕсли;
	
	ТекущийОбъект.Исполнитель = Исполнитель;
	ТекущийОбъект.РольИсполнителя = Справочники.ПолныеРоли.НайтиСоздатьПолнуюРоль(
		РольИсполнителя,
		ОсновнойОбъектАдресации,
		ДополнительныйОбъектАдресации);
		
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	Если ЗначениеЗаполнено(РольИсполнителя) Тогда
		ОповеститьОбИзменении(РольИсполнителя);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	ПроверяемыеРеквизиты.Добавить("Исполнитель");
	ПроверяемыеРеквизиты.Добавить("РольИсполнителя");
	
	Если ЗаданыТипыОсновногоОбъектаАдресации
		И НЕ ИспользуетсяБезОбъектовАдресации Тогда
		
		Если Не ЗначениеЗаполнено(ОсновнойОбъектАдресации) Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				СтрШаблон(НСтр("ru = 'Поле ""%1"" не заполнено.'"), ЗаголовокОсновногоОбъектаАдресации),, 
				"ОсновнойОбъектАдресации",, Отказ);
		КонецЕсли;	
		
	КонецЕсли;
	
	Если ЗаданыТипыДополнительногоОбъектаАдресации
		И НЕ ИспользуетсяБезОбъектовАдресации Тогда
		
		Если Не ЗначениеЗаполнено(ДополнительныйОбъектАдресации) Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				СтрШаблон(НСтр("ru = 'Поле ""%1"" не заполнено.'"), ЗаголовокДополнительногоОбъектаАдресации),, 
				"ДополнительныйОбъектАдресации",, Отказ);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура РольИсполнителяПриИзменении(Элемент)
	
	ОсновнойОбъектАдресации = Неопределено;
	ДополнительныйОбъектАдресации = Неопределено;
	УстановитьСостояниеЭлементов();
	
КонецПроцедуры

&НаКлиенте
Процедура ИсполнительОбработкаВыбора(Элемент, ВыбранноеЗначение, ДополнительныеДанные, СтандартнаяОбработка)
	
	СотрудникиКлиент.СотрудникОбработкаВыбора(ЭтотОбъект, "Исполнитель", ВыбранноеЗначение, СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьСостояниеЭлементов()
	
	ТипыОсновногоОбъектаАдресации = РольИсполнителя.ТипыОсновногоОбъектаАдресации.ТипЗначения;
	ТипыДополнительногоОбъектаАдресации = РольИсполнителя.ТипыДополнительногоОбъектаАдресации.ТипЗначения;
	ИспользуетсяСОбъектамиАдресации = РольИсполнителя.ИспользуетсяСОбъектамиАдресации;
	ИспользуетсяБезОбъектовАдресации = РольИсполнителя.ИспользуетсяБезОбъектовАдресации;
	
	ЗаданаРоль = НЕ РольИсполнителя.Пустая();
	ЗаголовокОсновногоОбъектаАдресации = ?(ЗаданаРоль, Строка(РольИсполнителя.ТипыОсновногоОбъектаАдресации), "");
	ЗаголовокДополнительногоОбъектаАдресации = ?(ЗаданаРоль, Строка(РольИсполнителя.ТипыДополнительногоОбъектаАдресации), "");
	
	ЗаданыТипыОсновногоОбъектаАдресации = ЗаданаРоль И ИспользуетсяСОбъектамиАдресации
		И ЗначениеЗаполнено(ТипыОсновногоОбъектаАдресации);
	ЗаданыТипыДополнительногоОбъектаАдресации = ЗаданаРоль И ИспользуетсяСОбъектамиАдресации 
		И ЗначениеЗаполнено(ТипыДополнительногоОбъектаАдресации);
	Элементы.ОсновнойОбъектАдресации.Доступность = ЗаданыТипыОсновногоОбъектаАдресации;
	Элементы.ДополнительныйОбъектАдресации.Доступность = ЗаданыТипыДополнительногоОбъектаАдресации;
	
	Элементы.ОсновнойОбъектАдресации.АвтоОтметкаНезаполненного = ЗаданыТипыОсновногоОбъектаАдресации
		И НЕ ИспользуетсяБезОбъектовАдресации;
	Если ТипыОсновногоОбъектаАдресации <> Неопределено Тогда
		Элементы.ОсновнойОбъектАдресации.ОграничениеТипа = ТипыОсновногоОбъектаАдресации;
	КонецЕсли;
	Элементы.ОсновнойОбъектАдресации.Заголовок = ЗаголовокОсновногоОбъектаАдресации;
	
	Элементы.ДополнительныйОбъектАдресации.АвтоОтметкаНезаполненного = ЗаданыТипыДополнительногоОбъектаАдресации
		И НЕ ИспользуетсяБезОбъектовАдресации;
	Если ТипыДополнительногоОбъектаАдресации <> Неопределено Тогда
		Элементы.ДополнительныйОбъектАдресации.ОграничениеТипа = ТипыДополнительногоОбъектаАдресации;
	КонецЕсли;
	Элементы.ДополнительныйОбъектАдресации.Заголовок = ЗаголовокДополнительногоОбъектаАдресации;
	
КонецПроцедуры

#КонецОбласти