#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Устанавливает факт обработки уведомления
Процедура ДобавитьОбработанноеУведомление(ВидСобытия, ОбъектУведомления, Пользователь, ДатаОбработки = Неопределено) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если ДатаОбработки = Неопределено Тогда
		ДатаОбработки = ТекущаяДатаСеанса();
	КонецЕсли;
	
	НаборЗаписей = СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.ВидСобытия.Установить(ВидСобытия);
	НаборЗаписей.Отбор.ОбъектУведомления.Установить(ОбъектУведомления);
	НаборЗаписей.Отбор.Пользователь.Установить(Пользователь);
	
	НоваяЗапись = НаборЗаписей.Добавить();
	НоваяЗапись.ВидСобытия = ВидСобытия;
	НоваяЗапись.ОбъектУведомления = ОбъектУведомления;
	НоваяЗапись.Пользователь = Пользователь;
	НоваяЗапись.ДатаОбработки = ДатаОбработки;
	
	НаборЗаписей.Записать();
	
КонецПроцедуры

// Определяет дату обработки уведомления.
//
// Параметры:
//  ВидСобытия        - СправочникСсылка.ВидыБизнесСобытий,
//                      ПеречислениеСсылка.СобытияУведомлений - Обработанное событие.
//  ОбъектУведомления - ОпределяемыйТип.ОбъектУведомления     - Объект, по которому обработано событие.
//  Пользователь      - СправочникСсылка.Пользователи         - Пользователь, которому предназначено уведомление.
// 
// Возвращаемое значение:
//  Дата - Дата последней обработки событию по объекту уведомления по пользователю.
//
Функция ДатаОбработки(ВидСобытия, ОбъектУведомления, Пользователь) Экспорт
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	ОбработанныеУведомления.ДатаОбработки КАК ДатаОбработки
		|ИЗ
		|	РегистрСведений.ОбработанныеУведомления КАК ОбработанныеУведомления
		|ГДЕ
		|	ОбработанныеУведомления.ВидСобытия = &ВидСобытия
		|	И ОбработанныеУведомления.ОбъектУведомления = &ОбъектУведомления
		|	И ОбработанныеУведомления.Пользователь = &Пользователь");
	
	Запрос.УстановитьПараметр("ВидСобытия", ВидСобытия);
	Запрос.УстановитьПараметр("ОбъектУведомления", ОбъектУведомления);
	Запрос.УстановитьПараметр("Пользователь", Пользователь);
	
	РезультатЗапроса = Запрос.Выполнить();
	ВыборкаРезультата = РезультатЗапроса.Выбрать();
	
	ДатаОбработки = ?(ВыборкаРезультата.Следующий(), ВыборкаРезультата.ДатаОбработки, Дата(1, 1, 1));
	
	Возврат ДатаОбработки;
	
КонецФункции

#КонецОбласти

#КонецЕсли
