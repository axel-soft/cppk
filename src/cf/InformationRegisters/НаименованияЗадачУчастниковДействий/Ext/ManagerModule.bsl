#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает наименование и описание задачи по переданным параметрам. 
// 
// Параметры:
//  Действие - ОпределяемыйТип.Действия - ссылка на действие.
//  ФункцияУчастника - ОпределяемыйТип.ФункцииУчастниковДействий - фунция участника.
//  Этап - Строка - Этап
//  ПредметыСтрокой - Строка - Описание предметов
// 
// Возвращаемое значение:
//  Неопределено, Структура - Наименование и описание задачи:
// * Наименование - Неопределено, Строка - Наименование задачи
// * Описание - Строка, Неопределено - Описание задачи
Функция НаименованиеИОписаниеЗадачи(Действие, Знач ФункцияУчастника, Этап = "", ПредметыСтрокой = "") Экспорт
	
	Если Не ЗначениеЗаполнено(Действие) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Окончание = "";
	Если Не ПустаяСтрока(ПредметыСтрокой) Тогда
		Окончание = ?(СтрНачинаетсяС(ПредметыСтрокой, " "), "", " ") + ПредметыСтрокой;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ДействияВида.ВидДействия КАК Вид,
	|	ДействияВида.Предмет КАК Предмет
	|ПОМЕСТИТЬ ПредметИВидДействия
	|ИЗ
	|	Справочник.ДействияИсполнения КАК ДействияВида
	|ГДЕ
	|	ДействияВида.Ссылка = &Действие
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НаименованияЗадачУчастниковДействий.Наименование КАК Наименование,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(НаименованияЗадачУчастниковДействий.Этап, """") = &Этап
	|			ТОГДА 1
	|		ИНАЧЕ 3
	|	КОНЕЦ КАК Приоритет,
	|	НаименованияЗадачУчастниковДействий.ШаблонНаименования КАК ШаблонНаименования,
	|	НаименованияЗадачУчастниковДействий.Описание КАК Описание,
	|	НаименованияЗадачУчастниковДействий.ШаблонОписания КАК ШаблонОписания,
	|	ПредметИВидДействия.Предмет КАК Предмет
	|ИЗ
	|	РегистрСведений.НаименованияЗадачУчастниковДействий КАК НаименованияЗадачУчастниковДействий
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПредметИВидДействия КАК ПредметИВидДействия
	|		ПО (ПредметИВидДействия.Вид = НаименованияЗадачУчастниковДействий.ВидДействия)
	|ГДЕ
	|	НаименованияЗадачУчастниковДействий.Функция = &Функция
	|	И ЕСТЬNULL(НаименованияЗадачУчастниковДействий.Этап, """") В (&Этап, """")
	|
	|УПОРЯДОЧИТЬ ПО
	|	Приоритет");
	
	Запрос.УстановитьПараметр("Функция", ФункцияУчастника);
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, 
		"Справочник.ДействияИсполнения КАК ДействияВида",
		Действие.Метаданные().ПолноеИмя() + " КАК ДействияВида");
	
	Запрос.УстановитьПараметр("Действие", Действие);
	Запрос.УстановитьПараметр("Этап", Строка(Этап));
	Результат =  Запрос.Выполнить();
	
	Если Результат.Пустой() Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Выб = Результат.Выбрать();
	Выб.Следующий();
	Наименование =  Строка(Выб.Наименование) + Окончание;
	Если ЗначениеЗаполнено(Выб.ШаблонНаименования) 
		И ТипЗнч(Выб.ШаблонНаименования) = Тип("СправочникСсылка.АвтоподстановкиДляОбъектов") Тогда
		ДокументОбъект = Выб.Предмет.ПолучитьОбъект();
		ЗначениеАвтоподстановки = ШаблоныДокументов.ПолучитьЗначениеАвтоподстановки(
			Выб.ШаблонНаименования, ДокументОбъект,, Истина);
		Если ЗначениеАвтоподстановки <> Неопределено 
			И ТипЗнч(ЗначениеАвтоподстановки) = Тип("Строка") Тогда	
			Наименование = ЗначениеАвтоподстановки;
		КонецЕсли;	
	КонецЕсли;
	
	Описание =  Строка(Выб.Описание);
	Если ЗначениеЗаполнено(Выб.ШаблонОписания) 
		И ТипЗнч(Выб.ШаблонОписания) = Тип("СправочникСсылка.АвтоподстановкиДляОбъектов") Тогда
		ДокументОбъект = Выб.Предмет.ПолучитьОбъект();
		ЗначениеАвтоподстановки = ШаблоныДокументов.ПолучитьЗначениеАвтоподстановки(
			Выб.ШаблонОписания, ДокументОбъект,, Истина);
		Если ЗначениеАвтоподстановки <> Неопределено 
			И ТипЗнч(ЗначениеАвтоподстановки) = Тип("Строка") Тогда	
			Описание = ЗначениеАвтоподстановки;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Новый Структура("Наименование, Описание", Наименование, Описание);
		
КонецФункции

#КонецОбласти

#КонецЕсли