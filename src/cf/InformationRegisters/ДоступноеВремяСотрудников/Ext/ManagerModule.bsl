
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

Процедура ПеренестиЗаписиВДоступноеВремяФизическихЛиц(Параметры) Экспорт
	
	Запрос = Новый Запрос;
	
	Если Параметры.ПрогрессВыполнения.ВсегоОбъектов = 0 Тогда
		
		Запрос.Текст =
			"ВЫБРАТЬ
			|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Сотрудники.Владелец) КАК Количество
			|ИЗ
			|	РегистрСведений.ДоступноеВремяСотрудников КАК ДоступноеВремяСотрудников
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Сотрудники КАК Сотрудники
			|		ПО ДоступноеВремяСотрудников.Сотрудник = Сотрудники.Ссылка";
		Выборка = Запрос.Выполнить().Выбрать();
		Выборка.Следующий();
		
		Параметры.ПрогрессВыполнения.ВсегоОбъектов = Выборка.Количество;
		
	КонецЕсли;
	
	Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ ПЕРВЫЕ 100
		|	Сотрудники.Владелец КАК ФизическоеЛицо
		|ПОМЕСТИТЬ ФизическиеЛица
		|ИЗ
		|	РегистрСведений.ДоступноеВремяСотрудников КАК ДоступноеВремяСотрудников
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Сотрудники КАК Сотрудники
		|		ПО ДоступноеВремяСотрудников.Сотрудник = Сотрудники.Ссылка
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ФизическоеЛицо
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Сотрудники.Владелец КАК ФизическоеЛицо,
		|	ДоступноеВремяСотрудников.Сотрудник КАК Сотрудник,
		|	ДоступноеВремяСотрудников.ДеньНедели КАК ДеньНедели,
		|	ДоступноеВремяСотрудников.ВремяНачала КАК ВремяНачала,
		|	ДоступноеВремяСотрудников.ВремяОкончания КАК ВремяОкончания,
		|	ВЫБОР
		|		КОГДА ОсновныеСотрудники.ФизическоеЛицо ЕСТЬ NULL
		|			ТОГДА 2
		|		ИНАЧЕ 1
		|	КОНЕЦ КАК Приоритет
		|ИЗ
		|	РегистрСведений.ДоступноеВремяСотрудников КАК ДоступноеВремяСотрудников
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Сотрудники КАК Сотрудники
		|		ПО ДоступноеВремяСотрудников.Сотрудник = Сотрудники.Ссылка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ФизическиеЛица КАК ФизическиеЛица
		|		ПО Сотрудники.Владелец = ФизическиеЛица.ФизическоеЛицо
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОсновныеСотрудники КАК ОсновныеСотрудники
		|		ПО ДоступноеВремяСотрудников.Сотрудник = ОсновныеСотрудники.Сотрудник
		|
		|УПОРЯДОЧИТЬ ПО
		|	Приоритет
		|ИТОГИ
		|ПО
		|	ФизическоеЛицо";
	
	Выборка = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	ОбработаноОбъектов = 0;
	ПроблемныхОбъектов = 0;
	
	ПриоритетыФизЛиц = Новый Соответствие();
	
	Пока Выборка.Следующий() Цикл
		
		ВыборкаДетали = Выборка.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		
		НачатьТранзакцию();
		
		Попытка
			
			ОбработанныеСотрудники = Новый Соответствие();
			
			Пока ВыборкаДетали.Следующий() Цикл
				
				Если ОбработанныеСотрудники[ВыборкаДетали.Сотрудник] = Истина Тогда
					Продолжить;
				Конецесли;
				
				Набор = РегистрыСведений.ДоступноеВремяСотрудников.СоздатьНаборЗаписей();
				Набор.Отбор.Сотрудник.Установить(ВыборкаДетали.Сотрудник);
				ОбновлениеИнформационнойБазы.ЗаписатьДанные(Набор);
				
				ОбработанныеСотрудники[ВыборкаДетали.Сотрудник] = Истина;
				
			КонецЦикла;
			
			ВыборкаДетали.Сбросить();
			
			Пока ВыборкаДетали.Следующий() Цикл
				
				ПриоритетФизЛица = ПриоритетыФизЛиц[Выборка.ФизическоеЛицо];
				Если ПриоритетФизЛица = Неопределено Тогда
					ПриоритетыФизЛиц[Выборка.ФизическоеЛицо] = ВыборкаДетали.Приоритет;
					ПриоритетФизЛица = ВыборкаДетали.Приоритет;
				КонецЕсли;
				
				Если ПриоритетФизЛица <> ВыборкаДетали.Приоритет Тогда
					Продолжить;
				КонецЕсли;
				
				// Пишем в новый регистр только записи самого приоритетного физ. лица.
				// Приорите определяет самая первая запись по физ. лицу.
				// В общем случае в выборке будут записи по физ. лицу с одним приоритетом.
				
				Набор = РегистрыСведений.ДоступноеВремяФизическихЛиц.СоздатьНаборЗаписей();
				Набор.Отбор.ФизическоеЛицо.Установить(ВыборкаДетали.ФизическоеЛицо);
				Набор.Отбор.ДеньНедели.Установить(ВыборкаДетали.ДеньНедели);
				Набор.Отбор.ВремяНачала.Установить(ВыборкаДетали.ВремяНачала);
				Набор.Отбор.ВремяОкончания.Установить(ВыборкаДетали.ВремяОкончания);
				
				Запись = Набор.Добавить();
				Запись.ФизическоеЛицо = ВыборкаДетали.ФизическоеЛицо;
				Запись.ДеньНедели = ВыборкаДетали.ДеньНедели;
				Запись.ВремяНачала = ВыборкаДетали.ВремяНачала;
				Запись.ВремяОкончания = ВыборкаДетали.ВремяОкончания;
				ОбновлениеИнформационнойБазы.ЗаписатьДанные(Набор);
				
			КонецЦикла;
			
			ОбработаноОбъектов = ОбработаноОбъектов + 1;
			
			ЗафиксироватьТранзакцию();
			
		Исключение
			
			ОтменитьТранзакцию();
			
			ПроблемныхОбъектов = ПроблемныхОбъектов + 1;
			
			ТекстСообщения = СтрШаблон(
				НСтр("ru = 'Не удалось обработать настройки доступности сотрудников физ. лица: %1 по причине:
					|%2'"),
				Выборка.ФизическоеЛицо,
				ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			ЗаписьЖурналаРегистрации(
				ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
				УровеньЖурналаРегистрации.Ошибка,,,
				ТекстСообщения);
			
		КонецПопытки;
	
	КонецЦикла;
	
	Если ОбработаноОбъектов = 0 И ПроблемныхОбъектов > 0 Тогда
		ТекстСообщения = СтрШаблон(
			НСтр("ru = 'Процедуре ПеренестиЗаписиВДоступноеВремяФизическихЛиц не удалось обработать некоторые настройки доступности сотрудников (пропущены): %1'"),
			ПроблемныхОбъектов);
		ВызватьИсключение ТекстСообщения;
	КонецЕсли;
	
	Параметры.ПрогрессВыполнения.ОбработаноОбъектов = 
		Параметры.ПрогрессВыполнения.ОбработаноОбъектов + ОбработаноОбъектов;
	
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ДоступноеВремяСотрудников.Сотрудник
		|ИЗ
		|	РегистрСведений.ДоступноеВремяСотрудников КАК ДоступноеВремяСотрудников
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Сотрудники КАК Сотрудники
		|		ПО ДоступноеВремяСотрудников.Сотрудник = Сотрудники.Ссылка";
	
	Параметры.ОбработкаЗавершена = Запрос.Выполнить().Пустой();
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли