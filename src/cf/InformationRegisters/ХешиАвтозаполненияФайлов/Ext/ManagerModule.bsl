#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфей

//Обновляет хеш сумму автозаполняемого файла при необходимости
// Параметры
//  Файл - СправочникСсылка.Файлы - перезаполняемый файл
//  ХешСумма - Строка - Сформированное значение хеш-суммы значений автозаполнения
Процедура ОбновитьХешСумму(Файл, ХешСумма) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	НачатьТранзакцию();
	
	Попытка
		Удалить(Файл);
		
		МЗ = СоздатьМенеджерЗаписи();
		МЗ.Файл = Файл;;
		МЗ.ХешСумма = ХешСумма;

		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ХешиАвтозаполненияФайлов");
		ЭлементБлокировки.УстановитьЗначение("Файл", Файл);
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		Блокировка.Заблокировать();
		
		МЗ.Записать();
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		
		ОтменитьТранзакцию();
		
		ЗаписьЖурналаРегистрации("Ошибка при обновление хеш-суммы значений заполнения файла",
			УровеньЖурналаРегистрации.Ошибка, Метаданные.РегистрыСведений.ХешиАвтозаполненияФайлов.Имя,
			Файл, ОписаниеОшибки());
		
	КонецПопытки
	
КонецПроцедуры

//Удаляет запись из текущего РС с отбором по файлу
// Параметры:
//  Файл - СправочникСсылка.Файлы - Ссылка на файл
Процедура Удалить(Файл) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ Первые 1
		|	ХешиАвтозаполненияФайлов.Файл КАК Файл
		|ИЗ
		|	РегистрСведений.ХешиАвтозаполненияФайлов КАК ХешиАвтозаполненияФайлов
		|ГДЕ
		|	ХешиАвтозаполненияФайлов.Файл = &Файл";
	
	Запрос.УстановитьПараметр("Файл", Файл);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		//удалять нечего
		Возврат;
	КонецЕсли;
	
	НаборЗаписей = СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Файл.Установить(Файл);
	НаборЗаписей.Записать();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции
//Проверяет необходимость перезаполнения файла (Истина или Ложь)
// Параметры
//  Файл - СправочникСсылка.Файлы -  Ссылка на перезаполняемый файл
//  ХешСумма - Строка - проверяемая хеш-сумма строкой
//
//Возвращаемое значение: истина или ложь
Функция ТребуетсяОбновлениеХешСуммы(Файл, ХешСумма) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ Первые 1
		|	ХешиАвтозаполненияФайлов.Файл КАК Файл
		|ИЗ
		|	РегистрСведений.ХешиАвтозаполненияФайлов КАК ХешиАвтозаполненияФайлов
		|ГДЕ
		|	ХешиАвтозаполненияФайлов.Файл = &Файл
		|	И ХешиАвтозаполненияФайлов.ХешСумма = &ХешСумма";
	
	Запрос.УстановитьПараметр("Файл", Файл);
	Запрос.УстановитьПараметр("ХешСумма", ХешСумма);
	
	ТребуетсяПерезаполнениеФайла = Запрос.Выполнить().Пустой();
	
	Возврат ТребуетсяПерезаполнениеФайла;
	
КонецФункции

#КонецОбласти

#КонецЕсли
