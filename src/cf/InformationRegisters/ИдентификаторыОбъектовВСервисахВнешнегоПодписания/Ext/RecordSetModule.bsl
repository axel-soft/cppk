// @strict-types

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если Количество() <> 0 Тогда
		Возврат;
	КонецЕсли;
	
	Сервис = Отбор.Сервис.Значение; // СправочникСсылка.СервисыВнешнегоПодписания
	Объект = Отбор.Объект.Значение; // ОпределяемыйТип.СопоставляемыеОбъектыВнешнегоПодписания
	
	Если Не ЗначениеЗаполнено(Сервис) Или Не ЗначениеЗаполнено(Объект) Тогда
		Отказ = Истина;
		ОбщегоНазначения.СообщитьПользователю(
			НСтр("ru = 'Запись в регистр без установленного отбора не поддерживается'"));
		Возврат;
	КонецЕсли; 
	
	НаборЗаписей = РегистрыСведений.ИдентификаторыОбъектовВСервисахВнешнегоПодписания.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Сервис.Установить(Сервис);
	НаборЗаписей.Отбор.Объект.Установить(Объект);
	НаборЗаписей.Прочитать();
	
	Если НаборЗаписей.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ВозможноУдалениеИдентификатораОбъекта = 
		РаботаСВнешнимПодписанием.ВозможноУдалениеИдентификатораОбъектаСервиса(Сервис, Объект);
	Если Не ВозможноУдалениеИдентификатораОбъекта Тогда
		Отказ = Истина;
		ОбщегоНазначения.СообщитьПользователю(
			НСтр("ru = 'Удаление записи невозможно, по этому объекту есть активные сведения о внешнем подписании'"));
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли