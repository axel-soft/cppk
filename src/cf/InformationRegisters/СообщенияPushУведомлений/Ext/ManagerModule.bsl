///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2022, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

//Процедура - ДобавитьЗапись - добавить запись в РС
//
// Параметры:
//  ПолучательУведомления - СправочникСсылка.Пользователи - Ссылка на пользователя для которого формируется сообщение
//  					  -	СправочникСсылка.Сотрудники - Ссылка на сотрудника, пользователю которого формируется сообщение
//  ОбъектУведомления	  - ОпределяемыйТип - Ссылка на объект уведомления
//  Дата		 		  - Дата - Планируемая дата отправки уведомления
//  Сообщение	 		  - Строка - Текст сообщения
//
Процедура ДобавитьЗапись(ПолучательУведомления,
	ОбъектУведомления,
	Дата,
	Сообщение) Экспорт
	
	Если Не ЗначениеЗаполнено(ПолучательУведомления) Тогда 
		Возврат;
	КонецЕсли;
	
	ЕстьОбъектУведомления = ЗначениеЗаполнено(ОбъектУведомления);
	Запись = СоздатьМенеджерЗаписи();

	Если ТипЗнч(ПолучательУведомления) = Тип("СправочникСсылка.Сотрудники") Тогда
		Запись.Пользователь = Сотрудники.ПользовательСотрудника(ПолучательУведомления);
	Иначе
		Запись.Пользователь = ПолучательУведомления;
	КонецЕсли;
	
	ДатаПоследнейОтправкиPushУведомлений = Константы.ДатаПоследнейОтправкиPushУведомлений.Получить();
	
	Если Дата <= ДатаПоследнейОтправкиPushУведомлений Тогда
		Дата = ДатаПоследнейОтправкиPushУведомлений + 1;
	КонецЕсли;
	
	Запись.ДатаОтправки = Дата;
	
	Если ЕстьОбъектУведомления Тогда
		Запись.ОбъектУведомления = ОбъектУведомления;
	КонецЕсли;

	Запись.ИдентификаторСообщения = ?(ЕстьОбъектУведомления,
		Строка(ОбъектУведомления.УникальныйИдентификатор()),
		Строка(Новый УникальныйИдентификатор()));
	
	Запись.Сообщение = Сообщение;

	Запись.Записать(Истина);

КонецПроцедуры

#КонецОбласти

#КонецЕсли