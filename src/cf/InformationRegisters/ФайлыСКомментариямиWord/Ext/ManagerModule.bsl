
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

// Добавляет файл в регистр.
//
// Параметры:
//  Файл - СправочникССылка.Файлы
//  НомерИтерации - Число
//
Процедура ДобавитьФайл(Файл, НомерИтерации) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	НаборЗаписей = СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Файл.Установить(Файл);
	НаборЗаписей.Отбор.НомерИтерации.Установить(НомерИтерации);
	
	Запись = НаборЗаписей.Добавить();
	Запись.Файл = Файл;
	Запись.НомерИтерации = НомерИтерации;
	
	НаборЗаписей.Записать();
	
КонецПроцедуры

// Есть ли запись в РС по файлу и номеру итерации
//
//
// Параметры:
//  Файл - СправочникССылка.Файлы
//  НомерИтерации - Число
//
// Возвращаемое значение:
//   Булево
//
Функция ЕстьЗапись(Файл, НомерИтерации) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ФайлыСКомментариямиWord.Файл КАК Файл
		|ИЗ
		|	РегистрСведений.ФайлыСКомментариямиWord КАК ФайлыСКомментариямиWord
		|ГДЕ
		|	ФайлыСКомментариямиWord.Файл = &Файл
		|	И ФайлыСКомментариямиWord.НомерИтерации = &НомерИтерации";
		
	Запрос.УстановитьПараметр("Файл", Файл);
	Запрос.УстановитьПараметр("НомерИтерации", НомерИтерации);
	
	Возврат Не Запрос.Выполнить().Пустой();
	
КонецФункции

// Есть ли запись в РС по файлу
//
//
// Параметры:
//  Файл - СправочникССылка.Файлы
//
// Возвращаемое значение:
//   Булево
//
Функция ЕстьЗаписьПоФайлу(Файл) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Флаг = Неопределено;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ФайлыСКомментариямиWord.Файл КАК Файл
		|ИЗ
		|	РегистрСведений.ФайлыСКомментариямиWord КАК ФайлыСКомментариямиWord
		|ГДЕ
		|	ФайлыСКомментариямиWord.Файл = &Файл";
		
	Запрос.УстановитьПараметр("Файл", Файл);
	
	Возврат Не Запрос.Выполнить().Пустой();
	
КонецФункции

// Удаляет все записи из регистра с отбором по файл.
//
// Параметры:
//  Файл - СправочникССылка.Файлы
//
Процедура УдалитьЗаписиПоФайлу(Файл) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	НаборЗаписей = СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Файл.Установить(Файл);
	НаборЗаписей.Записать();
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли