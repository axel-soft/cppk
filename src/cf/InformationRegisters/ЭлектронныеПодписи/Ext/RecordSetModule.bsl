#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ИзмененныеИдентификаторы = РаботаСЭП.ИзмененныеИдентификаторыПодписей(ЭтотОбъект);
	ИмяСвойства = РаботаСЭП.ИмяСвойстваИзмененныхИдентификаторовПодписейНабораЗаписей();
	
	ДополнительныеСвойства.Вставить(ИмяСвойства, ИзмененныеИдентификаторы);
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если Количество() = 1 Тогда
		
		ЭП = Получить(0);
		
		Если ДелопроизводствоКлиентСервер.ЭтоДокумент(ЭП.ПодписанныйОбъект) Тогда
			
			// Добавление участников из самого документа
			Если РаботаСРабочимиГруппами.ПоОбъектуВедетсяАвтоматическоеЗаполнениеРабочейГруппы(ЭП.ПодписанныйОбъект) Тогда 
				
				ТаблицаУчастников = РаботаСРабочимиГруппами.ПолучитьРабочуюГруппуДокумента(ЭП.ПодписанныйОбъект);
				
				СотрудникДляДоступа = Сотрудники.ОсновнойСотрудникПользователя(ЭП.УстановившийПодпись);
				
				РаботаСРабочимиГруппами.ДобавитьУчастникаВТаблицуНабора(ТаблицаУчастников,
					СотрудникДляДоступа, ЭП.ПодписанныйОбъект);	
					
				РаботаСРабочимиГруппами.ПерезаписатьРабочуюГруппуОбъекта(ЭП.ПодписанныйОбъект, ТаблицаУчастников, Истина);
					
			КонецЕсли;
			
		КонецЕсли;
			
	КонецЕсли;
	
	Если ТипЗнч(Отбор.ПодписанныйОбъект.Значение) = Тип("СправочникСсылка.ВерсииФайлов") Тогда
		
		ВерсияФайла = Отбор.ПодписанныйОбъект.Значение;
		РаботаСЭП.УстановитьСтатусПроверкиДляФайла(ВерсияФайла);
		
		ВладелецФайла = ВерсияФайла.Владелец.ВладелецФайла;
		Если ДелопроизводствоКлиентСервер.ЭтоДокумент(ВладелецФайла) Тогда
			РаботаСЭП.УстановитьСтатусПроверкиДляДокумента(ВладелецФайла);
		КонецЕсли;
		
	ИначеЕсли ДелопроизводствоКлиентСервер.ЭтоДокумент(Отбор.ПодписанныйОбъект.Значение) Тогда
		РаботаСЭП.УстановитьСтатусПроверкиДляДокумента(Отбор.ПодписанныйОбъект.Значение);
	КонецЕсли;
	
	РаботаСЭП.ПриЗаписиЭлектронныхПодписей(ЭтотОбъект);
	
КонецПроцедуры

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли

