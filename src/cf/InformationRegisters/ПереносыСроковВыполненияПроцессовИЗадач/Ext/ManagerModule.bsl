#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Добавляет запись о переносе срока процессов и задач.
//
// Параметры:
//  Предмет - БизнесПроцессСсылка, ЗадачаСсылка.ЗадачаИсполнителя, ДокументСсылка.Задача, ДокументСсылка.ДействиеЗадачи.
//  СтарыйСрок - Дата.
//  НовыйСрок - Дата.
//  КомментарийАвтора - Строка.
//  БизнесПроцессПереноса - БизнесПроцессСсылка.РешениеВопросовВыполненияЗадач.
//
Процедура Добавить(Предмет, СтарыйСрок, НовыйСрок, КомментарийАвтора, БизнесПроцессПереноса) Экспорт
	
	ДатаПереносаСрока = ТекущаяДатаСеанса();
	
	НаборЗаписей = СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.ДатаПереносаСрока.Установить(ДатаПереносаСрока);
	НаборЗаписей.Отбор.Предмет.Установить(Предмет);
	
	НоваяЗапись = НаборЗаписей.Добавить();
	НоваяЗапись.ДатаПереносаСрока = ДатаПереносаСрока;
	НоваяЗапись.Предмет = Предмет;
	
	НоваяЗапись.КомментарийАвтора = КомментарийАвтора;
	НоваяЗапись.НовыйСрок = НовыйСрок;
	НоваяЗапись.Сотрудник = Сотрудники.ОсновнойСотрудник();
	НоваяЗапись.СтарыйСрок = СтарыйСрок;
	
	Если ЗначениеЗаполнено(БизнесПроцессПереноса) Тогда
		НоваяЗапись.БизнесПроцессПереноса = БизнесПроцессПереноса;
		НоваяЗапись.Итерация = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(БизнесПроцессПереноса, "Итерация");
	Конецесли;
	
	НаборЗаписей.Записать();
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
