
#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПрерватьОбработкуФайла(Команда)
	
	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Оповещение = Новый ОписаниеОповещения("ПрерватьОбработкуФайлаПродолжение", ЭтотОбъект, ТекущиеДанные);
	
	Кнопки = Новый СписокЗначений;
	Кнопки.Добавить(КодВозвратаДиалога.Да);
	Кнопки.Добавить(КодВозвратаДиалога.Отмена);
	
	ПараметрыВопроса = СтандартныеПодсистемыКлиент.ПараметрыВопросаПользователю();
	ПараметрыВопроса.Заголовок = "";
	ПараметрыВопроса.КнопкаПоУмолчанию = КодВозвратаДиалога.Отмена;
	ПараметрыВопроса.ПредлагатьБольшеНеЗадаватьЭтотВопрос = Ложь;
	ПараметрыВопроса.Картинка = БиблиотекаКартинок.УдалениеПолное;
	
	ТекстПредупреждения = НСтр("ru = 'Обработка файла будет прервана и данные работы сервиса будет удалены. Продолжить?'");
	
	СтандартныеПодсистемыКлиент.ПоказатьВопросПользователю(Оповещение, ТекстПредупреждения, Кнопки, ПараметрыВопроса);
	
КонецПроцедуры

&НаКлиенте
Процедура ПрерватьОбработкуФайлаПродолжение(Результат, ДанныеСтроки) Экспорт
	
	Если Результат = Неопределено Или Результат.Значение <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	ТаймлистВызовСервера.УдалитьДанныеФайла(ДанныеСтроки.ВерсияФайла);
	
	Элементы.Список.Обновить()
	
КонецПроцедуры

#КонецОбласти
