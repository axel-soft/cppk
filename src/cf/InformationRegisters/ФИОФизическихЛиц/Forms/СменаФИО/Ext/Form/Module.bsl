
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ВернутьРезультат = Параметры.ВернутьРезультат;
	ФизическоеЛицо = Объект.Ссылка;
	
	ПолФизЛица = Объект.Пол;
	ПолЧислом = ФизическиеЛица.ПолЦифрой(ПолФизЛица);
	
	ДатаИзменения = НачалоДня(ТекущаяДатаСеанса());
	
	МультиязычностьСервер.ПриСозданииНаСервере(ЭтотОбъект, Объект);
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	МультиязычностьСервер.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ПослеВводаСтрокНаРазныхЯзыках" Тогда
		
		ПросклонятьФИО();
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ФИОПриИзменении(Элемент)
	
	ПросклонятьФИО(); 
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда) 
	
	ДанныеСменыФИО = ВыполнитьДействие();
	Если ВернутьРезультат Тогда
		
		Закрыть(ДанныеСменыФИО);
		
	Иначе
		
		Оповестить(ФизическиеЛицаКлиент.ИмяОповещенияОбИзмененииФИО(), ФизическоеЛицо);
		Закрыть();
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти 

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура Подключаемый_Открытие(Элемент, СтандартнаяОбработка)
	
	МультиязычностьКлиент.ПриОткрытии(ЭтотОбъект, Объект, Элемент, СтандартнаяОбработка);
	
КонецПроцедуры

// Если ВернутьРезультат, то возвращает данные для изменения, иначе - записывает все изменения
// 
// Возвращаемое значение:
//  См. ДанныеСменыФИО
//
&НаСервере
Функция ВыполнитьДействие()
	
	// При чтении на сервере мы меняем местами реквизиты Наименования, чтобы пользователю отражалось ФИО на его языке
	// МультиязычностьСервер.ПередЗаписьюНаСервере меняет реквизиты Наименования местами обратно
	//
	ТекущийОбъект = РеквизитФормыВЗначение("Объект");
	МультиязычностьСервер.ПередЗаписьюНаСервере(ТекущийОбъект);
		Объект.Наименование = ТекущийОбъект.Наименование;
	Объект.Наименование = ТекущийОбъект.Наименование;
	Объект.НаименованиеЯзык1 = ТекущийОбъект.НаименованиеЯзык1;
	Объект.НаименованиеЯзык2 = ТекущийОбъект.НаименованиеЯзык2; 
	ДанныеСменыФИО = ДанныеСменыФИО();
	
	Если ВернутьРезультат Тогда
		
		Возврат ДанныеСменыФИО;
		
	КонецЕсли;
	
	ИспользуетсяЯзык1 = МультиязычностьСервер.ИспользуетсяПервыйДополнительныйЯзык();
	ИспользуетсяЯзык2 = МультиязычностьСервер.ИспользуетсяВторойДополнительныйЯзык();
	
	МенеджерЗаписи = РегистрыСведений.ФИОФизическихЛиц.СоздатьМенеджерЗаписи();
	ЗаполнитьЗначенияСвойств(МенеджерЗаписи, ДанныеСменыФИО);
	
	НовоеФИО = Объект.Наименование;
	
	Если ИспользуетсяЯзык1 Тогда
		НовоеФИО1 = Объект.НаименованиеЯзык1;
		МенеджерЗаписи.ФИОЯзык1 = ?(ЗначениеЗаполнено(НовоеФИО1), НовоеФИО1, НовоеФИО);
	КонецЕсли;
	
	Если ИспользуетсяЯзык2 Тогда
		НовоеФИО2 = Объект.НаименованиеЯзык2;
		МенеджерЗаписи.ФИОЯзык2 = ?(ЗначениеЗаполнено(НовоеФИО2), НовоеФИО2, НовоеФИО); 
	КонецЕсли;
	
	МенеджерЗаписи.Записать(Истина);
	
	ФизическиеЛица.ОбновитьНаименованиеИСклоненияФизЛица(ФизическоеЛицо);
	
	Возврат ДанныеСменыФИО;
	
КонецФункции

&НаКлиенте
Процедура ЗавершитьСклонение(Результат, ДопПараметры) Экспорт
	
	Если Результат = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	Если Склонения <> Неопределено Тогда
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, Склонения);
	КонецЕсли;
	
	Модифицированность = Ложь;
	
КонецПроцедуры

// Данные для записи в РС ФиоФизическихЛиц
// 
// Возвращаемое значение:
//  Структура:
// * ФИО - Строка
// * Период - Дата
// * ФизическоеЛицо - СправочникСсылка.ФизическиеЛица
// * ИменительныйПадеж - Строка
// * РодительныйПадеж - Строка
// * ВинительныйПадеж - Строка
// * ДательныйПадеж - Строка
// * ТворительныйПадеж - Строка
// * ПредложныйПадеж - Строка
//
&НаСервере
Функция ДанныеСменыФИО()
	
	ДанныеСменыФИО = Новый Структура();
	ДанныеСменыФИО.Вставить("ФИО", Объект.Наименование);
	ДанныеСменыФИО.Вставить("ФИОЯзык1", Объект.НаименованиеЯзык1);
	ДанныеСменыФИО.Вставить("ФИОЯзык2", Объект.НаименованиеЯзык2);
	ДанныеСменыФИО.Вставить("Период", ДатаИзменения);
	ДанныеСменыФИО.Вставить("ФизическоеЛицо", ФизическоеЛицо);
	ДанныеСменыФИО.Вставить("ИменительныйПадеж", Именительный);
	ДанныеСменыФИО.Вставить("РодительныйПадеж", Родительный);
	ДанныеСменыФИО.Вставить("ВинительныйПадеж", Винительный);
	ДанныеСменыФИО.Вставить("ДательныйПадеж", Дательный);
	ДанныеСменыФИО.Вставить("ТворительныйПадеж", Творительный);
	ДанныеСменыФИО.Вставить("ПредложныйПадеж", Предложный);
	
	Возврат ДанныеСменыФИО;
	
КонецФункции

// Возвращает суффикс русского языка
// 
// Возвращаемое значение:
//  Строка
//
&НаСервереБезКонтекста
Функция СуффиксРусскогоЯзыка()
	
	КодРусскогоЯзыка = "ru";
	
	Если ТекущийЯзык().КодЯзыка = КодРусскогоЯзыка Тогда
		
		Возврат "";
		
	КонецЕсли;
	
	Возврат МультиязычностьДокументооборот.СуффиксРусскогоЯзыка();
	
КонецФункции

// Склоняет ФИО на РУССКОМ языке (даже если основной английский, то процедура находит русский и его склоняет)
//
&НаКлиенте
Процедура ПросклонятьФИО()
	
	СуффиксРусскогоЯзыка = СуффиксРусскогоЯзыка();
	Если ЗначениеЗаполнено(Объект.Наименование) Тогда
		
		ОповещениеОЗавершении = Новый ОписаниеОповещения("ЗавершитьСклонение", ЭтотОбъект);
		ПараметрыСклонения = СклонениеПредставленийОбъектовКлиентСервер.ПараметрыСклонения();
		ПараметрыСклонения.ЭтоФИО = Истина; 
		Если ЗначениеЗаполнено(ПолЧислом) Тогда
			ПараметрыСклонения.Пол = ПолЧислом;
		КонецЕсли;
		Представление = Объект["Наименование" + СуффиксРусскогоЯзыка];
		СклонениеПредставленийОбъектовКлиент.НачатьСклонение(ЭтотОбъект, Представление,
			ПараметрыСклонения, Истина, ОповещениеОЗавершении);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
