#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Документ = Неопределено;
	Если Количество() = 1 Тогда
		Документ = ЭтотОбъект[0].Документ;
	ИначеЕсли Отбор.Документ.Использование = Истина Тогда
		Документ = Отбор.Документ.Значение;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Документ) Тогда
		Возврат;
	КонецЕсли;
	
	РегистрМенеджер = РегистрыСведений.ЖурналПередачиДокументов;
	РезультатПроверки = РегистрМенеджер.ПроверитьЗапретИзмененияПоСостояниюДела(Документ);
	Если РезультатПроверки.Запрет = Истина Тогда
		Отказ = Истина;
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			СтрШаблон(НСтр("ru = 'Изменение журнала по документу ""%1"" запрещено по причине: %2'"),
				Документ, РезультатПроверки.ПричинаЗапрета));
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ПараметрыСеанса.ЗагрузкаОбработанныхДанныхИзДругойСистемы Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого Запись Из ЭтотОбъект Цикл
		Если РаботаСРабочимиГруппами.ПоОбъектуВедетсяАвтоматическоеЗаполнениеРабочейГруппы(Запись.Документ)
				И Метаданные.ОпределяемыеТипы.КонтейнерыСотрудников.Тип.СодержитТип(ТипЗнч(Запись.Пользователь)) Тогда
			Участники = РаботаСРабочимиГруппами.ПолучитьПустуюТаблицуУчастников();
			Стр = Участники.Добавить();
			Стр.Участник = Запись.Пользователь;
			Стр.Изменение = Ложь;
			РаботаСРабочимиГруппами.ДобавитьУчастниковВРабочуюГруппуОбъекта(Запись.Документ, Участники);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли