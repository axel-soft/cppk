#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Функция ПроверитьЗапретИзмененияПоСостояниюДела(ДокументСсылка) Экспорт
	
	Результат = Новый Структура("Запрет, ПричинаЗапрета", Ложь, "");
	
	Если Пользователи.ЭтоПолноправныйПользователь() Тогда
		Возврат Результат;
	КонецЕсли;
	
	Дело = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДокументСсылка, "Дело");
	Если ЗначениеЗаполнено(Дело) Тогда
		СтруктураСостоянияДела = РегистрыСведений.СостоянияДелХраненияДокументов.СостояниеДела(Дело);
		СостоянияМенеджер = Перечисления.СостоянияДелХраненияДокументов;
		Если СтруктураСостоянияДела.Состояние = СостоянияМенеджер.Уничтожено Тогда
			Результат.Запрет = Истина;
			Результат.ПричинаЗапрета = НСтр("ru = 'документ уничтожен'");
		ИначеЕсли СтруктураСостоянияДела.Состояние = СостоянияМенеджер.ПереданоВАрхив
				И Константы.ИнтеграцияС1САрхивом.Получить() = Истина Тогда
			Результат.Запрет = Истина;
			Результат.ПричинаЗапрета = НСтр("ru = 'документ передан в 1С:Архив'");
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#КонецЕсли