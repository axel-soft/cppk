
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает права пользователя по дескрипторам объекта.
//
// Параметры:
//  СсылкаНаОбъект - Ссылка - ссылка на объект, чьи права нужно проверить.
//  Сотрудник - сотрудник, чьи права проверяются. Если не указан, то сотр. текущего пользователя.
//
// Возвращаемое значение:
//  ТаблицаЗначений - таблица прав доступа сотрудников к объекту.
//
Функция ПолучитьПраваСотрудниковПоОбъектам(
			ОбъектыДоступа,
			Сотрудники = Неопределено,
			ВключаяРуководителейИДелегатов = Истина) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если ОбъектыДоступа.Количество() = 0 Тогда
		Возврат ДокументооборотПраваДоступа.ТаблицаПравСотрудниковПоОбъектам();
	КонецЕсли;
	
	// Права по дескрипторам с учетом неограниченных прав.
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	ДескрипторыДляОбъектов.Объект КАК ОбъектДоступа,
		|	ПраваПоДескрипторамДоступаОбъектов.Сотрудник КАК Сотрудник,
		|	ИСТИНА КАК Чтение,
		|	МАКСИМУМ(ПраваПоДескрипторамДоступаОбъектов.Добавление) КАК Добавление,
		|	МАКСИМУМ(ПраваПоДескрипторамДоступаОбъектов.Изменение) КАК Изменение,
		|	МАКСИМУМ(ПраваПоДескрипторамДоступаОбъектов.Удаление) КАК Удаление,
		|	МАКСИМУМ(ПраваПоДескрипторамДоступаОбъектов.УправлениеПравами) КАК УправлениеПравами
		|ИЗ
		|	РегистрСведений.ДескрипторыДляОбъектов КАК ДескрипторыДляОбъектов
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ПраваПоДескрипторамДоступаОбъектов КАК ПраваПоДескрипторамДоступаОбъектов
		|		ПО ДескрипторыДляОбъектов.Дескриптор = ПраваПоДескрипторамДоступаОбъектов.Дескриптор
		|ГДЕ
		|	ДескрипторыДляОбъектов.Объект В(&ОбъектыДоступа)
		|	И ПраваПоДескрипторамДоступаОбъектов.Сотрудник В(&Сотрудники)
		|	И ПраваПоДескрипторамДоступаОбъектов.ОбъектОснование = НЕОПРЕДЕЛЕНО
		|
		|СГРУППИРОВАТЬ ПО
		|	ДескрипторыДляОбъектов.Объект,
		|	ПраваПоДескрипторамДоступаОбъектов.Сотрудник");
		
	Если Сотрудники = Неопределено Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст,
			"И ПраваПоДескрипторамДоступаОбъектов.Сотрудник В(&Сотрудники)", "");
	КонецЕсли;
	
	Если ВключаяРуководителейИДелегатов Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст,
			"И ПраваПоДескрипторамДоступаОбъектов.ОбъектОснование = Неопределено", "");
	КонецЕсли;
	
	Запрос.УстановитьПараметр("ОбъектыДоступа", ОбъектыДоступа);
	Запрос.УстановитьПараметр("Сотрудники", Сотрудники);
	
	ТаблицаПрав = ДокументооборотПраваДоступа.ТаблицаПравСотрудниковПоОбъектам();
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ЗаполнитьЗначенияСвойств(ТаблицаПрав.Добавить(), Выборка);
	КонецЦикла;
	
	Возврат ТаблицаПрав;
	
КонецФункции

#КонецОбласти

#КонецЕсли
