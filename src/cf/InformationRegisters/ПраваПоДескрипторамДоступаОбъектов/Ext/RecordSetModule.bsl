#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Холдинг_ЗанестиВОчередьДляОтслеживания();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура Холдинг_ЗанестиВОчередьДляОтслеживания()
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если Не КОДСлужебный.НужноФоновоеОбновлениеИсторическихДанных() Или Не Модифицированность() Тогда
		Возврат;
	КонецЕсли;
	
	
	СвернутыйНабор = Выгрузить();
	СвернутыйНабор.Свернуть("Дескриптор, Сотрудник");
	
	// Для одиночной записи сразу пишем, без доп. проверок:
	Если СвернутыйНабор.Количество() = 1 Тогда
		РегистрыСведений.ОчередьДескрипторовДляПоискаИсторическихДанныхКОД.СоздатьЗапись(
			СвернутыйНабор[0]);
		Возврат;
	КонецЕсли;
	
	
	// Может что-то в очереди уже есть, надо записать не все записи:
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	Т.Дескриптор,
		|	Т.Сотрудник
		|ПОМЕСТИТЬ втСвернутыйНабор
		|ИЗ
		|	&СвернутыйНабор КАК Т
		|;
		|
		|ВЫБРАТЬ
		|	СвернутыйНабор.Дескриптор,
		|	СвернутыйНабор.Сотрудник
		|ИЗ
		|	втСвернутыйНабор КАК СвернутыйНабор
		|	ЛЕВОЕ СОЕДИНЕНИЕ
		|	РегистрСведений.ОчередьДескрипторовДляПоискаИсторическихДанныхКОД КАК Очередь
		|		ПО СвернутыйНабор.Дескриптор = Очередь.Дескриптор
		|			И СвернутыйНабор.Сотрудник = Очередь.Сотрудник
		|ГДЕ
		|	Очередь.Дескриптор IS NULL");
	Запрос.УстановитьПараметр("СвернутыйНабор", СвернутыйНабор);
	ВыборкаОтсутствующиеЗаписи = Запрос.Выполнить().Выбрать();
	Пока ВыборкаОтсутствующиеЗаписи.Следующий() Цикл
		РегистрыСведений.ОчередьДескрипторовДляПоискаИсторическихДанныхКОД.СоздатьЗапись(
			ВыборкаОтсутствующиеЗаписи);
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли