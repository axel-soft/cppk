#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура НавигационнаяСсылкаОтбораПриИзменении(Элемент)
	
	Ссылка = СсылкаПоНавигационной(НавигационнаяСсылкаОтбора);
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список,
		"Ключ",
		Ссылка,,,
		ЗначениеЗаполнено(Ссылка));
	
КонецПроцедуры
	
#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Функция СсылкаПоНавигационной(НавигационнаяСсылкаОтбора) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ИД = Прав(СокрЛП(НавигационнаяСсылкаОтбора), 32);
	ИД = Сред(ИД, 25, 8)
		+ "-" + Сред(ИД, 21, 4)
		+ "-" + Сред(ИД, 17, 4)
		+ "-" + Сред(ИД, 1,  4)
		+ "-" + Сред(ИД, 5,  12);
		
	Имя = Сред(НавигационнаяСсылкаОтбора, СтрНайти(НавигационнаяСсылкаОтбора, "/", НаправлениеПоиска.СКонца) + 1);
	Имя = Лев(Имя, СтрНайти(Имя, "?") - 1);
	
	Попытка
		Возврат ОбщегоНазначения.МенеджерОбъектаПоПолномуИмени(Имя).ПолучитьСсылку(Новый УникальныйИдентификатор(ИД));
	Исключение
		Возврат Неопределено;
	КонецПопытки;

КонецФункции

#КонецОбласти