#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

Функция УдалитьПорциюУстаревшихДанных() Экспорт
	
	СрокХранения = Константы.СрокХраненияНепринятыхВерсийДанных.Получить();
	Если СрокХранения = 0 Тогда
		Возврат Ложь;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	ЕстьДанныеДляУдаления = Ложь;
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ ПЕРВЫЕ 1000
		|	НепринятыеВерсииНаборовЗаписей.Объект,
		|	НепринятыеВерсииНаборовЗаписей.Ключ,
		|	НепринятыеВерсииНаборовЗаписей.Узел,
		|	НепринятыеВерсииНаборовЗаписей.ИмяСообщения
		|ИЗ
		|	РегистрСведений.УдалитьНепринятыеВерсииНаборовЗаписей КАК НепринятыеВерсииНаборовЗаписей
		|ГДЕ
		|	НепринятыеВерсииНаборовЗаписей.Дата < &ДатаОчистки");
	Запрос.УстановитьПараметр("ДатаОчистки", ТекущаяДатаСеанса() - СрокХранения * 86400);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		ЕстьДанныеДляУдаления = Истина;
		
		МенеджерЗаписи = СоздатьМенеджерЗаписи();
		ЗаполнитьЗначенияСвойств(МенеджерЗаписи, Выборка);
		МенеджерЗаписи.Удалить();
			
	КонецЦикла;
	
	Возврат ЕстьДанныеДляУдаления;
	
КонецФункции

#КонецОбласти

#КонецЕсли