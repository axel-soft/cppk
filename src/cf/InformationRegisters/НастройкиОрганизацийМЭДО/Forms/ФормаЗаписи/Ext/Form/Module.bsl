#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ПриСозданииНаСервереРедакцииКонфигурации();
	
	Если ЗначениеЗаполнено(Параметры.Организация) Тогда
		
		АвтоЗаголовок = Ложь;
		Заголовок = СтрШаблон(
			"%1: %2", Метаданные.РегистрыСведений.НастройкиОрганизацийМЭДО.ПредставлениеЗаписи, Параметры.Организация);
		
		ЗаписьВРегистре = РегистрыСведений.НастройкиОрганизацийМЭДО.СоздатьМенеджерЗаписи();
		ЗаписьВРегистре.Организация = Параметры.Организация;
		ЗаписьВРегистре.Прочитать();
		
		Если ЗначениеЗаполнено(ЗаписьВРегистре.Организация) Тогда
			ЗначениеВДанныеФормы(ЗаписьВРегистре, Запись);
		Иначе // Нет в БД, это новая запись:
			Запись.Организация = Параметры.Организация;
			Запись.НаименованиеМЭДО = МЭДОПереопределяемый.НаименованиеЮрлицаНаДату(
				Параметры.Организация, ТекущаяДатаСеанса());
			
			// Штамп ЭП и рег.штамп, по умолчанию:
			Запись.ШтампСлева = 30;
			Запись.ШтампСверху = 220;
			Запись.СтраницаВставкиШтампаЭП = Перечисления.СтраницаВставкиКартинки.Последняя;
			Запись.РегШтампСлева = 30;
			Запись.РегШтампСверху = 10;
			
			Запись.СпособОбмена = Перечисления.СпособыОбменаМЭДО.ДискСеть;
			
			Заголовок = Заголовок + НСтр("ru = ' - Создание'");
		КонецЕсли;
		
	КонецЕсли;
	
	ДоступностьВидимость();
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	Оповестить("ЗаписьНастроекМЭДО", Запись.Организация, ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура СпособОбменаПриИзменении(Элемент)
	
	ДоступностьВидимость();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ДоступностьВидимость()
	
	Элементы.ГруппаДискСеть.Видимость = (Запись.СпособОбмена = Перечисления.СпособыОбменаМЭДО.ДискСеть);
	Элементы.ГруппаFTP.Видимость = (Запись.СпособОбмена = Перечисления.СпособыОбменаМЭДО.FTP); // В данной версии функционал недоступен.
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервереРедакцииКонфигурации()
	
	Элементы.НаименованиеМЭДО.Заголовок = 
		СтрШаблон(НСтр("ru = 'Наименование %1 для МЭДО'"), МЭДОПереопределяемый.ОрганизацииРодительный());
	
КонецПроцедуры

#КонецОбласти