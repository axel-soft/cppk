#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

// Функция - Получает настройки организации для МЭДО.
//
// Параметры:
//  Организация - ОпределяемыйТип.Организация - организация, для которой надо получить настройки.
//  ДанныеОтвета - см. МЭДОСтруктурыДанных.НовыйОтвет.
//  СписокНастроек - Строка,Неопределено - Список настроек через запятую, если не указан, то получить все настройки
//  ВыдаватьОшибку - Булево - Если передать Ложь, то получение настроек не критично для дальнейшего выполнения.
// 
// Возвращаемое значение:
//  См. НовыйНастройки
Функция НастройкиОрганизации(
	Знач Организация, ДанныеОтвета, СписокНастроек = Неопределено, ВыдаватьОшибку = Истина) Экспорт
	
	Если Не ЗначениеЗаполнено(Организация) Тогда
		Текст = НСтр("ru = 'Ошибка в вызове настроек'");
		ТекстОшибкиПодробно = СтрШаблон(НСтр("ru = 'Не передано значение %1 при получении настроек МЭДО'"),
			МЭДОПереопределяемый.ОрганизацииРодительный());
		МЭДО.ЗаписьВЖурналСобытий(
			Перечисления.УровниСобытийМЭДО.Ошибка, Текст, Текст, ТекстОшибкиПодробно, ДанныеОтвета);
		Данные = НовыйНастройки();
		Данные.Организация = Организация;
		Возврат Данные;
	КонецЕсли;
	
	Данные = МЭДОПовтИсп.НастройкиОрганизации(Организация, СписокНастроек);
	
	Если ВыдаватьОшибку Тогда
		ПроверкаКритичноВажныхПолей(Организация, Данные, ДанныеОтвета);
	КонецЕсли;
	
	Возврат Данные;
	
КонецФункции

// Конструктор настроек организации.
// 
// Возвращаемое значение:
//  Структура:
//	* Организация - ОпределяемыйТип.Организация -
//				  - Неопределено -
//	* АдресМЭДО - Строка -
//	* ВидВходящегоДокументаПоУмолчанию - ОпределяемыйТип.ВидДокументаМЭДО -
//									   - Неопределено -
//	* ГрифИнформацияОграниченногоРаспространения - ОпределяемыйТип.ГрифДоступаМЭДО -
//												 - Неопределено -
//	* ГрифОбычнаяИнформация - ОпределяемыйТип.ГрифДоступаМЭДО -
//							- Неопределено -
//	* ИдентификаторМЭДО - Строка -
//	* КаталогОтправки - Строка -
//	* КаталогПолучения - Строка -
//	* НаименованиеМЭДО - Строка -
//	* РегШтампСверху - Число -
//	* РегШтампСлева - Число -
//	* СтраницаВставкиРегШтампа - ПеречислениеСсылка.СтраницаВставкиКартинки -
//	* СтраницаВставкиШтампаЭП - ПеречислениеСсылка.СтраницаВставкиКартинки -
//	* ШтампСверху - Число -
//	* ШтампСлева - Число -
Функция НовыйНастройки() Экспорт
	
	Данные = Новый Структура();
	Данные.Вставить("Организация", Неопределено);
	Данные.Вставить("АдресМЭДО", "");
	Данные.Вставить("ВидВходящегоДокументаПоУмолчанию", Неопределено);
	Данные.Вставить("ГрифИнформацияОграниченногоРаспространения", Неопределено);
	Данные.Вставить("ГрифОбычнаяИнформация", Неопределено);
	Данные.Вставить("ИдентификаторМЭДО", "");
	Данные.Вставить("КаталогОтправки", "");
	Данные.Вставить("КаталогПолучения", "");
	Данные.Вставить("НаименованиеМЭДО", "");
	Данные.Вставить("РегШтампСверху", 0);
	Данные.Вставить("РегШтампСлева", 0);
	Данные.Вставить("СтраницаВставкиРегШтампа", Перечисления.СтраницаВставкиКартинки.ПустаяСсылка());
	Данные.Вставить("СтраницаВставкиШтампаЭП", Перечисления.СтраницаВставкиКартинки.ПустаяСсылка());
	Данные.Вставить("ШтампСверху", 0);
	Данные.Вставить("ШтампСлева", 0);
	Возврат Данные;
	
КонецФункции

// Функция - Получает настройки организации для МЭДО.
//
// Параметры:
//  Организация - ОпределяемыйТип.Организация - организация, для которой надо получить настройки.
//  Данные - Структура - Поля структуры соответствуют ресурсам регистра.
//  ДанныеОтвета - см. МЭДОСтруктурыДанных.НовыйОтвет.
Процедура ПроверкаКритичноВажныхПолей(Организация, Данные, ДанныеОтвета) Экспорт
	
	Если Данные.Свойство("Организация") И Не ЗначениеЗаполнено(Данные.Организация) Тогда
		ТекстОшибкиПодробно = СтрШаблон(
			НСтр("ru = 'Не заданы настройки %1 ""%2"" для МЭДО'"),
			МЭДОПереопределяемый.ОрганизацииРодительный(), Организация);
		МЭДО.ЗаписьВЖурналСобытий(
			Перечисления.УровниСобытийМЭДО.Ошибка, 
			Организация,
			Текст_ОшибкаВНастройках(),
			ТекстОшибкиПодробно,
			ДанныеОтвета);
		Возврат;
	КонецЕсли;
	
	
	// Некоторые настройки особенно важны:
	МетаРесурсы = Метаданные.РегистрыСведений.НастройкиОрганизацийМЭДО.Ресурсы;
	ПолеНеЗаполнено(Организация, МетаРесурсы.ГрифОбычнаяИнформация, Данные, ДанныеОтвета);
	ПолеНеЗаполнено(Организация, МетаРесурсы.ГрифИнформацияОграниченногоРаспространения, Данные, ДанныеОтвета);
	ПолеНеЗаполнено(Организация, МетаРесурсы.ИдентификаторМЭДО, Данные, ДанныеОтвета);
	ПолеНеЗаполнено(Организация, МетаРесурсы.НаименованиеМЭДО , Данные, ДанныеОтвета);
	ПолеНеЗаполнено(Организация, МетаРесурсы.КаталогОтправки, Данные, ДанныеОтвета);
	ПолеНеЗаполнено(Организация, МетаРесурсы.КаталогПолучения, Данные, ДанныеОтвета);
	ПолеНеЗаполнено(Организация, МетаРесурсы.РегШтампСлева, Данные, ДанныеОтвета);
	ПолеНеЗаполнено(Организация, МетаРесурсы.РегШтампСверху, Данные, ДанныеОтвета);
	ПолеНеЗаполнено(Организация, МетаРесурсы.СтраницаВставкиШтампаЭП, Данные, ДанныеОтвета);
	ПолеНеЗаполнено(Организация, МетаРесурсы.ШтампСлева, Данные, ДанныеОтвета);
	ПолеНеЗаполнено(Организация, МетаРесурсы.ШтампСверху, Данные, ДанныеОтвета);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПолеНеЗаполнено(Организация, МетаРесурс, Данные, ДанныеОтвета)
	
	Если Данные.Свойство(МетаРесурс.Имя) И Не ЗначениеЗаполнено(Данные[МетаРесурс.Имя]) Тогда
		ТекстОшибкиПодробно = СтрШаблон(
			НСтр("ru = 'В настройках МЭДО для %1 ""%2"" не указано значение поля ""%3""'"),
			МЭДОПереопределяемый.ОрганизацииРодительный(), Организация, МетаРесурс.Синоним);
		МЭДО.ЗаписьВЖурналСобытий(
			Перечисления.УровниСобытийМЭДО.Ошибка, Организация,
			Текст_ОшибкаВНастройках(), ТекстОшибкиПодробно, ДанныеОтвета);
		Возврат Истина;
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

Функция Текст_ОшибкаВНастройках()
	
	Возврат СтрШаблон(НСтр("ru = 'Ошибка в настройках %1'"), МЭДОПереопределяемый.ОрганизацииРодительный());
	
КонецФункции

#КонецОбласти

#КонецЕсли