#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Записывает использование рекомедованных настроеки пользователем в базу данных.
//
// Параметры:
//  ИмяФормы                    - Строка                        - Полное имя формы.
//  КлючНазначенияИспользования - Строка                        - Ключ назначения использования.
//  Пользователь                - СправочникСсылка.Пользователи - Пользователь, использующий настройки.
//  ВерсияНастроек              - УникальныйИдентификатор       - Версия используемых рекомендованных настроек.
//
Процедура ЗаписатьИспользованиеНастроек(ИмяФормы, КлючНазначенияИспользования, Пользователь, ВерсияНастроек) Экспорт
	
	СокращенноеИмяФормы = СокращенноеИмяФормы(ИмяФормы);
	
	НаборЗаписей = СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.СокращенноеИмяФормы.Установить(СокращенноеИмяФормы);
	НаборЗаписей.Отбор.КлючНазначенияИспользования.Установить(КлючНазначенияИспользования);
	НаборЗаписей.Отбор.Пользователь.Установить(Пользователь);
	
	НоваяЗапись = НаборЗаписей.Добавить();
	НоваяЗапись.СокращенноеИмяФормы = СокращенноеИмяФормы;
	НоваяЗапись.КлючНазначенияИспользования = КлючНазначенияИспользования;
	НоваяЗапись.Пользователь = Пользователь;
	НоваяЗапись.ВерсияНастроек = ВерсияНастроек;
	НоваяЗапись.ИмяФормы = ИмяФормы;
	
	НаборЗаписей.Записать();
	
КонецПроцедуры

// Преобразует полное имя формы в сокращенное, для хранения в базе данных.
//
// Параметры:
//  ИмяФормы - Строка - Полное имя формы.
// 
// Возвращаемое значение:
//  Строка - Сокращенное имя формы для хранения в базе даннных.
//
Функция СокращенноеИмяФормы(ИмяФормы) Экспорт
	
	МетаданныеРегистра = Метаданные.РегистрыСведений.ИспользованиеРекомендованныхНастроек;
	СокращенноеИмяФормы = ОбщегоНазначенияДокументооборот.СтрХеш(
		ИмяФормы,
		МетаданныеРегистра.Измерения.СокращенноеИмяФормы.Тип.КвалификаторыСтроки.Длина);
	
	Возврат СокращенноеИмяФормы;
	
КонецФункции

// Возвращает текущую версию используемых рекомендованных настроек формы.
//
// Параметры:
//  ИмяФормы                    - Строка                        - Полное имя формы.
//  КлючНазначенияИспользования - Строка                        - Ключ назначения использования.
//  Пользователь                - СправочникСсылка.Пользователи - Пользователь, использующий настройки.
// 
// Возвращаемое значение:
//  УникальныйИдентификатор - Текущая версия рекомендованных настроек формы.
//
Функция ТекущаяВерсияНастроек(ИмяФормы, КлючНазначенияИспользования, Пользователь) Экспорт
	
	СокращенноеИмяФормы = СокращенноеИмяФормы(ИмяФормы);
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
		|	ИспользованиеРекомендованныхНастроек.ВерсияНастроек КАК ВерсияНастроек
		|ИЗ
		|	РегистрСведений.ИспользованиеРекомендованныхНастроек КАК ИспользованиеРекомендованныхНастроек
		|ГДЕ
		|	ИспользованиеРекомендованныхНастроек.СокращенноеИмяФормы = &СокращенноеИмяФормы
		|	И ИспользованиеРекомендованныхНастроек.КлючНазначенияИспользования = &КлючНазначенияИспользования
		|	И ИспользованиеРекомендованныхНастроек.Пользователь = &Пользователь");
	
	Запрос.УстановитьПараметр("СокращенноеИмяФормы", СокращенноеИмяФормы);
	Запрос.УстановитьПараметр("КлючНазначенияИспользования", КлючНазначенияИспользования);
	Запрос.УстановитьПараметр("Пользователь", Пользователь);
	
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	
	ТекущаяВерсияНастроек = ?(Выборка.Следующий(), Выборка.ВерсияНастроек, УникальныйИдентификаторПустой());
	
	Возврат ТекущаяВерсияНастроек;
	
КонецФункции

#КонецОбласти

#КонецЕсли