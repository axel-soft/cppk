#Область ОбработчикиСобытий

&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	ФормаДляСохраненияНастроек = ПараметрыВыполненияКоманды.Источник;
	Если ТипЗнч(ФормаДляСохраненияНастроек) <> Тип("ФормаКлиентскогоПриложения") Тогда
		
		ТекстОшибки = СтрШаблон(
			НСтр("ru = 'Невозможно сохранить рекомендованные настройки для объекта типа ""%1""'"),
			ТипЗнч(ФормаДляСохраненияНастроек));
		
		ВызватьИсключение ТекстОшибки;
		
	КонецЕсли;
	
	ФормаДляСохраненияНастроек.Закрыть();
	Если ФормаДляСохраненияНастроек.Открыта() Тогда
		
		ПоказатьПредупреждение(,
			НСтр("ru = 'Не удалось сохранить настройки, так как форма осталась открытой'"),,
			НСтр("ru = 'Настройки не сохранены'"));
		
		Возврат;
		
	КонецЕсли;
	
	СохранитьНастройкиКакРекомендованные(
		ФормаДляСохраненияНастроек.ИмяФормы,
		ФормаДляСохраненияНастроек.КлючНазначенияИспользования);
	
	ПоказатьОповещениеПользователя(
		НСтр("ru = 'Настройки сохранены'"),,
		НСтр("ru = 'Настройки формы сохранены как рекомендованные'"));
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура СохранитьНастройкиКакРекомендованные(Знач ИмяФормы, Знач КлючНазначенияИспользования)
	
	РаботаСРекомендованнымиНастройками.СохранитьНастройкиКакРекомендованные(ИмяФормы, КлючНазначенияИспользования);
	
КонецПроцедуры

#КонецОбласти