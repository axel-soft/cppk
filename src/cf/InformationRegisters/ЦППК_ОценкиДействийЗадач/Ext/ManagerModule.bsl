#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Получает значение оценки по ссылке действия задачи.
Функция ОценкаДействияЗадачи(ДействиеЗадачи) Экспорт
	
	Если ДействиеЗадачи.Пустая() Тогда
		Возврат 0;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ЦППК_ОценкиДействийЗадач.Оценка КАК Оценка
	|ИЗ
	|	РегистрСведений.ЦППК_ОценкиДействийЗадач КАК ЦППК_ОценкиДействийЗадач
	|ГДЕ
	|	ЦППК_ОценкиДействийЗадач.ДействиеЗадачи = &ДействиеЗадачи";
	
	Запрос.УстановитьПараметр("ДействиеЗадачи", ДействиеЗадачи);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат 0;
	КонецЕсли;
	
	Выборка = РезультатЗапроса.Выбрать();
	Выборка.Следующий();
	
	Возврат Выборка.Оценка;
	
КонецФункции

// Создает запись или обновляет текущую запись оценки действия задачи.
Процедура ЗаписатьОбновитьОценкуДействияЗадачи(ДействиеЗадачи, Оценка) Экспорт
	
	Если ДействиеЗадачи.Пустая() Тогда
		Возврат;
	КонецЕсли;
	
	//++AxelSoft NM 12.08.2024 САНФ-027688
	//Если НЕ РольДоступна("ЦППК_УказаниеОценкиПриСогласовании") Тогда
	Если НЕ РольДоступна("ЦППК_УказаниеОценкиПриСогласовании") И НЕ РольДоступна("АдминистраторСистемы") И НЕ РольДоступна("ПолныеПрава") Тогда
	//--AxelSoft NM 12.08.2024 САНФ-027688
		Возврат;
	КонецЕсли;
	
	Если ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДействиеЗадачи, "ВидДействия") <> Справочники.ВидыДействийЗадач.Согласовать Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	МенеджерЗаписи = РегистрыСведений.ЦППК_ОценкиДействийЗадач.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.ДействиеЗадачи = ДействиеЗадачи;
	МенеджерЗаписи.Оценка = Оценка;
	МенеджерЗаписи.Записать();

КонецПроцедуры			

#КонецОбласти

#КонецЕсли
