
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Фиксирует ошибку работы с сервисом.
// 
// Параметры:
//  ВерсияФайла - СправочникСсылка.ВерсииФайлов
//  ТекстОшибки - Строка
// 
Процедура Добавить(ВерсияФайла, ТекстОшибки) Экспорт
	
	МенеджерЗаписи = СоздатьМенеджерЗаписи();
	МенеджерЗаписи.ВерсияФайла = ВерсияФайла;
	МенеджерЗаписи.Прочитать();
	
	Если МенеджерЗаписи.Выбран() Тогда
		Если МенеджерЗаписи.ТекстОшибки = ТекстОшибки Тогда
			МенеджерЗаписи.КоличествоПопыток = МенеджерЗаписи.КоличествоПопыток + 1;
		Иначе
			МенеджерЗаписи.КоличествоПопыток = 1;
			МенеджерЗаписи.ТекстОшибки = ТекстОшибки;
		КонецЕсли;
	Иначе
		МенеджерЗаписи.ВерсияФайла = ВерсияФайла;
		МенеджерЗаписи.ТекстОшибки = ТекстОшибки;
		МенеджерЗаписи.КоличествоПопыток = 1;
	КонецЕсли;
	
	МенеджерЗаписи.Дата = ТекущаяДатаСеанса();
	МенеджерЗаписи.Записать();
	
КонецПроцедуры

// Удаляет ошибку работы с сервисом.
// 
// Параметры:
//  ВерсияФайла - СправочникСсылка.ВерсииФайлов
// 
Процедура Удалить(ВерсияФайла) Экспорт
	
	МенеджерЗаписи = СоздатьМенеджерЗаписи();
	МенеджерЗаписи.ВерсияФайла = ВерсияФайла;
	МенеджерЗаписи.Удалить();
	
КонецПроцедуры

// Удаляет все ошибки.
// 
Процедура УдалитьВсе() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаймлистОшибкиРаботыССервисом.ВерсияФайла
	|ИЗ
	|	РегистрСведений.ТаймлистОшибкиРаботыССервисом КАК ТаймлистОшибкиРаботыССервисом";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		МенеджерЗаписи = СоздатьМенеджерЗаписи();
		ЗаполнитьЗначенияСвойств(МенеджерЗаписи, Выборка);
		МенеджерЗаписи.Удалить();
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли