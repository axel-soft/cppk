#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Добавляет запись в регистр по переданным параметрам
// 
// Параметры:
//  Документ - СправочникСсылка.ДокументыПредприятия
//  ОтменяемыйДокумент - СправочникСсылка.ДокументыПредприятия
//  ДатаОтмены - Дата
Процедура ЗаписатьДанные(Документ, ОтменяемыйДокумент, ДатаОтмены) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);	
	МенеджерЗаписиРегистра = РегистрыСведений.ЦППК_ИзмененныеДокументы.СоздатьМенеджерЗаписи();
	
	МенеджерЗаписиРегистра.Документ = Документ;
	МенеджерЗаписиРегистра.ОтменяемыйДокумент = ОтменяемыйДокумент;
	МенеджерЗаписиРегистра.ДатаОтмены = ДатаОтмены;
	
	МенеджерЗаписиРегистра.Записать();
	
КонецПроцедуры

// Удаляет запись из регистра по переданным параметрам
// 
// Параметры:
//  Документ  - СправочникСсылка.ДокументыПредприятия
//  ОтменяемыйДокумент  - СправочникСсылка.ДокументыПредприятия
Процедура УдалитьДанные(Документ, ОтменяемыйДокумент) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);	
	МенеджерЗаписиРегистра = РегистрыСведений.ЦППК_ИзмененныеДокументы.СоздатьМенеджерЗаписи();
	
	МенеджерЗаписиРегистра.Документ = Документ;
	МенеджерЗаписиРегистра.ОтменяемыйДокумент = ОтменяемыйДокумент;
	
	МенеджерЗаписиРегистра.Удалить();
	
КонецПроцедуры

// Читает данные в регистре по переданным параметрам (Документ)
// 
// Параметры:
//  Документ  - СправочникСсылка.ДокументыПредприятия
// 
// Возвращаемое значение:
//  Массив из Структура
// 	*Документ - СправочникСсылка.ДокументыПредприятия
//	*ДатаОтмены - Дата
Функция ПрочитатьДанные(Документ) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);	
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	ЦППК_ИзмененныеДокументы.Документ,
	|	ЦППК_ИзмененныеДокументы.ОтменяемыйДокумент,
	|	ЦППК_ИзмененныеДокументы.ДатаОтмены
	|ИЗ
	|	РегистрСведений.ЦППК_ИзмененныеДокументы КАК ЦППК_ИзмененныеДокументы
	|ГДЕ
	|	ЦППК_ИзмененныеДокументы.Документ = &Документ";
	
	Запрос.Параметры.Вставить("Документ", Документ);
	Таблица = Запрос.Выполнить().Выгрузить();
	
	МассивДокументов = Новый Массив;
	
	Для Каждого Запись Из Таблица Цикл
		
		ОписаниеДок = Новый Структура("Документ, ДатаОтмены", Запись.ОтменяемыйДокумент, Запись.ДатаОтмены);
		МассивДокументов.Добавить(ОписаниеДок);	
		
	КонецЦикла;
	
	Возврат МассивДокументов;
	
КонецФункции

// Читает данные в регистре по переданным параметрам (ОтменяемыйДокумент)
// 
// Параметры:
//  ОтменяемыйДокумент - СправочникСсылка.ДокументыПредприятия
// 
// Возвращаемое значение:
//  Массив из Структура
// 	*Документ - СправочникСсылка.ДокументыПредприятия
//	*ДатаОтмены - Дата
Функция ПрочитатьНовыеДокументы(ОтменяемыйДокумент) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);	

	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	ЦППК_ИзмененныеДокументы.Документ,
	|	ЦППК_ИзмененныеДокументы.ОтменяемыйДокумент,
	|	ЦППК_ИзмененныеДокументы.ДатаОтмены
	|ИЗ
	|	РегистрСведений.ЦППК_ИзмененныеДокументы КАК ЦППК_ИзмененныеДокументы
	|ГДЕ
	|	ЦППК_ИзмененныеДокументы.ОтменяемыйДокумент = &ОтменяемыйДокумент";
	
	Запрос.Параметры.Вставить("ОтменяемыйДокумент", ОтменяемыйДокумент);
	Таблица = Запрос.Выполнить().Выгрузить();
	
	МассивДокументов = Новый Массив;
	
	Для Каждого Запись Из Таблица Цикл
		
		ОписаниеДок = Новый Структура("Документ, ДатаОтмены", Запись.Документ, Запись.ДатаОтмены);
		МассивДокументов.Добавить(ОписаниеДок);	
		
	КонецЦикла;
	
	Возврат МассивДокументов;
	
КонецФункции

#КонецОбласти

#КонецЕсли
