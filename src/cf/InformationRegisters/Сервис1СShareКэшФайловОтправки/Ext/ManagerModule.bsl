// @strict-types

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Записать файлы.
// 
// Параметры:
//  ДокументОтправки - СправочникСсылка.ДокументыПредприятия
//  Файлы - Массив Из СправочникСсылка.Файлы
//
Процедура ЗаписатьФайлы(ДокументОтправки, Файлы) Экспорт
	
	НаборЗаписей = СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.ДокументОтправки.Установить(ДокументОтправки, Истина);
	
	Для Каждого Файл Из Файлы Цикл
		НоваяЗапись = НаборЗаписей.Добавить();
		НоваяЗапись.ДокументОтправки = ДокументОтправки;
		НоваяЗапись.Файл = Файл;
	КонецЦикла;	
	
	НаборЗаписей.Записать();
		
КонецПроцедуры

// Удалить файлы.
// 
// Параметры:
//  ДокументОтправки - СправочникСсылка.ДокументыПредприятия
//
Процедура УдалитьФайлыОтправки(ДокументОтправки) Экспорт
	
	НаборЗаписей = СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.ДокументОтправки.Установить(ДокументОтправки, Истина);
	НаборЗаписей.Записать();
		
КонецПроцедуры

// Файлы отправки.
// 
// Параметры:
//  ДокументОтправки - СправочникСсылка.ДокументыПредприятия
// 
// Возвращаемое значение:
//  Массив Из СправочникСсылка.Файлы
//
Функция ФайлыОтправки(ДокументОтправки) Экспорт
	
	Файлы = Новый Массив; // Массив Из СправочникСсылка.Файлы
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Сервис1СShareКэшФайловОтправки.Файл
		|ИЗ
		|	РегистрСведений.Сервис1СShareКэшФайловОтправки КАК Сервис1СShareКэшФайловОтправки
		|ГДЕ
		|	Сервис1СShareКэшФайловОтправки.ДокументОтправки = &ДокументОтправки";
	
	Запрос.УстановитьПараметр("ДокументОтправки", ДокументОтправки);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Файлы.Добавить(Выборка.Файл);
	КонецЦикла;
	
	Возврат Файлы;
	
КонецФункции	

#КонецОбласти

#КонецЕсли
