
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

Процедура ПеренестиЗаписиВЗанятостьФизическихЛиц(Параметры) Экспорт
	
	Запрос = Новый Запрос;
	
	Если Параметры.ПрогрессВыполнения.ВсегоОбъектов = 0 Тогда
		
		Запрос.Текст =
			"ВЫБРАТЬ
			|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Сотрудники.Владелец) КАК Количество
			|ИЗ
			|	РегистрСведений.ЗанятостьСотрудников КАК ЗанятостьСотрудников
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Сотрудники КАК Сотрудники
			|		ПО ЗанятостьСотрудников.Сотрудник = Сотрудники.Ссылка";
		Выборка = Запрос.Выполнить().Выбрать();
		Выборка.Следующий();
		
		Параметры.ПрогрессВыполнения.ВсегоОбъектов = Выборка.Количество;
		
	КонецЕсли;
	
	Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ ПЕРВЫЕ 100
		|	Сотрудники.Владелец КАК ФизическоеЛицо
		|ПОМЕСТИТЬ ФизическиеЛица
		|ИЗ
		|	РегистрСведений.ЗанятостьСотрудников КАК ЗанятостьСотрудников
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Сотрудники КАК Сотрудники
		|		ПО ЗанятостьСотрудников.Сотрудник = Сотрудники.Ссылка
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ФизическоеЛицо
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Сотрудники.Владелец КАК ФизическоеЛицо,
		|	ЗанятостьСотрудников.Сотрудник КАК Сотрудник,
		|	ЗанятостьСотрудников.ДатаНачала КАК ДатаНачала,
		|	ЗанятостьСотрудников.ДатаОкончания КАК ДатаОкончания,
		|	ЗанятостьСотрудников.Занят КАК Занят,
		|	ВЫБОР
		|		КОГДА ОсновныеСотрудники.ФизическоеЛицо ЕСТЬ NULL
		|			ТОГДА 2
		|		ИНАЧЕ 1
		|	КОНЕЦ КАК Приоритет
		|ИЗ
		|	РегистрСведений.ЗанятостьСотрудников КАК ЗанятостьСотрудников
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Сотрудники КАК Сотрудники
		|		ПО ЗанятостьСотрудников.Сотрудник = Сотрудники.Ссылка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ФизическиеЛица КАК ФизическиеЛица
		|		ПО Сотрудники.Владелец = ФизическиеЛица.ФизическоеЛицо
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОсновныеСотрудники КАК ОсновныеСотрудники
		|		ПО ЗанятостьСотрудников.Сотрудник = ОсновныеСотрудники.Сотрудник
		|
		|УПОРЯДОЧИТЬ ПО
		|	Приоритет
		|ИТОГИ
		|ПО
		|	ФизическоеЛицо";
	
	Выборка = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	ОбработаноОбъектов = 0;
	ПроблемныхОбъектов = 0;
	
	ПриоритетыФизЛиц = Новый Соответствие();
	
	Пока Выборка.Следующий() Цикл
		
		ВыборкаДетали = Выборка.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		
		НачатьТранзакцию();
		
		Попытка
			
			ОбработанныеСотрудники = Новый Соответствие();
			
			Пока ВыборкаДетали.Следующий() Цикл
				
				Если ОбработанныеСотрудники[ВыборкаДетали.Сотрудник] = Истина Тогда
					Продолжить;
				Конецесли;
				
				Набор = РегистрыСведений.ЗанятостьСотрудников.СоздатьНаборЗаписей();
				Набор.Отбор.Сотрудник.Установить(ВыборкаДетали.Сотрудник);
				ОбновлениеИнформационнойБазы.ЗаписатьДанные(Набор);
				
				ОбработанныеСотрудники[ВыборкаДетали.Сотрудник] = Истина;
				
			КонецЦикла;
			
			ВыборкаДетали.Сбросить();
			
			Пока ВыборкаДетали.Следующий() Цикл
				
				ПриоритетФизЛица = ПриоритетыФизЛиц[Выборка.ФизическоеЛицо];
				Если ПриоритетФизЛица = Неопределено Тогда
					ПриоритетыФизЛиц[Выборка.ФизическоеЛицо] = ВыборкаДетали.Приоритет;
					ПриоритетФизЛица = ВыборкаДетали.Приоритет;
				КонецЕсли;
				
				Если ПриоритетФизЛица <> ВыборкаДетали.Приоритет Тогда
					Продолжить;
				КонецЕсли;
				
				// Пишем в новый регистр только записи самого приоритетного физ. лица.
				// Приорите определяет самая первая запись по физ. лицу.
				// В общем случае в выборке будут записи по физ. лицу с одним приоритетом.
				
				Набор = РегистрыСведений.ЗанятостьФизическихЛиц.СоздатьНаборЗаписей();
				Набор.Отбор.ФизическоеЛицо.Установить(ВыборкаДетали.ФизическоеЛицо);
				Набор.Отбор.ДатаНачала.Установить(ВыборкаДетали.ДатаНачала);
				Набор.Отбор.ДатаОкончания.Установить(ВыборкаДетали.ДатаОкончания);
				
				Запись = Набор.Добавить();
				Запись.ФизическоеЛицо = ВыборкаДетали.ФизическоеЛицо;
				Запись.ДатаНачала = ВыборкаДетали.ДатаНачала;
				Запись.ДатаОкончания = ВыборкаДетали.ДатаОкончания;
				Запись.Занят = ВыборкаДетали.Занят;
				ОбновлениеИнформационнойБазы.ЗаписатьДанные(Набор);
				
			КонецЦикла;
			
			ОбработаноОбъектов = ОбработаноОбъектов + 1;
			
			ЗафиксироватьТранзакцию();
			
		Исключение
			
			ОтменитьТранзакцию();
			
			ПроблемныхОбъектов = ПроблемныхОбъектов + 1;
			
			ТекстСообщения = СтрШаблон(
				НСтр("ru = 'Не удалось обработать записи занятости сотрудников физ. лица: %1 по причине:
					|%2'"),
				Выборка.ФизическоеЛицо,
				ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			ЗаписьЖурналаРегистрации(
				ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
				УровеньЖурналаРегистрации.Ошибка,,,
				ТекстСообщения);
			
		КонецПопытки;
	
	КонецЦикла;
	
	Если ОбработаноОбъектов = 0 И ПроблемныхОбъектов > 0 Тогда
		ТекстСообщения = СтрШаблон(
			НСтр("ru = 'Процедуре ПеренестиЗаписиВЗанятостьФизическихЛиц не удалось обработать записи занятости сотрудников (пропущены): %1'"),
			ПроблемныхОбъектов);
		ВызватьИсключение ТекстСообщения;
	КонецЕсли;
	
	Параметры.ПрогрессВыполнения.ОбработаноОбъектов = 
		Параметры.ПрогрессВыполнения.ОбработаноОбъектов + ОбработаноОбъектов;
	
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ЗанятостьСотрудников.Сотрудник
		|ИЗ
		|	РегистрСведений.ЗанятостьСотрудников КАК ЗанятостьСотрудников
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Сотрудники КАК Сотрудники
		|		ПО ЗанятостьСотрудников.Сотрудник = Сотрудники.Ссылка";
	
	Параметры.ОбработкаЗавершена = Запрос.Выполнить().Пустой();
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли