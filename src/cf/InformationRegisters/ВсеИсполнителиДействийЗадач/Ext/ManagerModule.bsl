#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает актуальные замещения по действию.
//
// Параметры:
//  ДействиеЗадачи - ДокументСсылка.ДействиеЗадачи - ссылка на действие задачи.
//  ВидЗамещения - ПеречислениеСсылка.ВидыЗамещения - вид замещения для отбора.
//  	Если не указан, то все виды.
//
// Возвращаемое значение:
//  ТаблицаЗначений - таблица замещений.
//   * Замещение - СправочникСсылка.ЗамещающиеИПомощники.
//   * ВидЗамещения - ПеречислениеСсылка.ВидыЗамещения.
//   * ВидЗЗамещающийамещения - СправочникСсылка.Сотрудники.
//
Функция ЗамещенияПоДействию(ДействиеЗадачи, ВидЗамещения = Неопределено) Экспорт
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	ЗамещающиеИПомощники.Ссылка КАК Замещение,
		|	ЗамещающиеИПомощники.ВидЗамещения,
		|	ЗамещающиеИПомощники.Замещающий
		|ИЗ
		|	РегистрСведений.ВсеИсполнителиДействийЗадач КАК ВсеИсполнителиДействийЗадач
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ЗамещающиеИПомощники КАК ЗамещающиеИПомощники
		|		ПО ВсеИсполнителиДействийЗадач.Основание = ЗамещающиеИПомощники.Ссылка
		|ГДЕ
		|	ВсеИсполнителиДействийЗадач.ДействиеЗадачи = &ДействиеЗадачи");
	Если ЗначениеЗаполнено(ВидЗамещения) Тогда
		Запрос.Текст = Запрос.Текст + "
			|	И ЗамещающиеИПомощники.ВидЗамещения = &ВидЗамещения";
	КонецЕсли;
	Запрос.УстановитьПараметр("ДействиеЗадачи", ДействиеЗадачи);
	Запрос.УстановитьПараметр("ВидЗамещения", ВидЗамещения);
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

// Возвращает признак наличия замещающих по действию (помощники не учитываются).
//
// Параметры:
//  ДействиеЗадачи - ДокументСсылка.ДействиеЗадачи - ссылка на действие задачи.
//
// Возвращаемое значение:
//  Булево - Истина, если есть хотя бы один замещающий по этому действию.
//
Функция ЕстьЗамещающиеПоДействию(ДействиеЗадачи) Экспорт
	
	Замещения = ЗамещенияПоДействию(ДействиеЗадачи, Перечисления.ВидыЗамещения.Замещающие);
	
	Возврат Замещения.Количество() > 0;
	
КонецФункции

#КонецОбласти

#КонецЕсли
