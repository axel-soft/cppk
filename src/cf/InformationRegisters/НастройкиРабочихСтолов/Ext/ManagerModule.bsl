#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает значение настройки для указанного пользователя.
//
// Параметры:
//  Пользователь - СправочникСсылка.Пользователи - Пользователь.
//  Настройка - ПеречислениеСсылка.НастройкиРабочегоСтола - Настройка рабочего стола.
// 
// Возвращаемое значение:
//  Произвольный - Значение настройки.
//
Функция ЗначениеНастройки(Пользователь, Настройка) Экспорт
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	НастройкиРабочихСтолов.ЗначениеНастройки КАК ЗначениеНастройки
		|ИЗ
		|	РегистрСведений.НастройкиРабочихСтолов КАК НастройкиРабочихСтолов
		|ГДЕ
		|	НастройкиРабочихСтолов.Пользователь = &Пользователь
		|	И НастройкиРабочихСтолов.Настройка = &Настройка");
	
	Запрос.УстановитьПараметр("Пользователь", Пользователь);
	Запрос.УстановитьПараметр("Настройка", Настройка);
	
	Выборка = Запрос.Выполнить().Выбрать();

	Если Не ПараметрыСеанса.ЭтоМобильныйКлиент Тогда
		ЗначениеНастройки = ?(Выборка.Следующий(), Выборка.ЗначениеНастройки.Получить(), Неопределено);
	Иначе
		ЗначениеНастройки = ?(Настройка = Перечисления.НастройкиРабочегоСтола.ВариантКомпоновки,
			Неопределено,
			?(Выборка.Следующий(), Выборка.ЗначениеНастройки.Получить(), Неопределено));
	КонецЕсли;
	
	Если ЗначениеНастройки = Неопределено Тогда
		ЗначениеНастройки = Перечисления.НастройкиРабочегоСтола.ЗначениеНастройкиПоУмолчанию(Настройка);
	КонецЕсли;
	
	Возврат ЗначениеНастройки;
	
КонецФункции

// Устанавливает значение настройки для указанного пользователя.
//
// Параметры:
//  Пользователь - СправочникСсылка.Пользователи - Пользователь.
//  Настройка - ПеречислениеСсылка.НастройкиРабочегоСтола - Настройка рабочего стола.
//  ЗначениеНастройки - Произвольный - Значение настройки.
//
Процедура УстановитьЗначениеНастройки(Пользователь, Настройка, ЗначениеНастройки) Экспорт
	
	НаборЗаписей = СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Пользователь.Установить(Пользователь);
	НаборЗаписей.Отбор.Настройка.Установить(Настройка);
	
	Если ЗначениеНастройки <> Неопределено Тогда
		СтрокаНабора = НаборЗаписей.Добавить();
		СтрокаНабора.Пользователь = Пользователь;
		СтрокаНабора.Настройка = Настройка;
		СтрокаНабора.ЗначениеНастройки = Новый ХранилищеЗначения(ЗначениеНастройки);
		СтрокаНабора.ПредставлениеНастройки = Строка(ЗначениеНастройки);
	КонецЕсли;
	
	НаборЗаписей.Записать();
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли