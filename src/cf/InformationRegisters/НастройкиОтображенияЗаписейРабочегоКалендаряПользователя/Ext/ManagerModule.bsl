
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

Процедура ПеренестиЗаписиВНастройкиОтображенияЗаписейРабочегоКалендаря(Параметры) Экспорт
	
	Запрос = Новый Запрос;
	
	Если Параметры.ПрогрессВыполнения.ВсегоОбъектов = 0 Тогда
		
		Запрос.Текст =
			"ВЫБРАТЬ
			|	КОЛИЧЕСТВО(*) КАК Количество
			|ИЗ
			|	РегистрСведений.НастройкиОтображенияЗаписейРабочегоКалендаряПользователя КАК
			|		НастройкиОтображенияЗаписейРабочегоКалендаряПользователя";
		Выборка = Запрос.Выполнить().Выбрать();
		Выборка.Следующий();
		
		Параметры.ПрогрессВыполнения.ВсегоОбъектов = Выборка.Количество;
		
	КонецЕсли;
	
	Запрос.Текст =
		"ВЫБРАТЬ ПЕРВЫЕ 1000
		|	НастройкиОтображенияЗаписейРабочегоКалендаряПользователя.Пользователь,
		|	НастройкиОтображенияЗаписейРабочегоКалендаряПользователя.Событие,
		|	НастройкиОтображенияЗаписейРабочегоКалендаряПользователя.Настройка,
		|	НастройкиОтображенияЗаписейРабочегоКалендаряПользователя.ЗначениеНастройки
		|ИЗ
		|	РегистрСведений.НастройкиОтображенияЗаписейРабочегоКалендаряПользователя КАК
		|		НастройкиОтображенияЗаписейРабочегоКалендаряПользователя";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	ОбработаноОбъектов = 0;
	ПроблемныхОбъектов = 0;
	
	Пока Выборка.Следующий() Цикл
		
		НачатьТранзакцию();
		
		Попытка
			
			Набор = РегистрыСведений.НастройкиОтображенияЗаписейРабочегоКалендаряПользователя.СоздатьНаборЗаписей();
			Набор.Отбор.Пользователь.Установить(Выборка.Пользователь);
			Набор.Отбор.Событие.Установить(Выборка.Событие);
			Набор.Отбор.Настройка.Установить(Выборка.Настройка);
			ОбновлениеИнформационнойБазы.ЗаписатьДанные(Набор);
			
			ФизическоеЛицо = ПользователиДокументооборот.ФизЛицоПользователя(Выборка.Пользователь);
			
			Набор = РегистрыСведений.НастройкиОтображенияЗаписейРабочегоКалендаря.СоздатьНаборЗаписей();
			Набор.Отбор.ФизическоеЛицо.Установить(ФизическоеЛицо);
			Набор.Отбор.Событие.Установить(Выборка.Событие);
			Набор.Отбор.Настройка.Установить(Выборка.Настройка);
			
			Запись = Набор.Добавить();
			Запись.ФизическоеЛицо = ФизическоеЛицо;
			Запись.Событие = Выборка.Событие;
			Запись.Настройка = Выборка.Настройка;
			Запись.ЗначениеНастройки = Выборка.ЗначениеНастройки;
			ОбновлениеИнформационнойБазы.ЗаписатьДанные(Набор);
			
			ОбработаноОбъектов = ОбработаноОбъектов + 1;
			
			ЗафиксироватьТранзакцию();
			
		Исключение
			
			ОтменитьТранзакцию();
			
			ПроблемныхОбъектов = ПроблемныхОбъектов + 1;
			
			ТекстСообщения = СтрШаблон(
				НСтр("ru = 'Не удалось обработать настройку отображения события календаря ""%1"" для пользователя ""%2"", по причине:
					|%3'"),
				Выборка.Событие,
				Выборка.Пользователь,
				ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			ЗаписьЖурналаРегистрации(
				ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
				УровеньЖурналаРегистрации.Ошибка,,,
				ТекстСообщения);
			
		КонецПопытки;
	
	КонецЦикла;
	
	Если ОбработаноОбъектов = 0 И ПроблемныхОбъектов > 0 Тогда
		ТекстСообщения = СтрШаблон(
			НСтр("ru = 'Процедуре ПеренестиЗаписиВНастройкиОтображенияЗаписейРабочегоКалендаря не удалось обработать некоторые настройки отображения (пропущены): %1'"),
			ПроблемныхОбъектов);
		ВызватьИсключение ТекстСообщения;
	КонецЕсли;
	
	Параметры.ПрогрессВыполнения.ОбработаноОбъектов = 
		Параметры.ПрогрессВыполнения.ОбработаноОбъектов + ОбработаноОбъектов;
	
	Параметры.ОбработкаЗавершена = (Выборка.Количество() = 0);
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли