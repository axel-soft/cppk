#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Записать результат условия.
// 
// Параметры:
//  ИмяЭлемента  - Строка
//  Процесс - БизнесПроцессСсылка.КомплексныйПроцесс
//  РезультатУсловия - Булево
Процедура ЗаписатьРезультатУсловия(ИмяЭлемента, Процесс, РезультатУсловия) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	МенеджерЗаписи = РегистрыСведений.РезультатыВыполненияУсловийСхемПроцессов.СоздатьМенеджерЗаписи();
	
	МенеджерЗаписи.Процесс = Процесс;
	МенеджерЗаписи.ИмяЭлемента = ИмяЭлемента;
	МенеджерЗаписи.Результат = РезультатУсловия;
	МенеджерЗаписи.Период = ТекущаяДатаСеанса();
	
	МенеджерЗаписи.Записать();
	
	РаботаСПроцессамиПоОбработкамОбъектовСобытия.ПриЗаписиРезультатаУсловия(ИмяЭлемента, Процесс, РезультатУсловия);

КонецПроцедуры

// Прочитать результаты условий.
// 
// Параметры:
//  Процесс - БизнесПроцессСсылка.КомплексныйПроцесс
//  
//  Возвращаемое значение:
//  Массив Из Структура
//     * ИмяЭлемента - Строка
//     * Результат - Булево
//     * Процесс - БизнесПроцессССылка.КомплексныйПроцесс
//
Функция РезультатыВыполненияУсловий(Процесс) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ДатаСреза = ТекущаяДатаСеанса();
	Отбор = Новый Структура("Процесс", Процесс);
	
	Таблица = РегистрыСведений.РезультатыВыполненияУсловийСхемПроцессов.СрезПоследних(
		ДатаСреза, Отбор);
		
	РезультатыУсловий = Новый Массив;
	
	Для Каждого Стр Из Таблица Цикл
		
		ЗначениеСтруктура = Новый Структура("Процесс, ИмяЭлемента, Результат");
		ЗаполнитьЗначенияСвойств(ЗначениеСтруктура, Стр);
		
		РезультатыУсловий.Добавить(ЗначениеСтруктура);
		
	КонецЦикла;	 
	
	Возврат РезультатыУсловий;	
	
КонецФункции

#КонецОбласти

#КонецЕсли
