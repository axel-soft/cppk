
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Вернет настройку или Неопределено, если ее нет
//
//
// Параметры:
//  Объект - СправочникССылка.ДокументыПредприятия
//
// Возвращаемое значение:
//   СправочникССылка.НастройкиГенерацииШтамповЭП
//
Функция ПолучитьНастройку(Объект) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	НастройкиГенерацииШтамповЭППоВидамДокументов.Настройка КАК Настройка
	|ИЗ
	|	РегистрСведений.НастройкиГенерацииШтамповЭППоВидамДокументов КАК НастройкиГенерацииШтамповЭППоВидамДокументов
	|ГДЕ
	|	НастройкиГенерацииШтамповЭППоВидамДокументов.Объект = &Объект";
	Запрос.УстановитьПараметр("Объект", Объект);
	
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		Возврат Неопределено
	КонецЕсли;
	
	Выборка = Результат.Выбрать();
	Выборка.Следующий();
	Возврат Выборка.Настройка;
	
КонецФункции

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Добавляет запись в регистр.
//
// Параметры:
//  Объект - СправочникССылка.ДокументыПредприятия
//  Настройка - СправочникССылка.НастройкиГенерацииШтамповЭП
//
Процедура ДобавитьЗапись(Объект, Настройка) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	НаборЗаписей = СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Объект.Установить(Объект);
	
	Запись = НаборЗаписей.Добавить();
	Запись.Объект = Объект;
	Запись.Настройка = Настройка;
	
	НаборЗаписей.Записать();
	
КонецПроцедуры

// Удаляет запись из регистра.
//
// Параметры:
//  Объект - СправочникССылка.ДокументыПредприятия
//
Процедура УдалитьЗапись(Объект) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	НаборЗаписей = СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Объект.Установить(Объект);
	
	НаборЗаписей.Записать();
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли