
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("Контрагент") И ЗначениеЗаполнено(Параметры.Контрагент) Тогда
		
		АвтоЗаголовок = Ложь;
		Заголовок = СтрШаблон(
			"%1: %2", Метаданные.РегистрыСведений.НастройкиКонтрагентовМЭДО.ПредставлениеЗаписи, Параметры.Контрагент);
		
		ЗаписьВРегистре = РегистрыСведений.НастройкиКонтрагентовМЭДО.СоздатьМенеджерЗаписи();
		ЗаписьВРегистре.Контрагент = Параметры.Контрагент;
		ЗаписьВРегистре.Прочитать();
		
		Если ЗначениеЗаполнено(ЗаписьВРегистре.Контрагент) Тогда
			ЗначениеВДанныеФормы(ЗаписьВРегистре, Запись);
		Иначе // Нет в БД, это новая запись:
			Запись.Контрагент = Параметры.Контрагент;
			Запись.АктуализироватьВерсию = Истина; // По умолчанию.
			
			Заголовок = Заголовок + НСтр("ru = ' - Создание'");
		КонецЕсли;
		
		ИдентификаторМЭДО = МЭДОПереопределяемый.ИдентификаторВнешнегоОбъекта(
			Запись.Контрагент, МЭДО.Обозначение_ВнешнийКонтрагент());
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	// Проверки только на уровне формы, т.к. регистр заполняется в основном программно:
	Если Не ЗначениеЗаполнено(Запись.АдресМЭДО) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(
			НСтр("ru = 'Не указан ""Адрес МЭДО""'"), , "Запись.АдресМЭДО", , Отказ);
	КонецЕсли;
	Если Не ЗначениеЗаполнено(Запись.ВерсияФорматаМЭДО) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(
			НСтр("ru = 'Не указана ""Версия формата МЭДО""'"), , "Запись.ВерсияФорматаМЭДО", , Отказ);
	КонецЕсли;
	Если Не ЗначениеЗаполнено(ИдентификаторМЭДО) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(
			НСтр("ru = 'Не указан ""Идентификатор МЭДО""'"), , "ИдентификаторМЭДО", , Отказ);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	Оповестить("ЗаписьНастроекМЭДО", Запись.Контрагент, ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ПриЗаписиНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Если ИдентификаторМенялся Тогда
		Если Не СтроковыеФункцииКлиентСервер.ЭтоУникальныйИдентификатор(ИдентификаторМЭДО) Тогда
			ОбщегоНазначения.СообщитьПользователю(
				НСтр("ru = 'Идентификатор МЭДО должен иметь вид: xxxxxxxx-xxxx-xxxx-xxxx-xxxxxxxxxxxx'"),
				,
				"ИдентификаторМЭДО",
				"",
				Отказ);
			Возврат;
		КонецЕсли;
		
		МЭДОПереопределяемый.СоздатьПроверитьСвязьОбъектаИБИВнешнегоОбъекта(
			ИдентификаторМЭДО, МЭДО.Обозначение_ВнешнийКонтрагент(), Запись.Контрагент);
		ИдентификаторМенялся = Ложь;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ИдентификаторМЭДОПриИзменении(Элемент)
	
	ИдентификаторМенялся = Истина;
	
КонецПроцедуры

#КонецОбласти