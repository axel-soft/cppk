// @strict-types

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

// Получает настройки контрагента для МЭДО.
// 
// Параметры:
//  Контрагент - ОпределяемыйТип.КонтрагентМЭДО - Контрагент, для которого надо получить настройки.
//  ДанныеОтвета - см. МЭДОСтруктурыДанных.НовыйОтвет.
// 
// Возвращаемое значение:
//  См. НовыйНастройкиКонтрагента
Функция НастройкиКонтрагента(Контрагент, ДанныеОтвета) Экспорт
	
	Если Не ЗначениеЗаполнено(Контрагент) Тогда
		Текст = НСтр("ru = 'Не задан контрагент'");
		МЭДО.ЗаписьВЖурналСобытий(
			Перечисления.УровниСобытийМЭДО.Ошибка, Текст, Текст,
			НСтр("ru = 'При получении настроек контрагента для МЭДО не задан контрагент'"), ДанныеОтвета);
		Возврат НовыйНастройкиКонтрагента();
	КонецЕсли;
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ *
		|ИЗ
		|	РегистрСведений.НастройкиКонтрагентовМЭДО КАК Настройки
		|ГДЕ
		|	Настройки.Контрагент = &Контрагент");
	Запрос.УстановитьПараметр("Контрагент", Контрагент);
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		МЭДО.ЗаписьВЖурналСобытий(
			Перечисления.УровниСобытийМЭДО.Ошибка,
			Контрагент,
			Текст_ОшибкаВНастройках(), 
			СтрШаблон(
				НСтр("ru = 'Не заданы настройки контрагента ""%1"" для МЭДО'"),
				Контрагент),
			ДанныеОтвета);
	КонецЕсли;
	
	Выборка = Результат.Выбрать();
	Настройки = НовыйНастройкиКонтрагента();
	Если Выборка.Следующий() Тогда
		ЗаполнитьЗначенияСвойств(Настройки, Выборка);
	КонецЕсли;
	Возврат Настройки;
	
КонецФункции

// Получает настройки массива контрагентов для МЭДО.
// 
// Параметры:
//  Контрагенты - Массив из ОпределяемыйТип.КонтрагентМЭДО - Контрагенты, для которых надо получить настройки.
//  ДанныеОтвета - См. МЭДОСтруктурыДанных.НовыйОтвет.
// 
// Возвращаемое значение:
//  ТаблицаЗначений - Строки таблицы см. НовыйНастройкиКонтрагента
Функция НастройкиМассиваКонтрагентов(Контрагенты, ДанныеОтвета) Экспорт
	
	Если Контрагенты.Количество() = 0 Тогда
		Текст = НСтр("ru = 'Не заданы контрагенты'");
		МЭДО.ЗаписьВЖурналСобытий(
			Перечисления.УровниСобытийМЭДО.Ошибка, Текст, Текст,
			НСтр("ru = 'При получении настроек контрагентов для МЭДО не заданы контрагенты'"), ДанныеОтвета);
		
		ТаблицаНастроек = Новый ТаблицаЗначений();
		Настройки = НовыйНастройкиКонтрагента();
		Для Каждого КлючЗначение Из Настройки Цикл
			ТаблицаНастроек.Колонки.Добавить(КлючЗначение.Ключ);
		КонецЦикла;
		Возврат ТаблицаНастроек;
	КонецЕсли;
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ *
		|ИЗ
		|	РегистрСведений.НастройкиКонтрагентовМЭДО КАК Настройки
		|ГДЕ
		|	Настройки.Контрагент В (&Контрагенты)");
	Запрос.УстановитьПараметр("Контрагенты", Контрагенты);
	ТаблицаНастроек = Запрос.Выполнить().Выгрузить();
	Если ТаблицаНастроек.Количество() <> Контрагенты.Количество() Тогда
		КонтрагентыДляЛога = Новый Массив(); // Массив Из Строка
		Для Каждого Контрагент Из Контрагенты Цикл
			Если ТаблицаНастроек.Найти(Контрагент, "Контрагент") = Неопределено Тогда
				КонтрагентыДляЛога.Добавить("" + Контрагент);
			КонецЕсли;
		КонецЦикла;
		КонтрагентыДляЛогаСтрокой = СтрСоединить(КонтрагентыДляЛога, ", ");
		МЭДО.ЗаписьВЖурналСобытий(
			Перечисления.УровниСобытийМЭДО.Ошибка,
			КонтрагентыДляЛогаСтрокой,
			Текст_ОшибкаВНастройках(), 
			НСтр("ru = 'Не заданы настройки списка контрагентов для МЭДО'"),
			ДанныеОтвета);
	КонецЕсли;
	
	Возврат ТаблицаНастроек;
	
КонецФункции

// Обновить настройки контрагента. Если они не изменились, но ничего не делать.
// 
// Параметры:
//  Контрагент - ОпределяемыйТип.КонтрагентМЭДО - Контрагент
//  Настройки - См. НовыйНастройкиКонтрагента
// 
// Возвращаемое значение:
//  Булево - Обновление произошло или нет.
Функция ОбновитьНастройки(Контрагент, Настройки) Экспорт
	
	Если Не ЗначениеЗаполнено(Контрагент) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ЕстьАдрес = Настройки.Свойство("АдресМЭДО") И ЗначениеЗаполнено(Настройки.АдресМЭДО);
	ЕстьВерсия = Настройки.Свойство("ВерсияФорматаМЭДО") И ЗначениеЗаполнено(Настройки.ВерсияФорматаМЭДО);
	
	Если ЕстьАдрес Или ЕстьВерсия Тогда
		Набор = СоздатьНаборЗаписей();
		Набор.Отбор.Контрагент.Установить(Контрагент);
		Набор.Прочитать();
		Записать = Ложь;
		Запись = Неопределено;
		Если Набор.Количество() = 0 Тогда
			Запись = Набор.Добавить();
			Запись.АктуализироватьВерсию = Истина;
		Иначе
			Запись = Набор[0];
		КонецЕсли;
		Запись.Контрагент = Контрагент;
		Если ЕстьАдрес И МЭДО.ПрисвоитьОтличающееся(Запись.АдресМЭДО, Настройки.АдресМЭДО) Тогда
			Записать = Истина;
		КонецЕсли;
		Если ЕстьВерсия
			И Запись.АктуализироватьВерсию
			И МЭДО.ПрисвоитьОтличающееся(Запись.ВерсияФорматаМЭДО, Настройки.ВерсияФорматаМЭДО) Тогда
			Записать = Истина;
		КонецЕсли;
		Если Записать Тогда
			Набор.Записать();
			Возврат Истина;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Конструктор настроек контрагента для МЭДО
// 
// Возвращаемое значение:
//  Структура:
// * Контрагент - СправочникСсылка.Контрагенты -
// * АдресМЭДО - Строка -
// * АктуализироватьВерсию - Булево -
// * ВерсияФорматаМЭДО - ПеречислениеСсылка.ВерсииФорматаМЭДО -
Функция НовыйНастройкиКонтрагента()
	
	Настройки = Новый Структура();
	Настройки.Вставить("Контрагент", Справочники.Контрагенты.ПустаяСсылка());
	Настройки.Вставить("АдресМЭДО", "");
	Настройки.Вставить("АктуализироватьВерсию", Ложь);
	Настройки.Вставить("ВерсияФорматаМЭДО", Перечисления.ВерсииФорматаМЭДО.ПустаяСсылка());
	Возврат Настройки;
	
КонецФункции

Функция Текст_ОшибкаВНастройках()
	
	Возврат НСтр("ru = 'Ошибка в настройках контрагента'");
	
КонецФункции

#КонецОбласти

#КонецЕсли
