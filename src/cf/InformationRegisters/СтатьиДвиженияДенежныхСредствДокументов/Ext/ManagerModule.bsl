#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

// Записывает статьи ДДС указанного документа в привилегированном режиме.
//
// Параметры:
//   Документ - СправочникСсылка.ДокументПредприятия - документ, статьи которого записываются.
//   СтатьиДвиженияДенежныхСредств - ДанныеФормыКоллекция или ТаблицаЗначений - записываемые статьи.
//
Процедура ЗаписатьСтатьиДокумента(Документ, СтатьиДвиженияДенежныхСредств) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	НаборЗаписей = РегистрыСведений.СтатьиДвиженияДенежныхСредствДокументов.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Документ.Установить(Документ);
	
	СуммаСтатей = 0;
	
	Для каждого СтрокаСтатьи Из СтатьиДвиженияДенежныхСредств Цикл
		Запись = НаборЗаписей.Добавить();
		Запись.Документ = Документ;
		Запись.СтатьяДвиженияДенежныхСредств = СтрокаСтатьи.СтатьяДвиженияДенежныхСредств;
		Запись.Сумма = СтрокаСтатьи.Сумма;
		Запись.СуммаНДС = СтрокаСтатьи.СуммаНДС;
		
		СуммаСтатей = СуммаСтатей + СтрокаСтатьи.Сумма;
	КонецЦикла;
	
	Если СуммаСтатей <> Документ.Сумма Тогда
		ТекстОшибки = СтрШаблон(
			НСтр("ru = 'В документе %1 сумма не соответствует ее расшифровке по статьям движения денежных средств'"),
			Документ);
		ВызватьИсключение ТекстОшибки;
	КонецЕсли;
	
	НаборЗаписей.Записать();
	
КонецПроцедуры

// Читает статьи ДДС указанного документа в привилегированном режиме.
//
// Параметры:
//   Документ - СправочникСсылка.ДокументПредприятия - документ, статьи которого записываются.
//   СтатьиДвиженияДенежныхСредств - ДанныеФормыКоллекция или ТаблицаЗначений - неявно возвращаемый параметр.
//
Процедура ПрочитатьСтатьиДокумента(Документ, СтатьиДвиженияДенежныхСредств) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	НаборЗаписей = РегистрыСведений.СтатьиДвиженияДенежныхСредствДокументов.СоздатьНаборЗаписей();
	
	НаборЗаписей.Отбор.Документ.Установить(Документ);
	НаборЗаписей.Прочитать();
	
	СтатьиДвиженияДенежныхСредств.Очистить();
	Для каждого Запись Из НаборЗаписей Цикл
		СтрокаСтатьи = СтатьиДвиженияДенежныхСредств.Добавить();
		СтрокаСтатьи.СтатьяДвиженияДенежныхСредств = Запись.СтатьяДвиженияДенежныхСредств;
		СтрокаСтатьи.Сумма = Запись.Сумма;
		СтрокаСтатьи.СуммаНДС = Запись.СуммаНДС;
	КонецЦикла;
	
КонецПроцедуры

#КонецЕсли
