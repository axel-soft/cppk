#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает данные авторизации в интегрированную систему.
//
// Параметры:
//   ИнтегрированнаяСистема - ПланОбменаСсылка.ИнтегрированныеСистемы - интегрированная система.
//   ВызыватьИсключение - Булево - требуется вызов исключения, если данные авторизации не заданы.
//
// Возвращаемое значение:
//   Структура:
//     * АдресВебСервиса - Строка
//     * ИмяПользователя - Строка
//     * Пароль - Строка
//
Функция ДанныеАвторизации(ИнтегрированнаяСистема, ВызыватьИсключение = Истина) Экспорт
	
	Результат = Новый Структура("АдресВебСервиса, ИмяПользователя, Пароль");
	ТекстОшибки = "";
	
	Если ЗначениеЗаполнено(ИнтегрированнаяСистема) Тогда
		Запрос = Новый Запрос;
		Запрос.Текст =
			"ВЫБРАТЬ
			|	ИнтегрированныеСистемы.АдресВебСервиса КАК АдресВебСервиса,
			|	ДанныеИнтегрированныхСистем.ИмяПользователя КАК ИмяПользователя,
			|	ДанныеИнтегрированныхСистем.Пароль КАК Пароль
			|ИЗ
			|	РегистрСведений.ДанныеИнтегрированныхСистем КАК ДанныеИнтегрированныхСистем
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПланОбмена.ИнтегрированныеСистемы КАК ИнтегрированныеСистемы
			|		ПО ДанныеИнтегрированныхСистем.УзелИнтегрированнойСистемы = ИнтегрированныеСистемы.Ссылка
			|ГДЕ
			|	ДанныеИнтегрированныхСистем.УзелИнтегрированнойСистемы = &УзелИнтегрированнойСистемы";
		Запрос.УстановитьПараметр("УзелИнтегрированнойСистемы", ИнтегрированнаяСистема);
		
		// Привилегированный режим нужен для того чтобы пользователь с ролью НастройкаИнтеграции мог настроить
		// интеграцию, но не мог просматривать данные интегрированных систем в интерактивном режиме.
		УстановитьПривилегированныйРежим(Истина);
		Выборка = Запрос.Выполнить().Выбрать();
		УстановитьПривилегированныйРежим(Ложь);
		
		Если Выборка.Следующий() Тогда
			ЗаполнитьЗначенияСвойств(Результат, Выборка);
			Возврат Результат;
		Иначе
			ТекстОшибки = НСтр(
				"ru = 'Ошибка подключения.
				|
				|Не найдены данные для авторизации в интегрированную систему.'");
		КонецЕсли;
	Иначе
		ТекстОшибки = НСтр(
			"ru = 'Ошибка подключения.
			|
			|Не указана интегрированная система.'");
	КонецЕсли;
	
	Если ВызыватьИсключение Тогда
		ВызватьИсключение ТекстОшибки;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

// Записывает данные интегрированной системы.
//
// Параметры:
//   УзелИнтегрированнойСистемы - ПланОбменаСсылка.ИнтегрированныеСистемы - интегрированная система.
//   ИмяПользователя - Строка - имя пользователя, под которым будет выполняться авторизация на веб-сервисе.
//   Пароль - Строка - пароль пользователя.
//
Процедура ЗаписатьДанные(УзелИнтегрированнойСистемы, ИмяПользователя, Пароль) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запись = РегистрыСведений.ДанныеИнтегрированныхСистем.СоздатьМенеджерЗаписи();
	Запись.УзелИнтегрированнойСистемы = УзелИнтегрированнойСистемы;
	Запись.ИмяПользователя = ИмяПользователя;
	Запись.Пароль = Пароль;
	Запись.Записать();
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли