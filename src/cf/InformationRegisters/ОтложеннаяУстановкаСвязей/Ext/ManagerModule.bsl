#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс
	
// Добавляет запись в регистр по переданным параметрам
// 
// Параметры:
//  Откуда  - СправочникССылка.ДокументыПредприятия
//  Куда - СправочникССылка.ДокументыПредприятия
//  ТипСвязи - СправочникССылка.ТипыСвязей
//  Дата - Дата
Процедура ЗаписатьДанные(Откуда, Куда, ТипСвязи, Дата) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);	
	МенеджерЗаписиРегистра = РегистрыСведений.ОтложеннаяУстановкаСвязей.СоздатьМенеджерЗаписи();
	
	МенеджерЗаписиРегистра.Откуда = Откуда;
	МенеджерЗаписиРегистра.Куда = Куда;
	МенеджерЗаписиРегистра.ТипСвязи = ТипСвязи;
	МенеджерЗаписиРегистра.Дата = Дата;
	
	МенеджерЗаписиРегистра.Записать();
	
КонецПроцедуры

// Удаляет запись в регистре по переданным параметрам
// 
// Параметры:
//  Откуда  - СправочникССылка.ДокументыПредприятия
//  Куда - СправочникССылка.ДокументыПредприятия
//  ТипСвязи - СправочникССылка.ТипыСвязей
Процедура УдалитьДанные(Откуда, Куда, ТипСвязи) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);	
	МенеджерЗаписиРегистра = РегистрыСведений.ОтложеннаяУстановкаСвязей.СоздатьМенеджерЗаписи();
	
	МенеджерЗаписиРегистра.Откуда = Откуда;
	МенеджерЗаписиРегистра.Куда = Куда;
	МенеджерЗаписиРегистра.ТипСвязи = ТипСвязи;
	
	МенеджерЗаписиРегистра.Удалить();
	
КонецПроцедуры

// Возвращает записи в регистре по Откуда
// 
// Параметры:
//  Откуда  - СправочникССылка.ДокументыПредприятия
//  ТипСвязи - СправочникССылка.ТипыСвязей
// 
// Возвращаемое значение:
//  Массив из Структура
//    * Документ	- СправочникССылка.ДокументыПредприятия
//    * ДатаОтмены - Дата
Функция ПрочитатьДанные(Откуда, ТипСвязи) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);	
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	ОтложеннаяУстановкаСвязей.Откуда,
	|	ОтложеннаяУстановкаСвязей.Куда,
	|	ОтложеннаяУстановкаСвязей.ТипСвязи,
	|	ОтложеннаяУстановкаСвязей.Дата
	|ИЗ
	|	РегистрСведений.ОтложеннаяУстановкаСвязей КАК ОтложеннаяУстановкаСвязей
	|ГДЕ
	|	ОтложеннаяУстановкаСвязей.Откуда = &Откуда
	|	И ОтложеннаяУстановкаСвязей.ТипСвязи = &ТипСвязи";
	
	Запрос.Параметры.Вставить("Откуда", Откуда);
	Запрос.Параметры.Вставить("ТипСвязи", ТипСвязи);
	
	Таблица = Запрос.Выполнить().Выгрузить();
	
	МассивДокументов = Новый Массив;
	
	Для Каждого Запись Из Таблица Цикл
		
		ОписаниеДок = Новый Структура("Документ, ДатаОтмены", Запись.Куда, Запись.Дата);
		МассивДокументов.Добавить(ОписаниеДок);	
		
	КонецЦикла;
	
	Возврат МассивДокументов;
	
КонецФункции

// Возвращает записи в регистре по Куда
// 
// Параметры:
//  ОтменяемыйДокумент  - СправочникССылка.ДокументыПредприятия
//  ТипСвязи - СправочникССылка.ТипыСвязей
// 
// Возвращаемое значение:
//  Массив из Структура
//    * Документ	- СправочникССылка.ДокументыПредприятия
//    * ДатаОтмены - Дата
Функция ПрочитатьНовыеДокументы(ОтменяемыйДокумент, ТипСвязи) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);	
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	ОтложеннаяУстановкаСвязей.Откуда,
	|	ОтложеннаяУстановкаСвязей.Куда,
	|	ОтложеннаяУстановкаСвязей.ТипСвязи,
	|	ОтложеннаяУстановкаСвязей.Дата
	|ИЗ
	|	РегистрСведений.ОтложеннаяУстановкаСвязей КАК ОтложеннаяУстановкаСвязей
	|ГДЕ
	|	ОтложеннаяУстановкаСвязей.Куда = &Куда
	|	И ОтложеннаяУстановкаСвязей.ТипСвязи = &ТипСвязи";
	
	Запрос.Параметры.Вставить("Куда", ОтменяемыйДокумент);
	Запрос.Параметры.Вставить("ТипСвязи", ТипСвязи);
	
	Таблица = Запрос.Выполнить().Выгрузить();
	
	МассивДокументов = Новый Массив;
	
	Для Каждого Запись Из Таблица Цикл
		
		ОписаниеДок = Новый Структура("Документ", Запись.Откуда);
		МассивДокументов.Добавить(ОписаниеДок);	
		
	КонецЦикла;
	
	Возврат МассивДокументов;
	
КонецФункции

#КонецОбласти

#КонецЕсли
