
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

Процедура ПеренестиЗаписиВНастройкиРабочегоКалендаря(Параметры) Экспорт
	
	Запрос = Новый Запрос;
	
	Если Параметры.ПрогрессВыполнения.ВсегоОбъектов = 0 Тогда
		
		Запрос.Текст =
			"ВЫБРАТЬ
			|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Сотрудники.Владелец) КАК Количество
			|ИЗ
			|	РегистрСведений.НастройкиРабочегоКалендаряСотрудника КАК НастройкиРабочегоКалендаряСотрудника
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Сотрудники КАК Сотрудники
			|		ПО НастройкиРабочегоКалендаряСотрудника.Сотрудник = Сотрудники.Ссылка";
		Выборка = Запрос.Выполнить().Выбрать();
		Выборка.Следующий();
		
		Параметры.ПрогрессВыполнения.ВсегоОбъектов = Выборка.Количество;
		
	КонецЕсли;
	
	Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ ПЕРВЫЕ 500
		|	Сотрудники.Владелец КАК ФизическоеЛицо
		|ПОМЕСТИТЬ ФизическиеЛица
		|ИЗ
		|	РегистрСведений.НастройкиРабочегоКалендаряСотрудника КАК НастройкиРабочегоКалендаряСотрудника
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Сотрудники КАК Сотрудники
		|		ПО НастройкиРабочегоКалендаряСотрудника.Сотрудник = Сотрудники.Ссылка
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ФизическоеЛицо
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Сотрудники.Владелец КАК ФизическоеЛицо,
		|	НастройкиРабочегоКалендаряСотрудника.Сотрудник КАК Сотрудник,
		|	НастройкиРабочегоКалендаряСотрудника.Настройка КАК Настройка,
		|	НастройкиРабочегоКалендаряСотрудника.Значение КАК Значение,
		|	ВЫБОР
		|		КОГДА ОсновныеСотрудники.ФизическоеЛицо ЕСТЬ NULL
		|			ТОГДА 2
		|		ИНАЧЕ 1
		|	КОНЕЦ КАК Приоритет
		|ИЗ
		|	РегистрСведений.НастройкиРабочегоКалендаряСотрудника КАК НастройкиРабочегоКалендаряСотрудника
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Сотрудники КАК Сотрудники
		|		ПО НастройкиРабочегоКалендаряСотрудника.Сотрудник = Сотрудники.Ссылка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ФизическиеЛица КАК ФизическиеЛица
		|		ПО Сотрудники.Владелец = ФизическиеЛица.ФизическоеЛицо
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОсновныеСотрудники КАК ОсновныеСотрудники
		|		ПО НастройкиРабочегоКалендаряСотрудника.Сотрудник = ОсновныеСотрудники.Сотрудник
		|
		|УПОРЯДОЧИТЬ ПО
		|	Приоритет";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	ОбработаноОбъектов = 0;
	ПроблемныхОбъектов = 0;
	
	ОбработанныеФизЛица = Новый Соответствие();
	
	Пока Выборка.Следующий() Цикл
		
		НачатьТранзакцию();
		
		Попытка
			
			Набор = РегистрыСведений.НастройкиРабочегоКалендаряСотрудника.СоздатьНаборЗаписей();
			Набор.Отбор.Сотрудник.Установить(Выборка.Сотрудник);
			Набор.Отбор.Настройка.Установить(Выборка.Настройка);
			ОбновлениеИнформационнойБазы.ЗаписатьДанные(Набор);
			
			Если ОбработанныеФизЛица[Выборка.ФизическоеЛицо] = Неопределено Тогда
				
				// Пишем в новый регистр только первую запись, относящуюся к физ. лицу.
				// В общем случае запись для физ. лица будет одна.
				//
				// Различные варианты настроек тут не учитываем, т.к. на момент перехода
				// на новый регистр, настройка рабочего календаря одна.
				
				Набор = РегистрыСведений.НастройкиРабочегоКалендаря.СоздатьНаборЗаписей();
				Набор.Отбор.ФизическоеЛицо.Установить(Выборка.ФизическоеЛицо);
				Набор.Отбор.Настройка.Установить(Выборка.Настройка);
				
				Запись = Набор.Добавить();
				Запись.ФизическоеЛицо = Выборка.ФизическоеЛицо;
				Запись.Настройка = Выборка.Настройка;
				Запись.Значение = Выборка.Значение;
				
				ОбновлениеИнформационнойБазы.ЗаписатьДанные(Набор);
				
				ОбработанныеФизЛица[Выборка.ФизическоеЛицо] = Истина;
				
				ОбработаноОбъектов = ОбработаноОбъектов + 1;
				
			КонецЕсли;
			
			ЗафиксироватьТранзакцию();
			
		Исключение
			
			ОтменитьТранзакцию();
			
			ПроблемныхОбъектов = ПроблемныхОбъектов + 1;
			
			ТекстСообщения = СтрШаблон(
				НСтр("ru = 'Не удалось обработать настройку рабочего календаря сотрудника: %1 по причине:
					|%2'"),
				Выборка.Сотрудник,
				ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			ЗаписьЖурналаРегистрации(
				ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
				УровеньЖурналаРегистрации.Ошибка,,,
				ТекстСообщения);
			
		КонецПопытки;
	
	КонецЦикла;
	
	Если ОбработаноОбъектов = 0 И ПроблемныхОбъектов > 0 Тогда
		ТекстСообщения = СтрШаблон(
			НСтр("ru = 'Процедуре ПеренестиЗаписиВНастройкиРабочегоКалендаря не удалось обработать некоторые настройки сотрудников (пропущены): %1'"),
			ПроблемныхОбъектов);
		ВызватьИсключение ТекстСообщения;
	КонецЕсли;
	
	Параметры.ПрогрессВыполнения.ОбработаноОбъектов = 
		Параметры.ПрогрессВыполнения.ОбработаноОбъектов + ОбработаноОбъектов;
	
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	НастройкиРабочегоКалендаряСотрудника.Сотрудник
		|ИЗ
		|	РегистрСведений.НастройкиРабочегоКалендаряСотрудника КАК НастройкиРабочегоКалендаряСотрудника
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Сотрудники КАК Сотрудники
		|		ПО НастройкиРабочегоКалендаряСотрудника.Сотрудник = Сотрудники.Ссылка";
	
	Параметры.ОбработкаЗавершена = Запрос.Выполнить().Пустой();
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли