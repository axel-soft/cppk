#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПриЗаписи(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ДополнительныеСвойства.Свойство("НеОбновлятьИсторию")
		И ДополнительныеСвойства.НеОбновлятьИсторию Тогда
		
		Возврат;
		
	КонецЕсли;
	
	Для Каждого Запись Из ЭтотОбъект Цикл
		
		Объект = Запись.Объект;
		Если ТипЗнч(Объект) <> Тип("СправочникСсылка.ФизическиеЛица") Тогда
			Продолжить;
		КонецЕсли;
		
		ОтборФИО = Новый Структура;
		ОтборФИО.Вставить("ФизическоеЛицо", Запись.Объект);
		
		ПоследняяЗаписьИстории = РегистрыСведений.ФИОФизическихЛиц.СрезПоследних(, ОтборФИО);
		Если ПоследняяЗаписьИстории.Количество() > 0 Тогда
			 
			ПоследняяЗаписьИстории = ПоследняяЗаписьИстории[0];
			МенеджерЗаписи = РегистрыСведений.ФИОФизическихЛиц.СоздатьМенеджерЗаписи();
			ЗаполнитьЗначенияСвойств(МенеджерЗаписи, ПоследняяЗаписьИстории);
			ЗаполнитьЗначенияСвойств(МенеджерЗаписи, Запись);
			МенеджерЗаписи.Период = ПоследняяЗаписьИстории.Период;
			МенеджерЗаписи.Записать(Истина);
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
