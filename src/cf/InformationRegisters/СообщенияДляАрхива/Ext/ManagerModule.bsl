#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Записывает новое сообщение.
//
// Параметры:
//  ИдентификаторСообщения - Строка - строковый идентификатор сообщения.
//  ТелоСообщения - Произвольный - тело создаваемого сообщения.
//
Процедура Добавить(ИдентификаторСообщения, Знач ТелоСообщения) Экспорт
	
	Если ТипЗнч(ТелоСообщения) <> Тип("ХранилищеЗначения") Тогда
		ТелоСообщения = Новый ХранилищеЗначения(ТелоСообщения);
	КонецЕсли;
	
	Запись = СоздатьМенеджерЗаписи();
	Запись.ИдентификаторСообщения = ИдентификаторСообщения;
	Запись.ТелоСообщения = ТелоСообщения;
	Запись.ДатаСоздания = ТекущаяДатаСеанса();
	
	Запись.Записать();
	
КонецПроцедуры

// Отмечает сообщение как отправленное.
//
// Параметры:
//  ИдентификаторСообщения - Строка - идентификатор в виде строки.
//
Процедура ОтметитьОтправку(ИдентификаторСообщения) Экспорт
	
	Запись = СоздатьМенеджерЗаписи();
	Запись.ИдентификаторСообщения = ИдентификаторСообщения;
	Запись.Прочитать();
	Если Не Запись.Выбран() Тогда
		ТекстОшибки = СтрШаблон(НСтр("ru = 'Не удалось отметить отправку сообщения ""%1"": запись не найдена'"),
			ИдентификаторСообщения);
		ВызватьИсключение ТекстОшибки;
	КонецЕсли;
	Запись.ДатаОтправки = ТекущаяДатаСеанса();
	Запись.ТекстОшибки = "";
	Запись.Записать();
	
КонецПроцедуры

// Фиксирует информацию о неудачной попытке отправки сообщения.
//
// Параметры:
//  ИдентификаторСообщения - Строка - идентификатор в виде строки.
//
Процедура ЗафиксироватьНеудачнуюПопытку(ИдентификаторСообщения, ТекстОшибки) Экспорт
	
	Запись = СоздатьМенеджерЗаписи();
	Запись.ИдентификаторСообщения = ИдентификаторСообщения;
	Запись.Прочитать();
	Если Не Запись.Выбран() Тогда
		Возврат;
	КонецЕсли;
	Запись.КоличествоПопытокОбработки = Запись.КоличествоПопытокОбработки + 1;
	Запись.ТекстОшибки = ТекстОшибки;
	Запись.Записать();
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
