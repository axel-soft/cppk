#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает основное контактное лицо контрагента
// 
// Параметры:
//  Контрагент - СправочникСсылка.Контрагенты
//
// Возвращаемое значение:
//  СправочникСсылка.КонтактныеЛица
// 
Функция ОсновноеКонтактноеЛицоКонтрагента(Контрагент) Экспорт
	
	Если Не ЗначениеЗаполнено(Контрагент) Тогда
		Возврат Справочники.КонтактныеЛица.ПустаяСсылка();
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ОсновныеКонтактныеЛицаКонтрагентов.КонтактноеЛицо
		|ИЗ
		|	РегистрСведений.ОсновныеКонтактныеЛицаКонтрагентов КАК ОсновныеКонтактныеЛицаКонтрагентов
		|ГДЕ
		|	ОсновныеКонтактныеЛицаКонтрагентов.Контрагент = &Контрагент";
	
	Запрос.УстановитьПараметр("Контрагент", Контрагент);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		Возврат Выборка.КонтактноеЛицо;
	КонецЕсли;
	
	Возврат Справочники.КонтактныеЛица.ПустаяСсылка();
	
КонецФункции

// Устанавливает основное контактное лицо контрагента
// 
// Параметры:
//  Контрагент - СправочникСсылка.Контрагенты
//  КонтактноеЛицо - СправочникСсылка.КонтактныеЛица
// 
Процедура УстановитьОсновноеКонтактноеЛицоКонтрагента(Контрагент, КонтактноеЛицо) Экспорт
	
	МенеджерЗаписи = СоздатьМенеджерЗаписи();
	МенеджерЗаписи.Контрагент = Контрагент;
	МенеджерЗаписи.КонтактноеЛицо = КонтактноеЛицо;
	МенеджерЗаписи.Записать();
	
КонецПроцедуры

// Снимает отметку основного контактного лица контрагента
// 
// Параметры:
//  Контрагент - СправочникСсылка.Контрагенты
// 
Процедура СнятьОтметкуОсновногоКонтактногоЛицаКонтрагента(Контрагент) Экспорт
	
	МенеджерЗаписи = СоздатьМенеджерЗаписи();
	МенеджерЗаписи.Контрагент = Контрагент;
	МенеджерЗаписи.Прочитать();
	
	Если МенеджерЗаписи.Выбран() Тогда
		МенеджерЗаписи.Удалить();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли