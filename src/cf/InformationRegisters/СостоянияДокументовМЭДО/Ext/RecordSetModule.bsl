// @strict-types

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПриЗаписи(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	// Обновить РС актуальных состояний для показа в форме списка:
	Для Каждого Запись Из ЭтотОбъект Цикл
		ОбновитьАктуальноеСостояние(Запись.Документ);
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Обновить актуальное состояние.
// 
// Параметры:
//  Документ - ОпределяемыйТип.ПредметМЭДО - Документ
//  Состояние - ПеречислениеСсылка.СостоянияДокументовМЭДО - Состояние, какое нужно записать.
Процедура ОбновитьАктуальноеСостояние(Документ)
	
	Если Не ЗначениеЗаполнено(Документ) Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	СрезСостояния = РегистрыСведений.СостоянияДокументовМЭДО.СрезСостоянияДокумента(
		Документ, Неопределено, "Состояние", ТекущаяДатаСеанса(), Неопределено);
	Если Не ЗначениеЗаполнено(СрезСостояния.Состояние) Тогда
		СрезСостояния.Состояние = Перечисления.СостоянияДокументовМЭДО.ПустаяСсылка();
	КонецЕсли;
	
	МЭДОПереопределяемый.ЗаписатьСостояниеДокумента(Документ, СрезСостояния.Состояние);
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли