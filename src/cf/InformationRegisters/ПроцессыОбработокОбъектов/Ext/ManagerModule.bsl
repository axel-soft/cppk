#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Создает новую запись в регистре.
//
// Параметры:
//  Процесс - БизнесПроцессСсылка.КомплексныйПроцесс
//  Обработка - СправочникСсылка.ОбработкиОбъектов
//
Процедура Добавить(Процесс, Обработка) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	Менеджер = СоздатьМенеджерЗаписи();
	Менеджер.Процесс = Процесс;
	Менеджер.Обработка = Обработка;
	Менеджер.Записать();
	
КонецПроцедуры

// Возвращает обработку по процессу.
// 
// Параметры:
// 	Процесс - БизнесПроцессСсылка.КомплексныйПроцесс
// 	
// Возвращаемое значение:
// 	СправочникСсылка.ОбработкиОбъектов
//
Функция ОбработкаПоПроцессу(Процесс) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ТекстЗапроса =
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ПроцессыОбработокОбъектов.Обработка КАК Обработка
		|ИЗ
		|	РегистрСведений.ПроцессыОбработокОбъектов КАК ПроцессыОбработокОбъектов
		|ГДЕ
		|	ПроцессыОбработокОбъектов.Процесс = &Процесс";
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("Процесс", Процесс);
	
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		Возврат Справочники.ОбработкиОбъектов.ПустаяСсылка();
	КонецЕсли;	
	
	Выборка = Результат.Выбрать();
	Выборка.Следующий();
	Возврат Выборка.Обработка;
		
КонецФункции

// Возвращает процесс обработки объекта.
// 
// Параметры:
// 	Обработка - СправочникСсылка.ОбработкиОбъектов
// 	
// Возвращаемое значение:
// 	БизнесПроцессСсылка.КомплексныйПроцесс
//
Функция ПроцессОбработкиОбъекта(Обработка) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ПроцессыОбработокОбъектов.Процесс
		|ИЗ
		|	РегистрСведений.ПроцессыОбработокОбъектов КАК ПроцессыОбработокОбъектов
		|ГДЕ
		|	ПроцессыОбработокОбъектов.Обработка = &Обработка";
	Запрос.УстановитьПараметр("Обработка", Обработка);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Процесс;
	Иначе
		Возврат БизнесПроцессы.КомплексныйПроцесс.ПустаяСсылка();
	КонецЕсли;
	
КонецФункции

// Ищет все процессы обработки объекта.
// 
// Параметры:
//  Обработка - СправочникСсылка.ОбработкиОбъектов
// 
// Возвращаемое значение:
//   Массив
//    * см. БизнесПроцессСсылка.КомплексныйПроцесс
Функция ВсеПроцессыОбработкиОбъекта(Обработка) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ПроцессыОбработокОбъектов.Процесс
		|ИЗ
		|	РегистрСведений.ПроцессыОбработокОбъектов КАК ПроцессыОбработокОбъектов
		|ГДЕ
		|	ПроцессыОбработокОбъектов.Обработка = &Обработка";
	Запрос.УстановитьПараметр("Обработка", Обработка);
	
	Процессы = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Процесс");
	Возврат Процессы;
	
КонецФункции

#КонецОбласти

#КонецЕсли