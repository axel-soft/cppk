#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

// Устанавливает значение указанного ресурса регистра для переданного объекта.
//
Процедура УстановитьПризнак(Объект, Признак, Значение) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запись = СоздатьМенеджерЗаписи();
	Запись.Объект = Объект;
	Если Запись.Объект = Неопределено Тогда // данный тип объекта не обрабатывается
		Возврат;
	КонецЕсли;
	
	Результат = ПолучитьПризнаки(Объект, Признак);
	
	Если Результат[Признак] = Значение Тогда
		Возврат;
	КонецЕсли;
	
	Если ДелопроизводствоКлиентСервер.ЭтоДокумент(Объект) Тогда 
		Если Признак = "СрокИсполнения" Тогда
			Ресурс = "СрокКонтроля";
		ИначеЕсли Признак = "СрокИсполненияОбщий" Тогда
			Ресурс = "СрокКонтроляОбщий";
		Иначе
			Ресурс = Признак;
		КонецЕсли;
		
		Делопроизводство.ЗаписатьДанныеДокумента(Объект, Ресурс, Значение);
	КонецЕсли;
	
	Запись.Прочитать();
	Запись.Объект = Объект;
	Запись[Признак] = Значение;
	Запись.Записать();
	
КонецПроцедуры

// Получает значение указанных ресурсов регистра для переданного объекта.
//
Функция ПолучитьПризнаки(Объект, Признаки) Экспорт
	
	Результат = Новый Структура(Признаки);
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос(СтрШаблон(
		"ВЫБРАТЬ
		|	%1
		|ИЗ
		|	РегистрСведений.КешИнформацииОбОбъектах КАК КешИнформацииОбОбъектах
		|ГДЕ
		|	Объект = &Объект",
		Признаки));
	
	Запрос.УстановитьПараметр("Объект", Объект);
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		ЗаполнитьЗначенияСвойств(Результат, Выборка);
	Иначе
		ЗаполнитьЗначенияСвойств(Результат, СоздатьМенеджерЗаписи());
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецЕсли
