
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Добавляет файл в регистр.
//
// Параметры:
//  ВладелецСхемы - СправочникССылка.НастройкиОбработкиВидовОбъектов,  СправочникССылка.ОбработкиОбъектов
//  СхемаКорректна - Булево
//
Процедура Добавить(ВладелецСхемы, СхемаКорректна) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	НаборЗаписей = СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.ВладелецСхемы.Установить(ВладелецСхемы);
	
	Запись = НаборЗаписей.Добавить();
	Запись.ВладелецСхемы = ВладелецСхемы;
	Запись.СхемаКорректна = СхемаКорректна;
	
	НаборЗаписей.Записать();
	
КонецПроцедуры

// Есть ли запись в РС
//
//
// Параметры:
//  ВладелецСхемы - СправочникССылка.НастройкиОбработкиВидовОбъектов,  СправочникССылка.ОбработкиОбъектов
//  СхемаКорректна - Булево - тут вернет считанное из РС
//
// Возвращаемое значение:
//   Булево - если есть запись в РС
//
Функция ПрочитатьЗапись(ВладелецСхемы, СхемаКорректна) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	РезультатыПроверкиСхемДляОбработокОбъектов.СхемаКорректна КАК СхемаКорректна
		|ИЗ
		|	РегистрСведений.РезультатыПроверкиСхемДляОбработокОбъектов КАК РезультатыПроверкиСхемДляОбработокОбъектов
		|ГДЕ
		|	РезультатыПроверкиСхемДляОбработокОбъектов.ВладелецСхемы = &ВладелецСхемы";
		
	Запрос.УстановитьПараметр("ВладелецСхемы", ВладелецСхемы);
	
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		Возврат Ложь;
	КонецЕсли;
		
	Выборка = Результат.Выбрать();
	Выборка.Следующий();
	СхемаКорректна = Выборка.СхемаКорректна;	
	Возврат Истина;	
	
КонецФункции

// Удаляет все записи из регистра с отбором по ВладелецСхемы.
//
// Параметры:
//  ВладелецСхемы - СправочникССылка.НастройкиОбработкиВидовОбъектов,  СправочникССылка.ОбработкиОбъектов
//
Процедура УдалитьЗаписи(ВладелецСхемы) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	НаборЗаписей = СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.ВладелецСхемы.Установить(ВладелецСхемы);
	НаборЗаписей.Записать();
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли