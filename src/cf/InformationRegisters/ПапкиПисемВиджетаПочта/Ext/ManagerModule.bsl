#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает выбранные папки виджета "Почта" для пользователя.
//
// Параметры:
//  Пользователь - СправочникСсылка.Пользователи - Пользователь.
// 
// Возвращаемое значение:
//  Массив - Выбранные папки виджета "Почта" для пользователя.
//
Функция ВыбранныеПапкиПользователя(Пользователь) Экспорт
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ПапкиПисемВиджетаПочта.Папка КАК Папка
		|ИЗ
		|	РегистрСведений.ПапкиПисемВиджетаПочта КАК ПапкиПисемВиджетаПочта
		|ГДЕ
		|	ПапкиПисемВиджетаПочта.Пользователь = &Пользователь");
		
	Запрос.УстановитьПараметр("Пользователь", Пользователь);
	
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Папка");
	
КонецФункции

// Сохраняет выбранные папки виджета "Почта" для пользователя.
//
// Параметры:
//  Пользователь - СправочникСсылка.Пользователи - Пользователь.
//  Папки        - СправочникСсылка.ПапкиПисем   - Выбранные папки виджета "Почта".
//
Процедура СохранитьВыбранныеПапкиПользователя(Пользователь, Папки) Экспорт
	
	НаборЗаписей = РегистрыСведений.ПапкиПисемВиджетаПочта.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Пользователь.Установить(Пользователь);
	
	Для каждого Папка Из Папки Цикл
		Запись = НаборЗаписей.Добавить();
		Запись.Пользователь = Пользователь;
		Запись.Папка = Папка;
	КонецЦикла;
	
	НаборЗаписей.Записать();
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли