#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Если Не ТекущийОбъект.Выбран() Тогда
		ОбщегоНазначения.СообщитьПользователю(
			НСтр("ru = 'Интерактивно добавлять записи в журнал нельзя.'"), , , , Отказ);
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

// Записать файл ЭСД на диск.
&НаКлиенте
Асинх Процедура СохранитьНаДиск(Команда)
	
	Диалог = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.ВыборКаталога);
	Диалог.МножественныйВыбор = Ложь;
	Диалог.Заголовок = НСтр("ru = 'Укажите каталог для сохранения файла'");
	
	РезультатВыбораФайла = Ждать Диалог.ВыбратьАсинх();
	Если РезультатВыбораФайла = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	
	ИмяФайла = ?(ЗначениеЗаполнено(Запись.ИмяАрхиваЭСД), Запись.ИмяАрхиваЭСД, НСтр("ru = 'имя-файла-по-умолчанию.zip'"));
	
	Файл = Новый Файл(РезультатВыбораФайла[0]);
	Если Не Ждать Файл.СуществуетАсинх() Тогда
		ПоказатьПредупреждение( , 
			СтрШаблон(НСтр("ru = 'Каталог %1 не существует или не доступен!'"), Диалог.Каталог));
		Возврат;
	КонецЕсли;
	
	ПолноеИмяФайла = ОбщегоНазначенияКлиентСервер.ДобавитьКонечныйРазделительПути(Диалог.Каталог) + ИмяФайла;
	Файл = Новый Файл(ПолноеИмяФайла);
	ДопПараметры = Новый Структура("ПолноеИмяФайла", ПолноеИмяФайла);
	Если Ждать Файл.СуществуетАсинх() Тогда
		Если Ждать ВопросАсинх(
			СтрШаблон(НСтр("ru = 'Файл %1 существует, перезаписать?'"), ИмяФайла), РежимДиалогаВопрос.ДаНет)
			= КодВозвратаДиалога.Нет Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	ДвоичныеДанные = ПрочитатьДвоичныеДанныеФайлаЭсд();
	Если ТипЗнч(ДвоичныеДанные) <> Тип("ДвоичныеДанные") Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'В данной записи об ошибке не обнаружен файл.'"));
		Возврат;
	КонецЕсли;
	
	ДвоичныеДанные.Записать(ДопПараметры.ПолноеИмяФайла);
	ПоказатьОповещениеПользователя(
		НСтр("ru = 'Файл сохранен:'"), , ДопПараметры.ПолноеИмяФайла, БиблиотекаКартинок.Информация32);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Прочитать двоичные данные файла.
// 
// Возвращаемое значение:
//  Произвольный - Прочитать двоичные данные файла
&НаСервере
Функция ПрочитатьДвоичныеДанныеФайлаЭсд()
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	Регистр.АрхивЭСД КАК АрхивЭСД
		|ИЗ
		|	РегистрСведений.ЖурналСобытийМЭДО КАК Регистр
		|ГДЕ
		|	Регистр.ДатаСобытия = &ДатаСобытия
		|	И Регистр.Объект = &Объект
		|	И Регистр.Идентификатор = &Идентификатор");
	Запрос.УстановитьПараметр("ДатаСобытия", Запись.ДатаСобытия);
	Запрос.УстановитьПараметр("Объект", Запись.Объект);
	Запрос.УстановитьПараметр("Идентификатор", Запись.Идентификатор);
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка.АрхивЭСД.Получить();
	КонецЕсли;
	
КонецФункции

#КонецОбласти
