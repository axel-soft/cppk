#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	СтарыеЗаписи = НаборСтарыхЗаписей();
	ДополнительныеСвойства.Вставить("СтарыеЗаписи", СтарыеЗаписи);
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ЗапротоколироватьНазначенОсновным();
	ЗапротоколироватьБолееНеОсновной();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция НаборСтарыхЗаписей()
	
	НаборСтарыхЗаписей = РегистрыСведений.ОсновныеСотрудники.СоздатьНаборЗаписей();
	
	Для Каждого ЭлементОтбора Из Отбор Цикл
		НаборСтарыхЗаписей.Отбор[ЭлементОтбора.Имя].Установить(ЭлементОтбора.Значение);
	КонецЦикла;
	
	НаборСтарыхЗаписей.Прочитать();
	Возврат НаборСтарыхЗаписей;
	
КонецФункции

Процедура ЗапротоколироватьНазначенОсновным()
	
	Для Каждого НоваяЗапись Из ЭтотОбъект Цикл
		
		ПротоколированиеРаботыСотрудников.ЗаписатьСотрудникНазначенОсновным(
			НоваяЗапись.Сотрудник, НоваяЗапись.ФизическоеЛицо);
			
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗапротоколироватьБолееНеОсновной()
	
	Если ДополнительныеСвойства.Свойство("СтарыеЗаписи") Тогда
	
		Для Каждого СтараяЗапись Из ДополнительныеСвойства.СтарыеЗаписи Цикл
		
			ПротоколированиеРаботыСотрудников.ЗаписатьСотрудникБолееНеЯвляетсяОсновным(
				СтараяЗапись.Сотрудник, СтараяЗапись.ФизическоеЛицо);
		
		КонецЦикла;
	
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Иначе
	ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'"); 
#КонецЕсли