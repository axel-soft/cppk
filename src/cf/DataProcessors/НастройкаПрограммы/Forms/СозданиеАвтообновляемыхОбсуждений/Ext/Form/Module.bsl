#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ДопустимыеТипы = Новый Массив;
	ДопустимыеТипы.Добавить(Метаданные.Справочники.СтруктураПредприятия);
	ДопустимыеТипы.Добавить(Метаданные.Справочники.РабочиеГруппы);
	ДопустимыеТипы.Добавить(Метаданные.Справочники.Проекты);
	ДопустимыеТипы.Добавить(Метаданные.Справочники.Мероприятия);
	
	СписокВыбора = Элементы.ТипОбъектов.СписокВыбора;
	Для Каждого МетаданныеТипа Из ДопустимыеТипы Цикл
		СписокВыбора.Добавить(МетаданныеТипа.Синоним);
	КонецЦикла;
	
	//ТипОбъектов = СписокВыбора[0];
	ЛимитРазмераАвточатов = Константы.ЛимитРазмераАвточатов.Получить();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ТипОбъектовПриИзменении(Элемент)
	
	Состояние(НСтр("ru = 'Заполнение данных. Пожалуйста, подождите...'"));
	ЗаполнитьТаблицуНаСервере();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыОбъектыИОбсуждения

&НаКлиенте
Процедура ОбъектыИОбсужденияВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ТекущаяКолонка = Элементы.ОбъектыИОбсуждения.ТекущийЭлемент;
	Если ТекущаяКолонка = Элементы.ПредставлениеОбъекта Тогда
		ТекущиеДанные = Элементы.ОбъектыИОбсуждения.ТекущиеДанные;
		ПоказатьЗначение(, ТекущиеДанные.Объект);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПометкаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.ОбъектыИОбсуждения.ТекущиеДанные;
	
	Если ТекущиеДанные.Пометка = 2 Тогда
		ТекущиеДанные.Пометка = 0;
	ИначеЕсли ТекущиеДанные.Пометка = 0 Тогда // Серые флаги не снимаются.
		ТекущиеДанные.Пометка = 2;
		ТекстПредупреждения = "";
		Если ТекущиеДанные.ОбсуждениеСоздано Тогда
			ТекстПредупреждения = НСтр("ru = 'Обсуждение уже создано'");
		ИначеЕсли ТекущиеДанные.КоличествоПользователей > ЛимитРазмераАвточатов И ЛимитРазмераАвточатов > 0 Тогда
			ТекстПредупреждения = СтрШаблон(
				НСтр("ru = 'Количество пользователей в объекте превышает установленный лимит (%1)'"),
				ЛимитРазмераАвточатов);
		КонецЕсли;
		ПоказатьПредупреждение(, ТекстПредупреждения);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СоздатьОбсуждения(Команда)
	
	Состояние(НСтр("ru = 'Создание обсуждений...'"));
	СозданоЭлементов = СоздатьОбсужденияНаСервере();
	ПоказатьПредупреждение(, СтрШаблон(
		НСтр("ru = 'Операция завершена. Создано обсуждений: %1.'"),СозданоЭлементов));
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьОтметки(Команда)
	
	Для Каждого Стр Из ОбъектыИОбсуждения Цикл
		Если Стр.Пометка <> 2 Тогда
			Стр.Пометка = 1;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура СнятьОтметки(Команда)
	
	Для Каждого Стр Из ОбъектыИОбсуждения Цикл
		Если Стр.Пометка <> 2 Тогда
			Стр.Пометка = 0;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьТаблицуНаСервере()
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	
	Если ТипОбъектов = Метаданные.Справочники.СтруктураПредприятия.Синоним Тогда
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	СотрудникиВКонтейнерах.Контейнер КАК Объект,
			|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ СотрудникиПользователей.Пользователь) КАК КоличествоПользователей,
			|	ИСТИНА В
			|		(ВЫБРАТЬ ПЕРВЫЕ 1
			|			ИСТИНА
			|		ИЗ
			|			Справочник.СтруктураПредприятия
			|		ГДЕ
			|			Родитель = СотрудникиВКонтейнерах.Контейнер
			|			И НЕ ПометкаУдаления) КАК ЕстьНижестоящие
			|ИЗ
			|	РегистрСведений.СотрудникиВКонтейнерах КАК СотрудникиВКонтейнерах
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СотрудникиПользователей КАК СотрудникиПользователей
			|		ПО СотрудникиВКонтейнерах.Сотрудник = СотрудникиПользователей.Сотрудник
			|ГДЕ
			|	СотрудникиВКонтейнерах.Контейнер ССЫЛКА Справочник.СтруктураПредприятия
			|
			|СГРУППИРОВАТЬ ПО
			|	СотрудникиВКонтейнерах.Контейнер
			|АВТОУПОРЯДОЧИВАНИЕ";
	ИначеЕсли ТипОбъектов = Метаданные.Справочники.РабочиеГруппы.Синоним Тогда
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	СотрудникиВКонтейнерах.Контейнер КАК Объект,
			|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ СотрудникиПользователей.Пользователь) КАК КоличествоПользователей,
			|	ИСТИНА В
			|		(ВЫБРАТЬ ПЕРВЫЕ 1
			|			ИСТИНА
			|		ИЗ
			|			Справочник.РабочиеГруппы
			|		ГДЕ
			|			Родитель = СотрудникиВКонтейнерах.Контейнер
			|			И НЕ ПометкаУдаления) КАК ЕстьНижестоящие
			|ИЗ
			|	РегистрСведений.СотрудникиВКонтейнерах КАК СотрудникиВКонтейнерах
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СотрудникиПользователей КАК СотрудникиПользователей
			|		ПО СотрудникиВКонтейнерах.Сотрудник = СотрудникиПользователей.Сотрудник
			|
			|ГДЕ 
			|	СотрудникиВКонтейнерах.Контейнер ССЫЛКА Справочник.РабочиеГруппы
			|
			|СГРУППИРОВАТЬ ПО
			|	СотрудникиВКонтейнерах.Контейнер
			|АВТОУПОРЯДОЧИВАНИЕ";
	ИначеЕсли ТипОбъектов = Метаданные.Справочники.Проекты.Синоним Тогда
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	ВложенныйЗапрос.Объект КАК Объект,
			|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ВложенныйЗапрос.Участник) КАК КоличествоПользователей,
			|	ЛОЖЬ КАК ЕстьНижестоящие
			|ИЗ
			|	(ВЫБРАТЬ
			|		Проекты.Ссылка КАК Объект,
			|		СотрудникиПользователей.Пользователь КАК Участник
			|	ИЗ
			|		Справочник.Проекты КАК Проекты
			|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Проекты.ПроектнаяКоманда КАК ПроектыПроектнаяКоманда
			|			ПО Проекты.Ссылка = ПроектыПроектнаяКоманда.Ссылка
			|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СотрудникиВКонтейнерах КАК СотрудникиВКонтейнерах
			|			ПО ПроектыПроектнаяКоманда.Исполнитель = СотрудникиВКонтейнерах.Контейнер
			|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СотрудникиПользователей КАК СотрудникиПользователей
			|			ПО СотрудникиВКонтейнерах.Сотрудник = СотрудникиПользователей.Сотрудник
			|	ГДЕ
			|		НЕ Проекты.ПометкаУдаления
			|		И Проекты.Состояние <> ЗНАЧЕНИЕ(Перечисление.СостоянияПроектов.Завершен)
			|	
			|	ОБЪЕДИНИТЬ ВСЕ
			|	
			|	ВЫБРАТЬ
			|		Проекты.Ссылка,
			|		Проекты.Руководитель
			|	ИЗ
			|		Справочник.Проекты КАК Проекты
			|	ГДЕ
			|		НЕ Проекты.ПометкаУдаления
			|		И Проекты.Состояние <> ЗНАЧЕНИЕ(Перечисление.СостоянияПроектов.Завершен)
			|		И НЕ Проекты.Руководитель.ПометкаУдаления
			|		И НЕ Проекты.Руководитель.Недействителен) КАК ВложенныйЗапрос
			|
			|СГРУППИРОВАТЬ ПО
			|	ВложенныйЗапрос.Объект
			|АВТОУПОРЯДОЧИВАНИЕ";
	ИначеЕсли ТипОбъектов = Метаданные.Справочники.Мероприятия.Синоним Тогда
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	Мероприятия.Ссылка КАК Ссылка,
			|	Мероприятия.Председатель КАК Председатель,
			|	Мероприятия.Секретарь КАК Секретарь,
			|	Мероприятия.Куратор КАК Куратор,
			|	Мероприятия.Организатор КАК Организатор,
			|	Мероприятия.Подготовил КАК Подготовил
			|ПОМЕСТИТЬ ОтобранныеМероприятия
			|ИЗ
			|	Справочник.Мероприятия КАК Мероприятия
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СостоянияМероприятий КАК СостоянияМероприятий
			|		ПО Мероприятия.Ссылка = СостоянияМероприятий.Мероприятие
			|ГДЕ
			|	НЕ Мероприятия.ПометкаУдаления
			|	И НЕ СостоянияМероприятий.Состояние В (ЗНАЧЕНИЕ(Перечисление.СостоянияМероприятий.МероприятиеПроведено), ЗНАЧЕНИЕ(Перечисление.СостоянияМероприятий.МероприятиеОтменено))
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ВложенныйЗапрос.Объект КАК Объект,
			|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ВложенныйЗапрос.Участник) КАК КоличествоПользователей,
			|	ЛОЖЬ КАК ЕстьНижестоящие
			|ИЗ
			|	(ВЫБРАТЬ
			|		ОтобранныеМероприятия.Ссылка КАК Объект,
			|		СотрудникиПользователей.Пользователь КАК Участник
			|	ИЗ
			|		ОтобранныеМероприятия КАК ОтобранныеМероприятия
			|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УчастникиМероприятия КАК УчастникиМероприятия
			|			ПО ОтобранныеМероприятия.Ссылка = УчастникиМероприятия.Мероприятие
			|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СотрудникиВКонтейнерах КАК СотрудникиВКонтейнерах
			|			ПО УчастникиМероприятия.Исполнитель = СотрудникиВКонтейнерах.Контейнер
			|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СотрудникиПользователей КАК СотрудникиПользователей
			|			ПО СотрудникиВКонтейнерах.Сотрудник = СотрудникиПользователей.Сотрудник
			|	
			|	ОБЪЕДИНИТЬ ВСЕ
			|	
			|	ВЫБРАТЬ РАЗЛИЧНЫЕ
			|		ОтобранныеМероприятия.Ссылка,
			|		СотрудникиПользователей.Пользователь
			|	ИЗ
			|		ОтобранныеМероприятия КАК ОтобранныеМероприятия
			|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СотрудникиВКонтейнерах КАК СотрудникиВКонтейнерах
			|			ПО ОтобранныеМероприятия.Председатель = СотрудникиВКонтейнерах.Контейнер
			|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СотрудникиПользователей КАК СотрудникиПользователей
			|			ПО СотрудникиВКонтейнерах.Сотрудник = СотрудникиПользователей.Сотрудник
			|	
			|	ОБЪЕДИНИТЬ ВСЕ
			|	
			|	ВЫБРАТЬ РАЗЛИЧНЫЕ
			|		ОтобранныеМероприятия.Ссылка,
			|		СотрудникиПользователей.Пользователь
			|	ИЗ
			|		ОтобранныеМероприятия КАК ОтобранныеМероприятия
			|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СотрудникиВКонтейнерах КАК СотрудникиВКонтейнерах
			|			ПО ОтобранныеМероприятия.Секретарь = СотрудникиВКонтейнерах.Контейнер
			|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СотрудникиПользователей КАК СотрудникиПользователей
			|			ПО СотрудникиВКонтейнерах.Сотрудник = СотрудникиПользователей.Сотрудник
			|	
			|	ОБЪЕДИНИТЬ ВСЕ
			|	
			|	ВЫБРАТЬ РАЗЛИЧНЫЕ
			|		ОтобранныеМероприятия.Ссылка,
			|		СотрудникиПользователей.Пользователь
			|	ИЗ
			|		ОтобранныеМероприятия КАК ОтобранныеМероприятия
			|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СотрудникиВКонтейнерах КАК СотрудникиВКонтейнерах
			|			ПО ОтобранныеМероприятия.Куратор = СотрудникиВКонтейнерах.Контейнер
			|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СотрудникиПользователей КАК СотрудникиПользователей
			|			ПО СотрудникиВКонтейнерах.Сотрудник = СотрудникиПользователей.Сотрудник
			|	
			|	ОБЪЕДИНИТЬ ВСЕ
			|	
			|	ВЫБРАТЬ
			|		ОтобранныеМероприятия.Ссылка,
			|		СотрудникиПользователей.Пользователь
			|	ИЗ
			|		ОтобранныеМероприятия КАК ОтобранныеМероприятия
			|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СотрудникиВКонтейнерах КАК СотрудникиВКонтейнерах
			|			ПО ОтобранныеМероприятия.Организатор = СотрудникиВКонтейнерах.Контейнер
			|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СотрудникиПользователей КАК СотрудникиПользователей
			|			ПО СотрудникиВКонтейнерах.Сотрудник = СотрудникиПользователей.Сотрудник
			|	
			|	ОБЪЕДИНИТЬ ВСЕ
			|	
			|	ВЫБРАТЬ
			|		ОтобранныеМероприятия.Ссылка,
			|		СотрудникиПользователей.Пользователь
			|	ИЗ
			|		ОтобранныеМероприятия КАК ОтобранныеМероприятия
			|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СотрудникиВКонтейнерах КАК СотрудникиВКонтейнерах
			|			ПО ОтобранныеМероприятия.Подготовил = СотрудникиВКонтейнерах.Контейнер
			|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СотрудникиПользователей КАК СотрудникиПользователей
			|			ПО СотрудникиВКонтейнерах.Сотрудник = СотрудникиПользователей.Сотрудник
			|	
			|	ОБЪЕДИНИТЬ ВСЕ
			|	
			|	ВЫБРАТЬ
			|		ОтобранныеМероприятия.Ссылка,
			|		СотрудникиПользователей.Пользователь
			|	ИЗ
			|		ОтобранныеМероприятия КАК ОтобранныеМероприятия
			|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СотрудникиВКонтейнерах КАК СотрудникиВКонтейнерах
			|			ПО ОтобранныеМероприятия.Подготовил = СотрудникиВКонтейнерах.Контейнер
			|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СотрудникиПользователей КАК СотрудникиПользователей
			|			ПО СотрудникиВКонтейнерах.Сотрудник = СотрудникиПользователей.Сотрудник
			|	) КАК ВложенныйЗапрос
			|
			|СГРУППИРОВАТЬ ПО
			|	ВложенныйЗапрос.Объект
			|АВТОУПОРЯДОЧИВАНИЕ";
	КонецЕсли;
	
	Выборка = Запрос.Выполнить().Выбрать();
	ОбъектыИОбсуждения.Очистить();
	Пока Выборка.Следующий() Цикл
		
		СтрокаБезНижестоящих = ОбъектыИОбсуждения.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаБезНижестоящих, Выборка);
		СтрокаБезНижестоящих.СНижестоящими = Ложь;
		СтрокаБезНижестоящих.КлючОбсуждения = ОбсужденияДокументооборот.КлючАвтообновляемогоОбсуждения(
			СтрокаБезНижестоящих.Объект, Ложь);
		СтрокаБезНижестоящих.ИдентификаторОбсуждения = ОбсужденияДокументооборот.ИдентификаторОбсужденияПоКонтейнеру(
			СтрокаБезНижестоящих.Объект, Ложь);
		Обсуждение = ОбсужденияДокументооборот.ОбсуждениеПоИдентификатору(СтрокаБезНижестоящих.ИдентификаторОбсуждения);
		Если Обсуждение <> Неопределено Тогда
			СтрокаБезНижестоящих.ОбсуждениеСоздано = Истина;
			СтрокаБезНижестоящих.Пометка = 2;
		КонецЕсли;
		Если Выборка.ЕстьНижестоящие Тогда
			УчастникиБезНижестощих = ОбсужденияДокументооборот.СоставВладельцаАвтообновляемогоЧата(
				Выборка.Объект, Ложь);
			СтрокаБезНижестоящих.КоличествоПользователей = УчастникиБезНижестощих.Количество();
		КонецЕсли;
		Если СтрокаБезНижестоящих.КоличествоПользователей > ЛимитРазмераАвточатов И ЛимитРазмераАвточатов > 0 Тогда
			СтрокаБезНижестоящих.Пометка = 2;
		КонецЕсли;
		СтрокаБезНижестоящих.ПредставлениеОбъекта = Строка(Выборка.Объект);
		
		// Вторая строка - с нижестоящими.
		Если Выборка.ЕстьНижестоящие Тогда
			СтрокаСНижестоящими = ОбъектыИОбсуждения.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаСНижестоящими, Выборка);
			СтрокаСНижестоящими.СНижестоящими = Истина;
			СтрокаСНижестоящими.КлючОбсуждения = ОбсужденияДокументооборот.КлючАвтообновляемогоОбсуждения(
				СтрокаСНижестоящими.Объект, Истина);
			СтрокаСНижестоящими.ИдентификаторОбсуждения = ОбсужденияДокументооборот.ИдентификаторОбсужденияПоКонтейнеру(
				СтрокаСНижестоящими.Объект, Истина);
			Обсуждение = ОбсужденияДокументооборот.ОбсуждениеПоИдентификатору(СтрокаСНижестоящими.ИдентификаторОбсуждения);
			Если Обсуждение <> Неопределено Тогда
				СтрокаСНижестоящими.ОбсуждениеСоздано = Истина;
				СтрокаСНижестоящими.Пометка = 2;
			КонецЕсли;
			Если СтрокаСНижестоящими.КоличествоПользователей > ЛимитРазмераАвточатов И ЛимитРазмераАвточатов > 0 Тогда
				СтрокаСНижестоящими.Пометка = 2;
			КонецЕсли;
			СтрокаСНижестоящими.ПредставлениеОбъекта = Строка(Выборка.Объект) + НСтр("ru = ' (с нижестоящими)'");
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция СоздатьОбсужденияНаСервере()
	
	УстановитьПривилегированныйРежим(Истина);
	
	СозданоЭлементов = 0;
	Для Каждого Строка Из ОбъектыИОбсуждения Цикл
		Если Строка.Пометка = 1 И Не Строка.ОбсуждениеСоздано Тогда
			ОбсужденияДокументооборот.НовоеОбсуждениеПоКонтейнеру(Строка.Объект, Строка.СНижестоящими);
			Строка.ОбсуждениеСоздано = Истина;
			СозданоЭлементов = СозданоЭлементов + 1;
		КонецЕсли;
	КонецЦикла;
	
	Возврат СозданоЭлементов;
	
КонецФункции

#КонецОбласти
