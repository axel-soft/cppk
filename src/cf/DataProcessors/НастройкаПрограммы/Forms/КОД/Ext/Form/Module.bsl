#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ПрочитатьКонстантыСервер();
	
	ГраницыМенеджер =
		Константы.ГраницыОтметокВремениИсторическихДанных.СоздатьМенеджерЗначения();
	
	ГраницаОтметокИДТекущая = ГраницыМенеджер.ПолучитьТекущуюГраницу();
	ГраницаОтметокИДНачальная = ГраницыМенеджер.ПолучитьНачальнуюГраницу();
	
	Элементы.ГруппаОбновлениеИсторическихДанных.Доступность = КОДСервер.ЭтоЦентральныйУзел();
	Элементы.ГруппаРазмерПорцииОчередиГруппДоступа.Видимость = ОтправлятьИсторическиеДанныеПриРасширенииДоступаКОД;
	
	ОбновитьОписаниеСхемыДанных();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "КОД_ИзмененаСхемаДанных" Тогда
		ПрочитатьКонстантыСервер();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если Модифицированность Тогда
		Отказ = Истина;
		СтандартнаяОбработка = Ложь;
		СпроситьПередЗакрытием(Отказ, СтандартнаяОбработка);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОтправлятьИсторическиеДанныеПриРасширенииДоступаКОДПриИзменении(Элемент)
	
	Элементы.ГруппаРазмерПорцииОчередиГруппДоступа.Видимость
		= ОтправлятьИсторическиеДанныеПриРасширенииДоступаКОД;
	
	Если ОтправлятьИсторическиеДанныеПриРасширенииДоступаКОД
		И РазмерПорцииОчередиДляПоискаИсторическихДанныхКОД = 0 Тогда
		
		РазмерПорцииОчередиДляПоискаИсторическихДанныхКОД = 500;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗаписатьКонстантыИЗакрыть(Команда)
	
	ЗаписатьНастройки();
	Модифицированность = Ложь;
	Закрыть();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ЗаписатьНастройки()
	
	ЗаписатьКонстантыСервер();
	ОбновитьПовторноИспользуемыеЗначения();
	
КонецПроцедуры

#Область ЧтениеЗаписьКонстантВРеквизитыФормы

&НаКлиенте
Асинх Процедура СпроситьПередЗакрытием(Отказ, СтандартнаяОбработка)
	
	Если Не Модифицированность Тогда
		Возврат; // сразу обычное закрытие.
	КонецЕсли;
	
	
	Ответ = Ждать ВопросАсинх(
		НСтр("ru = 'Данные были изменены. Сохранить изменения?'"), РежимДиалогаВопрос.ДаНетОтмена); // КодВозвратаДиалога
	
	Если Ответ = КодВозвратаДиалога.Да Тогда
		ЗаписатьНастройки();
		Модифицированность = Ложь;
		Закрыть();
	ИначеЕсли Ответ = КодВозвратаДиалога.Нет Тогда
		Модифицированность = Ложь;
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаписатьКонстантыСервер()
	
	Обработки.НастройкаПрограммы.ЗаписатьКонстантыВФорме(ЭтотОбъект, ИменаКонстант());
	
КонецПроцедуры

&НаСервере
Процедура ПрочитатьКонстантыСервер()
	
	Обработки.НастройкаПрограммы.ПрочитатьКонстантыВФорме(ЭтотОбъект, ИменаКонстант());
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ИменаКонстант()
	
	Имена = Новый Массив(); // Массив Из Строка
	Имена.Добавить("ВестиПодробныеЗамерыВремениКОД");
	Имена.Добавить("ИспользоватьОтложеннуюОбработкуДанныхКОД");
	Имена.Добавить("МаксимальноеЧислоПотоковОтправкиКОД");
	Имена.Добавить("МаксимальноеЧислоПотоковПолученияКОД");
	Имена.Добавить("МаксимальноеКоличествоФайловОбрабатываемоеВсемиПотокамиПолученияКОД");
	Имена.Добавить("МаксимальныйОбъемФайловОбрабатываемыйПотокомПолученияКОД");
	Имена.Добавить("ОтправлятьИсторическиеДанныеПриРасширенииДоступаКОД");
	Имена.Добавить("РазмерПорцииОчередиДляПоискаИсторическихДанныхКОД");
	Имена.Добавить("ТаймаутОповещенияОНеактивностиКОД");
	Возврат Имена;
	
КонецФункции

#КонецОбласти

&НаСервере
Процедура ОбновитьОписаниеСхемыДанных()
	
	Дата = Константы.ДатаЗагрузкиСхемыДанныхКОД.Получить();
	Схема = Константы.ХранилищеСхемыДанныхКОД.Получить().Получить();
	
	Если Схема = Неопределено Тогда
		ОписаниеСхемыДанных = НСтр("ru = 'Из конфигурации'");
	ИначеЕсли ЗначениеЗаполнено(Дата) Тогда
		ОписаниеСхемыДанных =
			СтрШаблон(НСтр("ru = 'Загружена из файла %1'"), Формат(Дата, "ДЛФ=DT"));
	Иначе
		ОписаниеСхемыДанных = НСтр("ru = 'Загружена из файла %1'");
	КонецЕсли;
	
	Элементы.ОчиститьСхемуДанныхКОД.Доступность = Не Схема = Неопределено;
	
КонецПроцедуры

#КонецОбласти
