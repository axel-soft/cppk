// @strict-types

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Тогда

#Область СлужебныйПрограммныйИнтерфейс

// Записать константы из реквизитов формы в реквизиты формы. Чтобы не было блокировки, когда открыто несколько форм
// настроек. Для вызова из форм настроек.
// ПРИМЕЧАНИЕ: Имена элементов формы и реквизитов формы должны совпадать с именами констант!
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - Форма настроек
//  ИменаКонстант - Массив Из Строка - Константы, которые выведены на форму
Процедура ЗаписатьКонстантыВФорме(Форма, ИменаКонстант) Экспорт
	
	Для Каждого ИмяКонстанты Из ИменаКонстант Цикл
		
		Если Форма[ИмяКонстанты] <> Константы[ИмяКонстанты].Получить() Тогда
			Константы[ИмяКонстанты].Установить(Форма[ИмяКонстанты]);
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

// Прочитать из констант и поместить в реквизиты формы настроек. Чтобы не было блокировки, когда открыто несколько форм
// настроек. Для вызова из форм настроек.
// ПРИМЕЧАНИЕ: Имена элементов формы и реквизитов формы должны совпадать с именами констант!
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - Форма настроек
//  ИменаКонстант - Массив Из Строка - Константы, которые выведены на форму
Процедура ПрочитатьКонстантыВФорме(Форма, ИменаКонстант) Экспорт
	
	Для Каждого ИмяКонстанты Из ИменаКонстант Цикл
		МетаданныеКонстанты = Метаданные.Константы[ИмяКонстанты];
		
		Форма[ИмяКонстанты] = Константы[ИмяКонстанты].Получить();
		Если МетаданныеКонстанты.Подсказка <> "" И Форма.Элементы[ИмяКонстанты].Подсказка = "" Тогда
			Форма.Элементы[ИмяКонстанты].Подсказка = МетаданныеКонстанты.Подсказка;
		КонецЕсли;
		Если Форма.Элементы[ИмяКонстанты].Заголовок = "" Тогда
			Форма.Элементы[ИмяКонстанты].Заголовок = МетаданныеКонстанты.Синоним;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли