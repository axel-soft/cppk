
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗначениеВРеквизитФормы(ПолучитьСтруктуруХраненияБазыДанных(, Истина), "СтруктурaБД");
	
КонецПроцедуры

&НаКлиенте
Процедура СтруктурaБДПриАктивизацииСтроки(Элемент)
	
	Поля.Очистить();
	ЭлементыИндексов = Индексы.ПолучитьЭлементы();
	ЭлементыИндексов.Очистить();
	
	ТекущаяСтрока = Элементы.СтруктурaБД.ТекущаяСтрока;
	
	Если ТекущаяСтрока = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПоляТаблицы = СтруктурaБД[ТекущаяСтрока].Поля;
	Для Каждого СтрокаПолей Из ПоляТаблицы Цикл
		ЗаполнитьЗначенияСвойств(Поля.Добавить(), СтрокаПолей);
	КонецЦикла;
	
	ИндексыТаблицы = СтруктурaБД[ТекущаяСтрока].Индексы;
	Для Каждого СтрИндекса Из ИндексыТаблицы Цикл
		
		КорневаяСтр = ЭлементыИндексов.Добавить();
		КорневаяСтр.ИмяИндексаХранения = СтрИндекса.ИмяИндексаХранения;
		ЭлементыПолей = КорневаяСтр.ПолучитьЭлементы();
		
		ПоляИндекса = СтрИндекса.Поля;
		Для Каждого СтрПоляИндекса Из ПоляИндекса Цикл
			ЗаполнитьЗначенияСвойств(ЭлементыПолей.Добавить(), СтрПоляИндекса);
		КонецЦикла;
		
	КонецЦикла;
	
	Для Каждого Эл Из ЭлементыИндексов Цикл
		Элементы.Индексы.Развернуть(Эл.ПолучитьИдентификатор());
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтчетПоТекущемуОбъекту(Команда)
	
	Результат = Новый ТекстовыйДокумент;
	
	КолонкиОсновнойТаблицы = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(
		"ИмяТаблицыХранения,ИмяТаблицы,Метаданные,Назначение");
	
	КолонкиТаблицыПолей = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(
		"ИмяПоляХранения,ИмяПоля,Метаданные");
	
	КолонкиТаблицыИндексов = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(
		"ИмяИндексаХранения,ИмяПоляХранения,ИмяПоля,Метаданные");
	
	Результат.ДобавитьСтроку("Таблица:");
	ДобавитьСтрокуТаблицыВТекст(Результат, КолонкиОсновнойТаблицы, Неопределено);
	Результат.ДобавитьСтроку("");
	ДобавитьСтрокуТаблицыВТекст(Результат, КолонкиОсновнойТаблицы, Элементы.СтруктурaБД.ТекущиеДанные);
	
	Результат.ДобавитьСтроку("");
	Результат.ДобавитьСтроку("Поля:");
	Если Поля.Количество() > 0 Тогда
		ДобавитьСтрокуТаблицыВТекст(Результат, КолонкиТаблицыПолей, Неопределено);
		Результат.ДобавитьСтроку("");
		Для Каждого Стр Из Поля Цикл
	 		ДобавитьСтрокуТаблицыВТекст(Результат, КолонкиТаблицыПолей, Стр);
		КонецЦикла;
	КонецЕсли;
	
	Результат.ДобавитьСтроку("");
	Результат.ДобавитьСтроку("Индексы:");
	Если Индексы.ПолучитьЭлементы().Количество() > 0 Тогда
		ДобавитьСтрокуТаблицыВТекст(Результат, КолонкиТаблицыИндексов, Неопределено);
		Результат.ДобавитьСтроку("");
		Для Каждого Стр Из Индексы.ПолучитьЭлементы() Цикл
			ДобавитьСтрокуТаблицыВТекст(Результат, КолонкиТаблицыИндексов, Стр);
			Для Каждого СтрПоля Из Стр.ПолучитьЭлементы() Цикл
				ДобавитьСтрокуТаблицыВТекст(Результат, КолонкиТаблицыИндексов, СтрПоля);
			КонецЦикла;
		КонецЦикла;
	КонецЕсли;
	
	Результат.Показать("Структура таблицы БД");
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьСтрокуТаблицыВТекст(Текст, Колонки, Строка = Неопределено)
	
	ТекСтрока = "";
	
	ЭтоПерваяКолонка = Истина;
	Для Каждого Колонка Из Колонки Цикл
		Значение = ?(Строка = Неопределено, Колонка, Строка[Колонка]);
 		ТекСтрока = ТекСтрока + ?(ЭтоПерваяКолонка, "", Символы.Таб) + Значение;
		ЭтоПерваяКолонка = Ложь;
 	КонецЦикла;
	
	Текст.ДобавитьСтроку(ТекСтрока);
	
КонецПроцедуры
