#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Параметры.Свойство("ИнтегрированнаяСистема", ИнтегрированнаяСистема);
	Параметры.Свойство("ПредставлениеИС", ПредставлениеИС);
	
	ДанныеАвторизации = РегистрыСведений.ДанныеИнтегрированныхСистем.ДанныеАвторизации(ИнтегрированнаяСистема, Ложь);
	Если ЗначениеЗаполнено(ДанныеАвторизации) Тогда
		ИмяПользователя = ДанныеАвторизации.ИмяПользователя;
		Пароль = ДанныеАвторизации.Пароль;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ПредставлениеИС) Тогда
		АвтоЗаголовок = Ложь;
		Заголовок = СтрШаблон(НСтр("ru = 'Доступ к %1'"), ПредставлениеИС);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ИнтегрированнаяСистема) Тогда
		АдресВебСервиса = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ИнтегрированнаяСистема, "АдресВебСервиса");
	КонецЕсли;
	
#Если Не ВебКлиент Тогда
	// Добавим в список выбора имя пользователя ИС.
	ПользовательИБ = ПользователиИнформационнойБазы.ТекущийПользователь();
	Элементы.ИмяПользователя.СписокВыбора.Добавить(ПользовательИБ.Имя);
	Элементы.ИмяПользователя.КнопкаВыпадающегоСписка = Истина;
#КонецЕсли
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)
	
	ПроверитьПодключениеИЗаписатьДанныеИСНаСервере();
	
	Закрыть(Истина);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПроверитьПодключениеИЗаписатьДанныеИСНаСервере()
	
	РаботаСИнтегрированнымиСистемами.ПолучитьПрокси(АдресВебСервиса, ИмяПользователя, Пароль);
	
	Попытка
		
		РегистрыСведений.ДанныеИнтегрированныхСистем.ЗаписатьДанные(ИнтегрированнаяСистема, ИмяПользователя, Пароль);
		
	Исключение
		ЗаписьЖурналаРегистрации(
			РаботаСИнтегрированнымиСистемами.ИмяСобытияЖурналаРегистрации(),
			УровеньЖурналаРегистрации.Ошибка,,,
			ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		ВызватьИсключение;
	КонецПопытки;
	
КонецПроцедуры

#КонецОбласти