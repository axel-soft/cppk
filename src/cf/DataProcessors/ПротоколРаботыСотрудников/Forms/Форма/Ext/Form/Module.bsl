#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ПриСозданииНаСервереРедакцииКонфигурации();
	
	ТекущийПользователь = Пользователи.ТекущийПользователь();
	Дата = НачалоДня(ТекущаяДатаСеанса());
	
	ЗаполнитьДеревоСотрудников();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ДатаПриИзменении(Элемент)
	
	ОбновитьПротоколСервер();
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаПриАктивизацииДаты(Элемент)
	
	ОбновитьПротоколСервер();
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаПриВыводеПериода(Элемент, ОформлениеПериода)
	
	ОбщегоНазначенияДокументооборотКлиент.ПриВыводеПериода(Элемент, ОформлениеПериода);
	
КонецПроцедуры

&НаКлиенте
Процедура ГруппаСтраницыСправаПриСменеСтраницы(Элемент, ТекущаяСтраница)
	ОбновитьПротоколСервер(Ложь);
КонецПроцедуры

&НаКлиенте
Процедура СобытиеПриИзменении(Элемент)
	ОбновитьПротоколСервер();
КонецПроцедуры

&НаКлиенте
Процедура ТипДанныхПриИзменении(Элемент)
	ОбновитьПротоколСервер();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормы_ДеревоСотрудников

&НаКлиенте
Процедура ДеревоСотрудниковПриАктивизацииСтроки(Элемент)
	
	Если Элементы.ДеревоСотрудников.ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;	
	
	ПодключитьОбработчикОжидания("ОбновитьПротокол", 0.2, Истина);
	
	СотрСтрока = Элементы.ДеревоСотрудников.ТекущиеДанные.Ссылка;
	ЭтоСотрудник = (ТипЗнч(СотрСтрока) = Тип("СправочникСсылка.Сотрудники"));
	
	Элементы.ДеревоСотрудниковКонтекстноеМенюНаписатьВЧат.Доступность 
		= ЭтоСотрудник И Не СотрудникиКлиент.ЭтоСотрудникТекущегоПользователя(СотрСтрока);
	Элементы.ДеревоСотрудниковКонтекстноеМенюОткрытьЕжедневынйОтчет.Доступность = ЭтоСотрудник;

КонецПроцедуры

&НаКлиенте
Процедура ДеревоСотрудниковВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если Элементы.ДеревоСотрудников.ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;	
	
	ТекСсылка = Элементы.ДеревоСотрудников.ТекущиеДанные.Ссылка;
	ПоказатьЗначение(, ТекСсылка);
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоСотрудниковПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Отказ = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоСотрудниковПередУдалением(Элемент, Отказ)
	
	Отказ = Истина;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормы_ПротоколРаботы

&НаКлиенте
Процедура ПротоколРаботыПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Отказ = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ПротоколРаботыПередУдалением(Элемент, Отказ)
	
	Отказ = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ПротоколРаботыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если Элементы.ПротоколРаботы.ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Элементы.ПротоколРаботы.ТекущиеДанные.ОбъектДанных) Тогда
		ПоказатьЗначение(, Элементы.ПротоколРаботы.ТекущиеДанные.ОбъектДанных);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПротоколРаботыОбъектНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
КонецПроцедуры

&НаКлиенте
Процедура ПротоколРаботыОбъектНачалоВыбораИзСписка(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
КонецПроцедуры

&НаКлиенте
Процедура ПротоколРаботыОбъектОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
КонецПроцедуры

&НаКлиенте
Процедура ПротоколРаботыСотрудникНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормы_ТаблицаПоОтделу

&НаКлиенте
Процедура ТаблицаПоОтделуПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	Отказ = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаПоОтделуПередУдалением(Элемент, Отказ)
	Отказ = Истина;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура НаписатьВЧат(Команда)
	
	Если ЗначениеЗаполнено(ВыбранныйСотрудникИлиОтдел) Тогда
		
		Если Не ОбсужденияСлужебныйКлиент.Подключены() Тогда
			ПоказатьПредупреждение(, НСтр("ru = 'Обсуждения не подключены.'"));
			Возврат;
		КонецЕсли;
		
		Если ТипЗнч(ВыбранныйСотрудникИлиОтдел) = Тип("СправочникСсылка.Сотрудники") Тогда
			НовоеОбсуждение = НовоеОбсуждение(ВыбранныйСотрудникИлиОтдел);
		Иначе
			НовоеОбсуждение = АвтообновляемоеОбсуждение(ВыбранныйСотрудникИлиОтдел);
		КонецЕсли;	
		
		ФайловаяСистемаКлиент.ОткрытьНавигационнуюСсылку(НовоеОбсуждение);
		
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура НаписатьПисьмо(Команда)
	
	Если ЗначениеЗаполнено(ВыбранныйСотрудникИлиОтдел) Тогда
		
		МассивСотрудников = Новый Массив;
		
		Если ТипЗнч(ВыбранныйСотрудникИлиОтдел) = Тип("СправочникСсылка.Сотрудники") Тогда
			МассивСотрудников.Добавить(ВыбранныйСотрудникИлиОтдел);
		Иначе
			МассивСотрудников = ПолучитьСотрудниковОтдела(ВыбранныйСотрудникИлиОтдел);
		КонецЕсли;	
		
		ВстроеннаяПочтаКлиент.СоздатьПисьмоНаОснованииСотрудников(МассивСотрудников);
		
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьЕжедневныйОтчет(Команда)
	
	Если ЗначениеЗаполнено(ВыбранныйСотрудникИлиОтдел) И
		ТипЗнч(ВыбранныйСотрудникИлиОтдел) = Тип("СправочникСсылка.Сотрудники") Тогда
	
		ОтчетЗаСегодня = ПолучитьЕжедневныеОтчетыТекущейДаты(Дата, ВыбранныйСотрудникИлиОтдел);
		Если ЗначениеЗаполнено(ОтчетЗаСегодня) Тогда 
			ПараметрыФормы = Новый Структура;
			ПараметрыФормы.Вставить("Ключ", ОтчетЗаСегодня);
			ОткрытьФорму("Документ.ЕжедневныйОтчет.ФормаОбъекта", ПараметрыФормы);
		Иначе
			ПоказатьПредупреждение(, 
				СтрШаблон(НСтр("ru = 'Нет отчета за: %1.'"), 
				Формат(Дата, "ДЛФ=DD")));
		КонецЕсли;
		
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьСписок(Команда)
	
	НадоПоказатьВопрос = Истина;
	
	Если ТипЗнч(ВыбранныйСотрудникИлиОтдел) = Тип("СправочникСсылка.Сотрудники")
		И Элементы.Дата.ВыделенныеДаты.Количество() <= 2 Тогда
		НадоПоказатьВопрос = Ложь;
	КонецЕсли;	
	
	Если Не НадоПоказатьВопрос Тогда
		СписокЗаполненПоКнопке = Истина;
		ОбновитьПротоколТолькоСписок();
		Возврат;
	КонецЕсли;	
	
	ТекстВопроса = НСтр("ru = 'Заполнение может занять продолжительное время. Продолжить?'");
	
	Если Элементы.Дата.ВыделенныеДаты.Количество() > 4 Тогда
		ТекстВопроса = НСтр("ru = 'Заполнение может занять продолжительное время, т.к. выбран большой интервал дат.
		|Продолжить?'");
	КонецЕсли;	
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ЗаполнитьСписокПослеВопроса", ЭтотОбъект, Параметры);
	ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьСписокПослеВопроса(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	СписокЗаполненПоКнопке = Истина;
	ОбновитьПротоколТолькоСписок();

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьДеревоСотрудников()
	
	УстановитьПривилегированныйРежим(Истина);
	
	Дерево = РеквизитФормыВЗначение("ДеревоСотрудников");
	Дерево.Строки.Очистить();
	
	МассивОтделов = Новый Массив;
	
	Если Пользователи.ЭтоПолноправныйПользователь(,,Ложь) Тогда
		// Под полными правами показываем корневые отделы.
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	СтруктураПредприятия.Ссылка КАК Ссылка
			|ИЗ
			|	Справочник.СтруктураПредприятия КАК СтруктураПредприятия
			|ГДЕ
			|	НЕ СтруктураПредприятия.ПометкаУдаления
			|	И СтруктураПредприятия.Родитель = ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)";
		
		МассивОтделов = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
		
	Иначе
		
		СотрудникиТекущегоПользователя = Сотрудники.ВсеСотрудникиТекущегоПользователя();
		
		// Найдем кто делегировал полномочия текущему пользователю.
		Запрос = Новый Запрос;
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	ДелегированиеПравОбластиДелегирования.Ссылка.Сотрудник
			|ИЗ
			|	Справочник.ЗамещающиеИПомощники.ВопросыЗамещения КАК ДелегированиеПравОбластиДелегирования
			|ГДЕ
			|	ДелегированиеПравОбластиДелегирования.Ссылка.Действует = ИСТИНА
			|	И ДелегированиеПравОбластиДелегирования.Ссылка.Замещающий В (&СотрудникиПользователя)
			|	И ДелегированиеПравОбластиДелегирования.Область В (&ОбластиЗамещения)";
		
		ОбластиЗамещения = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(
			Справочники.ОбластиЗамещения.ВсеОбласти);
		ОбластиЗамещения.Добавить(Справочники.ОбластиЗамещения.ПротоколРаботыСотрудников);
		
		Запрос.УстановитьПараметр("СотрудникиПользователя", СотрудникиТекущегоПользователя);
		Запрос.УстановитьПараметр("ОбластиЗамещения", ОбластиЗамещения);
		
		МассивКтоДелегировал = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Сотрудник");
		
		// Найдем все отделы, где тек Сотрудник- руководитель
		// - каждое - это новый нод дерева - рекурсивно заполним.
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	СтруктураПредприятия.Ссылка КАК Ссылка
			|ИЗ
			|	Справочник.СтруктураПредприятия КАК СтруктураПредприятия
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ПодчиненностьПодразделений КАК ПодчиненностьПодразделений
			|		ПО СтруктураПредприятия.Ссылка = ПодчиненностьПодразделений.Подчиненное
			|		И СтруктураПредприятия.Ссылка = ПодчиненностьПодразделений.Вышестоящее
			|ГДЕ
			|	НЕ СтруктураПредприятия.ПометкаУдаления
			|	И ПодчиненностьПодразделений.РуководительПодчиненного В (&Сотрудники)";
		
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(
			МассивКтоДелегировал, СотрудникиТекущегоПользователя);
		Запрос.Параметры.Вставить("Сотрудники", МассивКтоДелегировал);
		
		МассивОтделовИзЗапроса = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
		
		// Исключим подотделы.
		Для Каждого Отдел Из МассивОтделовИзЗапроса Цикл
			
			ЭтоПодотдел = Ложь;
			
			Для Каждого ОтделРодитель Из МассивОтделовИзЗапроса Цикл
				
				Если ОтделРодитель <> Отдел 
					И Делопроизводство.ЭтоДочернееПодразделение(Отдел, ОтделРодитель) Тогда
					ЭтоПодотдел = Истина;
					Прервать;
				КонецЕсли;	
				
			КонецЦикла;
			
			Если ЭтоПодотдел = Ложь Тогда
				МассивОтделов.Добавить(Отдел);
			КонецЕсли;	
			
		КонецЦикла;	
		
	КонецЕсли;
	
	
	Для Каждого Отдел Из МассивОтделов Цикл
		ЗаполнитьОтдел(Отдел, Дерево.Строки);
	КонецЦикла;	
	
	ЗначениеВРеквизитФормы(Дерево, "ДеревоСотрудников");
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьОтдел(Отдел, Строки)
	
	УзелДерева = Строки.Добавить();
	УзелДерева.Представление = Отдел;
	УзелДерева.Ссылка = Отдел;
	УзелДерева.НомерКартинки = 0;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	СтруктураПредприятия.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.СтруктураПредприятия КАК СтруктураПредприятия
		|ГДЕ
		|	НЕ СтруктураПредприятия.ПометкаУдаления
		|	И СтруктураПредприятия.Родитель = &Отдел";
		
	Запрос.Параметры.Вставить("Отдел", Отдел);
	МассивОтделов = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
	Для Каждого ОтделТек Из МассивОтделов Цикл
		ЗаполнитьОтдел(ОтделТек, УзелДерева.Строки);
	КонецЦикла;	
	
	МассивСотрудников = Сотрудники.СотрудникиПодразделения(Отдел, Истина, Истина);
		
	Для Каждого Сотрудник Из МассивСотрудников Цикл
		
		УзелСотрудник = УзелДерева.Строки.Добавить();
		УзелСотрудник.Представление = Строка(Сотрудник);
		Если Не ЗначениеЗаполнено(УзелСотрудник.Представление) Тогда
			УзелСотрудник.Представление = Сотрудник;
		КонецЕсли;	
		УзелСотрудник.Ссылка = Сотрудник;
		УзелСотрудник.НомерКартинки = 5;
	
	КонецЦикла;	
	
	УзелДерева.Строки.Сортировать("Представление");
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьПротоколСервер(ОбновитьБезусловно = Истина)
	
	Если Не ЗначениеЗаполнено(ВыбранныйСотрудникИлиОтдел) Тогда
		Возврат;
	КонецЕсли;	
	
	Если ТипЗнч(ВыбранныйСотрудникИлиОтдел) = Тип("СправочникСсылка.Сотрудники") Тогда
		
		Если Элементы.ГруппаСтраницыСправа.ТекущаяСтраница = Элементы.ГруппаСтраницаПоСотрудникам Тогда
			Элементы.ГруппаСтраницыСправа.ТекущаяСтраница = Элементы.ГруппаСтраницаДинамика;
		КонецЕсли;	
		
		Если Элементы.ГруппаСтраницыСправа.ТекущаяСтраница = Элементы.ГруппаСтраницаИтог Тогда
			Элементы.ГруппаСтраницыСправа.ТекущаяСтраница = Элементы.ГруппаСтраницаДинамика;
		КонецЕсли;	
		
		Элементы.ГруппаСтраницаПоСотрудникам.Видимость = Ложь;
		Элементы.ГруппаСтраницаИтог.Видимость = Ложь;
		
	Иначе	
		
		Элементы.ГруппаСтраницаПоСотрудникам.Видимость = Истина;
		Элементы.ГруппаСтраницаИтог.Видимость = Истина;
		
	КонецЕсли;	
	
	Если ОбновитьБезусловно Тогда
		ДиаграммаПоСотрудникамЗагружена = Ложь;
		ДиаграммаПоСобытиямЗагружена = Ложь;
		ДиаграммаПоТипамДанныхЗагружена = Ложь;
		ДиаграммаДинамикаЗагружена = Ложь;
		
		СписокЗагружен = Ложь;
		СписокЗаполненПоКнопке = Ложь;
		Элементы.ЗаполнитьСписок.Видимость = Истина;
		ПротоколРаботы.Очистить();
	КонецЕсли;	
		
	Если Элементы.ГруппаСтраницыСправа.ТекущаяСтраница = Элементы.ГруппаСтраницаИтог Тогда
	
		ОбновитьПротоколСерверСписок(ОбновитьБезусловно);
		
	ИначеЕсли Элементы.ГруппаСтраницыСправа.ТекущаяСтраница = Элементы.ГруппаСтраницаПоСотрудникам Тогда
		
		Если ДиаграммаПоСотрудникамЗагружена Тогда
			Возврат;
		КонецЕсли;	
		ДиаграммаПоСотрудникамЗагружена = Истина;
		
		ОбновитьПротокол_ПоСотрудникам();
		
	ИначеЕсли Элементы.ГруппаСтраницыСправа.ТекущаяСтраница = Элементы.ГруппаСтраницаСобытия Тогда
		
		Если ДиаграммаПоСобытиямЗагружена Тогда
			Возврат;
		КонецЕсли;	
		ДиаграммаПоСобытиямЗагружена = Истина;
		
		ОбновитьПротокол_ПоСобытиям();
		
	ИначеЕсли Элементы.ГруппаСтраницыСправа.ТекущаяСтраница = Элементы.ГруппаСтраницаПоТипамДанных Тогда	
		
		Если ДиаграммаПоТипамДанныхЗагружена Тогда
			Возврат;
		КонецЕсли;	
		ДиаграммаПоТипамДанныхЗагружена = Истина;
		
		ОбновитьПротокол_ПоТипамДанных();
		
	ИначеЕсли Элементы.ГруппаСтраницыСправа.ТекущаяСтраница = Элементы.ГруппаСтраницаДинамика Тогда	
		
		Если ДиаграммаДинамикаЗагружена Тогда
			Возврат;
		КонецЕсли;	
		ДиаграммаДинамикаЗагружена = Истина;
		
		ОбновитьПротокол_Динамика();
		
	ИначеЕсли Элементы.ГруппаСтраницыСправа.ТекущаяСтраница = Элементы.ГруппаСтраницаВсеДействия Тогда	
		
		// таблица по отделу
		Если ТипЗнч(ВыбранныйСотрудникИлиОтдел) = Тип("СправочникСсылка.СтруктураПредприятия") Тогда	
			
			Элементы.ГруппаСнизуСтатус.Видимость = Ложь;
			
		Иначе
			
			Элементы.ГруппаСнизуСтатус.Видимость = Истина;
		КонецЕсли;	
		
	КонецЕсли;	
	
КонецПроцедуры	

&НаСервере
Процедура ОбновитьПротоколСерверСписок(ОбновитьБезусловно = Истина)
	
	Если ОбновитьБезусловно Тогда
		СписокЗагружен = Ложь;
		ТаблицаПоОтделуЗагружена = Ложь;
	КонецЕсли;	
	
	Если ТипЗнч(ВыбранныйСотрудникИлиОтдел) = Тип("СправочникСсылка.СтруктураПредприятия") Тогда	
		Элементы.ГруппаСнизуСтатус.Видимость = Ложь;
	Иначе
		
		Элементы.ГруппаСнизуСтатус.Видимость = Истина;
		
	КонецЕсли;	
	
	ЗаполнитьТаблицуОтдела();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТаблицуОтдела()
	
	Если ТипЗнч(ВыбранныйСотрудникИлиОтдел) <> Тип("СправочникСсылка.СтруктураПредприятия") Тогда	
		Возврат;
	КонецЕсли;	
	
	Если ТаблицаПоОтделуЗагружена Тогда
		Возврат;
	КонецЕсли;	
	ТаблицаПоОтделуЗагружена = Истина;
	
	УстановитьПривилегированныйРежим(Истина);
	
	МассивСотрудников = Сотрудники.СотрудникиПодразделения(
		ВыбранныйСотрудникИлиОтдел, Истина, Истина);
		
	ВыделенныеДаты = Элементы.Дата.ВыделенныеДаты;
	Если ВыделенныеДаты.Количество() = 0 Тогда
		ВыделенныеДаты.Добавить(Дата);
	КонецЕсли;	
	
	МинДата = Неопределено;
	МаксДата = Неопределено;
	Для Каждого СтрДата Из ВыделенныеДаты Цикл
		
		Если МинДата = Неопределено Тогда
			МинДата = СтрДата;
		Иначе	
			МинДата = Мин(МинДата, СтрДата);
		КонецЕсли;	
		
		Если МаксДата = Неопределено Тогда
			МаксДата = СтрДата;
		Иначе	
			МаксДата = Макс(МаксДата, СтрДата);
		КонецЕсли;	
		
	КонецЦикла;	
	
	РежимОдинДень = Ложь;
	Если МинДата = МаксДата Тогда // 1 день  - по часам
		РежимОдинДень = Истина;
	КонецЕсли;	
		
	ТаблицаОтдел = РеквизитФормыВЗначение("ТаблицаПоОтделу");
	ТаблицаОтдел.Очистить();
	
	Для Каждого Сотрудник Из МассивСотрудников Цикл
		
		НовСтр = ТаблицаОтдел.Добавить();
		НовСтр.Сотрудник = Сотрудник;
		НовСтр.ПолеСортировки = Строка(Сотрудник);
		
		Если Не РежимОдинДень Тогда
			НовСтр.Начало = Неопределено;
			НовСтр.Окончание = Неопределено;
		КонецЕсли;	
		
	КонецЦикла;	
	ТаблицаОтдел.Сортировать("ПолеСортировки");

	Запрос = Новый Запрос;
	
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПротоколРаботыСотрудников.Сотрудник КАК Сотрудник,
		|	КОЛИЧЕСТВО(*) КАК Всего,
		|	МИНИМУМ(ПротоколРаботыСотрудников.Дата) КАК Начало,
		|	МАКСИМУМ(ПротоколРаботыСотрудников.Дата) КАК Окончание,
		|	ПРЕДСТАВЛЕНИЕССЫЛКИ(ПротоколРаботыСотрудников.Сотрудник) КАК Представление
		|ИЗ
		|	РегистрСведений.ПротоколРаботыСотрудников КАК ПротоколРаботыСотрудников
		|ГДЕ
		|	ПротоколРаботыСотрудников.Дата >= &МинДата
		|	И ПротоколРаботыСотрудников.Дата <= &МаксДата
		|	И ПротоколРаботыСотрудников.Сотрудник В (&Сотрудники)
		|СГРУППИРОВАТЬ ПО
		|	ПротоколРаботыСотрудников.Сотрудник";
	
	Запрос.УстановитьПараметр("Сотрудники", МассивСотрудников);
	Запрос.УстановитьПараметр("МинДата", НачалоДня(МинДата));
	Запрос.УстановитьПараметр("МаксДата", КонецДня(МаксДата));
	
	ТаблицаРезультатаЗапроса = Запрос.Выполнить().Выгрузить();
	ТаблицаРезультатаЗапроса.Сортировать("Представление");
	
	Для Каждого Стр Из ТаблицаРезультатаЗапроса Цикл
		
		СтрокаТабл = ТаблицаОтдел.Найти(Стр.Сотрудник, "Сотрудник");
		Если СтрокаТабл <> Неопределено Тогда
			ЗаполнитьЗначенияСвойств(СтрокаТабл, Стр);
			
			Если Не РежимОдинДень Тогда
				СтрокаТабл.Начало = Неопределено;
				СтрокаТабл.Окончание = Неопределено;
			КонецЕсли;	
			
		КонецЕсли;	
		
	КонецЦикла;
	
	// теперь для Изменений
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПротоколРаботыСотрудников.Сотрудник КАК Сотрудник,
		|	КОЛИЧЕСТВО(*) КАК Изменений
		|ИЗ
		|	РегистрСведений.ПротоколРаботыСотрудников КАК ПротоколРаботыСотрудников
		|ГДЕ
		|	ПротоколРаботыСотрудников.Дата >= &МинДата
		|	И ПротоколРаботыСотрудников.Дата <= &МаксДата
		|	И ПротоколРаботыСотрудников.Сотрудник В(&Сотрудники)
		|	И (ПротоколРаботыСотрудников.ТипСобытия = ЗНАЧЕНИЕ(Перечисление.ТипыСобытийПротоколаРаботыСотрудников.Изменение)
		|			ИЛИ ПротоколРаботыСотрудников.ТипСобытия = ЗНАЧЕНИЕ(Перечисление.ТипыСобытийПротоколаРаботыСотрудников.ИзменениеФайла)
		|			ИЛИ ПротоколРаботыСотрудников.ТипСобытия = ЗНАЧЕНИЕ(Перечисление.ТипыСобытийПротоколаРаботыСотрудников.ПеремещениеФайлов)
		|			ИЛИ ПротоколРаботыСотрудников.ТипСобытия = ЗНАЧЕНИЕ(Перечисление.ТипыСобытийПротоколаРаботыСотрудников.ПеренаправлениеЗадачи)
		|			ИЛИ ПротоколРаботыСотрудников.ТипСобытия = ЗНАЧЕНИЕ(Перечисление.ТипыСобытийПротоколаРаботыСотрудников.ПодписаниеЭП)
		|			ИЛИ ПротоколРаботыСотрудников.ТипСобытия = ЗНАЧЕНИЕ(Перечисление.ТипыСобытийПротоколаРаботыСотрудников.ПрерываниеБизнесПроцесса)
		|			ИЛИ ПротоколРаботыСотрудников.ТипСобытия = ЗНАЧЕНИЕ(Перечисление.ТипыСобытийПротоколаРаботыСотрудников.Расшифровывание)
		|			ИЛИ ПротоколРаботыСотрудников.ТипСобытия = ЗНАЧЕНИЕ(Перечисление.ТипыСобытийПротоколаРаботыСотрудников.РегистрацияДокумента)
		|			ИЛИ ПротоколРаботыСотрудников.ТипСобытия = ЗНАЧЕНИЕ(Перечисление.ТипыСобытийПротоколаРаботыСотрудников.Создание)
		|			ИЛИ ПротоколРаботыСотрудников.ТипСобытия = ЗНАЧЕНИЕ(Перечисление.ТипыСобытийПротоколаРаботыСотрудников.СтартБизнесПроцесса)
		|			ИЛИ ПротоколРаботыСотрудников.ТипСобытия = ЗНАЧЕНИЕ(Перечисление.ТипыСобытийПротоколаРаботыСотрудников.УдалениеПодписиЭП)
		|			ИЛИ ПротоколРаботыСотрудников.ТипСобытия = ЗНАЧЕНИЕ(Перечисление.ТипыСобытийПротоколаРаботыСотрудников.Шифрование))
		|
		|СГРУППИРОВАТЬ ПО
		|	ПротоколРаботыСотрудников.Сотрудник
		|
		|УПОРЯДОЧИТЬ ПО
		|	Сотрудник";
	
	Запрос.УстановитьПараметр("Сотрудники", МассивСотрудников);
	Запрос.УстановитьПараметр("МинДата", НачалоДня(МинДата));
	Запрос.УстановитьПараметр("МаксДата", КонецДня(МаксДата));
	
	ТаблицаРезультатаЗапроса = Запрос.Выполнить().Выгрузить();
	
	Для Каждого Стр Из ТаблицаРезультатаЗапроса Цикл
		
		СтрокаТабл = ТаблицаОтдел.Найти(Стр.Сотрудник, "Сотрудник");
		Если СтрокаТабл <> Неопределено Тогда
			СтрокаТабл.Изменений = Стр.Изменений;
		КонецЕсли;	
		
	КонецЦикла;
	
	//  по отделу среднее
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПротоколРаботыСотрудников.Сотрудник КАК Сотрудник,
		|	КОЛИЧЕСТВО(*) КАК Всего
		|ИЗ
		|	РегистрСведений.ПротоколРаботыСотрудников КАК ПротоколРаботыСотрудников
		|ГДЕ
		|	ПротоколРаботыСотрудников.Дата >= &МинДата
		|	И ПротоколРаботыСотрудников.Дата <= &МаксДата
		|	И ПротоколРаботыСотрудников.Сотрудник В(&Сотрудники)
		|
		|СГРУППИРОВАТЬ ПО
		|	ПротоколРаботыСотрудников.Сотрудник
		|
		|УПОРЯДОЧИТЬ ПО
		|	Всего";
	
	Запрос.УстановитьПараметр("Сотрудники", МассивСотрудников);
	Запрос.УстановитьПараметр("МинДата", НачалоДня(МинДата));
	Запрос.УстановитьПараметр("МаксДата", КонецДня(МаксДата));
	
	ТаблицаРезультатаЗапроса = Запрос.Выполнить().Выгрузить();
	// найдем медиану
	Медиана = 0;
	Если ТаблицаРезультатаЗапроса.Количество() <> 0 Тогда
		Если ТаблицаРезультатаЗапроса.Количество() % 2 = 1 Тогда // нечетное
			
			Позиция = (ТаблицаРезультатаЗапроса.Количество() - 1) / 2;
			Медиана = ТаблицаРезультатаЗапроса[Позиция].Всего;
			
		Иначе	
			
			Позиция1 = ТаблицаРезультатаЗапроса.Количество() / 2 - 1;
			Позиция2 = ТаблицаРезультатаЗапроса.Количество() / 2;
			Медиана = (ТаблицаРезультатаЗапроса[Позиция1].Всего + ТаблицаРезультатаЗапроса[Позиция2].Всего) / 2;
			
		КонецЕсли;	
	КонецЕсли;	
	
	ВсегоОтдел = Медиана;
	
	//  по отделу изменения
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПротоколРаботыСотрудников.Сотрудник КАК Сотрудник,
		|	КОЛИЧЕСТВО(*) КАК Всего
		|ИЗ
		|	РегистрСведений.ПротоколРаботыСотрудников КАК ПротоколРаботыСотрудников
		|ГДЕ
		|	ПротоколРаботыСотрудников.Дата >= &МинДата
		|	И ПротоколРаботыСотрудников.Дата <= &МаксДата
		|	И ПротоколРаботыСотрудников.Сотрудник В(&Сотрудники)
		|	И (ПротоколРаботыСотрудников.ТипСобытия = ЗНАЧЕНИЕ(Перечисление.ТипыСобытийПротоколаРаботыСотрудников.Изменение)
		|			ИЛИ ПротоколРаботыСотрудников.ТипСобытия = ЗНАЧЕНИЕ(Перечисление.ТипыСобытийПротоколаРаботыСотрудников.ИзменениеФайла)
		|			ИЛИ ПротоколРаботыСотрудников.ТипСобытия = ЗНАЧЕНИЕ(Перечисление.ТипыСобытийПротоколаРаботыСотрудников.ПеремещениеФайлов)
		|			ИЛИ ПротоколРаботыСотрудников.ТипСобытия = ЗНАЧЕНИЕ(Перечисление.ТипыСобытийПротоколаРаботыСотрудников.ПеренаправлениеЗадачи)
		|			ИЛИ ПротоколРаботыСотрудников.ТипСобытия = ЗНАЧЕНИЕ(Перечисление.ТипыСобытийПротоколаРаботыСотрудников.ПодписаниеЭП)
		|			ИЛИ ПротоколРаботыСотрудников.ТипСобытия = ЗНАЧЕНИЕ(Перечисление.ТипыСобытийПротоколаРаботыСотрудников.ПрерываниеБизнесПроцесса)
		|			ИЛИ ПротоколРаботыСотрудников.ТипСобытия = ЗНАЧЕНИЕ(Перечисление.ТипыСобытийПротоколаРаботыСотрудников.Расшифровывание)
		|			ИЛИ ПротоколРаботыСотрудников.ТипСобытия = ЗНАЧЕНИЕ(Перечисление.ТипыСобытийПротоколаРаботыСотрудников.РегистрацияДокумента)
		|			ИЛИ ПротоколРаботыСотрудников.ТипСобытия = ЗНАЧЕНИЕ(Перечисление.ТипыСобытийПротоколаРаботыСотрудников.Создание)
		|			ИЛИ ПротоколРаботыСотрудников.ТипСобытия = ЗНАЧЕНИЕ(Перечисление.ТипыСобытийПротоколаРаботыСотрудников.СтартБизнесПроцесса)
		|			ИЛИ ПротоколРаботыСотрудников.ТипСобытия = ЗНАЧЕНИЕ(Перечисление.ТипыСобытийПротоколаРаботыСотрудников.УдалениеПодписиЭП)
		|			ИЛИ ПротоколРаботыСотрудников.ТипСобытия = ЗНАЧЕНИЕ(Перечисление.ТипыСобытийПротоколаРаботыСотрудников.Шифрование))
		|
		|СГРУППИРОВАТЬ ПО
		|	ПротоколРаботыСотрудников.Сотрудник
		|
		|УПОРЯДОЧИТЬ ПО
		|	Всего";
	
	Запрос.УстановитьПараметр("Сотрудники", МассивСотрудников);
	Запрос.УстановитьПараметр("МинДата", НачалоДня(МинДата));
	Запрос.УстановитьПараметр("МаксДата", КонецДня(МаксДата));
	
	ТаблицаРезультатаЗапроса = Запрос.Выполнить().Выгрузить();
	// найдем медиану
	Медиана = 0;
	Если ТаблицаРезультатаЗапроса.Количество() <> 0 Тогда
		Если ТаблицаРезультатаЗапроса.Количество() % 2 = 1 Тогда // нечетное
			
			Позиция = (ТаблицаРезультатаЗапроса.Количество() - 1) / 2;
			Медиана = ТаблицаРезультатаЗапроса[Позиция].Всего;
			
		Иначе	
			
			Позиция1 = ТаблицаРезультатаЗапроса.Количество() / 2 - 1;
			Позиция2 = ТаблицаРезультатаЗапроса.Количество() / 2;
			Медиана = (ТаблицаРезультатаЗапроса[Позиция1].Всего + ТаблицаРезультатаЗапроса[Позиция2].Всего) / 2;
			
		КонецЕсли;	
	КонецЕсли;	
	
	ИзмененияОтдел = Медиана;
	
	НовСтр = ТаблицаОтдел.Добавить();
	НовСтр.Сотрудник = НСтр("ru = 'Среднее по отделу'");
	НовСтр.Всего = ВсегоОтдел;
	НовСтр.Изменений = ИзмененияОтдел;
	НовСтр.ЭтоПоОтделу = Истина;
	
	ЗначениеВРеквизитФормы(ТаблицаОтдел, "ТаблицаПоОтделу");
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьПротокол_ПоСотрудникам()
	
	УстановитьПривилегированныйРежим(Истина);
	
	ВыделенныеДаты = Элементы.Дата.ВыделенныеДаты;
	Если ВыделенныеДаты.Количество() = 0 Тогда
		ВыделенныеДаты.Добавить(Дата);
	КонецЕсли;	
	
	МинДата = Неопределено;
	МаксДата = Неопределено;
	Для Каждого СтрДата Из ВыделенныеДаты Цикл
		
		Если МинДата = Неопределено Тогда
			МинДата = СтрДата;
		Иначе	
			МинДата = Мин(МинДата, СтрДата);
		КонецЕсли;	
		
		Если МаксДата = Неопределено Тогда
			МаксДата = СтрДата;
		Иначе	
			МаксДата = Макс(МаксДата, СтрДата);
		КонецЕсли;	
		
	КонецЦикла;	

	МассивСотрудников = Сотрудники.СотрудникиПодразделения(
		ВыбранныйСотрудникИлиОтдел, Истина, Истина);
		
	ТаблицаСотрудников = Новый ТаблицаЗначений;
	
	ТаблицаСотрудников.Колонки.Добавить("Сотрудник", 
		Новый ОписаниеТипов("СправочникСсылка.Сотрудники"));
	
	ТаблицаСотрудников.Колонки.Добавить("ПолеСортировки", Новый ОписаниеТипов("Строка"));
	
	Для Каждого ТекСотрудник Из МассивСотрудников Цикл
		НовСтр = ТаблицаСотрудников.Добавить();
		НовСтр.Сотрудник = ТекСотрудник;
		НовСтр.ПолеСортировки = Строка(ТекСотрудник);
	КонецЦикла;	
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ТаблицаСотрудников.Сотрудник КАК Сотрудник
		|ПОМЕСТИТЬ ТаблицаСотрудниковВременная
		|ИЗ
		|	&ТаблицаСотрудников КАК ТаблицаСотрудников
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Сотрудник
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаСотрудниковВременная.Сотрудник КАК Сотрудник,
		|	КОЛИЧЕСТВО(ПротоколРаботыСотрудников.Сотрудник) КАК ЧислоЗаписей,
		|	0 КАК ЧислоИзменений
		|ИЗ
		|	ТаблицаСотрудниковВременная КАК ТаблицаСотрудниковВременная
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПротоколРаботыСотрудников КАК ПротоколРаботыСотрудников
		|		ПО (ПротоколРаботыСотрудников.Сотрудник = ТаблицаСотрудниковВременная.Сотрудник)
		|			И (ПротоколРаботыСотрудников.Дата >= &МинДата)
		|			И (ПротоколРаботыСотрудников.Дата <= &МаксДата)
		|
		|СГРУППИРОВАТЬ ПО
		|	ТаблицаСотрудниковВременная.Сотрудник
		|
		|УПОРЯДОЧИТЬ ПО
		|	ТаблицаСотрудниковВременная.Сотрудник";
	
	Запрос.УстановитьПараметр("ТаблицаСотрудников", ТаблицаСотрудников);
	
	Запрос.УстановитьПараметр("МинДата", НачалоДня(МинДата));
	Запрос.УстановитьПараметр("МаксДата", КонецДня(МаксДата));
	
	ТаблицаРезультатаЗапроса = Запрос.Выполнить().Выгрузить();
	ТаблицаРезультатаЗапроса.Колонки.Добавить("ПолеСортировки", Новый ОписаниеТипов("Строка"));
	
	Для Каждого Строка Из ТаблицаРезультатаЗапроса Цикл
		
		СтрокаТабл = ТаблицаСотрудников.Найти(Строка.Сотрудник, "Сотрудник");
		Если СтрокаТабл <> Неопределено Тогда
			Строка.ПолеСортировки = СтрокаТабл.ПолеСортировки;
		КонецЕсли;	
		
	КонецЦикла;	
	ТаблицаРезультатаЗапроса.Сортировать("ПолеСортировки");
	
	// теперь изменения
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПротоколРаботыСотрудников.Сотрудник КАК Сотрудник,
		|	ПРЕДСТАВЛЕНИЕССЫЛКИ(ПротоколРаботыСотрудников.Сотрудник) КАК Представление,
		|	0 КАК ЧислоЗаписей,
		|	КОЛИЧЕСТВО(*) КАК ЧислоИзменений
		|ИЗ
		|	РегистрСведений.ПротоколРаботыСотрудников КАК ПротоколРаботыСотрудников
		|ГДЕ
		|	ПротоколРаботыСотрудников.Дата >= &МинДата
		|	И ПротоколРаботыСотрудников.Дата <= &МаксДата
		|	И ПротоколРаботыСотрудников.Сотрудник В (&Сотрудники)
		|	И (ПротоколРаботыСотрудников.ТипСобытия = ЗНАЧЕНИЕ(Перечисление.ТипыСобытийПротоколаРаботыСотрудников.Изменение)
		|	ИЛИ
		|		ПротоколРаботыСотрудников.ТипСобытия = ЗНАЧЕНИЕ(Перечисление.ТипыСобытийПротоколаРаботыСотрудников.ИзменениеФайла)
		|	ИЛИ
		|		ПротоколРаботыСотрудников.ТипСобытия = ЗНАЧЕНИЕ(Перечисление.ТипыСобытийПротоколаРаботыСотрудников.ПеремещениеФайлов)
		|	ИЛИ
		|		ПротоколРаботыСотрудников.ТипСобытия = ЗНАЧЕНИЕ(Перечисление.ТипыСобытийПротоколаРаботыСотрудников.ПеренаправлениеЗадачи)
		|	ИЛИ
		|		ПротоколРаботыСотрудников.ТипСобытия = ЗНАЧЕНИЕ(Перечисление.ТипыСобытийПротоколаРаботыСотрудников.ПодписаниеЭП)
		|	ИЛИ
		|		ПротоколРаботыСотрудников.ТипСобытия = ЗНАЧЕНИЕ(Перечисление.ТипыСобытийПротоколаРаботыСотрудников.ПрерываниеБизнесПроцесса)
		|	ИЛИ
		|		ПротоколРаботыСотрудников.ТипСобытия = ЗНАЧЕНИЕ(Перечисление.ТипыСобытийПротоколаРаботыСотрудников.Расшифровывание)
		|	ИЛИ
		|		ПротоколРаботыСотрудников.ТипСобытия = ЗНАЧЕНИЕ(Перечисление.ТипыСобытийПротоколаРаботыСотрудников.РегистрацияДокумента)
		|	ИЛИ ПротоколРаботыСотрудников.ТипСобытия = ЗНАЧЕНИЕ(Перечисление.ТипыСобытийПротоколаРаботыСотрудников.Создание)
		|	ИЛИ
		|		ПротоколРаботыСотрудников.ТипСобытия = ЗНАЧЕНИЕ(Перечисление.ТипыСобытийПротоколаРаботыСотрудников.СтартБизнесПроцесса)
		|	ИЛИ
		|		ПротоколРаботыСотрудников.ТипСобытия = ЗНАЧЕНИЕ(Перечисление.ТипыСобытийПротоколаРаботыСотрудников.УдалениеПодписиЭП)
		|	ИЛИ
		|		ПротоколРаботыСотрудников.ТипСобытия = ЗНАЧЕНИЕ(Перечисление.ТипыСобытийПротоколаРаботыСотрудников.Шифрование))
		|СГРУППИРОВАТЬ ПО
		|	ПротоколРаботыСотрудников.Сотрудник";
	
	Запрос.УстановитьПараметр("Сотрудники", МассивСотрудников);
	
	Запрос.УстановитьПараметр("МинДата", НачалоДня(МинДата));
	Запрос.УстановитьПараметр("МаксДата", КонецДня(МаксДата));
	
	ТаблицаРезультатаИзменения = Запрос.Выполнить().Выгрузить();
	ТаблицаРезультатаИзменения.Сортировать("Представление");
		
	Для Каждого Стр Из ТаблицаРезультатаИзменения Цикл
		
		СтрокаТабл = ТаблицаРезультатаЗапроса.Найти(Стр.Сотрудник, "Сотрудник");
		Если СтрокаТабл <> Неопределено Тогда
			СтрокаТабл.ЧислоИзменений = Стр.ЧислоИзменений;
		КонецЕсли;	
		
	КонецЦикла;
	
	// Заполняем гистограмму
	ДиаграммаПоСотрудникам.Обновление = Ложь;
	
	ДиаграммаПоСотрудникам.АвтоМаксимальноеЗначение	= Ложь;
	ДиаграммаПоСотрудникам.АвтоМинимальноеЗначение	= Ложь;
	ДиаграммаПоСотрудникам.МаксимальноеЗначение		= 1;
	ДиаграммаПоСотрудникам.МинимальноеЗначение		= 0;
	ДиаграммаПоСотрудникам.БазовоеЗначение			= 0;
	ДиаграммаПоСотрудникам.ПропускатьБазовоеЗначение	= Истина;
	
	ДиаграммаПоСотрудникам.Очистить();
	
	Серия = ДиаграммаПоСотрудникам.Серии.Добавить(НСтр("ru= 'Всего'"));
	Серия.Цвет = Новый Цвет(195,228,241);
	
	Серия = ДиаграммаПоСотрудникам.Серии.Добавить(НСтр("ru= 'Изменений'"));
	Серия.Цвет = Новый Цвет(132,115,215);
		
	Для Каждого Строка Из ТаблицаРезультатаЗапроса Цикл
			
		Точка = ДиаграммаПоСотрудникам.Точки.Добавить(НСтр("ru= 'Записей'"));
		Точка.Текст = Строка.Сотрудник;
		Точка.Значение = Строка.Сотрудник;
		
		НомерСерии = 0;
		Для Каждого Серия Из ДиаграммаПоСотрудникам.Серии Цикл
			
			Если НомерСерии = 0 Тогда
				ЗначениеТочки = Строка.ЧислоЗаписей;
			Иначе
				ЗначениеТочки = Строка.ЧислоИзменений;
			КонецЕсли;	
			
			ДиаграммаПоСотрудникам.УстановитьЗначение(Точка, Серия, ЗначениеТочки);
			
			НомерСерии = НомерСерии + 1;
			
		КонецЦикла;
		
	КонецЦикла;
	
	ДиаграммаПоСотрудникам.ТипДиаграммы = ТипДиаграммы.ГистограммаГоризонтальная;
	ДиаграммаПоСотрудникам.ПоложениеПодписей = ПоложениеПодписейКДиаграмме.КрайВнутри;
	ДиаграммаПоСотрудникам.ОтображатьЛегенду = Истина;
	ДиаграммаПоСотрудникам.ОбластьЛегенды.Расположение = РасположениеЛегендыДиаграммы.Верх;
	
	ДиаграммаПоСотрудникам.ВидПодписей = ВидПодписейКДиаграмме.Значение;
	
	ДиаграммаПоСотрудникам.Обновление = Истина;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьПротокол_ПоСобытиям()
	
	УстановитьПривилегированныйРежим(Истина);
	
	ВыделенныеДаты = Элементы.Дата.ВыделенныеДаты;
	Если ВыделенныеДаты.Количество() = 0 Тогда
		ВыделенныеДаты.Добавить(Дата);
	КонецЕсли;	
	
	МинДата = Неопределено;
	МаксДата = Неопределено;
	Для Каждого СтрДата Из ВыделенныеДаты Цикл
		
		Если МинДата = Неопределено Тогда
			МинДата = СтрДата;
		Иначе	
			МинДата = Мин(МинДата, СтрДата);
		КонецЕсли;	
		
		Если МаксДата = Неопределено Тогда
			МаксДата = СтрДата;
		Иначе	
			МаксДата = Макс(МаксДата, СтрДата);
		КонецЕсли;	
		
	КонецЦикла;	

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПротоколРаботыСотрудников.ТипСобытия КАК ТипСобытия,
		|	КОЛИЧЕСТВО(*) КАК ЧислоЗаписей
		|ИЗ
		|	РегистрСведений.ПротоколРаботыСотрудников КАК ПротоколРаботыСотрудников
		|ГДЕ
		|	ПротоколРаботыСотрудников.Дата >= &МинДата
		|	И ПротоколРаботыСотрудников.Дата <= &МаксДата";
	
	Если ТипЗнч(ВыбранныйСотрудникИлиОтдел) = Тип("СправочникСсылка.Сотрудники") Тогда
		
		Запрос.Текст = Запрос.Текст + Символы.ВК 
			+ " И ПротоколРаботыСотрудников.Сотрудник = &Сотрудник";
		
		Запрос.УстановитьПараметр("Сотрудник", ВыбранныйСотрудникИлиОтдел);
		
	ИначеЕсли ТипЗнч(ВыбранныйСотрудникИлиОтдел) = Тип("СправочникСсылка.СтруктураПредприятия") Тогда	
		
		Запрос.Текст = Запрос.Текст + Символы.ВК 
			+ " И ПротоколРаботыСотрудников.Сотрудник В (&Сотрудники)";
			
		МассивСотрудников = Сотрудники.СотрудникиПодразделения(
			ВыбранныйСотрудникИлиОтдел, Истина, Истина); //  с иерархией
		Запрос.УстановитьПараметр("Сотрудники", МассивСотрудников);
		
	КонецЕсли;	
	
	Запрос.Текст = Запрос.Текст + Символы.ВК + " СГРУППИРОВАТЬ ПО
		|	ПротоколРаботыСотрудников.ТипСобытия";
	
	Запрос.Текст = Запрос.Текст + Символы.ВК + " УПОРЯДОЧИТЬ ПО ЧислоЗаписей УБЫВ";
	
	Запрос.УстановитьПараметр("МинДата", НачалоДня(МинДата));
	Запрос.УстановитьПараметр("МаксДата", КонецДня(МаксДата));
	
	ТаблицаРезультатаЗапроса = Запрос.Выполнить().Выгрузить();
	
	// Заполняем гистограмму
	ДиаграммаПоСобытиям.Обновление = Ложь;
	
	ДиаграммаПоСобытиям.АвтоМаксимальноеЗначение	= Ложь;
	ДиаграммаПоСобытиям.АвтоМинимальноеЗначение	= Ложь;
	ДиаграммаПоСобытиям.МаксимальноеЗначение		= 1;
	ДиаграммаПоСобытиям.МинимальноеЗначение		= 0;
	ДиаграммаПоСобытиям.БазовоеЗначение			= 0;
	ДиаграммаПоСобытиям.ПропускатьБазовоеЗначение	= Истина;
	
	ДиаграммаПоСобытиям.Очистить();
	
	Серия = ДиаграммаПоСобытиям.Серии.Добавить(НСтр("ru= 'Активность пользователей'"));
	Серия.Текст = НСтр("ru= 'По событиям'");
	Серия.Цвет = Новый Цвет(148,194,161);
		
	Для Каждого Строка Из ТаблицаРезультатаЗапроса Цикл
		
		Точка = ДиаграммаПоСобытиям.Точки.Добавить(НСтр("ru= 'Записей'"));
		ЗначениеТочки = Строка.ЧислоЗаписей;
		Точка.Текст = Строка.ТипСобытия;
		Точка.Значение = Строка.ТипСобытия;
		Для Каждого Серия Из ДиаграммаПоСобытиям.Серии Цикл
			ДиаграммаПоСобытиям.УстановитьЗначение(Точка, Серия, ЗначениеТочки);
		КонецЦикла;
		
	КонецЦикла;
	
	ДиаграммаПоСобытиям.ТипДиаграммы = ТипДиаграммы.ГистограммаГоризонтальная;
	ДиаграммаПоСобытиям.ПоложениеПодписей = ПоложениеПодписейКДиаграмме.КрайВнутри;
	ДиаграммаПоСобытиям.ОтображатьЛегенду = Ложь;
	
	ДиаграммаПоСобытиям.ВидПодписей = ВидПодписейКДиаграмме.Значение;
	
	ДиаграммаПоСобытиям.Обновление = Истина;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьПротокол_ПоТипамДанных()
	
	УстановитьПривилегированныйРежим(Истина);
	
	ВыделенныеДаты = Элементы.Дата.ВыделенныеДаты;
	Если ВыделенныеДаты.Количество() = 0 Тогда
		ВыделенныеДаты.Добавить(Дата);
	КонецЕсли;	
	
	МинДата = Неопределено;
	МаксДата = Неопределено;
	Для Каждого СтрДата Из ВыделенныеДаты Цикл
		
		Если МинДата = Неопределено Тогда
			МинДата = СтрДата;
		Иначе	
			МинДата = Мин(МинДата, СтрДата);
		КонецЕсли;	
		
		Если МаксДата = Неопределено Тогда
			МаксДата = СтрДата;
		Иначе	
			МаксДата = Макс(МаксДата, СтрДата);
		КонецЕсли;	
		
	КонецЦикла;	

	Запрос = Новый Запрос;
	
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ТИПЗНАЧЕНИЯ(ПротоколРаботыСотрудников.ОбъектДанных) КАК ТипДанных,
		|	КОЛИЧЕСТВО(*) КАК ЧислоЗаписей
		|ИЗ
		|	РегистрСведений.ПротоколРаботыСотрудников КАК ПротоколРаботыСотрудников
		|ГДЕ
		|	ПротоколРаботыСотрудников.Дата >= &МинДата
		|	И ПротоколРаботыСотрудников.Дата <= &МаксДата
		|   И НЕ ПротоколРаботыСотрудников.ОбъектДанных ССЫЛКА Справочник.Сотрудники";
	
	Если ТипЗнч(ВыбранныйСотрудникИлиОтдел) = Тип("СправочникСсылка.Сотрудники") Тогда
		
		Запрос.Текст = Запрос.Текст + Символы.ВК 
			+ " И ПротоколРаботыСотрудников.Сотрудник = &Сотрудник";
		
		Запрос.УстановитьПараметр("Сотрудник", ВыбранныйСотрудникИлиОтдел);
		
	ИначеЕсли ТипЗнч(ВыбранныйСотрудникИлиОтдел) = Тип("СправочникСсылка.СтруктураПредприятия") Тогда	
		
		Запрос.Текст = Запрос.Текст + Символы.ВК 
			+ " И ПротоколРаботыСотрудников.Сотрудник В (&Сотрудники)";
			
		МассивСотрудников = Сотрудники.СотрудникиПодразделения(
			ВыбранныйСотрудникИлиОтдел, Истина, Истина); //  с иерархией
		Запрос.УстановитьПараметр("Сотрудники", МассивСотрудников);
		
	КонецЕсли;	
	
	Запрос.Текст = Запрос.Текст + Символы.ВК + " СГРУППИРОВАТЬ ПО
		|	ТИПЗНАЧЕНИЯ(ПротоколРаботыСотрудников.ОбъектДанных)";
	
	Запрос.Текст = Запрос.Текст + Символы.ВК + " УПОРЯДОЧИТЬ ПО ЧислоЗаписей УБЫВ";
	
	Запрос.УстановитьПараметр("МинДата", НачалоДня(МинДата));
	Запрос.УстановитьПараметр("МаксДата", КонецДня(МаксДата));
	
	ТаблицаРезультатаЗапроса = Запрос.Выполнить().Выгрузить();
	
	// Заполняем гистограмму
	ДиаграммаПоТипамДанных.Обновление = Ложь;
	
	ДиаграммаПоТипамДанных.АвтоМаксимальноеЗначение	= Ложь;
	ДиаграммаПоТипамДанных.АвтоМинимальноеЗначение	= Ложь;
	ДиаграммаПоТипамДанных.МаксимальноеЗначение		= 1;
	ДиаграммаПоТипамДанных.МинимальноеЗначение		= 0;
	ДиаграммаПоТипамДанных.БазовоеЗначение			= 0;
	ДиаграммаПоТипамДанных.ПропускатьБазовоеЗначение	= Истина;
	
	ДиаграммаПоТипамДанных.Очистить();
	
	Серия = ДиаграммаПоТипамДанных.Серии.Добавить(НСтр("ru= 'Активность пользователей'"));
	Серия.Текст = НСтр("ru= 'По типам данных'");
	Серия.Цвет = Новый Цвет(134,201,226);
		
	Для Каждого Строка Из ТаблицаРезультатаЗапроса Цикл
			
		Точка = ДиаграммаПоТипамДанных.Точки.Добавить(НСтр("ru= 'Записей'"));
		ЗначениеТочки = Строка.ЧислоЗаписей;
		Точка.Текст = Строка.ТипДанных;
		Точка.Значение = Строка.ТипДанных;
		Для Каждого Серия Из ДиаграммаПоТипамДанных.Серии Цикл
			ДиаграммаПоТипамДанных.УстановитьЗначение(Точка, Серия, ЗначениеТочки);
		КонецЦикла;
		
	КонецЦикла;
	
	ДиаграммаПоТипамДанных.ТипДиаграммы = ТипДиаграммы.ГистограммаГоризонтальная;
	ДиаграммаПоТипамДанных.ПоложениеПодписей = ПоложениеПодписейКДиаграмме.КрайВнутри;
	ДиаграммаПоТипамДанных.ОтображатьЛегенду = Ложь;
	
	ДиаграммаПоТипамДанных.ВидПодписей = ВидПодписейКДиаграмме.Значение;
	
	ДиаграммаПоТипамДанных.Обновление = Истина;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьПротокол_Динамика()
	
	УстановитьПривилегированныйРежим(Истина);
	
	МассивСотрудников = Новый Массив;
	
	ВыделенныеДаты = Элементы.Дата.ВыделенныеДаты;
	Если ВыделенныеДаты.Количество() = 0 Тогда
		ВыделенныеДаты.Добавить(Дата);
	КонецЕсли;	
	
	МинДата = Неопределено;
	МаксДата = Неопределено;
	Для Каждого СтрДата Из ВыделенныеДаты Цикл
		
		Если МинДата = Неопределено Тогда
			МинДата = СтрДата;
		Иначе	
			МинДата = Мин(МинДата, СтрДата);
		КонецЕсли;	
		
		Если МаксДата = Неопределено Тогда
			МаксДата = СтрДата;
		Иначе	
			МаксДата = Макс(МаксДата, СтрДата);
		КонецЕсли;	
		
	КонецЦикла;	
	
	ТаблицаДат = Новый ТаблицаЗначений;
	ТаблицаДат.Колонки.Добавить("День", Новый ОписаниеТипов("Число")); // просто порядковый номер
	ТаблицаДат.Колонки.Добавить("Дата", 
		Новый ОписаниеТипов("Дата",,,,,Новый КвалификаторыДаты(ЧастиДаты.ДатаВремя)));
		
	ЧислоСтолбиков = 24;
	РазмерШага = 3600; // Время в секундах
	
	РежимОдинДень = Ложь;
	
	Если МинДата = МаксДата Тогда // 1 день  - по часам
		
		РежимОдинДень = Истина;
		
		ЧислоСтолбиков = 24;
		РазмерШага = 3600; // Время в секундах
		НачалоВремени = НачалоДня(МинДата);
			
		Для Шаг = 0 По ЧислоСтолбиков - 1 Цикл
			
			Строка = ТаблицаДат.Добавить();
			Строка.День = Шаг;
			Строка.Дата = НачалоВремени + Шаг * РазмерШага;
			
		КонецЦикла;
			
	Иначе  // несколько дней - по дням.
		
		НачалоВремени = НачалоДня(МинДата);
		
		ЧислоСтолбиков = ВыделенныеДаты.Количество(); // т.к. режим - Диапазон
		РазмерШага = 86400; // Время в секундах
			
		Для Шаг = 0 По ЧислоСтолбиков - 1 Цикл
			
			Строка = ТаблицаДат.Добавить();
			Строка.День = Шаг;
			Строка.Дата = НачалоВремени + Шаг * РазмерШага;
			
		КонецЦикла;
		
	КонецЕсли;	
	
	// Заполняем гистограмму
	ДиаграммаДинамика.Обновление = Ложь;
	
	ДиаграммаДинамика.РежимСглаживания = РежимСглаживанияДиаграммы.ГладкаяКривая;
	
	ДиаграммаДинамика.АвтоМаксимальноеЗначение	= Ложь;
	ДиаграммаДинамика.АвтоМинимальноеЗначение	= Ложь;
	ДиаграммаДинамика.МаксимальноеЗначение		= 1;
	ДиаграммаДинамика.МинимальноеЗначение		= 0;
	ДиаграммаДинамика.БазовоеЗначение			= 0;
	ДиаграммаДинамика.ПропускатьБазовоеЗначение	= Истина;
	
	ДиаграммаДинамика.Очистить();
	
	ДиаграммаДинамика.ТипДиаграммы = ТипДиаграммы.График;
	ДиаграммаДинамика.ПоложениеПодписей = ПоложениеПодписейКДиаграмме.Авто; // КрайВнутри
	ДиаграммаДинамика.ОтображатьЛегенду = Истина;
	ДиаграммаДинамика.ВидПодписей = ВидПодписейКДиаграмме.Нет;
	
	ДиаграммаДинамика.ОбластьЛегенды.Расположение = РасположениеЛегендыДиаграммы.Верх;
	
	ДиаграммаДинамика.Обновление = Истина;
	
	Запрос = Новый Запрос;
	
	Если ТипЗнч(ВыбранныйСотрудникИлиОтдел) = Тип("СправочникСсылка.Сотрудники") Тогда
		
		СформироватьЗапросПоСотруднику(Запрос, ВыбранныйСотрудникИлиОтдел, РежимОдинДень, МассивСотрудников);
		
	ИначеЕсли ТипЗнч(ВыбранныйСотрудникИлиОтдел) = Тип("СправочникСсылка.СтруктураПредприятия") Тогда	
		
		СформироватьЗапросПоОтделу(Запрос, ВыбранныйСотрудникИлиОтдел, РежимОдинДень, МассивСотрудников);
		
	КонецЕсли;	
	
	Запрос.УстановитьПараметр("ТаблицаДат", ТаблицаДат);
	Запрос.УстановитьПараметр("МинДата", НачалоДня(МинДата));
	Запрос.УстановитьПараметр("МаксДата", КонецДня(МаксДата));
	
	ТаблицаРезультатаЗапроса = Запрос.Выполнить().Выгрузить();
	
	// теперь считаем медианы по отделу.
	
	Если ТипЗнч(ВыбранныйСотрудникИлиОтдел) = Тип("СправочникСсылка.Сотрудники") Тогда	
		
		СформироватьЗапросПоСотрудникамДляМедиан(
			Запрос, ВыбранныйСотрудникИлиОтдел, РежимОдинДень, МассивСотрудников);
		
		Запрос.УстановитьПараметр("ТаблицаДат", ТаблицаДат);
		Запрос.УстановитьПараметр("МинДата", НачалоДня(МинДата));
		Запрос.УстановитьПараметр("МаксДата", КонецДня(МаксДата));
		
		ТаблицаРезультатаДляМедиан = Запрос.Выполнить().Выгрузить();
		
		ТаблицаДат.Колонки.Добавить("МедианаВсего", Новый ОписаниеТипов("Число"));
		ТаблицаДат.Колонки.Добавить("МедианаИзменений", Новый ОписаниеТипов("Число"));
		
		ТаблицаРезультатаДляМедиан.Сортировать("ВсегоСотрудник");
		Для Каждого Стр Из ТаблицаДат Цикл
			
			Отбор = Новый Структура;
			Отбор.Вставить("День", Стр.День);
			Отбор.Вставить("ТипСерии", "ВсегоСотрудник");
			
			СтрокиДня = ТаблицаРезультатаДляМедиан.НайтиСтроки(Отбор);
			
			// найдем медиану Всего
			Медиана = 0;
			Если СтрокиДня.Количество() <> 0 Тогда
				Если СтрокиДня.Количество() % 2 = 1 Тогда // нечетное
					
					Позиция = (СтрокиДня.Количество() - 1) / 2;
					Медиана = СтрокиДня[Позиция].ВсегоСотрудник;
					
				Иначе	
					
					Позиция1 = СтрокиДня.Количество() / 2 - 1;
					Позиция2 = СтрокиДня.Количество() / 2;
					Медиана = (СтрокиДня[Позиция1].ВсегоСотрудник + СтрокиДня[Позиция2].ВсегоСотрудник) / 2;
					
				КонецЕсли;	
			КонецЕсли;	
			
			Стр.МедианаВсего = Медиана;
			
		КонецЦикла;	
		
		ТаблицаРезультатаДляМедиан.Сортировать("ИзмененийСотрудник");
		Для Каждого Стр Из ТаблицаДат Цикл
			
			Отбор = Новый Структура;
			Отбор.Вставить("День", Стр.День);
			Отбор.Вставить("ТипСерии", "ИзмененийСотрудник");
			
			СтрокиДня = ТаблицаРезультатаДляМедиан.НайтиСтроки(Отбор);
			
			// найдем медиану Изменений
			Медиана = 0;
			Если СтрокиДня.Количество() <> 0 Тогда
				Если СтрокиДня.Количество() % 2 = 1 Тогда // нечетное
					
					Позиция = (СтрокиДня.Количество() - 1) / 2;
					Медиана = СтрокиДня[Позиция].ИзмененийСотрудник;
					
				Иначе	
					
					Позиция1 = СтрокиДня.Количество() / 2 - 1;
					Позиция2 = СтрокиДня.Количество() / 2;
					Медиана = (СтрокиДня[Позиция1].ИзмененийСотрудник + СтрокиДня[Позиция2].ИзмененийСотрудник) / 2;
					
				КонецЕсли;	
			КонецЕсли;	
			
			Стр.МедианаИзменений = Медиана;
			
		КонецЦикла;	
		
	КонецЕсли;	
	
	// завершили медианы
	
	ДиаграммаДинамика.Серии.Очистить();

	Если ТипЗнч(ВыбранныйСотрудникИлиОтдел) = Тип("СправочникСсылка.Сотрудники") Тогда
		
		Серия = ДиаграммаДинамика.Серии.Добавить(НСтр("ru = 'Всего по отделу'"));
		Серия.Линия = Новый Линия(ТипЛинииДиаграммы.Пунктир, 1);
		Серия.Цвет = WebЦвета.Коричневый;

		Серия = ДиаграммаДинамика.Серии.Добавить(НСтр("ru = 'Изменений по отделу'"));
		Серия.Линия = Новый Линия(ТипЛинииДиаграммы.Пунктир, 1);
		Серия.Цвет = WebЦвета.Зеленый;

		Серия = ДиаграммаДинамика.Серии.Добавить(НСтр("ru = 'Всего по сотруднику'"));
		Серия.Линия = Новый Линия(ТипЛинииДиаграммы.Сплошная, 3);
		Серия.Цвет = WebЦвета.Коричневый;
		
		Серия = ДиаграммаДинамика.Серии.Добавить(НСтр("ru = 'Изменений по сотруднику'"));
		Серия.Линия = Новый Линия(ТипЛинииДиаграммы.Сплошная, 3);
		Серия.Цвет = WebЦвета.Зеленый;
		
	ИначеЕсли ТипЗнч(ВыбранныйСотрудникИлиОтдел) = Тип("СправочникСсылка.СтруктураПредприятия") Тогда	
		
		Серия = ДиаграммаДинамика.Серии.Добавить(НСтр("ru = 'Всего по отделу'"));
		Серия.Цвет = WebЦвета.Коричневый;
		
		Серия = ДиаграммаДинамика.Серии.Добавить(НСтр("ru = 'Изменений по отделу'"));
		Серия.Цвет = WebЦвета.Зеленый;
		
	КонецЕсли;	
	
	КоличествоСотрудников = МассивСотрудников.Количество();
	Если КоличествоСотрудников = 0 Тогда
		КоличествоСотрудников = 1;
	КонецЕсли;	
	
	ТекДень = -1;
	Точка = Неопределено; // это по оси X
	Для Каждого Строка Из ТаблицаРезультатаЗапроса Цикл
		
		СтрокаДат = ТаблицаДат.Найти(Строка.День, "День");
		
		Если ТекДень <> Строка.День Тогда   // один раз для этого дня
			
			Точка = ДиаграммаДинамика.Точки.Добавить(Строка.День);
			Точка.Значение = Строка.День;
			Если РежимОдинДень Тогда			
				Точка.Текст = СтрШаблон("%1ч", Формат(Строка.День, "ЧЦ=2; ЧН=0; ЧВН="));
			Иначе
				ТекДатаТочки = НачалоВремени + Строка.День * РазмерШага;
				Точка.Текст = Формат(ТекДатаТочки, "ДФ='дд МММ'");
			КонецЕсли;	
			
			Если ТипЗнч(ВыбранныйСотрудникИлиОтдел) = Тип("СправочникСсылка.Сотрудники") Тогда
				
				НомерСерии = 0;

				ЗначениеТочки = 0;
				Если СтрокаДат <> Неопределено Тогда
					ЗначениеТочки = СтрокаДат.МедианаВсего; //  медиана по отделу 
				КонецЕсли;
				
				Серия = ДиаграммаДинамика.Серии[НомерСерии];
				
				Если ЗначениеТочки = 0 Тогда
					ЗначениеТочки = 0.1; // чтобы линия не прекращалась
				КонецЕсли;	
				
				ДиаграммаДинамика.УстановитьЗначение(Точка, Серия, ЗначениеТочки);
				
				
				НомерСерии = 1;

				ЗначениеТочки = 0;
				Если СтрокаДат <> Неопределено Тогда
					ЗначениеТочки = СтрокаДат.МедианаИзменений; //  медиана по отделу
				КонецЕсли;
				
				Серия = ДиаграммаДинамика.Серии[НомерСерии];
				
				Если ЗначениеТочки = 0 Тогда
					ЗначениеТочки = 0.1; // чтобы линия не прекращалась
				КонецЕсли;	
				
				ДиаграммаДинамика.УстановитьЗначение(Точка, Серия, ЗначениеТочки);
				
			КонецЕсли;		
			
		КонецЕсли;	
		ТекДень = Строка.День;
		
		ЗначениеТочки = 0;
		Если Строка.ТипСерии = "ВсегоОтдел" Тогда
			
			НомерСерии = 0;
			
			Если ТипЗнч(ВыбранныйСотрудникИлиОтдел) = Тип("СправочникСсылка.Сотрудники") Тогда
			Иначе
				ЗначениеТочки = Строка.ВсегоОтдел;
			КонецЕсли;	
			
		ИначеЕсли Строка.ТипСерии = "ИзмененийОтдел" Тогда
			
			НомерСерии = 1;
			Если ТипЗнч(ВыбранныйСотрудникИлиОтдел) = Тип("СправочникСсылка.Сотрудники") Тогда
			Иначе
				ЗначениеТочки = Строка.ИзмененийОтдел;
			КонецЕсли;	
			
		ИначеЕсли Строка.ТипСерии = "ВсегоСотрудник" Тогда
			НомерСерии = 2;
			ЗначениеТочки = Строка.ВсегоСотрудник;
		ИначеЕсли Строка.ТипСерии = "ИзмененийСотрудник" Тогда	
			НомерСерии = 3;
			ЗначениеТочки = Строка.ИзмененийСотрудник;
		КонецЕсли;	
		
		Серия = ДиаграммаДинамика.Серии[НомерСерии];
		
		Если ЗначениеТочки = 0 Тогда
			ЗначениеТочки = 0.1; // чтобы линия не прекращалась
		КонецЕсли;	
		
		ДиаграммаДинамика.УстановитьЗначение(Точка, Серия, ЗначениеТочки);
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура СформироватьЗапросПоСотруднику(Запрос, 
	ВыбранныйСотрудникИлиОтдел, РежимОдинДень, МассивСотрудников)
	
	Сотрудник = ВыбранныйСотрудникИлиОтдел;
	Отдел = Сотрудники.ПодразделениеСотрудника(Сотрудник); 
		
	// "Всего" + "Изменений"  по отделу  + по сотруднику
	
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ТаблицаДат.День КАК День,
		|	ТаблицаДат.Дата КАК Дата
		|ПОМЕСТИТЬ ВременнаяТаблицаДат
		|ИЗ
		|	&ТаблицаДат КАК ТаблицаДат
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	День,
		|	Дата
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВременнаяТаблицаДат.День КАК День,
		|	0 КАК ВсегоОтдел,
		|	0 КАК ИзмененийОтдел,
		|	КОЛИЧЕСТВО(ПротоколРаботыСотрудников.Дата) КАК ВсегоСотрудник,
		|	0 КАК ИзмененийСотрудник,
		|	""ВсегоСотрудник"" КАК ТипСерии
		|ИЗ
		|	ВременнаяТаблицаДат КАК ВременнаяТаблицаДат
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПротоколРаботыСотрудников КАК ПротоколРаботыСотрудников
		|		ПО (ПротоколРаботыСотрудников.Дата >= &МинДата)
		|		И (ПротоколРаботыСотрудников.Дата <= &МаксДата)
		|		И (НАЧАЛОПЕРИОДА(ПротоколРаботыСотрудников.Дата, ЧАС) = ВременнаяТаблицаДат.Дата)
		|		И (ПротоколРаботыСотрудников.Сотрудник = &Сотрудник)
		|СГРУППИРОВАТЬ ПО
		|	ВременнаяТаблицаДат.День
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВременнаяТаблицаДат.День,
		|	0,
		|	0,
		|	0,
		|	КОЛИЧЕСТВО(ПротоколРаботыСотрудников.Дата),
		|	""ИзмененийСотрудник""
		|ИЗ
		|	ВременнаяТаблицаДат КАК ВременнаяТаблицаДат
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПротоколРаботыСотрудников КАК ПротоколРаботыСотрудников
		|		ПО (ПротоколРаботыСотрудников.Дата >= &МинДата)
		|		И (ПротоколРаботыСотрудников.Дата <= &МаксДата)
		|		И (ПротоколРаботыСотрудников.ТипСобытия = ЗНАЧЕНИЕ(Перечисление.ТипыСобытийПротоколаРаботыСотрудников.Изменение)
		|		ИЛИ
		|			ПротоколРаботыСотрудников.ТипСобытия = ЗНАЧЕНИЕ(Перечисление.ТипыСобытийПротоколаРаботыСотрудников.ИзменениеФайла)
		|		ИЛИ
		|			ПротоколРаботыСотрудников.ТипСобытия = ЗНАЧЕНИЕ(Перечисление.ТипыСобытийПротоколаРаботыСотрудников.ПеремещениеФайлов)
		|		ИЛИ
		|			ПротоколРаботыСотрудников.ТипСобытия = ЗНАЧЕНИЕ(Перечисление.ТипыСобытийПротоколаРаботыСотрудников.ПеренаправлениеЗадачи)
		|		ИЛИ
		|			ПротоколРаботыСотрудников.ТипСобытия = ЗНАЧЕНИЕ(Перечисление.ТипыСобытийПротоколаРаботыСотрудников.ПодписаниеЭП)
		|		ИЛИ
		|			ПротоколРаботыСотрудников.ТипСобытия = ЗНАЧЕНИЕ(Перечисление.ТипыСобытийПротоколаРаботыСотрудников.ПрерываниеБизнесПроцесса)
		|		ИЛИ
		|			ПротоколРаботыСотрудников.ТипСобытия = ЗНАЧЕНИЕ(Перечисление.ТипыСобытийПротоколаРаботыСотрудников.Расшифровывание)
		|		ИЛИ
		|			ПротоколРаботыСотрудников.ТипСобытия = ЗНАЧЕНИЕ(Перечисление.ТипыСобытийПротоколаРаботыСотрудников.РегистрацияДокумента)
		|		ИЛИ ПротоколРаботыСотрудников.ТипСобытия = ЗНАЧЕНИЕ(Перечисление.ТипыСобытийПротоколаРаботыСотрудников.Создание)
		|		ИЛИ
		|			ПротоколРаботыСотрудников.ТипСобытия = ЗНАЧЕНИЕ(Перечисление.ТипыСобытийПротоколаРаботыСотрудников.СтартБизнесПроцесса)
		|		ИЛИ
		|			ПротоколРаботыСотрудников.ТипСобытия = ЗНАЧЕНИЕ(Перечисление.ТипыСобытийПротоколаРаботыСотрудников.УдалениеПодписиЭП)
		|		ИЛИ
		|			ПротоколРаботыСотрудников.ТипСобытия = ЗНАЧЕНИЕ(Перечисление.ТипыСобытийПротоколаРаботыСотрудников.Шифрование))
		|		И (НАЧАЛОПЕРИОДА(ПротоколРаботыСотрудников.Дата, ЧАС) = ВременнаяТаблицаДат.Дата)
		|		И (ПротоколРаботыСотрудников.Сотрудник = &Сотрудник)
		|СГРУППИРОВАТЬ ПО
		|	ВременнаяТаблицаДат.День
		|УПОРЯДОЧИТЬ ПО
		|	ВременнаяТаблицаДат.День";
	
	Если РежимОдинДень = Ложь Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ЧАС", "ДЕНЬ");
	КонецЕсли;	
	
	МассивСотрудников = Сотрудники.СотрудникиПодразделения(Отдел, Истина, Истина); //  с иерархией
	Запрос.УстановитьПараметр("Сотрудники", МассивСотрудников);
	
	Запрос.УстановитьПараметр("Сотрудник", Сотрудник);
	
КонецПроцедуры

&НаСервере
Процедура СформироватьЗапросПоОтделу(Запрос, 
	ВыбранныйСотрудникИлиОтдел, РежимОдинДень, МассивСотрудников)
	
	// "Всего" + "Изменений"  по отделу 
	
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ТаблицаДат.День КАК День,
		|	ТаблицаДат.Дата КАК Дата
		|ПОМЕСТИТЬ ВременнаяТаблицаДат
		|ИЗ
		|	&ТаблицаДат КАК ТаблицаДат
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	День,
		|	Дата
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВременнаяТаблицаДат.День КАК День,
		|	КОЛИЧЕСТВО(ПротоколРаботыСотрудников.Дата) КАК ВсегоОтдел,
		|	0 КАК ИзмененийОтдел,
		|	""ВсегоОтдел"" КАК ТипСерии
		|ИЗ
		|	ВременнаяТаблицаДат КАК ВременнаяТаблицаДат
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПротоколРаботыСотрудников КАК ПротоколРаботыСотрудников
		|		ПО (ПротоколРаботыСотрудников.Дата >= &МинДата)
		|			И (ПротоколРаботыСотрудников.Дата <= &МаксДата)
		|			И (НАЧАЛОПЕРИОДА(ПротоколРаботыСотрудников.Дата, ЧАС) = ВременнаяТаблицаДат.Дата)
		|			И (ПротоколРаботыСотрудников.Сотрудник В (&Сотрудники))
		|
		|СГРУППИРОВАТЬ ПО
		|	ВременнаяТаблицаДат.День
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВременнаяТаблицаДат.День,
		|	0,
		|	КОЛИЧЕСТВО(ПротоколРаботыСотрудников.Дата),
		|	""ИзмененийОтдел""
		|ИЗ
		|	ВременнаяТаблицаДат КАК ВременнаяТаблицаДат
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПротоколРаботыСотрудников КАК ПротоколРаботыСотрудников
		|		ПО (ПротоколРаботыСотрудников.Дата >= &МинДата)
		|			И (ПротоколРаботыСотрудников.Дата <= &МаксДата)
		|			И (ПротоколРаботыСотрудников.ТипСобытия = ЗНАЧЕНИЕ(Перечисление.ТипыСобытийПротоколаРаботыСотрудников.Изменение)
		|				ИЛИ ПротоколРаботыСотрудников.ТипСобытия = ЗНАЧЕНИЕ(Перечисление.ТипыСобытийПротоколаРаботыСотрудников.Изменение)
		|				ИЛИ ПротоколРаботыСотрудников.ТипСобытия = ЗНАЧЕНИЕ(Перечисление.ТипыСобытийПротоколаРаботыСотрудников.ИзменениеФайла)
		|				ИЛИ ПротоколРаботыСотрудников.ТипСобытия = ЗНАЧЕНИЕ(Перечисление.ТипыСобытийПротоколаРаботыСотрудников.ПеремещениеФайлов)
		|				ИЛИ ПротоколРаботыСотрудников.ТипСобытия = ЗНАЧЕНИЕ(Перечисление.ТипыСобытийПротоколаРаботыСотрудников.ПеренаправлениеЗадачи)
		|				ИЛИ ПротоколРаботыСотрудников.ТипСобытия = ЗНАЧЕНИЕ(Перечисление.ТипыСобытийПротоколаРаботыСотрудников.ПодписаниеЭП)
		|				ИЛИ ПротоколРаботыСотрудников.ТипСобытия = ЗНАЧЕНИЕ(Перечисление.ТипыСобытийПротоколаРаботыСотрудников.ПрерываниеБизнесПроцесса)
		|				ИЛИ ПротоколРаботыСотрудников.ТипСобытия = ЗНАЧЕНИЕ(Перечисление.ТипыСобытийПротоколаРаботыСотрудников.Расшифровывание)
		|				ИЛИ ПротоколРаботыСотрудников.ТипСобытия = ЗНАЧЕНИЕ(Перечисление.ТипыСобытийПротоколаРаботыСотрудников.РегистрацияДокумента)
		|				ИЛИ ПротоколРаботыСотрудников.ТипСобытия = ЗНАЧЕНИЕ(Перечисление.ТипыСобытийПротоколаРаботыСотрудников.Создание)
		|				ИЛИ ПротоколРаботыСотрудников.ТипСобытия = ЗНАЧЕНИЕ(Перечисление.ТипыСобытийПротоколаРаботыСотрудников.СтартБизнесПроцесса)
		|				ИЛИ ПротоколРаботыСотрудников.ТипСобытия = ЗНАЧЕНИЕ(Перечисление.ТипыСобытийПротоколаРаботыСотрудников.УдалениеПодписиЭП)
		|				ИЛИ ПротоколРаботыСотрудников.ТипСобытия = ЗНАЧЕНИЕ(Перечисление.ТипыСобытийПротоколаРаботыСотрудников.Шифрование))
		|			И (НАЧАЛОПЕРИОДА(ПротоколРаботыСотрудников.Дата, ЧАС) = ВременнаяТаблицаДат.Дата)
		|			И (ПротоколРаботыСотрудников.Сотрудник В (&Сотрудники))
		|
		|СГРУППИРОВАТЬ ПО
		|	ВременнаяТаблицаДат.День
		|
		|УПОРЯДОЧИТЬ ПО
		|	ВременнаяТаблицаДат.День";
	
	Если РежимОдинДень = Ложь Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ЧАС", "ДЕНЬ");
	КонецЕсли;	
	
	МассивСотрудников = Сотрудники.СотрудникиПодразделения(
		ВыбранныйСотрудникИлиОтдел, Истина, Истина); //  с иерархией
	
	Запрос.УстановитьПараметр("Сотрудники", МассивСотрудников);
	
КонецПроцедуры

&НаСервере
Процедура СформироватьЗапросПоСотрудникамДляМедиан(Запрос, 
	ВыбранныйСотрудникИлиОтдел, РежимОдинДень, МассивСотрудников)
	
	Сотрудник = ВыбранныйСотрудникИлиОтдел;
	Отдел = Сотрудники.ПодразделениеСотрудника(Сотрудник);
	
	// "Всего" + "Изменений"  по отделу  + по сотруднику
	
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ТаблицаДат.День КАК День,
		|	ТаблицаДат.Дата КАК Дата
		|ПОМЕСТИТЬ ВременнаяТаблицаДат
		|ИЗ
		|	&ТаблицаДат КАК ТаблицаДат
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	День,
		|	Дата
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВременнаяТаблицаДат.День КАК День,
		|	ПротоколРаботыСотрудников.Сотрудник КАК Сотрудник,
		|	0 КАК ВсегоОтдел,
		|	0 КАК ИзмененийОтдел,
		|	КОЛИЧЕСТВО(ПротоколРаботыСотрудников.Дата) КАК ВсегоСотрудник,
		|	0 КАК ИзмененийСотрудник,
		|	""ВсегоСотрудник"" КАК ТипСерии
		|ИЗ
		|	ВременнаяТаблицаДат КАК ВременнаяТаблицаДат
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПротоколРаботыСотрудников КАК ПротоколРаботыСотрудников
		|		ПО (ПротоколРаботыСотрудников.Дата >= &МинДата)
		|			И (ПротоколРаботыСотрудников.Дата <= &МаксДата)
		|			И (НАЧАЛОПЕРИОДА(ПротоколРаботыСотрудников.Дата, ЧАС) = ВременнаяТаблицаДат.Дата)
		|			И (ПротоколРаботыСотрудников.Сотрудник В (&Сотрудники))
		|
		|СГРУППИРОВАТЬ ПО
		|	ВременнаяТаблицаДат.День,
		|	ПротоколРаботыСотрудников.Сотрудник
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВременнаяТаблицаДат.День,
		|	ПротоколРаботыСотрудников.Сотрудник,
		|	0,
		|	0,
		|	0,
		|	КОЛИЧЕСТВО(ПротоколРаботыСотрудников.Дата),
		|	""ИзмененийСотрудник""
		|ИЗ
		|	ВременнаяТаблицаДат КАК ВременнаяТаблицаДат
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПротоколРаботыСотрудников КАК ПротоколРаботыСотрудников
		|		ПО (ПротоколРаботыСотрудников.Дата >= &МинДата)
		|			И (ПротоколРаботыСотрудников.Дата <= &МаксДата)
		|			И (ПротоколРаботыСотрудников.ТипСобытия = ЗНАЧЕНИЕ(Перечисление.ТипыСобытийПротоколаРаботыСотрудников.Изменение)
		|				ИЛИ ПротоколРаботыСотрудников.ТипСобытия = ЗНАЧЕНИЕ(Перечисление.ТипыСобытийПротоколаРаботыСотрудников.ИзменениеФайла)
		|				ИЛИ ПротоколРаботыСотрудников.ТипСобытия = ЗНАЧЕНИЕ(Перечисление.ТипыСобытийПротоколаРаботыСотрудников.ПеремещениеФайлов)
		|				ИЛИ ПротоколРаботыСотрудников.ТипСобытия = ЗНАЧЕНИЕ(Перечисление.ТипыСобытийПротоколаРаботыСотрудников.ПеренаправлениеЗадачи)
		|				ИЛИ ПротоколРаботыСотрудников.ТипСобытия = ЗНАЧЕНИЕ(Перечисление.ТипыСобытийПротоколаРаботыСотрудников.ПодписаниеЭП)
		|				ИЛИ ПротоколРаботыСотрудников.ТипСобытия = ЗНАЧЕНИЕ(Перечисление.ТипыСобытийПротоколаРаботыСотрудников.ПрерываниеБизнесПроцесса)
		|				ИЛИ ПротоколРаботыСотрудников.ТипСобытия = ЗНАЧЕНИЕ(Перечисление.ТипыСобытийПротоколаРаботыСотрудников.Расшифровывание)
		|				ИЛИ ПротоколРаботыСотрудников.ТипСобытия = ЗНАЧЕНИЕ(Перечисление.ТипыСобытийПротоколаРаботыСотрудников.РегистрацияДокумента)
		|				ИЛИ ПротоколРаботыСотрудников.ТипСобытия = ЗНАЧЕНИЕ(Перечисление.ТипыСобытийПротоколаРаботыСотрудников.Создание)
		|				ИЛИ ПротоколРаботыСотрудников.ТипСобытия = ЗНАЧЕНИЕ(Перечисление.ТипыСобытийПротоколаРаботыСотрудников.СтартБизнесПроцесса)
		|				ИЛИ ПротоколРаботыСотрудников.ТипСобытия = ЗНАЧЕНИЕ(Перечисление.ТипыСобытийПротоколаРаботыСотрудников.УдалениеПодписиЭП)
		|				ИЛИ ПротоколРаботыСотрудников.ТипСобытия = ЗНАЧЕНИЕ(Перечисление.ТипыСобытийПротоколаРаботыСотрудников.Шифрование))
		|			И (НАЧАЛОПЕРИОДА(ПротоколРаботыСотрудников.Дата, ЧАС) = ВременнаяТаблицаДат.Дата)
		|			И (ПротоколРаботыСотрудников.Сотрудник В (&Сотрудники))
		|
		|СГРУППИРОВАТЬ ПО
		|	ВременнаяТаблицаДат.День,
		|	ПротоколРаботыСотрудников.Сотрудник
		|
		|УПОРЯДОЧИТЬ ПО
		|	ВременнаяТаблицаДат.День";
	
	Если РежимОдинДень = Ложь Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ЧАС", "ДЕНЬ");
	КонецЕсли;	
	
	МассивСотрудников = Сотрудники.СотрудникиПодразделения(Отдел, Истина, Истина); //  с иерархией
	Запрос.УстановитьПараметр("Сотрудники", МассивСотрудников);
	
	Запрос.УстановитьПараметр("Сотрудник", Сотрудник);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьПротокол()
	
	Если Элементы.ДеревоСотрудников.ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;	
	
	ПредыдущийСотрудникИлиОтдел = ВыбранныйСотрудникИлиОтдел;
	ВыбранныйСотрудникИлиОтдел = Элементы.ДеревоСотрудников.ТекущиеДанные.Ссылка;
	
	Если ПредыдущийСотрудникИлиОтдел = ВыбранныйСотрудникИлиОтдел Тогда
		Возврат;
	КонецЕсли;	
	
	ВсегоДействий = 0;
	Первое = Неопределено;
	Последнее = Неопределено;

	Если ЗначениеЗаполнено(ВыбранныйСотрудникИлиОтдел) Тогда
		ОбновитьПротоколСервер();	
	КонецЕсли;	
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция НовоеОбсуждение(СотрудникСсылка)
	
	Если Не ОбсужденияСлужебныйВызовСервера.Подключены() Тогда
		Если ПравоДоступа("РегистрацияИнформационнойБазыСистемыВзаимодействия", Метаданные) Тогда 
			Возврат "НеПодключеноВозможноПодключить";
		Иначе 
			Возврат "НеПодключеноНетПравНаПодключение";
		КонецЕсли;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	ПользовательСотрудника = Сотрудники.ПользовательСотрудника(СотрудникСсылка);
	Если Не ЗначениеЗаполнено(ПользовательСотрудника) Тогда
		Возврат "Недоступно";
	КонецЕсли;
	
	ИдентификаторПользователяИнформационнойБазы = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(
		ПользовательСотрудника, "ИдентификаторПользователяИБ");
	УстановитьПривилегированныйРежим(Ложь);
	
	Если Не ЗначениеЗаполнено(ИдентификаторПользователяИнформационнойБазы) Тогда
		Возврат "Недоступно";
	КонецЕсли;
	
	Попытка
		ИдентификаторПользователяСВ = СистемаВзаимодействия.ПолучитьИдентификаторПользователя(
			ИдентификаторПользователяИнформационнойБазы);
	Исключение
		ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Для начала обсуждения необходимо, чтобы Сотрудник %1
			           |хотя бы один раз запустил программу.'"),
			СотрудникСсылка);
	КонецПопытки;
	
	Если ИдентификаторПользователяСВ = СистемаВзаимодействия.ИдентификаторТекущегоПользователя() Тогда 
		ВызватьИсключение НСтр("ru = 'Для начала обсуждения выберите другого сотрудника.'");
	КонецЕсли;
	
	Обсуждение = СистемаВзаимодействия.СоздатьОбсуждение();
	Обсуждение.Групповое = Ложь;
	Обсуждение.Участники.Добавить(СистемаВзаимодействия.ИдентификаторТекущегоПользователя());
	Обсуждение.Участники.Добавить(ИдентификаторПользователяСВ);
	Обсуждение.Записать();
	
	Возврат ПолучитьНавигационнуюСсылку(Обсуждение.Идентификатор);
	
КонецФункции

&НаСервереБезКонтекста
Функция АвтообновляемоеОбсуждение(КонтейнерСсылка)
	
	Обсуждение = ОбсужденияДокументооборот.НавигационнаяСсылкаОбсужденияПоКонтейнеру(
		КонтейнерСсылка, Истина);
	Возврат Обсуждение;
	
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьСотрудниковОтдела(Отдел)
	
	МассивСотрудников = Сотрудники.СотрудникиПодразделения(Отдел, Истина, Истина); //  с иерархией  
	Возврат МассивСотрудников;	
		
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьЕжедневныеОтчетыТекущейДаты(Дата, Сотрудник)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ЕжедневныйОтчет.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.ЕжедневныйОтчет КАК ЕжедневныйОтчет
	|ГДЕ
	|	ЕжедневныйОтчет.Сотрудник = &Сотрудник
	|	И НАЧАЛОПЕРИОДА(ЕжедневныйОтчет.Дата, ДЕНЬ) = НАЧАЛОПЕРИОДА(&Дата, ДЕНЬ)
	|	И НЕ ЕжедневныйОтчет.ПометкаУдаления";
	
	Запрос.УстановитьПараметр("Дата", Дата);
	Запрос.УстановитьПараметр("Сотрудник", Сотрудник);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда 
		Возврат Выборка.Ссылка;
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

&НаСервере
Процедура ОбновитьПротоколТолькоСписок(ОбновитьПоКоманде = Ложь)
	
	Если Не ЗначениеЗаполнено(ВыбранныйСотрудникИлиОтдел) Тогда
		Возврат;
	КонецЕсли;	
	
	Элементы.ЗаполнитьСписок.Видимость = Ложь;
	
	УстановитьПривилегированныйРежим(Истина);
	
	ВыделенныеДаты = Элементы.Дата.ВыделенныеДаты;
	Если ВыделенныеДаты.Количество() = 0 Тогда
		ВыделенныеДаты.Добавить(Дата);
	КонецЕсли;	
	
	МинДата = Неопределено;
	МаксДата = Неопределено;
	Для Каждого СтрДата Из ВыделенныеДаты Цикл
		
		Если МинДата = Неопределено Тогда
			МинДата = СтрДата;
		Иначе	
			МинДата = Мин(МинДата, СтрДата);
		КонецЕсли;	
		
		Если МаксДата = Неопределено Тогда
			МаксДата = СтрДата;
		Иначе	
			МаксДата = Макс(МаксДата, СтрДата);
		КонецЕсли;	
		
	КонецЦикла;	

	ТаблицаПротокол = РеквизитФормыВЗначение("ПротоколРаботы");
	ТаблицаПротокол.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПротоколРаботыСотрудников.Дата КАК Дата,
		|	ПротоколРаботыСотрудников.ОбъектДанных КАК ОбъектДанных,
		|	ПРЕДСТАВЛЕНИЕ(ПротоколРаботыСотрудников.ОбъектДанных) КАК ОбъектДанныхПредставление,
		|	ПротоколРаботыСотрудников.Сотрудник КАК Сотрудник,
		|	ПротоколРаботыСотрудников.ТипСобытия КАК ТипСобытия,
		|	ПротоколРаботыСотрудников.Длительность КАК Длительность,
		|	ПротоколРаботыСотрудников.ДополнительныеСведения КАК ДополнительныеСведения,
		|	ПротоколРаботыСотрудников.ОписаниеСобытия КАК ОписаниеСобытия,
		|	ПротоколРаботыСотрудников.ИмяКомпьютера КАК ИмяКомпьютера
		|ИЗ
		|	РегистрСведений.ПротоколРаботыСотрудников КАК ПротоколРаботыСотрудников
		|ГДЕ
		|	ПротоколРаботыСотрудников.Дата >= &МинДата
		|	И ПротоколРаботыСотрудников.Дата <= &МаксДата";
	
	Если ТипЗнч(ВыбранныйСотрудникИлиОтдел) = Тип("СправочникСсылка.Сотрудники") Тогда
		
		Запрос.Текст = Запрос.Текст + Символы.ВК 
			+ " И ПротоколРаботыСотрудников.Сотрудник = &Сотрудник";
		
		Запрос.УстановитьПараметр("Сотрудник", ВыбранныйСотрудникИлиОтдел);
		
	ИначеЕсли ТипЗнч(ВыбранныйСотрудникИлиОтдел) = Тип("СправочникСсылка.СтруктураПредприятия") Тогда	
		
		Запрос.Текст = Запрос.Текст + Символы.ВК 
			+ " И ПротоколРаботыСотрудников.Сотрудник В (&Сотрудники)";
			
		МассивСотрудников = Сотрудники.СотрудникиПодразделения(
			ВыбранныйСотрудникИлиОтдел, Истина, Истина); //  с иерархией
		Запрос.УстановитьПараметр("Сотрудники", МассивСотрудников);
		
	КонецЕсли;	
	
	Если ЗначениеЗаполнено(Событие) Тогда
		
		Запрос.Текст = Запрос.Текст + Символы.ВК 
			+ " И ПротоколРаботыСотрудников.ТипСобытия = &Событие";
				
		Запрос.УстановитьПараметр("Событие", Событие);
		
	КонецЕсли;	
	
	Если ЗначениеЗаполнено(ТипДанных) Тогда
		
		Если ТипДанных = "Документы предприятия" Тогда
		
			Запрос.Текст = Запрос.Текст + Символы.ВК 
				+ " И ПротоколРаботыСотрудников.ОбъектДанных ССЫЛКА Справочник.ДокументыПредприятия";
				
		ИначеЕсли ТипДанных = "Задачи" Тогда
		
			Запрос.Текст = Запрос.Текст + Символы.ВК 
				+ " И (ПротоколРаботыСотрудников.ОбъектДанных ССЫЛКА Задача.ЗадачаИсполнителя
				| ИЛИ ПротоколРаботыСотрудников.ОбъектДанных ССЫЛКА БизнесПроцесс.Исполнение
				| ИЛИ ПротоколРаботыСотрудников.ОбъектДанных ССЫЛКА БизнесПроцесс.КомплексныйПроцесс
				| ИЛИ ПротоколРаботыСотрудников.ОбъектДанных ССЫЛКА БизнесПроцесс.Ознакомление
				| ИЛИ ПротоколРаботыСотрудников.ОбъектДанных ССЫЛКА БизнесПроцесс.Приглашение
				| ИЛИ ПротоколРаботыСотрудников.ОбъектДанных ССЫЛКА БизнесПроцесс.Рассмотрение
				| ИЛИ ПротоколРаботыСотрудников.ОбъектДанных ССЫЛКА БизнесПроцесс.Регистрация
				| ИЛИ ПротоколРаботыСотрудников.ОбъектДанных ССЫЛКА БизнесПроцесс.РешениеВопросовВыполненияЗадач
				| ИЛИ ПротоколРаботыСотрудников.ОбъектДанных ССЫЛКА БизнесПроцесс.Согласование
				| ИЛИ ПротоколРаботыСотрудников.ОбъектДанных ССЫЛКА БизнесПроцесс.Утверждение)";
				
		ИначеЕсли ТипДанных = "Файлы" Тогда
		
			Запрос.Текст = Запрос.Текст + Символы.ВК 
				+ " И (ПротоколРаботыСотрудников.ОбъектДанных ССЫЛКА Справочник.ВерсииФайлов
				| ИЛИ ПротоколРаботыСотрудников.ОбъектДанных ССЫЛКА Справочник.Файлы)";
				
		ИначеЕсли ТипДанных = "Мероприятия" Тогда
		
			Запрос.Текст = Запрос.Текст + Символы.ВК 
				+ " И ПротоколРаботыСотрудников.ОбъектДанных ССЫЛКА Справочник.Мероприятия";
				
		ИначеЕсли ТипДанных = "Проекты" Тогда
		
			Запрос.Текст = Запрос.Текст + Символы.ВК 
				+ " И (ПротоколРаботыСотрудников.ОбъектДанных ССЫЛКА Справочник.Проекты
				| ИЛИ ПротоколРаботыСотрудников.ОбъектДанных ССЫЛКА Справочник.ПроектныеЗадачи)";
				
		ИначеЕсли ТипДанных = "Отчеты" Тогда
		
			Запрос.Текст = Запрос.Текст + Символы.ВК 
				+ " И (ПротоколРаботыСотрудников.ОбъектДанных ССЫЛКА Документ.ЕжедневныйОтчет
				| ИЛИ ПротоколРаботыСотрудников.ОбъектДанных ССЫЛКА Документ.ЕженедельныйОтчет)";
				
		ИначеЕсли ТипДанных = "Бронь" Тогда
		
			Запрос.Текст = Запрос.Текст + Символы.ВК 
				+ " И ПротоколРаботыСотрудников.ОбъектДанных ССЫЛКА Документ.Бронь";
				
		ИначеЕсли ТипДанных = "Отсутствие" Тогда
		
			Запрос.Текст = Запрос.Текст + Символы.ВК 
				+ " И ПротоколРаботыСотрудников.ОбъектДанных ССЫЛКА Документ.Отсутствие";
				
		ИначеЕсли ТипДанных = "Почта" Тогда
		
			Запрос.Текст = Запрос.Текст + Символы.ВК 
				+ " И (ПротоколРаботыСотрудников.ОбъектДанных ССЫЛКА Документ.ВходящееПисьмо
				| ИЛИ ПротоколРаботыСотрудников.ОбъектДанных ССЫЛКА Документ.ИсходящееПисьмо)";
				
		КонецЕсли;	

	КонецЕсли;	
	
	Запрос.Текст = Запрос.Текст + Символы.ВК + " УПОРЯДОЧИТЬ ПО Дата";
	
	Запрос.УстановитьПараметр("МинДата", НачалоДня(МинДата));
	Запрос.УстановитьПараметр("МаксДата", КонецДня(МаксДата));
	
	Первое = Неопределено;
	Последнее = Неопределено;
	
	таблЗапроса = Запрос.Выполнить().Выгрузить();
	Для Каждого Стр Из таблЗапроса Цикл
		
		НовСтр = ТаблицаПротокол.Добавить();
		ЗаполнитьЗначенияСвойств(НовСтр, Стр);
		
		Если СтрНайти(НовСтр.ОбъектДанныхПредставление, НСтр("ru = 'Объект не найден'")) Тогда
			НовСтр.ОбъектДанныхПредставление = НСтр("ru = 'Объекта нет в этом узле'");
		КонецЕсли;	
		
		Если Первое = Дата('00010101') Тогда
			Первое = Стр.Дата;
		Иначе	
			Первое = Мин(Первое, Стр.Дата);
		КонецЕсли;	
		
		Если Последнее = Дата('00010101') Тогда
			Последнее = Стр.Дата;
		Иначе	
			Последнее = Макс(Последнее, Стр.Дата);
		КонецЕсли;	
		
	КонецЦикла;
	
	ЗначениеВРеквизитФормы(ТаблицаПротокол, "ПротоколРаботы");
	ВсегоДействий = таблЗапроса.Количество();
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервереРедакцииКонфигурации()
	
	ЭлементСписка = Элементы.ТипДанных.СписокВыбора.НайтиПоЗначению("Документы предприятия");
	Если ЭлементСписка <> Неопределено Тогда
		ЭлементСписка.Представление = СтрШаблон(НСтр("ru = 'Документы %1'"), РедакцииКонфигурацииКлиентСервер.ПредприятияРодительный());
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти