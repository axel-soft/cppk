#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает индекс картинки состояния из коллекции "КоллекцияСостоянияЗадач".
//
// Параметры:
//  ВидСостояния - ПеречислениеСсылка.ВидыСостоянийЗадач - Вид состояние.
// 
// Возвращаемое значение:
//  Число - Индекс картинки состояния.
//
Функция ИндексКартинкиСостояния(ВидСостояния) Экспорт
	
	Если ВидСостояния = Закрыта Тогда
		
		Возврат 6;
		
	ИначеЕсли ВидСостояния = Отменена Тогда
		
		Возврат 3;
		
	ИначеЕсли ВидСостояния = Черновик Тогда
		
		Возврат 0;
		
	ИначеЕсли ВидСостояния = Отложена Тогда
		
		Возврат 5;
		
	Иначе
		
		Возврат -1;
		
	КонецЕсли;
	
КонецФункции

// Возвращает картинку состояния.
//
// Параметры:
//  ВидСостояния - ПеречислениеСсылка.ВидыСостоянийЗадач.
// 
// Возвращаемое значение:
//  Картинка - Картинка состояния.
//
Функция КартинкаСостояния(ВидСостояния) Экспорт
	
	Если ВидСостояния = ВРаботе Тогда
		
		Возврат БиблиотекаКартинок.СостояниеЗадачиНовая;
		
	ИначеЕсли ВидСостояния = Закрыта Тогда
		
		Возврат БиблиотекаКартинок.СостояниеЗадачиЗакрыта;
		
	ИначеЕсли ВидСостояния = НоваяЗадача Тогда
		
		Возврат БиблиотекаКартинок.СостояниеЗадачиНовая;
		
	ИначеЕсли ВидСостояния = НаОбработкеРезультата Тогда
		
		Возврат БиблиотекаКартинок.СостояниеЗадачиНовая;
		
	ИначеЕсли ВидСостояния = Отложена Тогда
		
		Возврат БиблиотекаКартинок.СостояниеЗадачиОтложена;
		
	ИначеЕсли ВидСостояния = Отменена Тогда
		
		Возврат БиблиотекаКартинок.СостояниеЗадачиОтменена;
		
	ИначеЕсли ВидСостояния = Черновик Тогда
		
		Возврат БиблиотекаКартинок.СостояниеЗадачиЧерновик;
		
	Иначе
		
		Возврат Новый Картинка;
		
	КонецЕсли;
	
КонецФункции

// Определяет, что это выполненная задача.
//
// Параметры:
//  ВидСостояния - ПеречислениеСсылка.ВидыСостоянийЗадач - Вид состояние.
// 
// Возвращаемое значение:
//  Булево - Это выполненная задача.
//
Функция ЭтоВыполненнаяЗадача(ВидСостояния) Экспорт
	
	Возврат ВидСостояния = Закрыта;
	
КонецФункции

// Определяет, какие виды состояний задач относятся к ожидающим выполнения.
// 
// Возвращаемое значение:
//  Массив из ПеречислениеСсылка.ВидыСостоянийЗадач.
// 	
Функция ОжидающиеВыполненияЗадачи() Экспорт
	
	ОжидающиеВыполненияЗадачи = Новый Массив;
	ОжидающиеВыполненияЗадачи.Добавить(НоваяЗадача);
	ОжидающиеВыполненияЗадачи.Добавить(ВРаботе);
	ОжидающиеВыполненияЗадачи.Добавить(НаОбработкеРезультата);
	
	Возврат ОжидающиеВыполненияЗадачи;
	
КонецФункции

// Определяет, что это задача в работе.
//
// Параметры:
//  ВидСостояния - ПеречислениеСсылка.ВидыСостоянийЗадач - Вид состояния.
// 
// Возвращаемое значение:
//  Булево - Это задача в работе.
//
Функция ЭтоЗадачаВРаботе(ВидСостояния) Экспорт
	
	Возврат ВидСостояния = ВРаботе;
	
КонецФункции

// Определяет, что это новая задача.
//
// Параметры:
//  ВидСостояния - ПеречислениеСсылка.ВидыСостоянийЗадач - Вид состояние.
// 
// Возвращаемое значение:
//  Булево - Это новая задача.
//
Функция ЭтоНоваяЗадача(ВидСостояния) Экспорт
	
	Возврат ВидСостояния = НоваяЗадача;
	
КонецФункции

// Определяет, что это ожидающая выполнения задача.
//
// Параметры:
//  ВидСостояния - ПеречислениеСсылка.ВидыСостоянийЗадач - Вид состояние.
// 
// Возвращаемое значение:
//  Булево - Это ожидающая выполнения задача.
//
Функция ЭтоОжидающаяВыполненияЗадача(ВидСостояния) Экспорт
	
	ОжидающиеВыполненияЗадачи = ОжидающиеВыполненияЗадачи();
	ЭтоОжидающаяВыполненияЗадача = ОжидающиеВыполненияЗадачи.Найти(ВидСостояния) <> Неопределено;
	
	Возврат ЭтоОжидающаяВыполненияЗадача;
	
КонецФункции

// Определяет, что это ожидающая проверки задача.
//
// Параметры:
//  ВидСостояния - ПеречислениеСсылка.ВидыСостоянийЗадач - Вид состояние.
// 
// Возвращаемое значение:
//  Булево - Это ожидающая проверки задача.
//
Функция ЭтоОжидающаяПроверкиЗадача(ВидСостояния) Экспорт
	
	Возврат ВидСостояния = НаОбработкеРезультата;
	
КонецФункции

// Определяет, является ли указанное состояние задачи состоянием отмененной задачи.
//
// Параметры:
//  ВидСостояния - ПеречислениеСсылка.ВидыСостоянийЗадач - Вид состояние.
// 
// Возвращаемое значение:
//  Булево - Это отмененная задача.
//
Функция ЭтоОтмененнаяЗадача(ВидСостояния) Экспорт
	
	Возврат ВидСостояния = Отменена;
	
КонецФункции

// Определяет, является ли указанное состояние задачи состоянием отложенной задачи.
//
// Параметры:
//  ВидСостояния - ПеречислениеСсылка.ВидыСостоянийЗадач - Вид состояние.
// 
// Возвращаемое значение:
//  Булево - Это отложенная задача.
//
Функция ЭтоОтложеннаяЗадача(ВидСостояния) Экспорт
	
	Возврат ВидСостояния = Отложена;
	
КонецФункции

// Определяет, что это черновик.
//
// Параметры:
//  ВидСостояния - ПеречислениеСсылка.ВидыСостяонийЗадач.
// 
// Возвращаемое значение:
//  Булево - Это черновик.
//
Функция ЭтоЧерновик(ВидСостояния) Экспорт
	
	Возврат ВидСостояния = Черновик;
	
КонецФункции

#КонецОбласти

#КонецЕсли