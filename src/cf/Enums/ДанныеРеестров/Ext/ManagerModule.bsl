#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Определяет данные реестра по типу источника.
// 
// Параметры:
// 	ТипИсточника - Тип.
// 	
// Возвращаемое значение:
// 	ПеречислениеСсылка.ДанныеРеестров.
//
Функция НайтиПоТипуИсточника(ТипИсточника) Экспорт
	
	Если ТипИсточника = Тип("СправочникСсылка.ВидыДокументов") Тогда
		ДанныеРеестра = ДокументыПредприятия;
	ИначеЕсли ТипИсточника = Тип("СправочникСсылка.ВидыМероприятий") Тогда
		ДанныеРеестра = Мероприятия;
	Иначе
		ДанныеРеестра = ПустаяСсылка();
	КонецЕсли;
	
	Возврат ДанныеРеестра;
	
КонецФункции

// Определяет описание типов вида элементов данных реестра.
// 
// Параметры:
// 	ДанныеРеестра - ПеречислениеСсылка.ДанныеРеестров.
// 	
// Возвращаемое значение:
// 	ОписаниеТипов.
//
Функция ОписаниеТиповВидаЭлементов(ДанныеРеестра) Экспорт
	
	Если ДанныеРеестра = ДокументыПредприятия Тогда
		ОписаниеТиповВидаЭлементов = Новый ОписаниеТипов("СправочникСсылка.ВидыДокументов");
	ИначеЕсли ДанныеРеестра = Мероприятия Тогда
		ОписаниеТиповВидаЭлементов = Новый ОписаниеТипов("СправочникСсылка.ВидыМероприятий");
	Иначе
		ОписаниеТиповВидаЭлементов = Новый ОписаниеТипов("Неопределено");
	КонецЕсли;
	
	Возврат ОписаниеТиповВидаЭлементов;
	
КонецФункции

// Определяет синоним видов элементов данных реестра.
// 
// Параметры:
// 	ДанныеРеестра - ПеречислениеСсылка.ДанныеРеестров.
// 	
// Возвращаемое значение:
// 	Строка.
//
Функция СинонимВидовЭлементов(ДанныеРеестра) Экспорт
	
	Если ДанныеРеестра = ДокументыПредприятия Тогда
		СинонимВидовЭлементов = НСтр("ru = 'Виды документов'");
	ИначеЕсли ДанныеРеестра = Мероприятия Тогда
		СинонимВидовЭлементов = НСтр("ru = 'Виды мероприятий'");
	Иначе
		СинонимВидовЭлементов = НСтр("ru = 'Виды элементов'");
	КонецЕсли;
	
	Возврат СинонимВидовЭлементов;
	
КонецФункции

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ОбработкаПолученияДанныхВыбора(ДанныеВыбора, Параметры, СтандартнаяОбработка)
	
	Если Параметры.Свойство("Стандартные") И Параметры.Стандартные Тогда
		Возврат;
	КонецЕсли;
	
	СтандартнаяОбработка = Ложь;
	
	Параметры.Вставить("Стандартные", Истина);
	ДанныеВыбора = Перечисления.ДанныеРеестров.ПолучитьДанныеВыбора(Параметры);
	
	КоличествоЭлементов = ДанныеВыбора.Количество();
	Для Индекс = 1 По КоличествоЭлементов Цикл
		
		ОбратныйИндекс = КоличествоЭлементов - Индекс;
		ЭлементДанныхВыбора = ДанныеВыбора[ОбратныйИндекс];
		
		Если ЭлементДанныхВыбора.Значение.Значение = Мероприятия
			И Не ПолучитьФункциональнуюОпцию("ИспользоватьУправлениеМероприятиями") Тогда
			ДанныеВыбора.Удалить(ОбратныйИндекс);
			Продолжить;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли