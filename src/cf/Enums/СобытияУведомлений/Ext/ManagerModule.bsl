#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Преобразует событие уведомление в вид бизнес события, если возможно.
//
// Параметры:
//  СобытиеУведомления - СправочникСсылка.ВидыБизнесСобытий, ПеречислениеСсылка.СобытияУведомлений - Событие уведомления.
//
// Возвращаемое значение:
//  СправочникСсылка.ВидыБизнесСобытий, ПеречислениеСсылка.СобытияУведомлений - Приведенный вид события.
//
Функция ВидБизнесСобытия(СобытиеУведомления) Экспорт
	
	ВидБизнесСобытия = СобытиеУведомления;
	Если СобытиеУведомления = ВыполнениеМоейЗадачи Тогда
		ВидБизнесСобытия = Справочники.ВидыБизнесСобытий.ВыполнениеЗадачи;
		
	ИначеЕсли СобытиеУведомления = ПеренаправлениеМоейЗадачи Тогда
		ВидБизнесСобытия = Справочники.ВидыБизнесСобытий.ПеренаправлениеЗадачи;
		
	ИначеЕсли СобытиеУведомления = ОбменПоЭДОВыполненУспешно Тогда
		ВидБизнесСобытия = Справочники.ВидыБизнесСобытий.ИзменениеСостоянияДокументаПоЭДО;
		
	ИначеЕсли СобытиеУведомления = ОтклонениеДокументаКонтрагентомПоЭДО Тогда
		ВидБизнесСобытия = Справочники.ВидыБизнесСобытий.ИзменениеСостоянияДокументаПоЭДО;
	
	ИначеЕсли СобытиеУведомления = ОзнакомлениеСПоложительнымРезультатомПроцесса Тогда
		ВидБизнесСобытия = Справочники.ВидыБизнесСобытий.ЗавершениеБизнесПроцесса;
		
	ИначеЕсли СобытиеУведомления = ОзнакомлениеСПоложительнымРезультатомДействияОбработки Тогда
		ВидБизнесСобытия = Справочники.ВидыБизнесСобытий.ЗавершениеДействияОбработки;

	КонецЕсли;
	
	Возврат ВидБизнесСобытия;
	
КонецФункции

// Формирует список доступных видов событий по объекту.
//
// Параметры:
//  Объект - ЛюбаяСсылка - Объект.
//
// Возвращаемое значение:
//  СписокЗначений - Виды событий по объекту.
//
Функция ВидыСобытийПоОбъекту(Объект) Экспорт
	
	СписокВидовБизнесСобытий = Новый СписокЗначений;
	Если ТипЗнч(Объект) = Тип("СправочникСсылка.Файлы") Тогда
		СписокВидовБизнесСобытий.Добавить(Справочники.ВидыБизнесСобытий.ИзменениеФайла);
		СписокВидовБизнесСобытий.Добавить(Справочники.ВидыБизнесСобытий.ОсвобождениеФайла);
		СписокВидовБизнесСобытий.Добавить(Справочники.ВидыБизнесСобытий.ЗахватФайлаДляРедактирования);
		
	ИначеЕсли ТипЗнч(Объект) = Тип("СправочникСсылка.ПапкиФайлов") Тогда
		СписокВидовБизнесСобытий.Добавить(Справочники.ВидыБизнесСобытий.СозданиеФайла);
		
	ИначеЕсли ТипЗнч(Объект) = Тип("СправочникСсылка.ДокументыПредприятия") Тогда
		СписокВидовБизнесСобытий.Добавить(Справочники.ВидыБизнесСобытий.ИзменениеДокумента);
		СписокВидовБизнесСобытий.Добавить(Справочники.ВидыБизнесСобытий.ПеререгистрацияДокумента);
		СписокВидовБизнесСобытий.Добавить(Справочники.ВидыБизнесСобытий.РегистрацияДокумента);
		СписокВидовБизнесСобытий.Добавить(Справочники.ВидыБизнесСобытий.ИзменениеСоставаКомплекта);
		СписокВидовБизнесСобытий.Добавить(Справочники.ВидыБизнесСобытий.СозданиеФайла);
		
	ИначеЕсли ТипЗнч(Объект) = Тип("СправочникСсылка.ПапкиДокументов") Тогда
		СписокВидовБизнесСобытий.Добавить(Справочники.ВидыБизнесСобытий.СозданиеДокумента);
		
	ИначеЕсли ТипЗнч(Объект) = Тип("СправочникСсылка.ВидыДокументов") Тогда
		СписокВидовБизнесСобытий.Добавить(Справочники.ВидыБизнесСобытий.НазначениеОтветственного);
		СписокВидовБизнесСобытий.Добавить(ОбменПоЭДОВыполненУспешно);
		СписокВидовБизнесСобытий.Добавить(ОтклонениеДокументаКонтрагентомПоЭДО);
		
	ИначеЕсли РаботаСУведомлениями.ЭтоПоддерживаемыйБизнесПроцесс(Объект) Тогда
		СписокВидовБизнесСобытий.Добавить(Справочники.ВидыБизнесСобытий.ВозобновлениеБизнесПроцесса);
		СписокВидовБизнесСобытий.Добавить(Справочники.ВидыБизнесСобытий.ЗавершениеБизнесПроцесса);
		СписокВидовБизнесСобытий.Добавить(Справочники.ВидыБизнесСобытий.ОстановкаБизнесПроцесса);
		СписокВидовБизнесСобытий.Добавить(Справочники.ВидыБизнесСобытий.СтартБизнесПроцесса);
		СписокВидовБизнесСобытий.Добавить(Справочники.ВидыБизнесСобытий.ПрерываниеБизнесПроцесса);
		СписокВидовБизнесСобытий.Добавить(Справочники.ВидыБизнесСобытий.ВыполнениеЗадачиПроцесса);
		СписокВидовБизнесСобытий.Добавить(ОзнакомлениеСПоложительнымРезультатомПроцесса);
		
	ИначеЕсли ТипЗнч(Объект) = Тип("СправочникСсылка.Пользователи") Тогда
		СписокВидовБизнесСобытий.Добавить(Справочники.ВидыБизнесСобытий.НоваяЗадача);
		СписокВидовБизнесСобытий.Добавить(ПодошелСрокКонтроля);
		СписокВидовБизнесСобытий.Добавить(ПросроченКонтроль);
		СписокВидовБизнесСобытий.Добавить(Справочники.ВидыБизнесСобытий.СозданиеОтсутствия);
		СписокВидовБизнесСобытий.Добавить(Справочники.ВидыБизнесСобытий.ИзменениеОтсутствия);
		
	ИначеЕсли ТипЗнч(Объект) = Тип("СправочникСсылка.ТемыОбсуждений") Тогда
		СписокВидовБизнесСобытий.Добавить(Справочники.ВидыБизнесСобытий.СозданиеСообщения);
		
	ИначеЕсли ТипЗнч(Объект) = Тип("СправочникСсылка.Проекты") Тогда
		СписокВидовБизнесСобытий.Добавить(Справочники.ВидыБизнесСобытий.ИзменениеПроекта);
		
	ИначеЕсли ТипЗнч(Объект) = Тип("СправочникСсылка.ПроектныеЗадачи") Тогда
		СписокВидовБизнесСобытий.Добавить(Справочники.ВидыБизнесСобытий.ИзменениеПроектнойЗадачи);
		
	ИначеЕсли ТипЗнч(Объект) = Тип("СправочникСсылка.Контроль") Тогда
		СписокВидовБизнесСобытий.Добавить(ПодошелСрокКонтроля);
		СписокВидовБизнесСобытий.Добавить(ПросроченКонтроль);
		
	ИначеЕсли ТипЗнч(Объект) = Тип("ДокументСсылка.Отсутствие") Тогда
		СписокВидовБизнесСобытий.Добавить(Справочники.ВидыБизнесСобытий.ИзменениеОтсутствия);
		
	ИначеЕсли ТипЗнч(Объект) = Тип("СправочникСсылка.Мероприятия") Тогда
		СписокВидовБизнесСобытий.Добавить(Справочники.ВидыБизнесСобытий.ИзменениеМероприятия);
		СписокВидовБизнесСобытий.Добавить(Справочники.ВидыБизнесСобытий.СозданиеФайла);
		
	ИначеЕсли ТипЗнч(Объект) = Тип("СправочникСсылка.КатегорииДанных") Тогда
		СписокВидовБизнесСобытий.Добавить(Справочники.ВидыБизнесСобытий.ДобавлениеВКатегорию);
		СписокВидовБизнесСобытий.Добавить(Справочники.ВидыБизнесСобытий.УдалениеИзКатегории);
		
	ИначеЕсли ТипЗнч(Объект) = Тип("ДокументСсылка.Бронь") Тогда
		СписокВидовБизнесСобытий.Добавить(Справочники.ВидыБизнесСобытий.ИзменениеБрони);
		
	ИначеЕсли ТипЗнч(Объект) = Тип("СправочникСсылка.ТерриторииИПомещения") Тогда
		СписокВидовБизнесСобытий.Добавить(Справочники.ВидыБизнесСобытий.СозданиеБрони);
		СписокВидовБизнесСобытий.Добавить(Справочники.ВидыБизнесСобытий.ИзменениеБрони);
		СписокВидовБизнесСобытий.Добавить(Справочники.ВидыБизнесСобытий.ОтменаБрони);
		
	ИначеЕсли ТипЗнч(Объект) = Тип("СправочникСсылка.ПоказателиПроцессов") Тогда
		СписокВидовБизнесСобытий.Добавить(Справочники.ВидыБизнесСобытий.ИзменениеЗначенияПоказателяПроцесса);
		
	ИначеЕсли ТипЗнч(Объект) = Тип("СправочникСсылка.КонтрольныеТочки") Тогда
		СписокВидовБизнесСобытий.Добавить(ПросроченаКонтрольнаяТочка);
		СписокВидовБизнесСобытий.Добавить(ПодошелСрокКонтрольнойТочки);
		СписокВидовБизнесСобытий.Добавить(ПросроченаОценкаКонтрольнойТочки);
		
	ИначеЕсли ТипЗнч(Объект) = Тип("СправочникСсылка.ПапкиФорума") Тогда
		СписокВидовБизнесСобытий.Добавить(СозданиеТемыФорума);

	ИначеЕсли ДействияКлиентСервер.ЭтоДействие(Объект) Тогда
		СписокВидовБизнесСобытий.Добавить(ОзнакомлениеСПоложительнымРезультатомДействияОбработки);		
		
	КонецЕсли;
	
	Для Каждого ЭлементСписка Из СписокВидовБизнесСобытий Цикл
		ЭлементСписка.Представление = ПредставлениеВидаСобытия(ЭлементСписка.Значение, Объект);
	КонецЦикла;
	СписокВидовБизнесСобытий.СортироватьПоПредставлению();
	
	Возврат СписокВидовБизнесСобытий;
	
КонецФункции

// Формирует список видов событий подписки на бизнес-событие.
//
// Параметры:
//  ВидСобытия - СправочникСсылка.ВидыБизнесСобытий, ПеречислениеСсылка.СобытияУведомлений - Вид события.
//
// Возвращаемое значение:
//  Массив - Виды событий подписки.
//
Функция ВидыСобытийПодписки(ВидСобытия) Экспорт
	
	ВидыСобытий = Новый Массив;
	ВидыСобытий.Добавить(ВидСобытия);
	Если ВидСобытия = Справочники.ВидыБизнесСобытий.ВыполнениеЗадачи Тогда
		ВидыСобытий.Добавить(ВыполнениеМоейЗадачи);
		
	ИначеЕсли ВидСобытия = Справочники.ВидыБизнесСобытий.ПеренаправлениеЗадачи Тогда
		ВидыСобытий.Добавить(ПеренаправлениеМоейЗадачи);
		
	ИначеЕсли ВидСобытия = Справочники.ВидыБизнесСобытий.ИзменениеСостоянияДокументаПоЭДО Тогда
		ВидыСобытий.Добавить(ОбменПоЭДОВыполненУспешно);
		ВидыСобытий.Добавить(ОтклонениеДокументаКонтрагентомПоЭДО);
	
	ИначеЕсли ВидСобытия = Справочники.ВидыБизнесСобытий.ЗавершениеБизнесПроцесса Тогда
		ВидыСобытий.Добавить(ОзнакомлениеСПоложительнымРезультатомПроцесса);
		
	ИначеЕсли ВидСобытия = Справочники.ВидыБизнесСобытий.ЗавершениеДействияОбработки Тогда
		ВидыСобытий.Добавить(ОзнакомлениеСПоложительнымРезультатомДействияОбработки);

	КонецЕсли;
	
	Возврат ВидыСобытий;
	
КонецФункции

// Формирует представление события для конкретного объекта.
//
// Параметры:
//  ВидСобытия - СправочникСсылка.ВидыБизнесСобытий, ПеречислениеСсылка.СобытияУведомлений - Вид события.
//  Объект - ЛюбаяСсылка - Объект.
//  Пользователь - СправочникСсылка.Пользователи - Пользователь.
// 
// Возвращаемое значение:
//  Строка - Представление события.
//
Функция ПредставлениеВидаСобытия(ВидСобытия, Объект, Пользователь = Неопределено) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ПредставлениеБизнесСобытия = "";
	
	ВидыБизнесСобытийМенеджер = Справочники.ВидыБизнесСобытий;
	КодЯзыка = РаботаСУведомлениями.КодЯзыка(Пользователь);
	Если ТипЗнч(Объект) = Тип("СправочникСсылка.Файлы") Тогда
		
		Если ВидСобытия = ВидыБизнесСобытийМенеджер.ЗахватФайлаДляРедактирования Тогда
			ПредставлениеБизнесСобытия = НСтр("ru = 'Захвачен файл для редактирования'", КодЯзыка);
		ИначеЕсли ВидСобытия = ВидыБизнесСобытийМенеджер.ИзменениеФайла Тогда
			ПредставлениеБизнесСобытия = НСтр("ru = 'Изменился файл'", КодЯзыка);
		ИначеЕсли ВидСобытия = ВидыБизнесСобытийМенеджер.ОсвобождениеФайла Тогда
			ПредставлениеБизнесСобытия = НСтр("ru = 'Освобожден файл'", КодЯзыка);
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(Объект) = Тип("СправочникСсылка.ПапкиФайлов") Тогда
		
		Если ВидСобытия = ВидыБизнесСобытийМенеджер.СозданиеФайла Тогда
			ПредставлениеБизнесСобытия = НСтр("ru = 'Создан новый файл в папке'", КодЯзыка);
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(Объект) = Тип("СправочникСсылка.ВидыДокументов") Тогда
		
		Если ВидСобытия = ВидыБизнесСобытийМенеджер.НазначениеОтветственного Тогда
			ПредставлениеБизнесСобытия = НСтр("ru = 'Вы назначены ответственным за документ'", КодЯзыка);
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(Объект) = Тип("СправочникСсылка.ДокументыПредприятия") Тогда
		
		Если ВидСобытия = ВидыБизнесСобытийМенеджер.СозданиеФайла Тогда
			ПредставлениеБизнесСобытия = НСтр("ru = 'Присоединен новый файл к документу'", КодЯзыка)
		ИначеЕсли ВидСобытия = ВидыБизнесСобытийМенеджер.ИзменениеДокумента Тогда
			ПредставлениеБизнесСобытия = НСтр("ru = 'Изменился документ'", КодЯзыка);
		ИначеЕсли ВидСобытия = ВидыБизнесСобытийМенеджер.ИзменениеСоставаКомплекта Тогда
			ПредставлениеБизнесСобытия = НСтр("ru = 'Изменился состав комплекта'", КодЯзыка);
		ИначеЕсли ВидСобытия = ВидыБизнесСобытийМенеджер.ПеререгистрацияДокумента Тогда
			ПредставлениеБизнесСобытия = НСтр("ru = 'Перерегистрирован документ'", КодЯзыка);
		ИначеЕсли ВидСобытия = ВидыБизнесСобытийМенеджер.РегистрацияДокумента Тогда
			ПредставлениеБизнесСобытия = НСтр("ru = 'Зарегистрирован документ'", КодЯзыка);
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(Объект) = Тип("СправочникСсылка.ПапкиДокументов") Тогда
		
		Если ВидСобытия = ВидыБизнесСобытийМенеджер.СозданиеДокумента Тогда
			ПредставлениеБизнесСобытия = НСтр("ru = 'Создан новый документ в папке'", КодЯзыка);
		КонецЕсли;
		
	ИначеЕсли РаботаСУведомлениями.ЭтоПоддерживаемыйБизнесПроцесс(Объект) Тогда
		
		Если ВидСобытия = ВидыБизнесСобытийМенеджер.ВозобновлениеБизнесПроцесса Тогда
			ПредставлениеБизнесСобытия = НСтр("ru = 'Возобновлен процесс'", КодЯзыка);
		ИначеЕсли ВидСобытия = ВидыБизнесСобытийМенеджер.ЗавершениеБизнесПроцесса Тогда
			ПредставлениеБизнесСобытия = НСтр("ru = 'Завершен процесс'", КодЯзыка);
		ИначеЕсли ВидСобытия = ВидыБизнесСобытийМенеджер.ОстановкаБизнесПроцесса Тогда
			ПредставлениеБизнесСобытия = НСтр("ru = 'Остановлен процесс'", КодЯзыка);
		ИначеЕсли ВидСобытия = ВидыБизнесСобытийМенеджер.СтартБизнесПроцесса Тогда
			ПредставлениеБизнесСобытия = НСтр("ru = 'Стартован процесс'", КодЯзыка);
		ИначеЕсли ВидСобытия = ВидыБизнесСобытийМенеджер.ВыполнениеЗадачиПроцесса Тогда
			ПредставлениеБизнесСобытия = НСтр("ru = 'Ход выполнения процесса'", КодЯзыка);
		ИначеЕсли ВидСобытия = ВидыБизнесСобытийМенеджер.ПрерываниеБизнесПроцесса Тогда
			ПредставлениеБизнесСобытия = НСтр("ru = 'Прерван процесс'", КодЯзыка);
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(Объект) = Тип("СправочникСсылка.Пользователи") Тогда
		
		Если ВидСобытия = ВидыБизнесСобытийМенеджер.НоваяЗадача Тогда
			ПредставлениеБизнесСобытия = НСтр("ru = 'Поступила новая задача пользователю'", КодЯзыка);
		ИначеЕсли ВидСобытия = ПодошелСрокКонтроля Тогда
			ПредставлениеБизнесСобытия = НСтр("ru = 'Подошел срок контроля пользователя'", КодЯзыка);
		ИначеЕсли ВидСобытия = ПросроченКонтроль Тогда
			ПредставлениеБизнесСобытия = НСтр("ru = 'Истек срок контроля'", КодЯзыка);
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(Объект) = Тип("СправочникСсылка.ТемыОбсуждений") Тогда
		
		Если ВидСобытия = ВидыБизнесСобытийМенеджер.СозданиеСообщения Тогда
			ПредставлениеБизнесСобытия = НСтр("ru = 'Новое сообщение в теме обсуждения'", КодЯзыка);
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(Объект) = Тип("СправочникСсылка.Проекты") Тогда
		
		Если ВидСобытия = ВидыБизнесСобытийМенеджер.ИзменениеПроекта Тогда
			ПредставлениеБизнесСобытия = НСтр("ru = 'Изменился проект'", КодЯзыка);
		КонецЕсли;
			
	ИначеЕсли ТипЗнч(Объект) = Тип("СправочникСсылка.ПроектныеЗадачи") Тогда
		
		Если ВидСобытия = ВидыБизнесСобытийМенеджер.ИзменениеПроектнойЗадачи Тогда
			ПредставлениеБизнесСобытия = НСтр("ru = 'Изменилась проектная задача'", КодЯзыка);
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(Объект) = Тип("СправочникСсылка.Контроль") Тогда
		
		Если ВидСобытия = ПодошелСрокКонтроля Тогда
			ПредставлениеБизнесСобытия = НСтр("ru = 'Подходит срок контроля'", КодЯзыка);
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(Объект) = Тип("СправочникСсылка.Мероприятия") Тогда
		
		Если ВидСобытия = ВидыБизнесСобытийМенеджер.СозданиеФайла Тогда
			ПредставлениеБизнесСобытия = НСтр("ru = 'Присоединен новый файл к мероприятию'", КодЯзыка);
		ИначеЕсли ВидСобытия = ВидыБизнесСобытийМенеджер.ИзменениеМероприятия Тогда
			ПредставлениеБизнесСобытия = НСтр("ru = 'Изменилось мероприятие'", КодЯзыка);
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(Объект) = Тип("СправочникСсылка.КатегорииДанных") Тогда
		
		Если ВидСобытия = ВидыБизнесСобытийМенеджер.ДобавлениеВКатегорию Тогда
			ПредставлениеБизнесСобытия = НСтр("ru = 'Добавлен элемент в категорию'", КодЯзыка);
		ИначеЕсли ВидСобытия = ВидыБизнесСобытийМенеджер.УдалениеИзКатегории Тогда
			ПредставлениеБизнесСобытия = НСтр("ru = 'Удален элемент из категории'", КодЯзыка);
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(Объект) = Тип("ДокументСсылка.Бронь") Тогда
		
		Если ВидСобытия = ВидыБизнесСобытийМенеджер.ИзменениеБрони Тогда
			ПредставлениеБизнесСобытия = НСтр("ru = 'Изменена бронь'", КодЯзыка);
		ИначеЕсли ВидСобытия = Справочники.ВидыБизнесСобытий.ОтменаБрони Тогда
			ПредставлениеБизнесСобытия = НСтр("ru = 'Отменена бронь'", КодЯзыка);
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(Объект) = Тип("СправочникСсылка.ТерриторииИПомещения") Тогда
		
		Если ВидСобытия = ВидыБизнесСобытийМенеджер.СозданиеБрони Тогда
			ПредставлениеБизнесСобытия = НСтр("ru = 'Создана бронь помещения'", КодЯзыка);
		ИначеЕсли ВидСобытия = ВидыБизнесСобытийМенеджер.ИзменениеБрони Тогда
			ПредставлениеБизнесСобытия = НСтр("ru = 'Изменена бронь помещения'", КодЯзыка);
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(Объект) = Тип("СправочникСсылка.ПоказателиПроцессов") Тогда
		
		Если ВидСобытия = ВидыБизнесСобытийМенеджер.ИзменениеЗначенияПоказателяПроцесса Тогда
			ПредставлениеБизнесСобытия = НСтр("ru = 'Изменилось значение показателя процесса'", КодЯзыка);
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(Объект) = Тип("СправочникСсылка.КонтрольныеТочки") Тогда
		
		Если ВидСобытия = ПодошелСрокКонтрольнойТочки Тогда
			ПредставлениеБизнесСобытия = НСтр("ru = 'Подошел срок контрольной точки'", КодЯзыка);
		ИначеЕсли ВидСобытия = ПросроченаКонтрольнаяТочка Тогда
			ПредставлениеБизнесСобытия = НСтр("ru = 'Просрочена контрольная точка'", КодЯзыка);
		ИначеЕсли ВидСобытия = ПросроченаОценкаКонтрольнойТочки Тогда
			ПредставлениеБизнесСобытия = НСтр("ru = 'Просрочена оценка контрольной точки'", КодЯзыка);
		КонецЕсли;
			
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ПредставлениеБизнесСобытия) Тогда
		
		Если ВидСобытия = ВидыБизнесСобытийМенеджер.ВозобновлениеБизнесПроцесса Тогда
			ПредставлениеБизнесСобытия = НСтр("ru = 'Возобновлен процесс'", КодЯзыка);
		ИначеЕсли ВидСобытия = ВидыБизнесСобытийМенеджер.ВыполнениеЗадачи Тогда
			ПредставлениеБизнесСобытия = НСтр("ru = 'Выполнена задача'", КодЯзыка);
		ИначеЕсли ВидСобытия = ВидыБизнесСобытийМенеджер.ДобавлениеВКатегорию Тогда
			ПредставлениеБизнесСобытия = НСтр("ru = 'Добавление в категорию'", КодЯзыка);
		ИначеЕсли ВидСобытия = ВидыБизнесСобытийМенеджер.ЗавершениеБизнесПроцесса Тогда
			ПредставлениеБизнесСобытия = НСтр("ru = 'Завершен процесс'", КодЯзыка);
		ИначеЕсли ВидСобытия = ВидыБизнесСобытийМенеджер.ЗахватФайлаДляРедактирования Тогда
			ПредставлениеБизнесСобытия = НСтр("ru = 'Захвачен для редактирования файл'", КодЯзыка);
		ИначеЕсли ВидСобытия = ВидыБизнесСобытийМенеджер.ИзменениеБрони Тогда
			ПредставлениеБизнесСобытия = НСтр("ru = 'Изменена бронь'", КодЯзыка);
		ИначеЕсли ВидСобытия = ВидыБизнесСобытийМенеджер.ИзменениеДокумента Тогда
			ПредставлениеБизнесСобытия = НСтр("ru = 'Изменен документ'", КодЯзыка);
		ИначеЕсли ВидСобытия = ВидыБизнесСобытийМенеджер.ИзменениеЗначенияПоказателяПроцесса Тогда
			ПредставлениеБизнесСобытия = НСтр("ru = 'Изменено значение показателя процесса'", КодЯзыка);
		ИначеЕсли ВидСобытия = ВидыБизнесСобытийМенеджер.ИзменениеМероприятия Тогда
			ПредставлениеБизнесСобытия = НСтр("ru = 'Изменено мероприятие'", КодЯзыка);
		ИначеЕсли ВидСобытия = ВидыБизнесСобытийМенеджер.ИзменениеОтсутствия Тогда
			ПредставлениеБизнесСобытия = НСтр("ru = 'Изменено отсутствие'", КодЯзыка);
		ИначеЕсли ВидСобытия = ВидыБизнесСобытийМенеджер.ИзменениеПроекта Тогда
			ПредставлениеБизнесСобытия = НСтр("ru = 'Изменен проект'", КодЯзыка);
		ИначеЕсли ВидСобытия = ВидыБизнесСобытийМенеджер.ИзменениеПроектнойЗадачи Тогда
			ПредставлениеБизнесСобытия = НСтр("ru = 'Изменена проектная задача'", КодЯзыка);
		ИначеЕсли ВидСобытия = ВидыБизнесСобытийМенеджер.ИзменениеСоставаКомплекта Тогда
			ПредставлениеБизнесСобытия = НСтр("ru = 'Изменен состав комплекта'", КодЯзыка);
		ИначеЕсли ВидСобытия = ВидыБизнесСобытийМенеджер.ИзменениеСостоянияДокументаПоЭДО Тогда
			ПредставлениеБизнесСобытия = НСтр("ru = 'Изменено состояние документа по ЭДО'", КодЯзыка);
		ИначеЕсли ВидСобытия = ВидыБизнесСобытийМенеджер.ИзменениеФайла Тогда
			ПредставлениеБизнесСобытия = НСтр("ru = 'Изменен файл'", КодЯзыка);
		ИначеЕсли ВидСобытия = ВидыБизнесСобытийМенеджер.НазначениеОтветственного Тогда
			ПредставлениеБизнесСобытия = НСтр("ru = 'Назначение ответственным за документ'", КодЯзыка);
		ИначеЕсли ВидСобытия = ВидыБизнесСобытийМенеджер.НоваяЗадача Тогда
			ПредставлениеБизнесСобытия = НСтр("ru = 'Новая задача'", КодЯзыка);
		ИначеЕсли ВидСобытия = ВидыБизнесСобытийМенеджер.ИзменениеЗадачи Тогда
			ПредставлениеБизнесСобытия = НСтр("ru = 'Изменена задача'", КодЯзыка);
		ИначеЕсли ВидСобытия = ВидыБизнесСобытийМенеджер.ОсвобождениеФайла Тогда
			ПредставлениеБизнесСобытия = НСтр("ru = 'Освобожден файл'", КодЯзыка);
		ИначеЕсли ВидСобытия = ВидыБизнесСобытийМенеджер.ОстановкаБизнесПроцесса Тогда
			ПредставлениеБизнесСобытия = НСтр("ru = 'Остановлен процесс'", КодЯзыка);
		ИначеЕсли ВидСобытия = ВидыБизнесСобытийМенеджер.ОтменаВыполненияЗадачиПроцесса Тогда
			ПредставлениеБизнесСобытия = НСтр("ru = 'Отменено выполнение задачи'", КодЯзыка);
		ИначеЕсли ВидСобытия = ВидыБизнесСобытийМенеджер.ОтменаЗадачи Тогда
			ПредставлениеБизнесСобытия = НСтр("ru = 'Отменена задача'", КодЯзыка);
		ИначеЕсли ВидСобытия = ВидыБизнесСобытийМенеджер.ПеренаправлениеЗадачи Тогда
			ПредставлениеБизнесСобытия = НСтр("ru = 'Перенаправлена задача'", КодЯзыка);
		ИначеЕсли ВидСобытия = ВидыБизнесСобытийМенеджер.ПеререгистрацияДокумента Тогда
			ПредставлениеБизнесСобытия = НСтр("ru = 'Перерегистрирован документ'", КодЯзыка);
		ИначеЕсли ВидСобытия = ВидыБизнесСобытийМенеджер.ПолучениеВходящегоПисьма Тогда
			ПредставлениеБизнесСобытия = НСтр("ru = 'Получено входящее письмо'", КодЯзыка);
		ИначеЕсли ВидСобытия = ВидыБизнесСобытийМенеджер.ПрерываниеБизнесПроцесса Тогда
			ПредставлениеБизнесСобытия = НСтр("ru = 'Прерыван процесс'", КодЯзыка);
		ИначеЕсли ВидСобытия = ВидыБизнесСобытийМенеджер.РегистрацияДокумента Тогда
			ПредставлениеБизнесСобытия = НСтр("ru = 'Зарегистрирован документ'", КодЯзыка);
		ИначеЕсли ВидСобытия = ВидыБизнесСобытийМенеджер.СозданиеБрони Тогда
			ПредставлениеБизнесСобытия = НСтр("ru = 'Создана бронь'", КодЯзыка);
		ИначеЕсли ВидСобытия = ВидыБизнесСобытийМенеджер.СозданиеДокумента Тогда
			ПредставлениеБизнесСобытия = НСтр("ru = 'Создан документ'", КодЯзыка);
		ИначеЕсли ВидСобытия = ВидыБизнесСобытийМенеджер.СозданиеЗадачиПроцесса Тогда
			ПредставлениеБизнесСобытия = НСтр("ru = 'Создана задача'", КодЯзыка);
		ИначеЕсли ВидСобытия = ВидыБизнесСобытийМенеджер.СозданиеКонтрагента Тогда
			ПредставлениеБизнесСобытия = НСтр("ru = 'Создан контрагент'", КодЯзыка);
		ИначеЕсли ВидСобытия = ВидыБизнесСобытийМенеджер.СозданиеМероприятия Тогда
			ПредставлениеБизнесСобытия = НСтр("ru = 'Создано мероприятие'", КодЯзыка);
		ИначеЕсли ВидСобытия = ВидыБизнесСобытийМенеджер.СозданиеОтсутствия Тогда
			ПредставлениеБизнесСобытия = НСтр("ru = 'Создано отсутствие'", КодЯзыка);
		ИначеЕсли ВидСобытия = ВидыБизнесСобытийМенеджер.СозданиеПроекта Тогда
			ПредставлениеБизнесСобытия = НСтр("ru = 'Создан проект'", КодЯзыка);
		ИначеЕсли ВидСобытия = ВидыБизнесСобытийМенеджер.СозданиеПроектнойЗадачи Тогда
			ПредставлениеБизнесСобытия = НСтр("ru = 'Создана проектная задача'", КодЯзыка);
		ИначеЕсли ВидСобытия = ВидыБизнесСобытийМенеджер.СозданиеСообщения Тогда
			ПредставлениеБизнесСобытия = НСтр("ru = 'Новое сообщение'", КодЯзыка);
		ИначеЕсли ВидСобытия = ВидыБизнесСобытийМенеджер.СозданиеФайла Тогда
			ПредставлениеБизнесСобытия = НСтр("ru = 'Создан файл'", КодЯзыка);
		ИначеЕсли ВидСобытия = ВидыБизнесСобытийМенеджер.СтартБизнесПроцесса Тогда
			ПредставлениеБизнесСобытия = НСтр("ru = 'Старт процесса'", КодЯзыка);
		ИначеЕсли ВидСобытия = ВидыБизнесСобытийМенеджер.УдалениеИзКатегории Тогда
			ПредставлениеБизнесСобытия = НСтр("ru = 'Удаление из категории'", КодЯзыка);
		ИначеЕсли ВидСобытия = ВидыБизнесСобытийМенеджер.ЗамещающиеИПомощникиСоздание Тогда
			ПредставлениеБизнесСобытия = НСтр("ru = 'Создан замещающий или помощник'", КодЯзыка);
		ИначеЕсли ВидСобытия = ВидыБизнесСобытийМенеджер.ЗамещающиеИПомощникиИзменение Тогда
			ПредставлениеБизнесСобытия = НСтр("ru = 'Изменен замещающий или помощник'", КодЯзыка);
		ИначеЕсли ВидСобытия = ОзнакомлениеСПоложительнымРезультатомПроцесса Тогда
			ПредставлениеБизнесСобытия = НСтр("ru = 'Ознакомиться с результатом'", КодЯзыка);
		Иначе
			ПредставлениеБизнесСобытия = Строка(ВидСобытия);
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат ПредставлениеБизнесСобытия;
	
КонецФункции

// Формирует признак доступности подписка на событие для конкретного объекта.
//
// Параметры:
//  ВидСобытия - СправочникСсылка.ВидыБизнесСобытий, ПеречислениеСсылка.СобытияУведомлений - Вид события.
//  Объект - ЛюбаяСсылка - Объект.
// 
// Возвращаемое значение:
//  Булево - Доступна подписка на событие.
//
Функция ДоступнаПодпискаНаСобытие(ВидСобытия, ОбъектПодписки) Экспорт
	
	ДоступнаПодпискаНаСобытие = Ложь;
	Если ТипЗнч(ОбъектПодписки) = Тип("СправочникСсылка.ВидыДокументов") Тогда
		
		Если ВидСобытия = Справочники.ВидыБизнесСобытий.НазначениеОтветственного
			Или ВидСобытия = ОбменПоЭДОВыполненУспешно
			Или ВидСобытия = ОтклонениеДокументаКонтрагентомПоЭДО Тогда
			ДоступнаПодпискаНаСобытие = Истина;
		КонецЕсли;
		
	ИначеЕсли РаботаСУведомлениями.ЭтоПоддерживаемыйБизнесПроцесс(ОбъектПодписки) Тогда
		
		Если ВидСобытия = Справочники.ВидыБизнесСобытий.ВыполнениеЗадачиПроцесса Тогда
			ДоступнаПодпискаНаСобытие = Истина;
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(ОбъектПодписки) = Тип("СправочникСсылка.ТемыОбсуждений") Тогда
		
		Если ВидСобытия = Справочники.ВидыБизнесСобытий.СозданиеСообщения Тогда
			
			ДоступнаПодпискаНаСобытие = Истина;
			
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(ОбъектПодписки) = Тип("СправочникСсылка.Контроль") Тогда
		
		Если ВидСобытия = Перечисления.СобытияУведомлений.ПодошелСрокКонтроля
			Или ВидСобытия = Перечисления.СобытияУведомлений.ПросроченКонтроль Тогда
			
			ДоступнаПодпискаНаСобытие = Истина;
			
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(ОбъектПодписки) = Тип("СправочникСсылка.Пользователи") Тогда
		
		Если ВидСобытия = Справочники.ВидыБизнесСобытий.СозданиеОтсутствия
			Или ВидСобытия = Справочники.ВидыБизнесСобытий.ИзменениеОтсутствия Тогда
			
			ДоступнаПодпискаНаСобытие = Истина;
			
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(ОбъектПодписки) = Тип("СправочникСсылка.ТерриторииИПомещения") Тогда
		
		Если ВидСобытия = Справочники.ВидыБизнесСобытий.СозданиеБрони
			Или ВидСобытия = Справочники.ВидыБизнесСобытий.ИзменениеБрони
			Или ВидСобытия = Справочники.ВидыБизнесСобытий.ОтменаБрони Тогда
			ДоступнаПодпискаНаСобытие = Истина;;
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат ДоступнаПодпискаНаСобытие;
	
КонецФункции

// Выполняет дополнительную проверку подписки.
//
// Параметры:
//  Пользователь - СправочникСсылка.Пользователи - Пользователь.
//  ВидСобытия - СправочникСсылка.ВидыБизнесСобытий, ПеречислениеСсылка.СобытияУведомлений - Вид события.
//  Объект - ЛюбаяСсылка - Объект.
// 
// Возвращаемое значение:
//  Булево - Результат проверки.
//
Функция ДополнительнаяПроверкаПодписки(Пользователь, ВидСобытия, Объект) Экспорт
	
	Результат = Истина;
	
	Если ТипЗнч(Объект) = Тип("СправочникСсылка.ПоказателиПроцессов")
		И ВидСобытия = Справочники.ВидыБизнесСобытий.ИзменениеЗначенияПоказателяПроцесса Тогда
		
		НастройкаПроцентноеИзменение = РегистрыСведений.НастройкиУведомлений.ПолучитьДополнительнуюНастройку(
			Пользователь,
			Перечисления.НастройкиУведомлений.ПроцентноеИзменение);
		Если НастройкаПроцентноеИзменение <> 0 Тогда
			ТекущиеДанные = РегистрыСведений.ЗначенияПоказателейПроцессов.ТекущиеДанные(Объект);
			Результат = (ТекущиеДанные <> Неопределено)
				И (ТекущиеДанные.ИзменениеПроцент >= НастройкаПроцентноеИзменение);
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Определяет вид уведомления программы.
//
// Параметры:
//  ВидСобытия - СправочникСсылка.ВидыБизнесСобытий, ПеречислениеСсылка.СобытияУведомлений - Вид события.
// 
// Возвращаемое значение:
//  ПеречислениеСсылка.ВидыУведомленийПрограммы - Вид уведомления программы.
//
Функция ОпределитьВидУведомления(ВидСобытия) Экспорт
	
	ВидУведомления = Перечисления.ВидыУведомленийПрограммы.Информация;
	Если ВидСобытия = ПросроченаЗадачаАвтора
		Или ВидСобытия = ПодошелСрокДействияДокумента
		Или ВидСобытия = ЗакончилсяСрокДействияДокумента
		Или ВидСобытия = ПодошелСрокКонтроля
		Или ВидСобытия = ПросроченКонтроль
		Или ВидСобытия = ПросроченаКонтрольнаяТочка
		Или ВидСобытия = ПодошелСрокКонтрольнойТочки
		Или ВидСобытия = ПросроченаОценкаКонтрольнойТочки
		Или ВидСобытия = ОтсутствуетАвансовыйОтчет
		Или ВидСобытия = ПодошелСрокЗаявкиНаОплату
		Или ВидСобытия = СводкаОРабочемВремени Тогда
		ВидУведомления = Перечисления.ВидыУведомленийПрограммы.Предупреждение;
	ИначеЕсли ВидСобытия = УведомлениеПрограммы Тогда
		ВидУведомления = Перечисления.ВидыУведомленийПрограммы.Ошибка;
	КонецЕсли;
	
	Возврат ВидУведомления;
	
КонецФункции

#КонецОбласти

#КонецЕсли