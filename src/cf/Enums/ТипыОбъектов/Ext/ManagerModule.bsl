#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает тип объекта по ссылке.
// 
// Параметры:
//  СсылкаНаОбъект Ссылка на объект
// 
// Возвращаемое значение:
//  ПеречислениеСсылка.ТипыОбъектов,
//  Неопределено - если не удалось определить тип по ссылке.
//  
Функция ТипОбъектаПоСсылке(СсылкаНаОбъект) Экспорт
	
	Если ТипЗнч(СсылкаНаОбъект) = Тип("СправочникСсылка.ДокументыПредприятия") Тогда
		Возврат ДокументыПредприятия;
	ИначеЕсли ТипЗнч(СсылкаНаОбъект) = Тип("СправочникСсылка.Контрагенты") Тогда
		Возврат Контрагенты;
	ИначеЕсли ТипЗнч(СсылкаНаОбъект) = Тип("СправочникСсылка.Мероприятия") Тогда
		Возврат Мероприятия;
	ИначеЕсли ТипЗнч(СсылкаНаОбъект) = Тип("СправочникСсылка.Пользователи") Тогда
		Возврат Пользователи;
	ИначеЕсли ТипЗнч(СсылкаНаОбъект) = Тип("СправочникСсылка.Файлы") Тогда
		Возврат Файлы;
	ИначеЕсли ТипЗнч(СсылкаНаОбъект) = Тип("СправочникСсылка.Проекты") Тогда
		Возврат Проекты;
	ИначеЕсли ТипЗнч(СсылкаНаОбъект) = Тип("СправочникСсылка.ПроектныеЗадачи") Тогда
		Возврат ПроектныеЗадачи;
	ИначеЕсли ТипЗнч(СсылкаНаОбъект) = Тип("ЗадачаСсылка.ЗадачаИсполнителя") Тогда
		Возврат ЗадачиПроцессов;
	ИначеЕсли ТипЗнч(СсылкаНаОбъект) = Тип("ДокументСсылка.Задача") Тогда
		Возврат Задача;
	ИначеЕсли ТипЗнч(СсылкаНаОбъект) = Тип("ДокументСсылка.ДействиеЗадачи") Тогда 
		Возврат ДействиеЗадачи;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ОбработкаПолученияДанныхВыбора(ДанныеВыбора, Параметры, СтандартнаяОбработка)
	
	Перем Потребитель;
	
	Параметры.Свойство("Потребитель", Потребитель);
	
	Если ТипЗнч(Потребитель) = Тип("СправочникСсылка.АвтоподстановкиДляОбъектов") Тогда
		
		ПодходящиеТипы = Новый Массив;
		ПодходящиеТипы.Добавить(ДокументыПредприятия);
		ПодходящиеТипы.Добавить(Мероприятия);
		ПодходящиеТипы.Добавить(ЗадачиПроцессов);
		
		Параметры.Отбор.Вставить("Ссылка", ПодходящиеТипы);
		
	ИначеЕсли ТипЗнч(Потребитель) = Тип("СправочникСсылка.АлгоритмыПроверки") Тогда
		
		ПодходящиеТипы = Новый Массив;
		ПодходящиеТипы.Добавить(ДокументыПредприятия);
		ПодходящиеТипы.Добавить(ДействиеЗадачи);
		ПодходящиеТипы.Добавить(Контрагенты);
		ПодходящиеТипы.Добавить(Мероприятия);
		ПодходящиеТипы.Добавить(Файлы);
		ПодходящиеТипы.Добавить(Проекты);
		ПодходящиеТипы.Добавить(ПроектныеЗадачи);
		ПодходящиеТипы.Добавить(Пользователи);
		
		Параметры.Отбор.Вставить("Ссылка", ПодходящиеТипы);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// По значению типа объекта понимает, какой это объект метаданных. Для механизма алгоритмов проверки.
// 
// Параметры:
//  ТипОбъекта - ПеречислениеСсылка.ТипыОбъектов -
//  ВыдатьОшибку - Булево - Выдать ошибку, если передан тип объекта, не являющийся метаданными объекта.
// 
// Возвращаемое значение:
//  ОбъектМетаданныхСправочник, ОбъектМетаданныхБизнесПроцесс, ОбъектМетаданныхДокумент, Неопределено -
Функция МетаданныеПоТипуОбъекта(ТипОбъекта, ВыдатьОшибку = Истина) Экспорт
	
	ИмяЗначенияПеречисления = Метаданные.Перечисления.ТипыОбъектов.ЗначенияПеречисления[Индекс(ТипОбъекта)].Имя;
	
	Если Метаданные.Справочники.Найти(ИмяЗначенияПеречисления) <> Неопределено Тогда
		Возврат Метаданные.Справочники[ИмяЗначенияПеречисления];
	ИначеЕсли Метаданные.БизнесПроцессы.Найти(ИмяЗначенияПеречисления) <> Неопределено Тогда
		Возврат Метаданные.БизнесПроцессы[ИмяЗначенияПеречисления];
	ИначеЕсли Метаданные.Документы.Найти(ИмяЗначенияПеречисления) <> Неопределено Тогда
		Возврат Метаданные.Документы[ИмяЗначенияПеречисления];
	Иначе
		Если ВыдатьОшибку Тогда
			ВызватьИсключение НСтр("ru = 'Непредвиденное значение типа объекта, обратитесь к администратору.'");
		КонецЕсли;
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

// Возвращает тип значения предмета из алгоритма проверки по типу объекта
// Параметры:
//  ТипОбъекта - ПеречислениеСсылка.ТипыОбъектов -
//
// Возвращаемое значение - Тип
Функция ТипПоТипуОбъекта(ТипОбъекта) Экспорт
	
	ИмяЗначенияПеречисления = Метаданные.Перечисления.ТипыОбъектов.ЗначенияПеречисления[Индекс(ТипОбъекта)].Имя;
	
	Если Метаданные.Справочники.Найти(ИмяЗначенияПеречисления) <> Неопределено Тогда
		Возврат ТипЗнч(Справочники[ИмяЗначенияПеречисления].ПустаяСсылка());
	ИначеЕсли Метаданные.БизнесПроцессы.Найти(ИмяЗначенияПеречисления) <> Неопределено Тогда
		Возврат ТипЗнч(БизнесПроцессы[ИмяЗначенияПеречисления].ПустаяСсылка());
	ИначеЕсли Метаданные.Документы.Найти(ИмяЗначенияПеречисления) <> Неопределено Тогда
		Возврат ТипЗнч(Документы[ИмяЗначенияПеречисления].ПустаяСсылка());
	КонецЕсли;
	
	ВызватьИсключение НСтр("ru = 'Непредвиденное значение типа объекта, обратитесь к администратору.'");
	
КонецФункции

#КонецОбласти

#КонецЕсли