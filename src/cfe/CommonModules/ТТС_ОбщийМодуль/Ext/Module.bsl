
&НаКлиенте
&ИзменениеИКонтроль("ВставитьШтрихкодPDFПродолжение")
Процедура ЦППК_ВставитьШтрихкодPDFПродолжение(НастройкиПоложенияШК, Параметры)

	ТекущиеДанные = Параметры.ТекущиеДанные;
	ТекущийФайл = Параметры.ТекущийФайл;
	ДанныеОШтрихкодеФайла = Параметры.ДанныеОШтрихкодеФайла;

	Если НастройкиПоложенияШК = Неопределено Тогда	
		Возврат;
	КонецЕсли;
	
#Удаление	
	Если ТекущиеДанные.Расширение = "doc" Тогда
#КонецУдаления		
#Вставка
// Добавление ШК в колонтитул для docx через COM объект / 23.01.2024г. / Князев Д.С.
	Если ТекущиеДанные.Расширение = "doc" Или ТекущиеДанные.Расширение = "docx" Тогда
#КонецВставки

		#Если НЕ ВебКлиент Тогда
			Если ДанныеОШтрихкодеФайла <> Неопределено
				И ДанныеОШтрихкодеФайла.Свойство("ДвоичныеДанныеФайла")
				И (НастройкиПоложенияШК.ВысотаШК <> ДанныеОШтрихкодеФайла.НастройкиШтрихкода.ВысотаШК
				ИЛИ НастройкиПоложенияШК.ПоказыватьЦифры <> ДанныеОШтрихкодеФайла.НастройкиШтрихкода.ПоказыватьЦифры) Тогда
				ДанныеОШтрихкодеФайла.ДвоичныеДанныеИзображения = 
				ШтрихкодированиеСервер.ПолучитьКартинкуШтрихкода(ДанныеОШтрихкодеФайла.Штрихкод,, НастройкиПоложенияШК.ВысотаШК, НастройкиПоложенияШК.ПоказыватьЦифры).ПолучитьДвоичныеДанные(); 
			КонецЕсли;

			Состояние(НСтр("ru = 'Выполняется вставка штрихкода в файл.
			|Пожалуйста, подождите...'"));

			Если ДанныеОШтрихкодеФайла <> Неопределено И ДанныеОШтрихкодеФайла.Свойство("ДвоичныеДанныеФайла") Тогда
				Результат = ШтрихкодированиеКлиентСервер.ВставитьШтрихкодСИспользованиемНастроек(ТекущийФайл, 
				НастройкиПоложенияШК, 
				Истина, 
				ДанныеОШтрихкодеФайла.ДвоичныеДанныеИзображения, 
				ДанныеОШтрихкодеФайла.ДвоичныеДанныеФайла,
				ДанныеОШтрихкодеФайла.Расширение,
				ДанныеОШтрихкодеФайла.ФайлРедактируется, 
				ДанныеОШтрихкодеФайла.ИзменениеФайловMSWordТолькоНаСервере);
			Иначе
				Результат = Ложь;
			КонецЕсли;

			Если НЕ Результат Тогда
				Результат = ШтрихкодированиеСервер.ВставитьШтрихкод(ТекущийФайл, ДанныеОШтрихкодеФайла);
			КонецЕсли;
			Состояние();
		#Иначе
			Состояние(НСтр("ru = 'Выполняется вставка штрихкода в файл.
			|Пожалуйста, подождите...'"));
			Результат = ШтрихкодированиеСервер.ВставитьШтрихкод(ТекущийФайл, ДанныеОШтрихкодеФайла);
			Состояние();
		#КонецЕсли
	Иначе
		Результат = ШтрихкодированиеСервер.ВставитьШтрихкод(ТекущийФайл, ДанныеОШтрихкодеФайла);
	КонецЕсли;

	Если Результат Тогда

		// Производим преобразование в PDF.
		ПреобразоватьПрикрепленныйФайлВPdf(ТекущийФайл, ДанныеОШтрихкодеФайла.Расширение);

		Текст = НСтр("ru = 'Изображение штрихкода успешно вставлено в файл и преобразовано в PDF!'");

		ФайлСсылка = ТекущийФайл;
		Оповестить(
		"Запись_Файл", 
		Новый Структура("Событие, Файл, Владелец, ЕстьЗашифрованныеИлиЗанятыеФайлы, ИдентификаторРодительскойФормы", 
		"ДанныеФайлаИзменены", 
		ФайлСсылка, Неопределено, Неопределено,
		Неопределено),
		ФайлСсылка);

	Иначе
		Текст = НСтр("ru = 'Не удалось вставить изображение штрихкода в файл'");
	КонецЕсли;

	ПоказатьПредупреждение(, Текст);

КонецПроцедуры

// Записывает событие по задаче.
//
// Параметры:
//  Задача - Задача.ЗадачаИсполнителя - задача.
//  Событие - ПеречислеСсылка.ВидыСобытийЗадач - событие произошедшее с задачей.
//  Комментарий - Строка - комментарий события.
//  АвторСобытия - СправочникСсылка.Пользователи - пользователь под которым произошло событие,
//                если параметр не задан, то подставляется текущий пользователь.
//
&НаСервере
Процедура ЗаписатьСрокИполненияВРегистр(ПроцессОбъект) Экспорт
	
	Если ПроцессОбъект.Состояние = Перечисления.СостоянияБизнесПроцессов.Активен Тогда
	 	ТипыПредметов = Новый Массив;
		ТипыПредметов.Добавить(Тип("СправочникСсылка.ДокументыПредприятия"));
		ОбрабатываемыеПредметы = МультипредметностьКлиентСервер.ПолучитьМассивПредметовОбъекта(ПроцессОбъект, ТипыПредметов, Истина);
		Если ОбрабатываемыеПредметы.Количество() > 0 Тогда
			Для каждого НайденыйПредмет из ОбрабатываемыеПредметы Цикл
				Если НайденыйПредмет.ВидДокумента.ЯвляетсяЗаявкойНаВозврат Тогда
					Если ПроцессОбъект.СрокИсполнения <> ПроцессОбъект.НовыйСрок Тогда
						МенеджерЗаписи = РегистрыСведений.ИсторияСроковДокументов.СоздатьМенеджерЗаписи();
						МенеджерЗаписи.Документ = НайденыйПредмет;
						МенеджерЗаписи.Период = ТекущаяДата();
						МенеджерЗаписи.СрокРассмотрения = ПроцессОбъект.НовыйСрок;
						МенеджерЗаписи.Установил = ПроцессОбъект.Ссылка;
						МенеджерЗаписи.Записать();
						ДокПредмет = НайденыйПредмет.Ссылка.ПолучитьОбъект();
						ДокПредмет.СрокИсполнения = ПроцессОбъект.НовыйСрок;
						ДокПредмет.Записать();
					КонецЕсли;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// ЦППК Горбачев А.В. 26.07.2024 - САНФ-027024
//
// ++
&НаКлиенте
Процедура ЦППК_ОбработкаЗвездыОценкиЗадачи(Форма, ВыбраннаяЗвезда) Экспорт
	
	Элементы = Форма.Элементы;
	
	Если ТипЗнч(ВыбраннаяЗвезда) = Тип("ДекорацияФормы") Тогда
		ВыбраннаяЗвезда = Число(Прав(ВыбраннаяЗвезда.Имя, 1));
	КонецЕсли;
	
	ШаблонЗаголовка = НСтр("ru = 'Оценка выполнения (%1)'");
	
	Если Форма["ЦППК_ВыбраннаяОценка"] = 1
		И ВыбраннаяЗвезда = 1 Тогда
		
		Форма["ЦППК_ВыбраннаяОценка"] = 0;
		Элементы.ЦППК_Звезда1.Картинка = БиблиотекаКартинок.ОформлениеЗвездаПустая;
		Элементы.ЦППК_ОценкаВыполнения.Заголовок = СтрШаблон(ШаблонЗаголовка, "не выбрана");
		
		Возврат;
		
	КонецЕсли;
	
	Форма["ЦППК_ВыбраннаяОценка"] = ВыбраннаяЗвезда;
	Элементы.ЦППК_ОценкаВыполнения.Заголовок = СтрШаблон(ШаблонЗаголовка, Строка(Форма["ЦППК_ВыбраннаяОценка"]));
	
	Для Счетчик = 1 По 5 Цикл
		
		Картинка = Новый Картинка;
		
		Если Счетчик <= ВыбраннаяЗвезда Тогда
			Картинка = БиблиотекаКартинок.ОформлениеЗвездаЗаполненная;
		Иначе
			Картинка = БиблиотекаКартинок.ОформлениеЗвездаПустая;
		КонецЕсли;
		
		Элементы["ЦППК_Звезда" + Строка(Счетчик)].Картинка = Картинка;
	
	КонецЦикла;
	
КонецПроцедуры
// --
