
&ИзменениеИКонтроль("ОбработкаВыбора")
Процедура ЦППК_ОбработкаВыбора(Форма, Список, Поле, СтандартнаяОбработка) Экспорт
	
	ТекущиеДанные = ПолучитьДанныеТекущейСтрокиСписка(Список, Список.ТекущаяСтрока);
		
	Если ТекущиеДанные = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	СистемнаяИнформация = Новый СистемнаяИнформация();
	ИдентификаторКлиента = СистемнаяИнформация.ИдентификаторКлиента;
	
	Если СтрНайти(Поле.Имя, "Файлы") Тогда
		ПараметрыОткрытия = Новый Структура("Ключ, ОткрытьЗакладкуФайлы, ИдентификаторКлиента", 
			ТекущиеДанные.Ссылка, Истина, ИдентификаторКлиента);
		ОткрытьФорму("Справочник.ДокументыПредприятия.ФормаОбъекта", ПараметрыОткрытия, Список);
		Возврат;
	КонецЕсли;
	
	Если СтрНайти(Поле.Имя, "Подписан") Тогда
		ПараметрыОткрытия = Новый Структура("Ключ, ОткрытьЗакладкуЭП, ИдентификаторКлиента", 
			ТекущиеДанные.Ссылка, Истина, ИдентификаторКлиента);
		ОткрытьФорму("Справочник.ДокументыПредприятия.ФормаОбъекта", ПараметрыОткрытия, Список);
		Возврат;
	КонецЕсли;
	
	Если СтрНайти(Поле.Имя,"Задачи") Тогда
		ПараметрыОткрытия = Новый Структура("Ключ, ОткрытьЗакладкуОбработка, ИдентификаторКлиента", 
			ТекущиеДанные.Ссылка, Истина, ИдентификаторКлиента);
		ОткрытьФорму("Справочник.ДокументыПредприятия.ФормаОбъекта", ПараметрыОткрытия, Список);
		Возврат;
	КонецЕсли;
	
	Если СтрНайти(Поле.Имя, "ЕстьКатегории") Тогда
		ПараметрыОткрытия = Новый Структура("Ключ, ОткрытьЗакладкуКатегории, ИдентификаторКлиента", 
			ТекущиеДанные.Ссылка, Истина, ИдентификаторКлиента);
		ОткрытьФорму("Справочник.ДокументыПредприятия.ФормаОбъекта", ПараметрыОткрытия, Список);
		Возврат;
	КонецЕсли;
	
	Если СтрНайти(Поле.Имя, "СостояниеКонтроля") Тогда
		КонтрольКлиент.ОбработкаКомандыКонтроль(ТекущиеДанные.Ссылка, Форма);
		Возврат;
	КонецЕсли;
	
	Если СтрНайти(Поле.Имя, "КоличествоВажныхСвязей") Тогда
		ДелопроизводствоКлиент.ОткрытьФормуСвязи(ТекущиеДанные.Ссылка);
		Возврат;
	КонецЕсли;
	
	ПараметрыОткрытия = Новый Структура("Ключ, ИдентификаторКлиента", ТекущиеДанные.Ссылка, ИдентификаторКлиента);
#Удаление
	ОткрытьФорму("Справочник.ДокументыПредприятия.ФормаОбъекта", ПараметрыОткрытия, Список);
#КонецУдаления
#Вставка

	КлючеваяОперация = "ОткрытиеФормыЭлементаСправочникаДокументыПредприятияИзСпискаДокументов";
	УИДЗамера = ОценкаПроизводительностиКлиент.ЗамерВремени(КлючеваяОперация, , Ложь);

	ОткрытьФорму("Справочник.ДокументыПредприятия.ФормаОбъекта", ПараметрыОткрытия, Список);
	
	ОценкаПроизводительностиКлиент.ЗавершитьЗамерВремени(УИДЗамера);

#КонецВставки
	
КонецПроцедуры
