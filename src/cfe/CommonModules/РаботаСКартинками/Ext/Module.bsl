
&ИзменениеИКонтроль("УвеличитьРегШтамп")
Процедура ЦППК_УвеличитьРегШтамп(ИмяФайлаШтампа, ВременнаяПапкаДляРазархивирования) Экспорт
	
	ФайлыКУдалению = Новый Массив;
	
    #Удаление
	Параметры = СтрШаблон("mogrify -resize 960x130 %1",
		ИмяФайлаШтампа);
    #КонецУдаления
	#Вставка
	Параметры = СтрШаблон("mogrify -resize 1500x3000 %1",
		ИмяФайлаШтампа);
	#КонецВставки
	
	ИмяBatФайла = ВременнаяПапкаДляРазархивирования + ПолучитьРазделительПути() + "cnv.bat";
	ЗапуститьImageMagick(Параметры, ФайлыКУдалению, ИмяBatФайла,, ВременнаяПапкаДляРазархивирования);

КонецПроцедуры

&ИзменениеИКонтроль("КоординатыПоНастройкам")
Функция ЦППК_КоординатыПоНастройкам(Настройки, Размеры, НомерКартинки) Экспорт
	
	Координаты = НовыйКоординаты();
	Расположение = Настройки.ПоложениеНаСтранице;
	
	ЧислоШтамповВСтолбце = 10;           
	НомерСтолбца = Цел(НомерКартинки / ЧислоШтамповВСтолбце);
	НомерКартинкиY = НомерКартинки % ЧислоШтамповВСтолбце;
	
	// А4 - 210х297 мм, если ориентация 297х210 или другие размеры, то пока отработает некорректно.
	// Определение размеров страницы пока не поддерживается.
	
#Удаление
	Если Расположение = Перечисления.МестаВставкиКартинки.ЛевыйВерхний Тогда
		
		Координаты.Слева = 0 + НомерСтолбца * Размеры.Ширина;
		Координаты.Сверху = 5 + Размеры.Высота * НомерКартинкиY;
		
	ИначеЕсли Расположение = Перечисления.МестаВставкиКартинки.ЛевыйНижний Тогда
		
		Координаты.Слева = 0 + НомерСтолбца * Размеры.Ширина;
		Координаты.Сверху = 297 - (Размеры.Высота + Размеры.Высота * НомерКартинкиY);
		
	ИначеЕсли Расположение = Перечисления.МестаВставкиКартинки.ПравыйВерхний Тогда
		
		Координаты.Слева = 210 - Размеры.Ширина - НомерСтолбца * Размеры.Ширина;
		Координаты.Сверху = 5 + Размеры.Высота * НомерКартинкиY;
		
	ИначеЕсли Расположение = Перечисления.МестаВставкиКартинки.ПравыйНижний Тогда
		
		Координаты.Слева = 210 - Размеры.Ширина - НомерСтолбца * Размеры.Ширина;
		Координаты.Сверху = 297 - (Размеры.Высота + Размеры.Высота * НомерКартинкиY);
		
#КонецУдаления
#Вставка
	Если Расположение = Перечисления.МестаВставкиКартинки.ЛевыйВерхний Тогда

		Координаты.Слева = 1;
		Координаты.Сверху = 1 + Размеры.Высота * НомерКартинки;

	ИначеЕсли Расположение = Перечисления.МестаВставкиКартинки.ЛевыйНижний Тогда

		Координаты.Слева = 1;
		Координаты.Сверху = 300 - (Размеры.Высота + Размеры.Высота * НомерКартинки);

	ИначеЕсли Расположение = Перечисления.МестаВставкиКартинки.ПравыйВерхний Тогда

		Координаты.Слева = 220 - Размеры.Ширина;
		Координаты.Сверху = 1 + Размеры.Высота * НомерКартинки;

	ИначеЕсли Расположение = Перечисления.МестаВставкиКартинки.ПравыйНижний Тогда

		Координаты.Слева = 220 - Размеры.Ширина;
		Координаты.Сверху = 300 - (Размеры.Высота + Размеры.Высота * НомерКартинки);
#КонецВставки

	ИначеЕсли Расположение = Перечисления.МестаВставкиКартинки.ПроизвольноеПоложение Тогда
		
		Координаты.Слева = ОбщегоНазначенияДокументооборотКлиентСервер.СтрокаВЧисло(Настройки.СмещениеПоГоризонтали);
		Если Координаты.Слева < 0 Тогда
			Координаты.Слева = 0;
		ИначеЕсли Координаты.Слева > 210 Тогда
			Координаты.Слева = 210;
		КонецЕсли;
		
		Координаты.Сверху = ОбщегоНазначенияДокументооборотКлиентСервер.СтрокаВЧисло(Настройки.СмещениеПоВертикали);
		Если Координаты.Сверху < 0 Тогда
			Координаты.Сверху = 0;
		ИначеЕсли Координаты.Сверху >= 297 Тогда 
			Координаты.Сверху = 297;
		КонецЕсли;
		
	КонецЕсли;
	
	
	Возврат Координаты;
	
КонецФункции

&ИзменениеИКонтроль("СформироватьШтампПЭП")
Функция ЦППК_СформироватьШтампПЭП(ДанныеПодписанта, Формат, ИтоговыйФорматPdf)

	ПрозрачныйФон = Истина; 
	Если ИтоговыйФорматPdf = Истина Тогда	
		ПрозрачныйФон = Ложь; 
	КонецЕсли;	

	ТекстШтампа = ТекстШтампаПЭП(ДанныеПодписанта, ПрозрачныйФон);

	Поток = Новый ПотокВПамяти();
	ЗаписьТекста = Новый ЗаписьТекста(Поток, "utf-8");
	ЗаписьТекста.Записать(ТекстШтампа);
	ЗаписьТекста.Закрыть();

	ДанныеФайла = Поток.ЗакрытьИПолучитьДвоичныеДанные();
	ШтампSvg = Новый Картинка(ДанныеФайла);
	ШтампPng = ШтампSvg.Преобразовать(ФорматКартинки.PNG);

	ДанныеФайлаPng = ШтампPng.ПолучитьДвоичныеДанные(); 
	Штамп = Новый Картинка(ДанныеФайлаPng, Истина);

	ПутьНовогоФайла = ПолучитьИмяВременногоФайла("PNG");
	Штамп.Записать(ПутьНовогоФайла);

	Возврат ПутьНовогоФайла;

КонецФункции

&ИзменениеИКонтроль("НаложитьРегШтамп")
Функция ЦППК_НаложитьРегШтамп(
	ПутьPng, ИмяФайлаРегШтамп, ВременнаяПапкаДляРазархивирования, Положение, НастройкиРегНомера, НомерШтампа) Экспорт
	
	ПутьНовогоФайла = ВременнаяПапкаДляРазархивирования 
		+ ПолучитьРазделительПути() + СтрШаблон("regNew%1.png", НомерШтампа);
	ИсходныйФайл = ПутьPng;
	
	ОтступX = ""; //как строка
	ОтступY = "";
	
	ПоложениеШтампаСтрока = "NorthWest";
	Если МЭДО.ЭтоЗаполненноеПоложениеИзображения(Положение) Тогда
		
		РазмерыШтампа = ВсеРазмерыКартинки(ИмяФайлаРегШтамп);
		РазмерыСтраницы = ВсеРазмерыКартинки(ПутьPng);
		Если РазмерыСтраницы.Нулевые Тогда
			Возврат ПутьPng; // Ничего не преобразуем, картинка страницы остается самой собой.
		КонецЕсли;
		
		// Плотность у картинки не всегда правильно определяется, через пропорцию нужно высчитать правильную 
		// размер штампа. Небольшой погрещностью можно пренебречь.
		НоваяШирина = Окр(Положение.Ширина / РазмерыСтраницы.ШиринаМм * РазмерыСтраницы.ШиринаПикс);
		НоваяВысота = Окр(Положение.Высота / РазмерыСтраницы.ВысотаМм * РазмерыСтраницы.ВысотаПикс);
#Вставка
		//++AxelSoft Шарапова 09.09.2024 САНФ-028417
		// Расчет коэффициента исходя из размеров страницы А4
		КоэффициентШирина = Окр(РазмерыСтраницы.ШиринаМм / 210);
		КоэффициентВысота = Окр(РазмерыСтраницы.ВысотаМм / 297);

		НоваяШирина = НоваяШирина * КоэффициентШирина;
		НоваяВысота = НоваяВысота * КоэффициентВысота;
		//--AxelSoft Шарапова 09.09.2024 САНФ-028417
#КонецВставки
		Если МодульЧисла(НоваяШирина - РазмерыШтампа.ШиринаПикс) / РазмерыШтампа.ШиринаПикс > 0.02
			Или МодульЧисла(НоваяВысота - РазмерыШтампа.ВысотаПикс) / РазмерыШтампа.ВысотаПикс > 0.02 Тогда
			// Масштабируем и приводим штамп к такой-же плотности, как саму страницу:
			Dpi = ФайловыеФункцииПовтИсп.КачествоПреобразованияPDFДляПредпросмотра();
			МасштабироватьКартинку(ИмяФайлаРегШтамп, НоваяШирина, НоваяВысота, Dpi, ВременнаяПапкаДляРазархивирования);
		КонецЕсли;
		
		ПоложениеШтампаСтрока = "NorthWest";
		ОтступX = "+" + Формат(Положение.Слева * РазмерыСтраницы.ПлотностьПоГоризонтали / 25.4, "ЧДЦ=0; ЧГ=0;");
		ОтступY = "+" + Формат(Положение.Сверху * РазмерыСтраницы.ПлотностьПоВертикали / 25.4, "ЧДЦ=0; ЧГ=0;");
#Вставка
		//++AxelSoft Шарапова 09.09.2024 САНФ-028417
		ОтступX = "+" + Формат(РазмерыСтраницы.ШиринаПикс - НоваяШирина + 25 * КоэффициентШирина, "ЧДЦ=0; ЧГ=0;");
		ОтступY = "+" + Формат(РазмерыСтраницы.ВысотаПикс - НоваяВысота + 25 * КоэффициентВысота, "ЧДЦ=0; ЧГ=0;");
		//--AxelSoft Шарапова 09.09.2024 САНФ-028417
#КонецВставки
		
	Иначе
		РасположениеШтампа = НастройкиРегНомера.ПоложениеНаСтранице;
		
		Если РасположениеШтампа = Перечисления.МестаВставкиКартинки.ЛевыйВерхний Тогда
			ПоложениеШтампаСтрока = "NorthWest";
			ОтступX = "+120";
			ОтступY =  СтрШаблон("+%1", Формат(120, "ЧГ=0;"));
		ИначеЕсли РасположениеШтампа = Перечисления.МестаВставкиКартинки.ЛевыйНижний Тогда
			ПоложениеШтампаСтрока = "SouthWest";
			ОтступX = "+120";
			ОтступY =  СтрШаблон("+%1", Формат(120, "ЧГ=0;"));
		ИначеЕсли РасположениеШтампа = Перечисления.МестаВставкиКартинки.ПравыйВерхний Тогда
			ПоложениеШтампаСтрока = "NorthEast";
			ОтступX = "+60";
			ОтступY =  СтрШаблон("+%1", Формат(120, "ЧГ=0;"));
		ИначеЕсли РасположениеШтампа = Перечисления.МестаВставкиКартинки.ПравыйНижний Тогда
			ПоложениеШтампаСтрока = "SouthEast";
#Удаление
			ОтступX = "+60";
			ОтступY =  СтрШаблон("+%1", Формат(120, "ЧГ=0;"));
#КонецУдаления
#Вставка
			ОтступX = "-80";
			ОтступY =  СтрШаблон("-%1", Формат(20, "ЧГ=0;"));
#КонецВставки
		ИначеЕсли РасположениеШтампа = Перечисления.МестаВставкиКартинки.ПроизвольноеПоложение Тогда
			ПоложениеШтампаСтрока = "NorthWest";
			ОтступX = "+" + СокрЛП(Строка(НастройкиРегНомера.СмещениеПоГоризонтали));
			ОтступY = "+" + СокрЛП(Строка(НастройкиРегНомера.СмещениеПоВертикали));
		КонецЕсли;
		
	КонецЕсли;
	
	ФайлыКУдалению = Новый Массив;
	
	Параметры = СтрШаблон("composite -colorspace rgb -gravity %1 -geometry %2%3 %4 %5 %6",
		ПоложениеШтампаСтрока,
		ОтступX,
		ОтступY,
		ИмяФайлаРегШтамп,
		ИсходныйФайл,
		ПутьНовогоФайла);
		
	ИмяBatФайла = ВременнаяПапкаДляРазархивирования + ПолучитьРазделительПути() + "cnv.bat";
	ЗапуститьImageMagick(Параметры, ФайлыКУдалению,ИмяBatФайла,,ВременнаяПапкаДляРазархивирования);
	
	УдалитьФайлы(ПутьPng);
	
	Возврат ПутьНовогоФайла;
	
КонецФункции
