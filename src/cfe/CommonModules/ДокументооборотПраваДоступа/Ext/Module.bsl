
&ИзменениеИКонтроль("ДокументооборотПраваДоступаПриЗаписиФайла")
Процедура ЦППК_ДокументооборотПраваДоступаПриЗаписиФайла(Источник, Отказ)

	Если МиграцияПриложенийПереопределяемый.ЭтоЗагрузка(Источник) Тогда
		Возврат;
	КонецЕсли;

	Если Не ДокументооборотПраваДоступаПовтИсп.ВключеноИспользованиеПравДоступа() Тогда
		Возврат;
	КонецЕсли;
#Вставка
	Если ТипЗнч(Источник) = Тип("СправочникОбъект.Файлы") 
		И ТипЗнч(Источник.ВладелецФайла) = Тип("СправочникСсылка.ДокументыПредприятия")
		И Источник.ВладелецФайла.ДатаСоздания < Дата("20240101") Тогда
		Возврат;
	КонецЕсли;
#КонецВставки

	Если ДокументооборотПраваДоступаХолдинг.ДобавитьЗаданиеОпределитьПраваФайла(Источник) Тогда
		Возврат;
	КонецЕсли;

	ЕстьИзменения = ОпределитьПраваФайла(Источник);

	// Проверка прав на запись файла.
	Если ПривилегированныйРежим()
		Или ПользователиСерверПовтИсп.ЭтоПолноправныйПользовательИБ()
		Или (ОбщегоНазначенияДокументооборот.ЭтоДокументооборотХолдинга()
		И ОбщегоНазначенияДокументооборотХолдинг.ЭтоЗагрузкаОбъекта(Источник)) Тогда
		Возврат;
	КонецЕсли;

	ПроверяемоеПраво = "Изменение";
	Если Источник.ДополнительныеСвойства.ЭтоНовый
		Или ЕстьИзменения И ТипЗнч(Источник.ВладелецФайла) = Тип("СправочникСсылка.ПапкиФайлов") Тогда
		ПроверяемоеПраво = "Добавление";
	ИначеЕсли Источник.ДополнительныеСвойства.ЭтоУдаление Тогда
		ПроверяемоеПраво = "Удаление";
	КонецЕсли;
	
#Вставка
	//++AxelSoft Шарапова 06.09.2024 САНФ-028422
	Если ТипЗнч(Источник) = Тип("СправочникОбъект.Файлы") 
		И ТипЗнч(Источник.ВладелецФайла) = Тип("СправочникСсылка.Контрагенты") Тогда
		Возврат;
	КонецЕсли;
	//--AxelSoft Шарапова 06.09.2024 САНФ-028422
#КонецВставки

	Права = ПраваПользователяПоОбъекту(Источник.Ссылка);
	НетПрав = Не Права[ПроверяемоеПраво];
	Если НетПрав Тогда
		ТекстОшибки = СтрШаблон(
		НСтр("ru = 'Недостаточно прав для выполнения операции с файлом. 
		|Обратитесь к администратору.
		|
		|Файл: %1
		|Право: %2'"),
		Источник, ПроверяемоеПраво);
		Если ПроверяемоеПраво = "Добавление" Тогда
			ТекстОшибки = СтрШаблон(
			НСтр("ru = 'Недостаточно прав для добавления файла в папку
			|
			|Файл: %1
			|Папка: %2'"),
			Источник, Источник.ВладелецФайла);
		КонецЕсли;

		ВызватьИсключение ТекстОшибки;
	КонецЕсли;

КонецПроцедуры
