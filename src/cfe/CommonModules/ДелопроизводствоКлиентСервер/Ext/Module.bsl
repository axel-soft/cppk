
&ИзменениеИКонтроль("СформироватьПредставлениеСрокаДействия")
Функция ЦППК_СформироватьПредставлениеСрокаДействия(ДокументПредприятия, Комментарий = "")

	Результат = "";

	Если ЗначениеЗаполнено(ДокументПредприятия.ДатаНачалаДействия) Тогда
		Результат = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'с %1'"),
		Формат(ДокументПредприятия.ДатаНачалаДействия, "ДЛФ=D")); 
	КонецЕсли;

	Если ДокументПредприятия.Бессрочный Тогда
		ДобавитьЗначениеКСтрокеЧерезРазделитель(Результат, ", ", НСтр("ru = 'бессрочный'"));
	ИначеЕсли ЗначениеЗаполнено(ДокументПредприятия.ДатаОкончанияДействия) Тогда
		ДобавитьЗначениеКСтрокеЧерезРазделитель(
		Результат,
		" ",
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'по %1'"),
		Формат(ДокументПредприятия.ДатаОкончанияДействия, "ДЛФ=D")));
	КонецЕсли;

	Если ЗначениеЗаполнено(ДокументПредприятия.ПорядокПродления) Тогда
		Если ДокументПредприятия.ПорядокПродления = ПредопределенноеЗначение(
			"Перечисление.ПорядокПродления.НеПродлевается") Тогда 
			ДобавитьЗначениеКСтрокеЧерезРазделитель(
			Результат,
			Символы.ПС,
			НСтр("ru = 'Не продлевается'"));
		ИначеЕсли ДокументПредприятия.ПорядокПродления = ПредопределенноеЗначение(
			"Перечисление.ПорядокПродления.ДопускаетПродление") Тогда 
			ДобавитьЗначениеКСтрокеЧерезРазделитель(
			Результат,
			Символы.ПС,
			НСтр("ru = 'Допускает продление'"));
		Иначе		
			ДобавитьЗначениеКСтрокеЧерезРазделитель(
			Результат,
			Символы.ПС,
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Продлевается %1'"),
			НРег(ДокументПредприятия.ПорядокПродления)));
		КонецЕсли;		
	КонецЕсли;

	Если ЗначениеЗаполнено(Комментарий) Тогда 
		ДобавитьЗначениеКСтрокеЧерезРазделитель(
		Результат,
		"",
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = '. %1'"),
		Комментарий));
	КонецЕсли;   
#Вставка
	//Zayc + 23.05.2016 Добавляем комментарий в поле срока документа для реквизита до исполнения обязателсьтв		
	Структура = Новый Структура("ДоИсполненияОбязательств", NULL);
	ЗаполнитьЗначенияСвойств(Структура, ДокументПредприятия);
	Если Структура.ДоИсполненияОбязательств <> NULL Тогда
		Если ДокументПредприятия.ДоИсполненияОбязательств Тогда
			ДобавитьЗначениеКСтрокеЧерезРазделитель(
			Результат,
			", ",
			НСтр("ru = 'до исполнения обязательств'"));
		КонецЕсли;
	КонецЕсли;
	//Zayc - 23.05.2016 
#КонецВставки

	Возврат Результат;

КонецФункции

// Возвращает Истина, если установлен флаг "цппк_ИспользоватьФормуПодписиФайлов"
Функция ЦППК_ИспользоватьФормуПодписиФайлов(Значение) Экспорт
	
	Если Не ЭтоДокументПредприятия(Значение) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если ТипЗнч(Значение) = Тип("СправочникСсылка.ДокументыПредприятия") Тогда
		ИспользоватьФормуПодписиФайлов = ОбщегоНазначенияДокументооборотВызовСервера.ЗначениеРеквизитаОбъекта(
			Значение, "ВидДокумента.цппк_ИспользоватьФормуПодписиФайлов");
	Иначе
		ИспользоватьФормуПодписиФайлов = ОбщегоНазначенияДокументооборотВызовСервера.ЗначениеРеквизитаОбъекта(
			Значение.ВидДокумента, "цппк_ИспользоватьФормуПодписиФайлов");
	КонецЕсли;
	
	Возврат ИспользоватьФормуПодписиФайлов = Истина;
	
КонецФункции
