
&ИзменениеИКонтроль("ПроверкаЗаполненияФормыНаСервере")
Процедура ЦППК_ПроверкаЗаполненияФормыНаСервере(Форма, Отказ, ПроверяемыеРеквизиты) Экспорт
	
	ВидДокументаКэш = Форма.ВидДокументаКэш;
	ОбъектОснование = Форма.Объект;
	Объект = Форма.Объект;
	Если ВидДокументаКэш.ЯвляетсяОбращениемОтГраждан Тогда 
		ПроверяемыеРеквизиты.Добавить("ВидОбращения");
		
		Если ЗначениеЗаполнено(Форма.ПервичноеОбращение) 
			И Форма.ПервичноеОбращение = Объект.Ссылка Тогда 
			ОбщегоНазначения.СообщитьПользователю(
				НСтр("ru = 'Указана ссылка на самого себя'"),,"ПервичноеОбращение",,Отказ);
		КонецЕсли;	
		
		Если ЗначениеЗаполнено(Форма.ОсновноеОбращение) 
			И Форма.ОсновноеОбращение = Объект.Ссылка Тогда 
			ОбщегоНазначения.СообщитьПользователю(
				НСтр("ru = 'Указана ссылка на самого себя'"),,"ОсновноеОбращение",,Отказ);
		КонецЕсли;
		
		Если ПолучитьФункциональнуюОпцию("РегламентированныйУчетОбращений") Тогда
			Если Форма.ВопросыОбращения.Количество() = 0 Тогда 
				ДатаПроверкиНаличияВопросов = ДелопроизводствоСерверПовтИсп.ДатаПроверкиНаличияВопросов();
				
				Если Не ЗначениеЗаполнено(ДатаПроверкиНаличияВопросов)
					Или (ЗначениеЗаполнено(ДатаПроверкиНаличияВопросов) 
						И ЗначениеЗаполнено(Объект.ДатаРегистрации) И Объект.ДатаРегистрации > ДатаПроверкиНаличияВопросов)
					Или (ЗначениеЗаполнено(ДатаПроверкиНаличияВопросов) 
						И Не ЗначениеЗаполнено(Объект.ДатаРегистрации) И Объект.ДатаСоздания > ДатаПроверкиНаличияВопросов) Тогда 
					ОбщегоНазначения.СообщитьПользователю(
						НСтр("ru = 'Не указано ни одного вопроса документа'"),,
							"КодВопроса",, Отказ);
					Возврат;
				КонецЕсли;
			КонецЕсли;
			
			//Проверка табличной части ВопросыОбращения на задвоения
			КоличествоВопросыОбращения = Форма.ВопросыОбращения.Количество();
			Если КоличествоВопросыОбращения > 1 Тогда
				Для Инд1 = 0 По КоличествоВопросыОбращения - 2 Цикл
					Для Инд2 = Инд1 + 1 По КоличествоВопросыОбращения - 1 Цикл
						Если Форма.ВопросыОбращения[Инд1].Вопрос = Форма.ВопросыОбращения[Инд2].Вопрос Тогда 
							ТекстСообщения = СтрШаблон(
								НСтр("ru = 'Вопрос ""%1"" указан дважды в списке вопросов обращения'"),
								Форма.ВопросыОбращения[Инд2].КодВопроса);
							
							ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,,
								"ВопросыОбращения["+ Формат(Инд2, "ЧН=0; ЧГ=0") +"].КодВопроса",,Отказ);
						КонецЕсли;
					КонецЦикла;
				КонецЦикла;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Если Не ВидДокументаКэш.ЯвляетсяВходящейКорреспонденцией Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Форма.ИсходящаяДата) 
		Или Не ЗначениеЗаполнено(Форма.ИсходящийНомер)
		Или НРег(Форма.ИсходящийНомер) = НСтр("ru = 'б\н'") 
		Или НРег(Форма.ИсходящийНомер) = НСтр("ru = 'б/н'") Тогда 
		Возврат;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
#Удаление
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	Корреспонденция.Ссылка КАК Ссылка,
	|	ДокументОснование.РегистрационныйНомер КАК РегистрационныйНомер
	|ИЗ
	|	Документ.Корреспонденция КАК Корреспонденция
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ДокументыПредприятия КАК ДокументОснование
	|		ПО (ДокументОснование.Ссылка = Корреспонденция.Основание)
	|ГДЕ
	|	ДокументОснование.Ссылка <> &Ссылка
	|	И Корреспонденция.НомерКонтрагента = &НомерКонтрагента
	|	И Корреспонденция.ДатаКонтрагента = &ДатаКонтрагента
	|	И Корреспонденция.Корреспонденты.Корреспондент = &Контрагент
	|	И ДокументОснование.ВидДокумента = &ВидДокумента
	|	И НЕ Корреспонденция.ПометкаУдаления";
#КонецУдаления
#Вставка
	"ВЫБРАТЬ
	|	Корреспонденция.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ втКорреспонденция
	|ИЗ
	|	Документ.Корреспонденция.Корреспонденты КАК КорреспонденцияКорреспонденты
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.Корреспонденция КАК Корреспонденция
	|		ПО (КорреспонденцияКорреспонденты.Корреспондент = &Контрагент)
	|			И КорреспонденцияКорреспонденты.Ссылка = Корреспонденция.Ссылка
	|ГДЕ
	|	Корреспонденция.НомерКонтрагента = &НомерКонтрагента
	|	И Корреспонденция.ДатаКонтрагента = &ДатаКонтрагента
	|	И НЕ Корреспонденция.ПометкаУдаления
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	ДокументОснование.Ссылка КАК Ссылка,
	|	ДокументОснование.РегистрационныйНомер КАК РегистрационныйНомер
	|ИЗ
	|	втКорреспонденция КАК втКорреспонденция
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ДокументыПредприятия КАК ДокументОснование
	|		ПО втКорреспонденция.Ссылка.Основание = ДокументОснование.Ссылка
	|ГДЕ
	|	ДокументОснование.ВидДокумента = &ВидДокумента
	|	И ДокументОснование.Ссылка <> &Ссылка";
#КонецВставки
	
	Запрос.УстановитьПараметр("НомерКонтрагента", СокрЛП(Форма.ИсходящийНомер));
	Запрос.УстановитьПараметр("ДатаКонтрагента", Форма.ИсходящаяДата);
	Запрос.УстановитьПараметр("Контрагент", ОбъектОснование.Контрагент);
	Запрос.УстановитьПараметр("Ссылка", ОбъектОснование.Ссылка);
	
	Запрос.УстановитьПараметр("ВидДокумента", ОбъектОснование.ВидДокумента);
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьУчетПоОрганизациям") Тогда
#Удаление
		Запрос.Текст = Запрос.Текст + " И (Организация = &Организация) ";
#КонецУдаления
#Вставка
		Запрос.Текст = Запрос.Текст + " И (ДокументОснование.Организация = &Организация) ";
#КонецВставки
		Запрос.УстановитьПараметр("Организация", ОбъектОснование.Организация);
	КонецЕсли;
	
	Результат = Запрос.Выполнить();
	Если Не Результат.Пустой() Тогда
		
		Выборка = Результат.Выбрать();
		Выборка.Следующий();
		
		Если ДелопроизводствоКлиентСервер.ДокументЗарегистрирован(Выборка.РегистрационныйНомер) Тогда
			ТекстСообщения = СтрШаблон(
				НСтр("ru = 'Документ №%1 от %2 уже зарегистрирован!'"),
				СокрЛП(Форма.ИсходящийНомер),
				Формат(Форма.ИсходящаяДата, "ДЛФ=D"));
		Иначе
			ТекстСообщения = СтрШаблон(
				НСтр("ru = 'Документ №%1 от %2 уже записан!'"),
				СокрЛП(Форма.ИсходящийНомер),
				Формат(Форма.ИсходящаяДата, "ДЛФ=D"));
		КонецЕсли;
		
		ОбщегоНазначения.СообщитьПользователю(
			ТекстСообщения,,
			"ИсходящийНомер",,
			Отказ);
		
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры
