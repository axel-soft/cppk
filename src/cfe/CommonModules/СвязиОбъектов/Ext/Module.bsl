
&ИзменениеИКонтроль("ПолучитьНастройкиСвязи")
Функция ЦППК_ПолучитьНастройкиСвязи(Объект, СвязанныйОбъект)

	НастройкиСвязи = Новый ТаблицаЗначений;
	НастройкиСвязи.Колонки.Добавить("ТипСвязи");
	НастройкиСвязи.Колонки.Добавить("СсылкаИз");
	НастройкиСвязи.Колонки.Добавить("ТипСсылкаИз");
	НастройкиСвязи.Колонки.Добавить("ТипСсылкаИзПредставление");
	НастройкиСвязи.Колонки.Добавить("СсылкаНа");
	НастройкиСвязи.Колонки.Добавить("ТипСсылкаНа");
	НастройкиСвязи.Колонки.Добавить("ТипСсылкаНаПредставление");
	НастройкиСвязи.Колонки.Добавить("ХарактерСвязи");
	НастройкиСвязи.Колонки.Добавить("ТипОбратнойСвязи");
	НастройкиСвязи.Колонки.Добавить("ХарактерОбратнойСвязи");
	НастройкиСвязи.Колонки.Добавить("Предопределенная");
	НастройкиСвязи.Колонки.Добавить("Обязательная");
	НастройкиСвязи.Колонки.Добавить("СинхронизироватьДоступ");
	НастройкиСвязи.Колонки.Добавить("Важная");
	НастройкиСвязи.Колонки.Добавить("КомментарийСвязи");

	Если Не ЗначениеЗаполнено(Объект) Тогда
		Возврат НастройкиСвязи;
	КонецЕсли;

	ИспользованиеВстроеннойПочты = ПолучитьФункциональнуюОпцию("ИспользоватьВстроеннуюПочту")
	И ПолучитьФункциональнуюОпцию("ИспользованиеВстроеннойПочты");

	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	НастройкаСвязей.ТипСвязи КАК ТипСвязи,
	|	НастройкаСвязей.СсылкаИз КАК СсылкаИз,
	|	НастройкаСвязей.СсылкаНа КАК СсылкаНа,
	|	НастройкаСвязей.ХарактерСвязи КАК ХарактерСвязи,
	|	НастройкаСвязей.ТипОбратнойСвязи КАК ТипОбратнойСвязи,
	|	НастройкаСвязей.ХарактерОбратнойСвязи КАК ХарактерОбратнойСвязи,
	|	НастройкаСвязей.Предопределенная КАК Предопределенная,
	|	НастройкаСвязей.Обязательная КАК Обязательная,
	|	НастройкаСвязей.Комментарий КАК Комментарий,
	|	НастройкаСвязей.СинхронизироватьДоступ КАК СинхронизироватьДоступ,
	|	НастройкаСвязей.Важная КАК Важная,
	|	ТипыСвязей.Комментарий КАК КомментарийСвязи
	|ИЗ
	|	РегистрСведений.НастройкаСвязей КАК НастройкаСвязей
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ТипыСвязей КАК ТипыСвязей
	|		ПО НастройкаСвязей.ТипСвязи = ТипыСвязей.Ссылка
	|ГДЕ
	|	НЕ НастройкаСвязей.ТипСвязи.ПометкаУдаления
#Вставка
	//++AxelSoft Шарапова 26.02.2025 САНФ-032224
	|	И НЕ НастройкаСвязей.ЦППК_НеИспользовать
	//--AxelSoft Шарапова 26.02.2025 САНФ-032224
#КонецВставки
	|	И НЕ НастройкаСвязей.ТипСвязи.НеИспользовать";

	Если ТипЗнч(Объект) = Тип("СправочникСсылка.Файлы")
		Или ТипЗнч(Объект) = Тип("СправочникСсылка.Мероприятия")
		Или ТипЗнч(Объект) = Тип("СправочникСсылка.Проекты")
		Или ВстроеннаяПочтаКлиентСервер.ЭтоВходящееПисьмо(Объект)
		Или ВстроеннаяПочтаКлиентСервер.ЭтоИсходящееПисьмо(Объект) Тогда
		Запрос.Текст = Запрос.Текст +
		" И (ТИПЗНАЧЕНИЯ(НастройкаСвязей.СсылкаИз) = ТИПЗНАЧЕНИЯ(&Объект)) ";
		Запрос.УстановитьПараметр("Объект", Объект);
	Иначе
		Запрос.Текст = Запрос.Текст +
		" И (НастройкаСвязей.СсылкаИз = &ВидДокумента
		| ИЛИ НастройкаСвязей.СсылкаИз В (&Родители)
		| ИЛИ НастройкаСвязей.СсылкаИз = &ПустаяСсылка) 
		| И НастройкаСвязей.ВидКорреспонденцииДокументИз = &ВидКорреспонденцииИз";

		Если ТипЗнч(Объект) = Тип("Структура") Тогда 
			ВидДокументаСсылкаИз = Объект.ВидДокумента;
		Иначе 
			ВидДокументаСсылкаИз = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект, "ВидДокумента");
		КонецЕсли;

		РеквизитыВида = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ВидДокументаСсылкаИз,
		"ЯвляетсяВходящейКорреспонденцией, ЯвляетсяИсходящейКорреспонденцией");

		Если РеквизитыВида.ЯвляетсяВходящейКорреспонденцией = Истина Тогда 
			Запрос.УстановитьПараметр("ВидКорреспонденцииИз", Перечисления.ВидыКорреспонденции.Входящая);

		ИначеЕсли РеквизитыВида.ЯвляетсяИсходящейКорреспонденцией = Истина Тогда 
			Запрос.УстановитьПараметр("ВидКорреспонденцииИз", Перечисления.ВидыКорреспонденции.Исходящая);

		Иначе 
			Запрос.УстановитьПараметр("ВидКорреспонденцииИз", Перечисления.ВидыКорреспонденции.ПустаяСсылка());
		КонецЕсли;

		ПустаяСсылкаИз = Справочники[ВидДокументаСсылкаИз.Метаданные().Имя].ПустаяСсылка();
		РодителиИз = ПолучитьРодителей(ВидДокументаСсылкаИз);

		Запрос.УстановитьПараметр("ВидДокумента", ВидДокументаСсылкаИз);
		Запрос.УстановитьПараметр("Родители", РодителиИз);
		Запрос.УстановитьПараметр("ПустаяСсылка", ПустаяСсылкаИз);
	КонецЕсли;

	Если Не ИспользованиеВстроеннойПочты Тогда 
		Запрос.Текст = Запрос.Текст + "
		| И ТИПЗНАЧЕНИЯ(НастройкаСвязей.СсылкаИз) <> ТИП(Документ.ВходящееПисьмо)
		| И ТИПЗНАЧЕНИЯ(НастройкаСвязей.СсылкаИз) <> ТИП(Документ.ИсходящееПисьмо)
		| И ТИПЗНАЧЕНИЯ(НастройкаСвязей.СсылкаНа) <> ТИП(Документ.ВходящееПисьмо)
		| И ТИПЗНАЧЕНИЯ(НастройкаСвязей.СсылкаНа) <> ТИП(Документ.ИсходящееПисьмо)";
	КонецЕсли;

	Если СвязанныйОбъект <> Неопределено Тогда
		Если ТипЗнч(СвязанныйОбъект) = Тип("Строка")
			Или ТипЗнч(СвязанныйОбъект) = Тип("СправочникСсылка.Файлы")
			Или ТипЗнч(СвязанныйОбъект) = Тип("СправочникСсылка.Мероприятия")
			Или ТипЗнч(СвязанныйОбъект) = Тип("СправочникСсылка.Проекты")
			Или ВстроеннаяПочтаКлиентСервер.ЭтоВходящееПисьмо(СвязанныйОбъект)
			Или ВстроеннаяПочтаКлиентСервер.ЭтоИсходящееПисьмо(СвязанныйОбъект) Тогда
			Запрос.Текст = Запрос.Текст +
			" И (ТИПЗНАЧЕНИЯ(НастройкаСвязей.СсылкаНа) = ТИПЗНАЧЕНИЯ(&СвязанныйОбъект)) ";
			Запрос.УстановитьПараметр("СвязанныйОбъект", СвязанныйОбъект);
		Иначе
			Запрос.Текст = Запрос.Текст +
			" И (НастройкаСвязей.СсылкаНа = &ВидДокументаСсылкаНа
			| ИЛИ НастройкаСвязей.СсылкаНа В (&РодителиСсылкаНа)
			| ИЛИ НастройкаСвязей.СсылкаНа = &ПустаяСсылкаНа) 
			| И НастройкаСвязей.ВидКорреспонденцииДокументНа = &ВидКорреспонденцииНа";

			Если ТипЗнч(СвязанныйОбъект) = Тип("Структура") Тогда 
				ВидДокументаСсылкаНа = СвязанныйОбъект.ВидДокумента;
			Иначе 
				ВидДокументаСсылкаНа = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СвязанныйОбъект, "ВидДокумента");
			КонецЕсли;

			РеквизитыВида = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ВидДокументаСсылкаНа,
			"ЯвляетсяВходящейКорреспонденцией, ЯвляетсяИсходящейКорреспонденцией");

			Если РеквизитыВида.ЯвляетсяВходящейКорреспонденцией = Истина Тогда 
				Запрос.УстановитьПараметр("ВидКорреспонденцииНа", Перечисления.ВидыКорреспонденции.Входящая);

			ИначеЕсли РеквизитыВида.ЯвляетсяИсходящейКорреспонденцией = Истина Тогда 
				Запрос.УстановитьПараметр("ВидКорреспонденцииНа", Перечисления.ВидыКорреспонденции.Исходящая);

			Иначе 
				Запрос.УстановитьПараметр("ВидКорреспонденцииНа", Перечисления.ВидыКорреспонденции.ПустаяСсылка());
			КонецЕсли;

			РодителиСсылкаНа = ПолучитьРодителей(ВидДокументаСсылкаНа);
			ПустаяСсылкаНа = Справочники[ВидДокументаСсылкаНа.Метаданные().Имя].ПустаяСсылка();

			Запрос.УстановитьПараметр("ВидДокументаСсылкаНа", ВидДокументаСсылкаНа);
			Запрос.УстановитьПараметр("РодителиСсылкаНа", РодителиСсылкаНа);
			Запрос.УстановитьПараметр("ПустаяСсылкаНа", ПустаяСсылкаНа);
		КонецЕсли;
	КонецЕсли;

	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл

		НоваяСтрока = НастройкиСвязи.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
		ЗаполнитьТипИПредставлениеСсылки(ТипЗнч(Выборка.СсылкаИз),
		НоваяСтрока.ТипСсылкаИз,
		НоваяСтрока.ТипСсылкаИзПредставление);

		ЗаполнитьТипИПредставлениеСсылки(ТипЗнч(Выборка.СсылкаНа),
		НоваяСтрока.ТипСсылкаНа,
		НоваяСтрока.ТипСсылкаНаПредставление);

	КонецЦикла;

	Возврат НастройкиСвязи;

КонецФункции


Процедура ЦППК_ПрочитатьИзмененные(ДокументСсылка, РегистрационныйНомер, СписокИзмененныхДокументов) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	МассивДок = РегистрыСведений.ЦППК_ИзмененныеДокументы.ПрочитатьДанные(ДокументСсылка);
	Для Каждого Строка Из МассивДок Цикл
		НовСтр = СписокИзмененныхДокументов.Добавить();
		НовСтр.Документ = Строка.Документ;
		НовСтр.ДатаОтмены = Строка.ДатаОтмены;
		НовСтр.УжеУстановлен = Ложь;
		НовСтр.Источник = "ИзмененныеДокументы";
	КонецЦикла;	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры 

Процедура ЦППК_УстановитьИзменение(
	Документ,
	НачальныйСписокДокументов,
	СписокДокументов,
	ТипСвязи,
	Установил = Неопределено,
	ДатаУстановки = Неопределено,
	Комментарий = "",
	ЭтоРегистрация = Ложь,
	ДокументЗарегистрирован = Ложь) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	// Если заполнена дата: и если ЭтоРегистрация, 
	//  то стираем из РС ОтложеннаяУстановкаСвязей, и пишем в Связи
	
	ТекущаяДата = НачалоДня(ТекущаяДата());
	
	Расширить = Ложь;
	
	// Очистим старые связи - неважно, изменились или нет
	Для Каждого СтрокаДокумент Из НачальныйСписокДокументов Цикл 
		
		Если СтрокаДокумент.Источник = "СвязиОбъектов" Тогда
			УдалитьСвязь(Документ, СтрокаДокумент.Документ, ТипСвязи);
		ИначеЕсли СтрокаДокумент.Источник = "ОтложеннаяУстановкаСвязей" Тогда
			РегистрыСведений.ОтложеннаяУстановкаСвязей.УдалитьДанные(Документ, СтрокаДокумент.Документ, ТипСвязи);
		ИначеЕсли СтрокаДокумент.Источник = "ИзмененныйДокумент" Тогда
			РегистрыСведений.ЦППК_ИзмененныеДокументы.УдалитьДанные(Документ, СтрокаДокумент.Документ);
		КонецЕсли;	
		
	КонецЦикла;
	
	ОписаниеСтрока = НСтр("ru = 'УстановитьИзменение.'");
	ЕстьСтроки = Ложь;
	
	// всегда запишем новые связи
	Для Каждого СтрокаДокумент Из СписокДокументов Цикл 
		
		ЕстьСтроки = Истина;
		ОписаниеСтрока = ОписаниеСтрока + Символы.ВК 
			+ СтрШаблон(НСтр("ru = 'Документ %1.  ДатаИзменения %2'"), СтрокаДокумент.Документ, СтрокаДокумент.ДатаОтмены);
		
		НастройкаСвязи = ПолучитьНастройкуСвязи(Документ, СтрокаДокумент.Документ, Справочники.ТипыСвязей.ЦППК_ИзмененДокументом);	
		
		Если НастройкаСвязи <> Неопределено
			И (НастройкаСвязи.СинхронизироватьДоступ = Перечисления.ВариантыСинхронизацииДоступа.Копировать
				Или НастройкаСвязи.СинхронизироватьДоступ = Перечисления.ВариантыСинхронизацииДоступа.Расширять) Тогда 
			Расширить = Истина;
		КонецЕсли;
		
			
		РегистрыСведений.ЦППК_ИзмененныеДокументы.ЗаписатьДанные(Документ, СтрокаДокумент.Документ, СтрокаДокумент.ДатаОтмены);
		СтрокаДокумент.Источник = "ИзмененныйДокумент";	
		СоздатьСвязь(Документ, СтрокаДокумент.Документ, Справочники.ТипыСвязей.ЦППК_ИзменяетДокумент, Установил, ДатаУстановки, Комментарий);
		СоздатьСвязь(СтрокаДокумент.Документ, Документ, Справочники.ТипыСвязей.ЦППК_ИзмененДокументом, Установил, ДатаУстановки, Комментарий);
	
	КонецЦикла;
	
	Если ЕстьСтроки Тогда
		ПротоколированиеРаботыСотрудников.ЗаписатьИзменение(Документ,
			ОписаниеСтрока);
	КонецЕсли;		
	
	// Перезаполнение рабочих групп связанных объектов.
	Если Расширить Тогда 
		РаботаСРабочимиГруппами.РасширитьРабочуюГруппуСвязанныхОбъектов(Документ);
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

