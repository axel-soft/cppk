
&ИзменениеИКонтроль("ПриЧтенииНаСервере")
Процедура ЦППК_ПриЧтенииНаСервере(Форма)

	ЭтоФормаВидаДокумента = ДелопроизводствоКлиентСервер.ЭтоФормаВидаДокумента(Форма.ИмяФормы);
	ЭтоФормаШаблонаДокумента = ДелопроизводствоКлиентСервер.ЭтоФормаШаблонаДокумента(Форма.ИмяФормы);

	Если ЭтоФормаВидаДокумента Тогда 
		ОбновитьДополнительныеРеквизитыУчастников(Форма.ШаблонДокумента.РабочаяГруппаДокумента);
		ОбъектШаблон = Форма.ШаблонДокумента;
	Иначе 
		Если ЭтоФормаШаблонаДокумента Тогда
			ОбновитьДополнительныеРеквизитыУчастников(Форма.Объект.РабочаяГруппаДокумента);
		КонецЕсли;
		ОбъектШаблон = Форма.Объект;
	КонецЕсли;
	ШаблонСсылка = ОбъектШаблон.Ссылка;

	Форма.РабочаяГруппаТаблица.Очистить();
	КэшДокументыПредприятияФормаЭлемента =
	КэшиНаВремяВызоваПовтИсп.КэшДокументыПредприятияФормаЭлемента();
	Если КэшДокументыПредприятияФормаЭлемента.Инициализирован
		И КэшДокументыПредприятияФормаЭлемента.КэшДокумента.Ссылка = ШаблонСсылка Тогда
		УчастникиРГ = КэшДокументыПредприятияФормаЭлемента.УчастникиРабочейГруппы;
	Иначе
		УчастникиРГ = ПолучитьРабочуюГруппуДокумента(ШаблонСсылка);
	КонецЕсли;   
	ФормаДокументаДобавитьУчастников(Форма, УчастникиРГ); 
#Удаление
	Форма.РабочаяГруппаТаблица.Сортировать("Иконка, ЭтоРоль Убыв, Участник");
	Форма.Элементы.РабочаяГруппаТаблица.Видимость = Истина;
#КонецУдаления
КонецПроцедуры

&ИзменениеИКонтроль("ПроверитьПраваУчастниковРабочейГруппыНаОбъект")
Функция ЦППК_ПроверитьПраваУчастниковРабочейГруппыНаОбъект(СсылкаИлиДокументОбъект, Участники) Экспорт
	
	Если ПараметрыСеанса.ЗагрузкаОбработанныхДанныхИзДругойСистемы Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если ТипЗнч(СсылкаИлиДокументОбъект) = Тип("СправочникОбъект.ДокументыПредприятия") Тогда
		Ссылка = СсылкаИлиДокументОбъект.Ссылка;
		Представление = СсылкаИлиДокументОбъект.Заголовок;
	Иначе
		Ссылка = СсылкаИлиДокументОбъект;
		Представление = Строка(Ссылка);
	КонецЕсли;
	
	
	// Проверка на то, что рабочая группа не выходит за рамки настроек групп доступа контрагентов.
	ГДК = Новый Массив();
	Если ТипЗнч(СсылкаИлиДокументОбъект) = Тип("СправочникСсылка.ДокументыПредприятия") Тогда
		
		ЗапросГДК = Новый Запрос(
			"ВЫБРАТЬ
			|	ГруппыДоступаКонтрагентов.Ссылка КАК ГруппаДоступа
			|ИЗ
			|	Справочник.ГруппыДоступаКонтрагентов КАК ГруппыДоступаКонтрагентов
			|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
			|	Справочник.Контрагенты КАК КонтрагентыСпр
			|		ПО КонтрагентыСпр.ГруппаДоступа = ГруппыДоступаКонтрагентов.Ссылка
			|	ЛЕВОЕ СОЕДИНЕНИЕ
			|	Справочник.ДокументыПредприятия.Контрагенты КАК ДокументыТЧКонтрагенты
			|		ПО ДокументыТЧКонтрагенты.Контрагент = КонтрагентыСпр.Ссылка
			|ГДЕ
			|	ДокументыТЧКонтрагенты.Ссылка = &Ссылка");
		ЗапросГДК.УстановитьПараметр("Ссылка", Ссылка);
		ГДК = ЗапросГДК.Выполнить().Выгрузить().ВыгрузитьКолонку(0);
		
	ИначеЕсли ТипЗнч(СсылкаИлиДокументОбъект) = Тип("СправочникОбъект.ДокументыПредприятия") Тогда
		
		Контрагенты = СсылкаИлиДокументОбъект.Контрагенты.ВыгрузитьКолонку("Контрагент");
		ГДКСоответствие = ОбщегоНазначения.ЗначенияРеквизитовОбъектов(Контрагенты, "ГруппаДоступа");
		Для Каждого КлючИЗначение Из ГДКСоответствие Цикл
			Если ЗначениеЗаполнено(КлючИЗначение.Значение.ГруппаДоступа) Тогда
				ГДК.Добавить(КлючИЗначение.Значение.ГруппаДоступа);
			КонецЕсли;
		КонецЦикла;
		
	КонецЕсли;
	
	Если ГДК.Количество() > 0 Тогда
		Запрос = Новый Запрос(
			"ВЫБРАТЬ ПЕРВЫЕ 1
			|	СотрудникиУчастников.Контейнер КАК Участник,
			|	СотрудникиУчастников.Сотрудник КАК Сотрудник,
			|	ДоступТЧ.Ссылка КАК ГруппаДоступа
			|ИЗ
			|	РегистрСведений.СотрудникиВКонтейнерах КАК СотрудникиУчастников
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ГруппыДоступаКонтрагентов.Доступ КАК ДоступТЧ
			|		ПО ДоступТЧ.Ссылка В (&ГруппыДоступа)
			|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДескрипторыДляОбъектов КАК ДескрипторыДляОбъектов
			|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ПраваПоДескрипторамДоступаОбъектов КАК ПраваПоДескрипторам
			|			ПО ДескрипторыДляОбъектов.Дескриптор = ПраваПоДескрипторам.Дескриптор
			|		ПО ДоступТЧ.Ссылка = ДескрипторыДляОбъектов.Объект
			|		И ПраваПоДескрипторам.Сотрудник = СотрудникиУчастников.Сотрудник
			|ГДЕ
			|	СотрудникиУчастников.Контейнер В (&УчастникиРГ)
			|	И СотрудникиУчастников.Сотрудник.Действует
			|	И ДескрипторыДляОбъектов.Объект ЕСТЬ NULL");
		Запрос.УстановитьПараметр("УчастникиРГ", Участники.ВыгрузитьКолонку("Участник"));
		Запрос.УстановитьПараметр("ГруппыДоступа", ГДК);
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			ТекстОшибки = "";
			Если ТипЗнч(Выборка.Участник) = Тип("СправочникСсылка.Сотрудники") Тогда
				ТекстОшибки = СтрШаблон( 
					НСтр("ru = 'Нарушение настроек групп доступа контрагентов:
						|сотрудник %1 не имеет разрешения на группу ""%2"".
						|Объект: %3.'"),
					Выборка.Участник, Выборка.ГруппаДоступа, Представление);
			Иначе
				ТекстОшибки = СтрШаблон( 
					НСтр("ru = 'Нарушение настроек групп доступа контрагентов:
						|не все сотрудники ""%1"" имеют разрешение на группу ""%2"".
						|Пример: %3.
						|Объект: %4.'"),
					Выборка.Участник, Выборка.ГруппаДоступа, Выборка.Сотрудник, Представление);
			КонецЕсли;

			Возврат ТекстОшибки;
			
		КонецЕсли;
	КонецЕсли;


	// Проверка на то, что рабочая группа не выходит за рамки настроек грифов доступа.
	Если Ссылка.Метаданные().Реквизиты.Найти("ГрифДоступа") <> Неопределено Тогда
		
		Если ТипЗнч(СсылкаИлиДокументОбъект) = Тип("СправочникОбъект.ДокументыПредприятия") Тогда
			ГрифДоступа = СсылкаИлиДокументОбъект.ГрифДоступа;
		Иначе
			ГрифДоступа = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Ссылка, "ГрифДоступа");
		КонецЕсли;
		
#Удаление
		Если ЗначениеЗаполнено(ГрифДоступа) Тогда
			Запрос = Новый Запрос(
				"ВЫБРАТЬ ПЕРВЫЕ 1
				|	СотрудникиУчастников.Контейнер КАК Участник,
				|	СотрудникиУчастников.Сотрудник КАК Сотрудник,
				|	ДоступТЧ.Ссылка КАК ГрифДоступа
				|ИЗ
				|	РегистрСведений.СотрудникиВКонтейнерах КАК СотрудникиУчастников
				|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ГрифыДоступа.Доступ КАК ДоступТЧ
				|		ПО ДоступТЧ.Ссылка = &ГрифДоступа
				|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДескрипторыДляОбъектов КАК ДескрипторыДляОбъектов
				|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ПраваПоДескрипторамДоступаОбъектов КАК ПраваПоДескрипторам
				|			ПО ДескрипторыДляОбъектов.Дескриптор = ПраваПоДескрипторам.Дескриптор
				|		ПО ДоступТЧ.Ссылка = ДескрипторыДляОбъектов.Объект
				|		И ПраваПоДескрипторам.Сотрудник = СотрудникиУчастников.Сотрудник
				|ГДЕ
				|	СотрудникиУчастников.Контейнер В (&УчастникиРГ)
				|	И СотрудникиУчастников.Сотрудник.Действует
				|	И ДескрипторыДляОбъектов.Объект ЕСТЬ NULL");
			Запрос.УстановитьПараметр("УчастникиРГ", Участники.ВыгрузитьКолонку("Участник"));
			Запрос.УстановитьПараметр("ГрифДоступа", ГрифДоступа);
			Выборка = Запрос.Выполнить().Выбрать();
			Если Выборка.Следующий() Тогда
				ТекстОшибки = "";
				Если ТипЗнч(Выборка.Участник) = Тип("СправочникСсылка.Сотрудники") Тогда
					ТекстОшибки = СтрШаблон( 
						НСтр("ru = 'Нарушение настроек грифа доступа:
							|сотрудник %1 не имеет разрешения на гриф ""%2"".
							|Объект: %3.'"),
						Выборка.Участник, Выборка.ГрифДоступа, Представление);
				Иначе
					ТекстОшибки = СтрШаблон( 
						НСтр("ru = 'Нарушение настроек грифа доступа:
							|не все сотрудники ""%1"" имеют разрешение на гриф ""%2"".
							|Пример: %3.
							|Объект: %4.'"),
						Выборка.Участник, Выборка.ГрифДоступа, Выборка.Сотрудник, Представление);
				КонецЕсли;
				
				Возврат ТекстОшибки;
				
			КонецЕсли;
		КонецЕсли;
#КонецУдаления
#Вставка
		Если ЗначениеЗаполнено(ГрифДоступа) И 
			ГрифДоступа <> Справочники.ГрифыДоступа.ПолучитьСсылку(Новый УникальныйИдентификатор("d7c3da58-74bb-11e1-909a-00155d01ea01")) Тогда 
			Запрос = Новый Запрос(
			"ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	СотрудникиУчастников.Контейнер КАК Участник,
			|	СотрудникиУчастников.Сотрудник КАК Сотрудник,
			|	ДоступТЧ.Ссылка КАК ГрифДоступа
			|ИЗ
			|	РегистрСведений.СотрудникиВКонтейнерах КАК СотрудникиУчастников
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ГрифыДоступа.Доступ КАК ДоступТЧ
			|		ПО ДоступТЧ.Ссылка = &ГрифДоступа
			|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДескрипторыДляОбъектов КАК ДескрипторыДляОбъектов
			|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ПраваПоДескрипторамДоступаОбъектов КАК ПраваПоДескрипторам
			|			ПО ДескрипторыДляОбъектов.Дескриптор = ПраваПоДескрипторам.Дескриптор
			|		ПО ДоступТЧ.Ссылка = ДескрипторыДляОбъектов.Объект
			|		И ПраваПоДескрипторам.Сотрудник = СотрудникиУчастников.Сотрудник
			|ГДЕ
			|	СотрудникиУчастников.Контейнер В (&УчастникиРГ)
			|	И СотрудникиУчастников.Сотрудник.Действует
			|	И ДескрипторыДляОбъектов.Объект ЕСТЬ NULL");
			Запрос.УстановитьПараметр("УчастникиРГ", Участники.ВыгрузитьКолонку("Участник"));
			Запрос.УстановитьПараметр("ГрифДоступа", ГрифДоступа);
			Выборка = Запрос.Выполнить().Выбрать();
			
			ТекстОшибки = ""; 
			ТекстСотрудники = "";
			
			Пока Выборка.Следующий() Цикл
				
				Если СтрНайти(ТекстСотрудники, Выборка.Сотрудник) > 0 Тогда
					Продолжить;
				КонецЕсли;
				
				ТекстСотрудники =  ?(ЗначениеЗаполнено(ТекстСотрудники), 
							ТекстСотрудники  + Символы.ПС +  "- " + Выборка.Сотрудник, 
							ТекстСотрудники +  "- " + Выборка.Сотрудник);	
			
			КонецЦикла; 
			
			Если ЗначениеЗаполнено(ТекстСотрудники) Тогда
				ТекстОшибки = СтрШаблон( 
				НСтр("ru = ' Нарушение настроек грифа доступа, сотрудники:
				|%1 
				|не имеют разрешения на гриф ""%2"".
				|Объект: %3.'"),
				ТекстСотрудники, ГрифДоступа, Ссылка);
				
				Возврат ТекстОшибки;

			КонецЕсли;
		КонецЕсли;
#КонецВставки
		
	КонецЕсли;
	
	
	// Проверка на то, что рабочая группа не выходит за рамки настроек доступа организаций
#Удаление
	Если Ссылка.Метаданные().Реквизиты.Найти("Организация") <> Неопределено Тогда
#КонецУдаления
#Вставка
	// Отключаем проверку, т.к. сильно нагружает систему.
	Если Ложь Тогда
#КонецВставки
		
		Если ТипЗнч(СсылкаИлиДокументОбъект) = Тип("СправочникОбъект.ДокументыПредприятия") Тогда
			Организация = СсылкаИлиДокументОбъект.Организация;
		Иначе
			Организация = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Ссылка, "Организация");
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Организация) Тогда
			Запрос = Новый Запрос(
#Удаление
				"ВЫБРАТЬ
				|	СотрудникиУчастников.Контейнер КАК Участник,
				|	СотрудникиУчастников.Сотрудник КАК Сотрудник,
				|	ДоступТЧ.Ссылка КАК Организация,
				|	ДескрипторыДляОбъектов.Дескриптор КАК Дескриптор
				|ПОМЕСТИТЬ ОрганизацииИДескрипторы
				|ИЗ
				|	РегистрСведений.СотрудникиВКонтейнерах КАК СотрудникиУчастников
				|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Организации.Доступ КАК ДоступТЧ
				|		ПО (ДоступТЧ.Ссылка = &Организация)
				|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ДескрипторыДляОбъектов КАК ДескрипторыДляОбъектов
				|		ПО (ДоступТЧ.Ссылка = ДескрипторыДляОбъектов.Объект)
				|ГДЕ
				|	СотрудникиУчастников.Контейнер В(&УчастникиРГ)
				|	И СотрудникиУчастников.Сотрудник.Действует
				|;
				|
				|////////////////////////////////////////////////////////////////////////////////
				|ВЫБРАТЬ РАЗЛИЧНЫЕ
				|	ОрганизацииИДескрипторы.Организация КАК Организация,
				|	ПраваПоДескрипторамДоступаОбъектов.Сотрудник КАК Сотрудник
				|ПОМЕСТИТЬ ИмеющиеПрава
				|ИЗ
				|	ОрганизацииИДескрипторы КАК ОрганизацииИДескрипторы
				|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ПраваПоДескрипторамДоступаОбъектов КАК ПраваПоДескрипторамДоступаОбъектов
				|		ПО ОрганизацииИДескрипторы.Дескриптор = ПраваПоДескрипторамДоступаОбъектов.Дескриптор
				|;
				|
				|////////////////////////////////////////////////////////////////////////////////
				|ВЫБРАТЬ РАЗЛИЧНЫЕ
				|	ОрганизацииИДескрипторы.Участник КАК Участник,
				|	ОрганизацииИДескрипторы.Сотрудник КАК Сотрудник,
				|	ОрганизацииИДескрипторы.Организация КАК Организация
				|ИЗ
				|	ОрганизацииИДескрипторы КАК ОрганизацииИДескрипторы
				|		ЛЕВОЕ СОЕДИНЕНИЕ ИмеющиеПрава КАК ИмеющиеПрава
				|		ПО ОрганизацииИДескрипторы.Сотрудник = ИмеющиеПрава.Сотрудник
				|ГДЕ
				|	ИмеющиеПрава.Сотрудник ЕСТЬ NULL");
#КонецУдаления
#Вставка
				"ВЫБРАТЬ
				|	СотрудникиУчастников.Контейнер КАК Участник,
				|	СотрудникиУчастников.Сотрудник КАК Сотрудник,
				|	&Организация КАК Организация,
				|	ДескрипторыДляОбъектов.Дескриптор КАК Дескриптор
				|ПОМЕСТИТЬ ОрганизацииИДескрипторы
				|ИЗ
				|	РегистрСведений.СотрудникиВКонтейнерах КАК СотрудникиУчастников
				|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ДескрипторыДляОбъектов КАК ДескрипторыДляОбъектов
				|		ПО (ДескрипторыДляОбъектов.Объект = &Организация)
				|			И (СотрудникиУчастников.Контейнер В (&УчастникиРГ))
				|ГДЕ
				|	СотрудникиУчастников.Сотрудник.Действует
				|;
				|
				|////////////////////////////////////////////////////////////////////////////////
				|ВЫБРАТЬ РАЗЛИЧНЫЕ
				|	ОрганизацииИДескрипторы.Организация КАК Организация,
				|	ОрганизацииИДескрипторы.Участник КАК Участник,
				|	ОрганизацииИДескрипторы.Сотрудник КАК Сотрудник
				|ИЗ
				|	ОрганизацииИДескрипторы КАК ОрганизацииИДескрипторы
				|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПраваПоДескрипторамДоступаОбъектов КАК ПраваПоДескрипторамДоступаОбъектов
				|		ПО ОрганизацииИДескрипторы.Дескриптор = ПраваПоДескрипторамДоступаОбъектов.Дескриптор
				|ГДЕ
				|	ПраваПоДескрипторамДоступаОбъектов.Сотрудник ЕСТЬ NULL");			
#КонецВставки
			Запрос.УстановитьПараметр("УчастникиРГ", Участники.ВыгрузитьКолонку("Участник"));
			Запрос.УстановитьПараметр("Организация", Организация);
			Выборка = Запрос.Выполнить().Выбрать();
			Если Выборка.Следующий() Тогда
				ТекстОшибки = "";
				Если ТипЗнч(Выборка.Участник) = Тип("СправочникСсылка.Сотрудники") Тогда
					ТекстОшибки = СтрШаблон( 
						НСтр("ru = 'Нарушение настроек доступа организации:
							|сотрудник %1 не имеет разрешения на организацию ""%2"".
							|Объект: %3.'"),
						Выборка.Участник, Выборка.Организация, Представление);
				Иначе
					ТекстОшибки = СтрШаблон( 
						НСтр("ru = 'Нарушение настроек доступа организации:
							|не все сотрудники ""%1"" имеют разрешение на организацию ""%2"".
							|Пример: %3.
							|Объект: %4.'"),
						Выборка.Участник, Выборка.Организация, Выборка.Сотрудник, Представление);
				КонецЕсли;
				
				Возврат ТекстОшибки;
				
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецФункции
