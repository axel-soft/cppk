
&После("ОбработкаПроверкиЗаполнения")
Процедура ЦППК_ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	// { *Грошев (ТТС) [29.06.2020]
	Если НЕ ЭтотОбъект.ЭтоГруппа И ЭтотОбъект.ТТС_ЯвляетсяЖурналомТУ Тогда
		ПроверяемыеРеквизиты.Добавить("ТТС_ВидЖурналаТУ");
	КонецЕсли;
	// }
КонецПроцедуры

&После("ПередЗаписью")
Процедура ЦППК_ПередЗаписью(Отказ)
	// Котляров 2025,07,10 САНФ-034810 QR-7259012-1
	//  очищаем данные
	Если НЕ ЭтотОбъект.ЦППК_УчитыватьВноситИзменения И Ссылка.ЦППК_УчитыватьВноситИзменения Тогда 
	
		Запрос = Новый Запрос;
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	ЦППК_ИзмененныеДокументы.Документ КАК Документ,
			|	ЦППК_ИзмененныеДокументы.ОтменяемыйДокумент КАК ОтменяемыйДокумент
			|ИЗ
			|	РегистрСведений.ЦППК_ИзмененныеДокументы КАК ЦППК_ИзмененныеДокументы
			|ГДЕ
			|	ЦППК_ИзмененныеДокументы.Документ.ВидДокумента = &Ссылка
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	ЦППК_ИзмененныеДокументы.Документ,
			|	ЦППК_ИзмененныеДокументы.ОтменяемыйДокумент
			|ИЗ
			|	РегистрСведений.ЦППК_ИзмененныеДокументы КАК ЦППК_ИзмененныеДокументы
			|ГДЕ
			|	ЦППК_ИзмененныеДокументы.ОтменяемыйДокумент.ВидДокумента = &Ссылка";
		
		Запрос.УстановитьПараметр("Ссылка", Ссылка);
		
		РезультатЗапроса = Запрос.Выполнить();
		
		Выборка = РезультатЗапроса.Выбрать();
		
		Пока Выборка.Следующий() Цикл
			НаборЗаписей = РегистрыСведений["ЦППК_ИзмененныеДокументы"].СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.Документ.Установить(Выборка.Документ);
			НаборЗаписей.Отбор.ОтменяемыйДокумент.Установить(Выборка.ОтменяемыйДокумент);
			НаборЗаписей.Записать();			
		КонецЦикла;
	   ///////////////////////////////////////////////
		Запрос = Новый Запрос;
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	ДокументыПредприятия.Ссылка КАК Ссылка,
			|	ДокументыПредприятия.ВидДокумента КАК ВидДокумента
			|ИЗ
			|	Справочник.ДокументыПредприятия КАК ДокументыПредприятия
			|ГДЕ
			|	ДокументыПредприятия.ЦППК_Изменен = ИСТИНА
			|	И ДокументыПредприятия.ВидДокумента = &Ссылка";
		
		Запрос.УстановитьПараметр("Ссылка", Ссылка);
		РезультатЗапроса = Запрос.Выполнить();
		
		Выборка = РезультатЗапроса.Выбрать();
		
		Пока Выборка.Следующий() Цикл
			ОбъектДокумента = Выборка.Ссылка.ПолучитьОбъект();
			ОбъектДокумента.ЦППК_Изменен = Ложь;
			ОбъектДокумента.Записать();
		КонецЦикла;
		//////////////////////////////////
	
		Запрос = Новый Запрос;
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	СвязиОбъектов.Объект КАК Объект,
			|	СвязиОбъектов.ТипСвязи КАК ТипСвязи,
			|	СвязиОбъектов.СвязанныйОбъект КАК СвязанныйОбъект
			|ИЗ
			|	РегистрСведений.СвязиОбъектов КАК СвязиОбъектов
			|ГДЕ
			|	ВЫРАЗИТЬ(СвязиОбъектов.Объект КАК Справочник.ДокументыПредприятия).ВидДокумента = &Ссылка
			|	И СвязиОбъектов.ТипСвязи = &ТипСвязи
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	СвязиОбъектов.Объект,
			|	СвязиОбъектов.ТипСвязи,
			|	СвязиОбъектов.СвязанныйОбъект
			|ИЗ
			|	РегистрСведений.СвязиОбъектов КАК СвязиОбъектов
			|ГДЕ
			|	ВЫРАЗИТЬ(СвязиОбъектов.СвязанныйОбъект КАК Справочник.ДокументыПредприятия).ВидДокумента = &Ссылка
			|	И СвязиОбъектов.ТипСвязи = &ТипСвязиВторогоЗапроса";
		
		Запрос.УстановитьПараметр("Ссылка", Ссылка);
		Запрос.УстановитьПараметр("ТипСвязи", Справочники.ТипыСвязей.ЦППК_ИзменяетДокумент);
		Запрос.УстановитьПараметр("ТипСвязиВторогоЗапроса", Справочники.ТипыСвязей.ЦППК_ИзмененДокументом);
		
		РезультатЗапроса = Запрос.Выполнить();
		
		Выборка = РезультатЗапроса.Выбрать();
		
		Пока Выборка.Следующий() Цикл
			НаборЗаписей = РегистрыСведений["СвязиОбъектов"].СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.Объект.Установить(Выборка.Объект);
			НаборЗаписей.Отбор.ТипСвязи.Установить(Выборка.ТипСвязи);
			НаборЗаписей.Отбор.СвязанныйОбъект.Установить(Выборка.СвязанныйОбъект);
			НаборЗаписей.Записать();			
		КонецЦикла;
	
	//}}КОНСТРУКТОР_ЗАПРОСА_С_ОБРАБОТКОЙ_РЕЗУЛЬТАТА

	КонецЕсли;
	
КонецПроцедуры
