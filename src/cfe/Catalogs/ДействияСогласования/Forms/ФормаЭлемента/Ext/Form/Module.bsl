
&НаСервере
&Перед("ОбновитьДеревоУчастниковПоОбъекту")
Процедура ЦППК_ОбновитьДеревоУчастниковПоОбъекту()
	
	Если Не ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.НастройкаДействия, "ЦППК_УпроститьПовторноеСогласование") Тогда
		Возврат;	
	КонецЕсли;
	
	Если Не Объект.Этапы.Количество() Тогда
		Возврат;
	КонецЕсли;
	
	ПервыйШагИД = Объект.Этапы[0].Идентификатор; 
	Объект.Этапы[0].ПорядокВыполненияУчастниками = Перечисления.ПорядокВыполненияЭтапаДействия.Параллельно;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВизыСогласования.ИдентификаторУчастника КАК ИдентификаторУчастника
		|ИЗ
		|	Справочник.ВизыСогласования КАК ВизыСогласования
		|ГДЕ
		|	ВизыСогласования.Документ = &Документ
		|	И ВизыСогласования.РезультатСогласования = &РезультатСогласования
		|	И НЕ ВизыСогласования.ПомещенаВИсторию";
	
	Запрос.УстановитьПараметр("Документ", Объект.Предмет);
	Запрос.УстановитьПараметр("РезультатСогласования", Перечисления.РезультатыСогласования.Согласовано);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат;
	КонецЕсли; 
	 
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		
		СтрокиСотрудника = Объект.Участники.НайтиСтроки(Новый Структура("Идентификатор", ВыборкаДетальныеЗаписи.ИдентификаторУчастника));
		Для каждого СтрокаСотрудника Из СтрокиСотрудника Цикл
		   СтрокаСотрудника.ИдентификаторЭтапа = ПервыйШагИД;
		   СтрокаСотрудника.СрокДни = 1;
		КонецЦикла;
		
	КонецЦикла;  
	
	// Для всех строк первого этапа срок 1 день.
	СтрокиПервогоЭтапа = Объект.Участники.НайтиСтроки(Новый Структура("ИдентификаторЭтапа", ПервыйШагИД));
	Для каждого СтрокаСотрудника Из СтрокиПервогоЭтапа Цикл
		СтрокаСотрудника.СрокДни = 1;
	КонецЦикла;
	
	Для каждого Этап Из Объект.Этапы Цикл
		
		СтрокиЭтапа = Объект.Участники.НайтиСтроки(Новый Структура("ИдентификаторЭтапа", Этап.Идентификатор));
        Если Не СтрокиЭтапа.Количество() Тогда
			Объект.Этапы.Удалить(Этап);
		КонецЕсли;		
	
	КонецЦикла;
		
КонецПроцедуры
