
&ИзменениеИКонтроль("ДанныеБлоковНавигации")
Функция ЦППК_ДанныеБлоковНавигации(БлокиНавигации = Неопределено, СУчетомНастроек = Истина, ДанныеКоманд = Неопределено, ДанныеПоказателей = Неопределено) Экспорт
	
	ДанныеБлоковНавигации = Новый Соответствие;
#Вставка
	Если Не Пользователи.РолиДоступны("ПолныеПрава")И БлокиНавигации <> Неопределено Тогда
		
		БлокНастройкиМДК = Справочники.БлокиНавигации.НайтиПоНаименованию("Настройки_МДК");
		БлокСправочникиМДК = Справочники.БлокиНавигации.НайтиПоНаименованию("Справочники_МДК");
		
		Если ЗначениеЗаполнено(БлокНастройкиМДК) И ЗначениеЗаполнено(БлокСправочникиМДК) Тогда
			
			БлокНастройкиТиповой = БлокиНавигации.Найти(Справочники.БлокиНавигации.Настройки);
			Если БлокНастройкиТиповой <> Неопределено Тогда
				
				БлокиНавигации.Удалить(БлокНастройкиТиповой);
				БлокиНавигации.Добавить(БлокНастройкиМДК);
				
			КонецЕсли;
			
			БлокСправочникиТиповой = БлокиНавигации.Найти(Справочники.БлокиНавигации.БлокСправочники);
			Если БлокСправочникиТиповой <> Неопределено Тогда
				
				БлокиНавигации.Удалить(БлокСправочникиТиповой);
				БлокиНавигации.Добавить(БлокСправочникиМДК);

			КонецЕсли;

		КонецЕсли;
		
	КонецЕсли;
#КонецВставки
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	БлокиНавигации.ВерсияДанных,
		|	БлокиНавигации.ВключатьРеестры,
		|	БлокиНавигации.Заголовок,
		|	БлокиНавигации.ИмяПредопределенныхДанных,
		|	БлокиНавигации.Команды.(
		|		ВспомогательнаяКоманда,
		|		НомерСтроки,
		|		ДополнительныйПоказатель,
		|		ОсновнаяКомандаПоказатель,
		|		СкрытьВЕще,
		|		Ссылка),
		|	БлокиНавигации.Наименование,
		|	БлокиНавигации.НаименованиеЯзык1,
		|	БлокиНавигации.ЗаголовокЯзык1,
		|	БлокиНавигации.ПодсказкаЯзык1,
		|	БлокиНавигации.Подсказка,
		|	БлокиНавигации.ПометкаУдаления,
		|	БлокиНавигации.Предопределенный,
		|	БлокиНавигации.Представление,
		|	БлокиНавигации.Ссылка,
		|	БлокиНавигации.Условие,
		|	ЕСТЬNULL(НастройкиБлоковНавигации.ЗначениеНастройки, НЕОПРЕДЕЛЕНО) КАК НастройкиБлокаНавигации
		|ИЗ
		|	Справочник.БлокиНавигации КАК БлокиНавигации
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкиБлоковНавигации КАК НастройкиБлоковНавигации
		|		ПО БлокиНавигации.Ссылка = НастройкиБлоковНавигации.БлокНавигации
		|		И НастройкиБлоковНавигации.Пользователь = &ТекущийПользователь
		|ГДЕ
		|	БлокиНавигации.Ссылка В (&БлокиНавигации)
		|	И БлокиНавигации.ПометкаУдаления = ЛОЖЬ");
	
	Запрос.УстановитьПараметр("ТекущийПользователь", Пользователи.ТекущийПользователь());
	
	Если БлокиНавигации <> Неопределено Тогда
		Запрос.УстановитьПараметр("БлокиНавигации", БлокиНавигации);
	Иначе
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "БлокиНавигации.Ссылка В (&БлокиНавигации)", "");
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "И БлокиНавигации.ПометкаУдаления = ЛОЖЬ", "БлокиНавигации.ПометкаУдаления = ЛОЖЬ");
	КонецЕсли;
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		ДанныеБлокаНавигации = СформироватьДанныеБлокаНавигации(Выборка, СУчетомНастроек);
		
		Если Не БлокНавигацииДоступен(ДанныеБлокаНавигации) Тогда
			Продолжить;
		КонецЕсли;
		
		ИнициализироватьБлокНавигации(ДанныеБлокаНавигации, ДанныеКоманд, ДанныеПоказателей);
		
		Если ДанныеБлокаНавигации.Команды.Количество() = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		ДанныеБлоковНавигации[Выборка.Ссылка] = ДанныеБлокаНавигации;
		
	КонецЦикла;
	
	Возврат ДанныеБлоковНавигации;
	
КонецФункции
