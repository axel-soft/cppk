
&ИзменениеИКонтроль("ДобавитьВЛог")
Процедура ЦППК_ДобавитьВЛог(Задание, ТекстДобавки)

	УстановитьПривилегированныйРежим(Истина);

	НачатьТранзакцию();
	Попытка

		БлокировкаДанных = Новый БлокировкаДанных;

		ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.ПротоколВыполненияЗаданий");
		ЭлементБлокировки.УстановитьЗначение("Задание", Задание);
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;

		БлокировкаДанных.Заблокировать();

		МенеджерЗаписи = СоздатьМенеджерЗаписи();
		МенеджерЗаписи.Задание = Задание;
		МенеджерЗаписи.Прочитать();

		Если МенеджерЗаписи.Выбран() Тогда

			Если СтрДлина(МенеджерЗаписи.Протокол) > 32000 Тогда
				МенеджерЗаписи.Протокол = ""; // очистим накопленное
			КонецЕсли;	

#Удаление
			МенеджерЗаписи.Протокол = ТекстДобавки + Символы.ВК + МенеджерЗаписи.Протокол; 
#КонецУдаления
#Вставка
			ПозицияПереноса = СтрНайти(МенеджерЗаписи.Протокол, Символы.ВК);
			ПозицияРазделительВремени = СтрНайти(МенеджерЗаписи.Протокол, "(UTC) ")+6;
			ПозицияТекстДобавки = СтрНайти(ТекстДобавки, "(UTC) ")+6; 
			Если ПозицияПереноса <> 0 И Сред(МенеджерЗаписи.Протокол, ПозицияРазделительВремени, ПозицияПереноса-ПозицияРазделительВремени) = Сред(ТекстДобавки, ПозицияТекстДобавки) Тогда
				ОтменитьТранзакцию();
				Возврат;
			КонецЕсли;
			МенеджерЗаписи.Протокол = СтрШаблон("%1%2%3", ТекстДобавки, Символы.ВК, МенеджерЗаписи.Протокол);
#КонецВставки
		Иначе
			МенеджерЗаписи.Протокол = ТекстДобавки;
		КонецЕсли;

		МенеджерЗаписи.Задание = Задание;	 
		МенеджерЗаписи.Записать();

		ЗафиксироватьТранзакцию();

	Исключение
		ОтменитьТранзакцию();
		ВызватьИсключение;
	КонецПопытки;	

КонецПроцедуры
