Процедура ЦППК_ОбновитьДанныеЖурналаТУ(Объект) Экспорт
	
	Если НЕ ЗначениеЗаполнено(Объект) Тогда
		Возврат;
	КонецЕсли;
		
	Запрос = Новый Запрос(
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ДокументыПредприятия.Ссылка КАК Документ,
	|	Максимум(ТТС_ОтметкиОПроверкеЖурналов.РольИсполнителя = ЗНАЧЕНИЕ(Справочник.РолиИсполнителей.ТТС_СБ)) КАК СБ,
	|	Максимум(ТТС_ОтметкиОПроверкеЖурналов.РольИсполнителя = ЗНАЧЕНИЕ(Справочник.РолиИсполнителей.ТТС_АГР)) КАК АГР,
	|	Максимум(ТТС_ОтметкиОПроверкеЖурналов.РольИсполнителя = ЗНАЧЕНИЕ(Справочник.РолиИсполнителей.ТТС_ГР)) КАК ГР,
	|	Максимум(ТТС_ОтметкиОПроверкеЖурналов.РольИсполнителя = ЗНАЧЕНИЕ(Справочник.РолиИсполнителей.ТТС_УЭПС)) КАК УЭПС,
	|	Максимум(ТТС_ОтметкиОПроверкеЖурналов.РольИсполнителя = ЗНАЧЕНИЕ(Справочник.РолиИсполнителей.ТТС_НачальникЦРН)) КАК ЦРН,
	|	Максимум(СвязиОбъектов.ТипСвязи = ЗНАЧЕНИЕ(Справочник.ТипыСвязей.ТТС_ЖТУ2)) КАК ЕстьСвязьЖТУ2,
	|	Максимум(СвязиОбъектов.ТипСвязи = ЗНАЧЕНИЕ(Справочник.ТипыСвязей.ТТС_ЖТУ3)) КАК ЕстьСвязьЖТУ3
	|ИЗ
	|	Справочник.ДокументыПредприятия КАК ДокументыПредприятия
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ТТС_ОтметкиОПроверкеЖурналов КАК ТТС_ОтметкиОПроверкеЖурналов
	|		ПО (ТТС_ОтметкиОПроверкеЖурналов.ВнутреннийДокумент = ДокументыПредприятия.Ссылка)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СвязиОбъектов КАК СвязиОбъектов
	|		ПО (СвязиОбъектов.Объект = ДокументыПредприятия.Ссылка)
	|ГДЕ
	|	ДокументыПредприятия.ВидДокумента.ТТС_ЯвляетсяЖурналомТУ
	|	И ДокументыПредприятия.Ссылка = &Объект
	|
	|СГРУППИРОВАТЬ ПО
	|	ДокументыПредприятия.Ссылка
	|;
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТТС_ЖурналыТУ.НомерПоезда КАК НомерПоезда
	|ИЗ
	|	РегистрСведений.ТТС_ЖурналыТУ КАК ТТС_ЖурналыТУ
	|ГДЕ
	|	ТТС_ЖурналыТУ.ВнутреннийДокумент = &Объект
	|	И ТТС_ЖурналыТУ.НомерПоезда <> """"");
	
	Запрос.УстановитьПараметр("Объект", Объект);
	
	РезультатыЗапросов = Запрос.ВыполнитьПакет();
	Номера = Новый Массив;
	ВыборкаНомера = РезультатыЗапросов[1].Выбрать();
	Пока ВыборкаНомера.Следующий() Цикл
		Номера.Добавить(ВыборкаНомера.НомерПоезда);
	КонецЦикла;
	
	НаборЗаписей = РегистрыСведений.ЦППК_ДанныеЖурналаТУ.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Найти("Документ").Установить(Объект);
	НаборЗаписей.Прочитать();
	НаборЗаписей.Очистить();
	
	Выборка = РезультатыЗапросов[0].Выбрать();	
	Если Выборка.Следующий() Тогда
		
		Запись = НаборЗаписей.Добавить();
		ЗаполнитьЗначенияСвойств(Запись, Выборка);
		Запись.НомераПоездов = СтрСоединить(Номера, "; ");
		
	КонецЕсли;
	
	НаборЗаписей.Записать(Истина);

	
КонецПроцедуры
