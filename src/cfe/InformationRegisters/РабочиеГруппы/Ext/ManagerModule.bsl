
&ИзменениеИКонтроль("ПолучитьУчастниковПоОбъекту")
Функция ЦППК_ПолучитьУчастниковПоОбъекту(Объект) Экспорт
	
	Набор = СоздатьНаборЗаписей();
	ТаблицаУчастников = Набор.ВыгрузитьКолонки();
	ТаблицаУчастников.Колонки.Добавить("Недействителен", Новый ОписаниеТипов("Булево"));
	
	Запрос = Новый Запрос(
#Удаление
		"ВЫБРАТЬ
		|	РабочиеГруппы.Объект КАК Объект,
		|	РабочиеГруппы.Участник КАК Участник,
		|	ВЫБОР
		|		КОГДА РабочиеГруппы.Участник ССЫЛКА Справочник.Пользователи
		|			ТОГДА РабочиеГруппы.Участник.Недействителен
		|		КОГДА РабочиеГруппы.Участник ССЫЛКА Справочник.РабочиеГруппы
		|			ТОГДА РабочиеГруппы.Участник.Недействительна
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК Недействителен,
		|	РабочиеГруппы.Изменение КАК Изменение,
		|	РабочиеГруппы.Автоподстановка КАК Автоподстановка,
		|	РабочиеГруппы.Источник КАК Источник
		|ИЗ
		|	РегистрСведений.РабочиеГруппы КАК РабочиеГруппы
		|ГДЕ
		|	РабочиеГруппы.Объект = &Объект");
#КонецУдаления
#Вставка
		"ВЫБРАТЬ
		|	РабочиеГруппы.Объект КАК Объект,
		|	РабочиеГруппы.Участник КАК Участник,
		|	РабочиеГруппы.Изменение КАК Изменение,
		|	РабочиеГруппы.Автоподстановка КАК Автоподстановка,
		|	РабочиеГруппы.Источник КАК Источник
		|ПОМЕСТИТЬ втУчастники
		|ИЗ
		|	РегистрСведений.РабочиеГруппы КАК РабочиеГруппы
		|ГДЕ
		|	РабочиеГруппы.Объект = &Объект
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	втУчастники.Объект КАК Объект,
		|	втУчастники.Участник КАК Участник,
		|	втУчастники.Участник.ПометкаУдаления КАК ПометкаУдаления,
		|	ВЫБОР
		|		КОГДА втУчастники.Участник ССЫЛКА Справочник.Пользователи
		|			ТОГДА втУчастники.Участник.Недействителен
		|		КОГДА втУчастники.Участник ССЫЛКА Справочник.РабочиеГруппы
		|			ТОГДА втУчастники.Участник.Недействительна
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК Недействителен,
		|	втУчастники.Изменение КАК Изменение,
		|	втУчастники.Автоподстановка КАК Автоподстановка,
		|	втУчастники.Источник КАК Источник
		|ИЗ
		|	втУчастники КАК втУчастники");
#КонецВставки		
	Запрос.УстановитьПараметр("Объект", Объект);
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ЗаполнитьЗначенияСвойств(ТаблицаУчастников.Добавить(), Выборка);
	КонецЦикла;
	
	Возврат ТаблицаУчастников;
	
КонецФункции
