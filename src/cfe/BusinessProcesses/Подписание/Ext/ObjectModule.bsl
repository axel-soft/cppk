
&ИзменениеИКонтроль("ОбработкаЗаполнения")
Процедура ЦППК_ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)

	Если ЭтоНовый() Тогда

		Дата = ТекущаяДатаСеанса();

		Если Не ЗначениеЗаполнено(Автор) Тогда
			Автор = Сотрудники.ОсновнойСотрудник();
		КонецЕсли;

		СтрокаУчастника_ОбработатьРезультат = 
		РаботаСБизнесПроцессами.СтрокаУчастника_ОбработатьРезультатПодписания(ЭтотОбъект);
		СтрокаУчастника_ОбработатьРезультат.Участник = Автор;

		Важность = Перечисления.ВариантыВажностиОбъектов.Обычная;
		НомерИтерации = 0;

		Если Не ЗначениеЗаполнено(Проект) Тогда
			Проект = РаботаСПроектами.ПолучитьПроектПоУмолчанию();
		КонецЕсли;

		СпособПодписания = Перечисления.СпособыПодписанияПредметаПроцесса.ПЭП;
		ПорядокВыполненияУчастниками = Перечисления.ПорядокВыполненияЭтапаПроцесса.Параллельно;
	КонецЕсли;

	Если ДанныеЗаполнения <> Неопределено И ТипЗнч(ДанныеЗаполнения) <> Тип("Структура") Тогда
		Мультипредметность.ПередатьПредметыПроцессу(ЭтотОбъект, ДанныеЗаполнения, Ложь, Истина);
	КонецЕсли;

	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда

		Если ДанныеЗаполнения.Свойство("ВедущаяЗадача") Тогда
			ВедущаяЗадача = ДанныеЗаполнения.ВедущаяЗадача;
		КонецЕсли;

		Если ДанныеЗаполнения.Свойство("Важность") Тогда
			Важность = ДанныеЗаполнения.Важность;
		КонецЕсли;

		Если ДанныеЗаполнения.Свойство("Наименование") Тогда
			Наименование = ДанныеЗаполнения.Наименование;
		КонецЕсли;

		Если ДанныеЗаполнения.Свойство("Шаблон") Тогда
			Мультипредметность.ЗаполнитьПредметыПроцессаПоШаблону(ДанныеЗаполнения.Шаблон,
			ЭтотОбъект);
		КонецЕсли;

		Если ДанныеЗаполнения.Свойство("Предметы") Тогда
			Мультипредметность.ПередатьПредметыПроцессу(ЭтотОбъект, ДанныеЗаполнения.Предметы,
			Ложь, Истина);
			Проект = МультипредметностьПереопределяемый.ПолучитьОсновнойПроектПоПредметам(
			ДанныеЗаполнения.Предметы);
		КонецЕсли;

		Если ДанныеЗаполнения.Свойство("АвторСобытия") Тогда
			Автор = ДанныеЗаполнения.АвторСобытия;
		КонецЕсли;

		Если ДанныеЗаполнения.Свойство("Автор") И ЗначениеЗаполнено(ДанныеЗаполнения.Автор) Тогда
			Автор = ДанныеЗаполнения.Автор;
		КонецЕсли;

		Если ДанныеЗаполнения.Свойство("Шаблон") Тогда
			ЗаполнитьПоШаблону(ДанныеЗаполнения.Шаблон);
		КонецЕсли;

		Если ДанныеЗаполнения.Свойство("ЗадачаИсполнителя") Тогда
			ЗадачаСсылка = ДанныеЗаполнения.ЗадачаИсполнителя;
			ЗаполнитьБизнесПроцессПоЗадаче(ЗадачаСсылка);
			#Вставка
			//+ЦППК САНФ-023288 31.01.2024			
			ТипыПредметов = Новый Массив;
			ТипыПредметов.Добавить(Тип("СправочникСсылка.ДокументыПредприятия"));
			ОбрабатываемыеПредметы = МультипредметностьКлиентСервер.ПолучитьМассивПредметовОбъекта(ЭтотОбъект, ТипыПредметов, Истина);
			СрокИсполнения = ЦППК_ОбщийМодульВызовСервера.ЗаполнитьСрокИсполнения(ОбрабатываемыеПредметы, ДанныеЗаполнения.ЗадачаИсполнителя);
			
			Если ЗначениеЗаполнено(СрокИсполнения) Тогда
				
				ВариантУстановкиСрокаОбработкиРезультатов = Перечисления.ВариантыУстановкиСрокаИсполнения.ТочныйСрок;

				СрокИсполненияПроцесса = СрокИсполнения;
				Для каждого стр Из Участники Цикл
					ЦППК_ОбщийМодульКлиентСервер.ЗаполнитьСтрокуСрокИсполнения(стр, СрокИсполненияПроцесса, Дата);
					стр.ВариантУстановкиСрокаИсполнения = Перечисления.ВариантыУстановкиСрокаИсполнения.ТочныйСрок;
				КонецЦикла;	
			КонецЕсли;
				
			//-ЦППК 31.01.2024
			#КонецВставки
		КонецЕсли;

		Если ДанныеЗаполнения.Свойство("ПроектнаяЗадача") Тогда
			ЗаполнитьПоПроектнойЗадаче(ДанныеЗаполнения.ПроектнаяЗадача);
		КонецЕсли;

		Если ДанныеЗаполнения.Свойство("Проект") Тогда
			Проект = ДанныеЗаполнения.Проект;
		КонецЕсли;

		Если ДанныеЗаполнения.Свойство("КоличествоИтераций") Тогда
			КоличествоИтераций = ДанныеЗаполнения.КоличествоИтераций;
		КонецЕсли;

		Если ДанныеЗаполнения.Свойство("СпособПодписания") Тогда
			СпособПодписания = ДанныеЗаполнения.СпособПодписания;
		КонецЕсли;

		Если ДанныеЗаполнения.Свойство("Этапы") И ТипЗнч(ДанныеЗаполнения.Этапы) = Тип(
			"ТаблицаЗначений") Тогда

			Для Каждого СтрокаЭтапа Из ДанныеЗаполнения.Этапы Цикл
				ЗаполнитьЗначенияСвойств(Этапы.Добавить(), СтрокаЭтапа);
			КонецЦикла;
		КонецЕсли;

		Если ДанныеЗаполнения.Свойство("ПорядокВыполненияУчастниками") Тогда
			ПорядокВыполненияУчастниками = ДанныеЗаполнения.ПорядокВыполненияУчастниками;
		КонецЕсли;

		Если ДанныеЗаполнения.Свойство("Участники") И ТипЗнч(ДанныеЗаполнения.Участники) = Тип(
			"ТаблицаЗначений") Тогда

			ТочкиМаршрута = БизнесПроцессы.Подписание.ТочкиМаршрута;

			СтрокаУчастника_ОбработатьРезультат = 
			РаботаСБизнесПроцессами.СтрокаУчастника_ОбработатьРезультатПодписания(ЭтотОбъект);

			Для Каждого СтрокаУчастника Из ДанныеЗаполнения.Участники Цикл
				Если СтрокаУчастника.ТочкаМаршрута = ТочкиМаршрута.ОбработатьРезультат Тогда
					СтрокаНовогоУчастника = СтрокаУчастника_ОбработатьРезультат;
				Иначе
					СтрокаНовогоУчастника = Участники.Добавить();
				КонецЕсли;
				ЗаполнитьЗначенияСвойств(СтрокаНовогоУчастника, СтрокаУчастника);
			КонецЦикла;

		КонецЕсли;

		Если ДанныеЗаполнения.Свойство("Исполнители") И ДанныеЗаполнения.Свойство("Исполнители")
			= Тип("Массив") И Этапы.Количество() = 0 Тогда

			ТочкиМаршрута = БизнесПроцессы.Подписание.ТочкиМаршрута;

			ИндексИсполнителя = ДанныеЗаполнения.Исполнители.Количество() - 1;
			Пока ИндексИсполнителя >= 0 Цикл
				СтрокаУчастника = Участники.Вставить(0);
				СтрокаУчастника.Участник = ДанныеЗаполнения.Исполнители[ИндексИсполнителя];
				СтрокаУчастника.ТочкаМаршрута = ТочкиМаршрута.Подписать;
				ИндексИсполнителя = ИндексИсполнителя - 1;
			КонецЦикла;
		КонецЕсли;

	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("ЗадачаСсылка.ЗадачаИсполнителя") Тогда
		
		ЗадачаСсылка = ДанныеЗаполнения;
		ЗаполнитьБизнесПроцессПоЗадаче(ЗадачаСсылка);
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("СправочникСсылка.ПроектныеЗадачи") Тогда

		ЗаполнитьПоПроектнойЗадаче(ДанныеЗаполнения);

	КонецЕсли;

	СтрокаУчастника_ОбработатьРезультат = 
	РаботаСБизнесПроцессами.СтрокаУчастника_ОбработатьРезультатПодписания(ЭтотОбъект);
	Если Не ЗначениеЗаполнено(СтрокаУчастника_ОбработатьРезультат.Участник) Тогда
		СтрокаУчастника_ОбработатьРезультат.Участник = Автор;
	КонецЕсли;

	Если Не ЗначениеЗаполнено(Наименование) И Предметы.Количество() > 0 Тогда
		МультипредметностьКлиентСервер.ЗаполнитьНаименованиеПроцесса(
		ЭтотОбъект, НаименованиеПроцессаБезПредметовПоУмолчанию());
	КонецЕсли;
	
	#Вставка
	//++МельниченкоНН 16.08.2024 САНФ-027815
	Если НЕ ЗначениеЗаполнено(ЭтотОбъект.Наименование) И ДанныеЗаполнения.Свойство("ЗадачаИсполнителя") Тогда
		ЦППК_ОбщийМодульВызовСервера.СформироватьНаименованиеБП_ПоПриложениямЗадачи(ЭтотОбъект, ДанныеЗаполнения.ЗадачаИсполнителя);
	КонецЕсли;
	//--МельниченкоНН 16.08.2024 САНФ-027815
	#КонецВставки
	
	БизнесПроцессыИЗадачиСервер.ЗаполнитьГлавнуюЗадачу(ЭтотОбъект, ДанныеЗаполнения);

КонецПроцедуры

&ИзменениеИКонтроль("ЗаполнитьНаименованиеЗадачиПроцессаДляЭтапаПодписания")
Процедура ЦППК_ЗаполнитьНаименованиеЗадачиПроцессаДляЭтапаПодписания(ЗадачаПроцесса)

	ТочкиМаршрута = БизнесПроцессы.Подписание.ТочкиМаршрута;
	ПредметыСтрокой = МультипредметностьКлиентСервер.ПредметыСтрокой(ЗадачаПроцесса.Предметы, Истина, Ложь);
	НаименованиеИОписаниеПоДействию = РаботаСПроцессамиПоДействиям.НаименованиеИОписаниеЗадачиПоДействию(
	ЗадачаПроцесса, ПредметыСтрокой, Истина);

	Если ЗадачаПроцесса.ТочкаМаршрута = ТочкиМаршрута.Подписать Тогда

		Если НаименованиеИОписаниеПоДействию <> Неопределено 
			И ЗначениеЗаполнено(НаименованиеИОписаниеПоДействию.Наименование) Тогда
#Удаление
			Если СпособПодписания = Перечисления.СпособыПодписанияПредметаПроцесса.НаБумаге Тогда
				ПостфиксНаименования = НСтр("ru = '(На бумаге)'"); 
			ИначеЕсли СпособПодписания = Перечисления.СпособыПодписанияПредметаПроцесса.ПЭП Тогда
				ПостфиксНаименования = НСтр("ru = '(Простая ЭП)'");
			ИначеЕсли СпособПодписания = Перечисления.СпособыПодписанияПредметаПроцесса.УЭП Тогда
				ПостфиксНаименования = НСтр("ru = '(Усиленная ЭП)'");
			КонецЕсли;
			НаименованиеЗадачиПроцесса = НаименованиеИОписаниеПоДействию.Наименование + " " + ПостфиксНаименования;
#КонецУдаления

		ИначеЕсли СтрНачинаетсяС(НаименованиеПроцессаПоУмолчанию(), Наименование) Тогда
#Удаление
			Если СпособПодписания = Перечисления.СпособыПодписанияПредметаПроцесса.НаБумаге Тогда
				ПрефиксНаименования = НСтр("ru = 'Подписать на бумаге'"); 
			ИначеЕсли СпособПодписания = Перечисления.СпособыПодписанияПредметаПроцесса.ПЭП Тогда
				ПрефиксНаименования = НСтр("ru = 'Подписать (Простая ЭП)'");
			ИначеЕсли СпособПодписания = Перечисления.СпособыПодписанияПредметаПроцесса.УЭП Тогда
				ПрефиксНаименования = НСтр("ru = 'Подписать (Усиленная ЭП)'");
			КонецЕсли;
#КонецУдаления
#Вставка
			ПрефиксНаименования = НСтр("ru = 'Подписать'");
#КонецВставки
			НаименованиеЗадачиПроцесса = СтрЗаменить(Наименование,
			НаименованиеПроцессаБезПредметовПоУмолчанию(), ПрефиксНаименования);
		Иначе
			НаименованиеЗадачиПроцесса = Наименование;
		КонецЕсли;

	ИначеЕсли ЗадачаПроцесса.ТочкаМаршрута = ТочкиМаршрута.ОбеспечитьПодписание Тогда

		ПрефиксНаименования = НСтр("ru = 'Обеспечить подписание на бумаге'");
		Если НаименованиеИОписаниеПоДействию <> Неопределено 
			И ЗначениеЗаполнено(НаименованиеИОписаниеПоДействию.Наименование) Тогда
			НаименованиеЗадачиПроцесса = НаименованиеИОписаниеПоДействию.Наименование + " " + НСтр("ru = '(На бумаге)'");

		ИначеЕсли СтрНачинаетсяС(НаименованиеПроцессаПоУмолчанию(), Наименование) Тогда
			НаименованиеЗадачиПроцесса = СтрЗаменить(Наименование,
			НаименованиеПроцессаБезПредметовПоУмолчанию(), ПрефиксНаименования);
		Иначе

			НаименованиеЗадачиПроцесса = СтрШаблон("%1: %2", ПрефиксНаименования, Наименование);
		КонецЕсли;

	КонецЕсли;

	// Заполняем наименование задачи, только если оно отличается от вычисленного.
	Если Не ЗначениеЗаполнено(ЗадачаПроцесса.Наименование) Или Не СтрНачинаетсяС(
		НаименованиеЗадачиПроцесса, ЗадачаПроцесса.Наименование) Тогда

		ЗадачаПроцесса.Наименование = НаименованиеЗадачиПроцесса;
	КонецЕсли;

КонецПроцедуры
